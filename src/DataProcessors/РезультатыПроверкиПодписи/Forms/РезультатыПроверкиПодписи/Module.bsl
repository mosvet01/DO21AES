#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Параметры.Свойство("СвойстваПодписи") Тогда
		ВызватьИсключение НСтр("ru = 'Обработка не предназначена для непосредственного использования.'");
	КонецЕсли;
	
	ОбщегоНазначенияБЭД.СброситьРазмерыИПоложениеОкна(ЭтотОбъект);
	
	СвойстваПодписи = Новый Структура;
	СвойстваДоверенности = Новый Структура;
	РезультатПроверки = Новый Структура;
	
	Параметры.Свойство("СвойстваПодписи", СвойстваПодписи);
	Параметры.Свойство("СвойстваДоверенности", СвойстваДоверенности);
	Параметры.Свойство("РезультатПроверки", РезультатПроверки);
	
	ПодписьОпределена = СвойстваПодписи <> Неопределено;
			
	Если Не ПодписьОпределена Тогда 
	  
		СвойстваПодписи = Новый Структура;  
		СвойстваПодписи.Вставить("ДатаПроверкиПодписи", ТекущаяДатаСеанса());
		СвойстваПодписи.Вставить("ПодписьВерна", Ложь); 
		СвойстваПодписи.Вставить("Комментарий", "Подпись не определена");
		
	КонецЕсли;

	ОтобразитьПротоколНаФорме(СвойстваПодписи, СвойстваДоверенности, РезультатПроверки);
	
КонецПроцедуры 

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОтобразитьПротоколНаФорме(СвойстваПодписи, СвойстваДоверенности, РезультатПроверки)
	
	ИмеетсяПротоколПроверки = Ложь; 
	ПроверкаПодписи			= СвойстваПодписи; 
	ПодписьВерна			= СвойстваПодписи.ПодписьВерна;
	ПодписьОписаниеОшибки	= СвойстваПодписи.Комментарий;
	ДатаПроверкиПодписи 	= СвойстваПодписи.ДатаПроверкиПодписи;
	
	ТребуетсяДоверенность 	= Ложь;
	ДоверенностьНайдена 	= Ложь;
	
	Если ЗначениеЗаполнено(РезультатПроверки) Тогда 
		ПротоколПроверки 				= РезультатПроверки.ПротоколПроверки;
		ИмеетсяПротоколПроверки 		= ПротоколПроверки <> Неопределено;
		ПодписьВерна 					= РезультатПроверки.ПодписьВерна;
		Доверенность 					= РезультатПроверки.Доверенность;
		ДоверенностьНайдена			 	= Доверенность <> Неопределено;
		ТребуетсяДоверенность			= РезультатПроверки.ТребуетсяДоверенность;
		Элементы.ОткрытьМЧД.Видимость 	= ДоверенностьНайдена;
	КонецЕсли;

	Если ПодписьВерна Тогда   
		Элементы.ЗаголовокПредставлениеПроверки.Заголовок = НСтр("ru='Подпись верна'");
		Элементы.ЗаголовокПредставлениеПроверки.ЦветТекста = ЦветаСтиля.ЦветРазделаПанелиФункций;
		Элементы.ГруппаПроверкиШапка.ЦветФона = ЦветаСтиля.ЦветФонаПодсказки;	
	Иначе 
		Элементы.ЗаголовокПредставлениеПроверки.Заголовок = НСтр("ru='Подпись неверна'");
		Элементы.ЗаголовокПредставлениеПроверки.ЦветТекста = ЦветаСтиля.ЦветТекстаФормы;
		Элементы.ГруппаПроверкиШапка.ЦветФона = ЦветаСтиля.ЦветФонаПредупреждения;	
	КонецЕсли;
	
	Если ИмеетсяПротоколПроверки И ТребуетсяДоверенность И ДоверенностьНайдена Тогда
		Если ПротоколПроверки.ВерсияПротокола = "2.0" Тогда	
			ПроверкаПодписи 		= ПротоколПроверки.ПроверкаПодписиДокумента;
			ПодписьВерна			= ПроверкаПодписи.Успех;
			ПодписьОписаниеОшибки	= ПроверкаПодписи.Ошибка;
			ДатаПроверкиПодписи		= ПроверкаПодписи.ДатаПроверки;
			
			ПроверкаДоверенности = ПротоколПроверки.ПроверкаМЧД;
			ПоказатьПроверкуМЧД(ПроверкаДоверенности, СвойстваПодписи, СвойстваДоверенности);
		Иначе
			Элементы.ДиагностикаМЧД.Видимость = Ложь;
		КонецЕсли;
	ИначеЕсли ТребуетсяДоверенность И Не ДоверенностьНайдена Тогда
		ПодписьОписаниеОшибки = НСтр("ru='Не найдена подходящая машиночитаемая доверенность.'");
		Элементы.ЗаголовокПредставлениеДоверенности.Видимость = Истина;
		Элементы.ЗаголовокПредставлениеДоверенности.Заголовок = НСтр("ru='Доверенность не найдена'");
		Элементы.ЗаголовокПредставлениеДоверенности.ЦветТекста = ЦветаСтиля.ЦветТекстаНеУдачнаяПроверкаМЧД; 
		Элементы.ГруппаПроверкиШапка.ЦветФона = ЦветаСтиля.ЦветФонаПредупреждения;
		Элементы.ДиагностикаМЧД.Видимость = Ложь;
	Иначе
		Элементы.ДиагностикаМЧД.Видимость = Ложь;
	КонецЕсли;
	
	ДатаПроверкиПоФормату = Формат(ДатаПроверкиПодписи, НСтр("ru='ДФ=''dd.MM.yyyy HH:mm'''"));
	ТекстЗаголовка = СтрШаблон("%1 %2", НСтр("ru='Проверено'"), ДатаПроверкиПоФормату);

	Элементы.ЗаголовокДатаПроверки.Заголовок = ТекстЗаголовка;
	
	Элементы.ГруппаПодписьВерна.Видимость = СвойстваПодписи.ПодписьВерна;
	Элементы.ГруппаПодписьНеВерна.Видимость = Не СвойстваПодписи.ПодписьВерна;
	ПредставлениеПодписьНеВернаПоле = ПодписьОписаниеОшибки;
		
КонецПроцедуры

&НаСервере
Процедура ПоказатьПроверкуМЧД(ПроверкаДоверенности, СвойстваПодписи, СвойстваДоверенности)
	
	Элементы.ЗаголовокПредставлениеДоверенности.Видимость = Истина;
	
	ДоверенностьДействует = Истина;
	ДоверенностьОтозвана = Ложь; 
	ДоверенностьОтозванаНаМоментПодписи = Ложь;
	
	Для Каждого ВложеннаяПроверка Из ПроверкаДоверенности Цикл 
		
		НаименованиеПроверки = ВложеннаяПроверка.Ключ; 
		ГруппаЭлементовПроверки = Элементы.Найти("Группа" + НаименованиеПроверки);
		
		Если ГруппаЭлементовПроверки = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если ПроверкаДоверенности[НаименованиеПроверки] = Неопределено Тогда
			ГруппаЭлементовПроверки.Видимость = Ложь;
			Продолжить;
		КонецЕсли;
		
		УспешнаяПроверка = ПроверкаДоверенности[НаименованиеПроверки].Успех;
		ТекстОшибки = ПроверкаДоверенности[НаименованиеПроверки].Ошибка;
		
		Если Не УспешнаяПроверка Тогда
			ДоверенностьДействует = Ложь;
		КонецЕсли;
		
		Если ПроверкаДоверенности[НаименованиеПроверки] = ПроверкаДоверенности.ПроверкаОтзываМЧД Тогда 
			
			ДоверенностьОтозвана = СвойстваДоверенности.Отозвана;
			
			Если ЗначениеЗаполнено(СвойстваДоверенности.ДатаОтзыва) Тогда
				ДоверенностьОтозванаНаМоментПодписи = СвойстваДоверенности.ДатаОтзыва < СвойстваПодписи.ДатаПодписи;
			КонецЕсли;
			
			УспешнаяПроверка = Не ДоверенностьОтозвана;
			
			ЛокализованныйФорматДата = НСтр("ru='ДФ=''dd.MM.yyyy HH:mm'''");
			
			ТекстОшибки = НСтр("ru='Отозвана с '") 
				+ Формат(СвойстваДоверенности.ДатаОтзыва, ЛокализованныйФорматДата)
				+ НСтр("ru=', документ подписан '")
				+ Формат(СвойстваПодписи.ДатаПодписи, ЛокализованныйФорматДата);
			
			Если ДоверенностьОтозвана И Не ДоверенностьОтозванаНаМоментПодписи Тогда 
				
				ТекстЗаголовка = НСтр("ru='Доверенность была отозвана после подписания документа'");
				Элементы.ЗаголовокПроверкаОтзываМЧДНеВыполненоТекст.Заголовок = ТекстЗаголовка;
				Элементы.ЗаголовокПроверкаОтзываМЧДНеВыполненоКартинка.Картинка = БиблиотекаКартинок.ЖелтыйШарБЭД;
				
 			КонецЕсли;
			
		КонецЕсли;
		
		ЭлементПоложительнойПроверки = СтрШаблон("Группа%1Выполнено", НаименованиеПроверки);
		Элементы[ЭлементПоложительнойПроверки].Видимость = УспешнаяПроверка;
		
		ЭлементОтрицательнойПроверки = СтрШаблон("Группа%1НеВыполнено", НаименованиеПроверки);
		Элементы[ЭлементОтрицательнойПроверки].Видимость = Не УспешнаяПроверка;
		
		ИмяРеквизитаОшибки = СтрШаблон("Представление%1НеВыполненоПоле", НаименованиеПроверки);
		ЭтотОбъект[ИмяРеквизитаОшибки] = ТекстОшибки;
		
		Если ПустаяСтрока(ТекстОшибки) Тогда
			ИмяЭлементаОписанияОшибки = СтрШаблон("Группа%1НеВыполненоПодсказка", НаименованиеПроверки);
			Элементы[ИмяЭлементаОписанияОшибки].Видимость = Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ДоверенностьДействует Тогда 
		Элементы.ЗаголовокПредставлениеДоверенности.Заголовок = НСтр("ru='Доверенность действительна'");
		Элементы.ЗаголовокПредставлениеДоверенности.ЦветТекста = ЦветаСтиля.ЦветРазделаПанелиФункций;
	Иначе  
		Элементы.ЗаголовокПредставлениеДоверенности.Заголовок = НСтр("ru='Доверенность недействительна'");
		Элементы.ЗаголовокПредставлениеДоверенности.ЦветТекста = ЦветаСтиля.ЦветТекстаНеУдачнаяПроверкаМЧД; 
	КонецЕсли;
	
	Если ДоверенностьОтозвана И ДоверенностьОтозванаНаМоментПодписи Тогда 
		Элементы.ЗаголовокПредставлениеДоверенности.Заголовок = НСтр("ru='Доверенность отозвана'");
		Элементы.ЗаголовокПредставлениеДоверенности.ЦветТекста = ЦветаСтиля.ЦветТекстаНеУдачнаяПроверкаМЧД; 
		Элементы.ГруппаПроверкиШапка.ЦветФона = ЦветаСтиля.ЦветФонаПредупреждения;		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьМЧДНажатие(Элемент) 
	Если ЗначениеЗаполнено(Доверенность) Тогда
		ПоказатьЗначение( , Доверенность);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти 