
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбновитьВидимостьДоступность();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОбсуждениеСтрокойПриИзменении(Элемент)
	
	ЗаполнитьОбсуждение();
	
	Если Не ЗначениеЗаполнено(ИдентфикаторОбсуждения) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Обсуждение не найдено'"));
	КонецЕсли;
	
	ТекущийЭлемент = Элементы.УчастникиОбсуждения;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОбсуждение()
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТемаОбсуждения = "";
	КлючОбсуждения = "";
	ИдентфикаторОбсуждения = "";
	
	УчастникиОбсуждения.Очистить();
	
	Обсуждение = СистемаВзаимодействия.ПолучитьОбсуждение(ОбсуждениеСтрокой);
	Если Обсуждение = Неопределено Тогда
		// Поиск по навигационно ссылке.
		ТекстПередСсылкой = "/conv?id=";
		ПозицияНачалаСсылки = СтрНайти(ОбсуждениеСтрокой, ТекстПередСсылкой);
		Если ПозицияНачалаСсылки > 0 Тогда
			УУИДСтрокой = Сред(ОбсуждениеСтрокой, ПозицияНачалаСсылки + СтрДлина(ТекстПередСсылкой));
			Попытка
				Обсуждение = СистемаВзаимодействия.ПолучитьОбсуждение(
					Новый ИдентификаторОбсужденияСистемыВзаимодействия(УУИДСтрокой));
			Исключение
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
	
	Если Обсуждение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТемаОбсуждения = Обсуждение.Заголовок;
	КлючОбсуждения = Обсуждение.Ключ;
	ИдентфикаторОбсуждения = Обсуждение.Идентификатор;
	Групповое = Обсуждение.Групповое;
	
	Для Каждого Участник Из Обсуждение.Участники Цикл
		СтрУчастника = УчастникиОбсуждения.Добавить();
		СтрУчастника.ИДПользователяСВ = Участник;
		СтрУчастника.Имя = СтрШаблон(НСтр("ru = 'Не найден <%1>'"), Участник);
		СтрУчастника.ПолноеИмя = СтрШаблон(НСтр("ru = 'Не найден <%1>'"), Участник);
		Пользователь = СистемаВзаимодействия.ПолучитьПользователя(Участник);
		Если Пользователь <> Неопределено Тогда
			СтрУчастника.Имя = Пользователь.Имя;
			СтрУчастника.ПолноеИмя = Пользователь.ПолноеИмя;
		КонецЕсли;
	КонецЦикла;
	
	УчастникиОбсуждения.Сортировать("ПолноеИмя");
	
	ОбновитьВидимостьДоступность();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУчастникиОбсуждения

&НаКлиенте
Процедура УчастникиОбсужденияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Пользователь = ПользовательПоИдентификаторуСВ(ТекущиеДанные.ИДПользователяСВ);
	Если ЗначениеЗаполнено(Пользователь) Тогда
		ПоказатьЗначение(, Пользователь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УчастникиОбсужденияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	ПараметрыАК = Новый Структура;
	ПараметрыАК.Вставить("РежимВыбора", Истина);
	ПараметрыАК.Вставить("ЗакрыватьПриЗакрытииВладельца", Истина);
	ПараметрыАК.Вставить("РежимРаботыФормы", 2);
	ПараметрыАК.Вставить("УпрощенныйИнтерфейс", Истина);
	ПараметрыАК.Вставить("ОтображатьСотрудников", Истина);
	ПараметрыАК.Вставить("ЗаголовокФормы", НСтр("ru = 'Выбор пользователя'"));
	ПараметрыАК.Вставить("ВыбиратьПользователейСистемыВзаимодействия", Истина);
	
	ОткрытьФорму("Справочник.АдреснаяКнига.ФормаСписка", ПараметрыАК, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура УчастникиОбсужденияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбработатьВыборУчастниковНаСервере(ВыбранноеЗначение);
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьВыборУчастниковНаСервере(ВыбранныеЗначения)
	
	ПрисутствующиеУчастники = Новый Соответствие;
	Для Каждого Стр Из УчастникиОбсуждения Цикл
		ПрисутствующиеУчастники[Стр.ИДПользователяСВ] = Истина;
	КонецЦикла;
	
	Для Каждого ИдентификаторСВ Из ВыбранныеЗначения Цикл
		
		Если ПрисутствующиеУчастники[Строка(ИдентификаторСВ)] <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ПользовательСВ = СистемаВзаимодействия.ПолучитьПользователя(ИдентификаторСВ);
		Если ПользовательСВ = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		СтрУчастника = УчастникиОбсуждения.Добавить();
		СтрУчастника.ИДПользователяСВ = ИдентификаторСВ;
		СтрУчастника.Имя = ПользовательСВ.Имя;
		СтрУчастника.ПолноеИмя = ПользовательСВ.ПолноеИмя;
		ПрисутствующиеУчастники[Строка(ИдентификаторСВ)] = Истина;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИзменения(Команда)
	
	ЗаписатьИзмененияНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьИзмененияНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Обсуждение = СистемаВзаимодействия.ПолучитьОбсуждение(
		Новый ИдентификаторОбсужденияСистемыВзаимодействия(ИдентфикаторОбсуждения));
	
	Если Обсуждение = Неопределено Тогда
		Сообщить(НСтр("ru = 'Не выбрано обсуждение'"));
		Возврат;
	КонецЕсли;
	
	ТекущиеУчастникиДляПоиска = Новый Соответствие;
	Для Каждого Эл Из Обсуждение.Участники Цикл
		ТекущиеУчастникиДляПоиска[Строка(Эл)] = Истина;
	КонецЦикла;
	
	НовыеУчастникиДляПоиска = Новый Соответствие;
	Для Каждого Стр Из УчастникиОбсуждения Цикл
		НовыеУчастникиДляПоиска[Стр.ИДПользователяСВ] = Истина;
	КонецЦикла;
	
	ЕстьИзменения = Ложь;
	
	Если Обсуждение.Заголовок <> ТемаОбсуждения Тогда
		ЕстьИзменения = Истина;
		Обсуждение.Заголовок = ТемаОбсуждения;
	КонецЕсли;
	
	// Удаление неактуальных.
	Для Каждого КлючИЗначение Из ТекущиеУчастникиДляПоиска Цикл
		УчастникСтрокой = КлючИЗначение.Ключ;
		Если НовыеУчастникиДляПоиска[УчастникСтрокой] = Неопределено Тогда
			ЕстьИзменения = Истина;
			Обсуждение.Участники.Удалить(
				Новый ИдентификаторПользователяСистемыВзаимодействия(УчастникСтрокой));
		КонецЕсли;
	КонецЦикла;
	
	// Добавление новых.
	Для Каждого КлючИЗначение Из НовыеУчастникиДляПоиска Цикл
		УчастникСтрокой = КлючИЗначение.Ключ;
		Если ТекущиеУчастникиДляПоиска[УчастникСтрокой] = Неопределено Тогда
			ЕстьИзменения = Истина;
			Обсуждение.Участники.Добавить(
				Новый ИдентификаторПользователяСистемыВзаимодействия(УчастникСтрокой));
		КонецЕсли;
	КонецЦикла;
	
	Если ЕстьИзменения Тогда
		Обсуждение.Записать();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ЗаполнитьОбсуждение();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьВидимостьДоступность()
	
	Элементы.ТемаОбсуждения.ТолькоПросмотр = Не Групповое;
	Элементы.УчастникиОбсуждения.ТолькоПросмотр = Не Групповое;
	Элементы.ЗаписатьИзменения.Доступность = Групповое;
	
КонецПроцедуры

&НаСервере
Функция ПользовательПоИдентификаторуСВ(ИДПользователяСВСтрокой)
	
	Пользователь = Неопределено;
	
	ПользовательСВ = СистемаВзаимодействия.ПолучитьПользователя(
		Новый ИдентификаторПользователяСистемыВзаимодействия(ИДПользователяСВСтрокой));
	
	Если ПользовательСВ = Неопределено Тогда
		Возврат Пользователь;
	КонецЕсли;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Пользователи.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Пользователи КАК Пользователи
		|ГДЕ
		|	Пользователи.ИдентификаторПользователяИБ = &ИдентификаторПользователяИБ");
	
	Запрос.УстановитьПараметр("ИдентификаторПользователяИБ",
		ПользовательСВ.ИдентификаторПользователяИнформационнойБазы);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Пользователь = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Пользователь;
	
КонецФункции

#КонецОбласти
