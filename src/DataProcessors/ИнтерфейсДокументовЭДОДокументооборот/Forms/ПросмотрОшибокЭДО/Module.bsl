
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ТипЗнч(Параметры.Ошибки) <> Тип("Структура") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ЗаполнитьТаблицуОшибок();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицы

&НаКлиенте
Процедура СписокОшибокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.СписокОшибок.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.Документ) Тогда
		ПоказатьЗначение(, ТекущиеДанные.Документ);
	ИначеЕсли ЗначениеЗаполнено(ТекущиеДанные.ИдентификаторПакета) Тогда
		ОбменСКонтрагентамиДОСлужебныйКлиент.ОткрытьПакетЭДО(ТекущиеДанные.ИдентификаторПакета);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьТаблицуОшибок()
	
	ОшибкиДокументов = Новый Соответствие;
	Если Параметры.Ошибки.Свойство("Документы") Тогда
		ОшибкиДокументов = Параметры.Ошибки.Документы;
	КонецЕсли;
	
	ОшибкиПакетов = Новый Соответствие;
	Если Параметры.Ошибки.Свойство("Пакеты") Тогда
		ОшибкиПакетов = Параметры.Ошибки.Пакеты;
	КонецЕсли;
	
	Для Каждого ЭлементДокумента Из ОшибкиДокументов Цикл
		
		Документ = ЭлементДокумента.Ключ;
		ОписаниеОшибки = ЭлементДокумента.Значение;
		
		НоваяСтрока = СписокОшибок.Добавить();
		
		НоваяСтрока.Документ = Документ;
		НоваяСтрока.ПредставлениеОбъекта = Строка(Документ);
		НоваяСтрока.ОписаниеОшибки = ОписаниеОшибки;
		
	КонецЦикла;
	
	ПредставленияПакетов = ПредставленияПакетов(ОшибкиПакетов);
	
	Для Каждого ЭлементПакета Из ОшибкиПакетов Цикл
		
		ИДПакета = ЭлементПакета.Ключ;
		ОписаниеОшибки = ЭлементПакета.Значение;
		
		НоваяСтрока = СписокОшибок.Добавить();
		
		НоваяСтрока.ИдентификаторПакета = ИДПакета;
		НоваяСтрока.ПредставлениеОбъекта = ПредставленияПакетов[ИДПакета];
		НоваяСтрока.ОписаниеОшибки = ОписаниеОшибки;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПредставленияПакетов(ОшибкиПакетов)
	
	ПредставленияПакетов = Новый Соответствие;
	
	Пакеты = Новый Массив;
	Для Каждого Элемент Из ОшибкиПакетов Цикл
		Пакеты.Добавить(Элемент.Ключ);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СоставПакетовЭДОДокументооборот.ИдентификаторПакета КАК ИдентификаторПакета,
		|	СоставПакетовЭДОДокументооборот.Документ
		|ИЗ
		|	РегистрСведений.СоставПакетовЭДОДокументооборот КАК СоставПакетовЭДОДокументооборот
		|ГДЕ
		|	СоставПакетовЭДОДокументооборот.ИдентификаторПакета В (&Пакеты)
		|ИТОГИ
		|ПО
		|	ИдентификаторПакета";
	Запрос.УстановитьПараметр("Пакеты", Пакеты);
	
	ВыборкаПакетов = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаПакетов.Следующий() Цикл
		
		СтрокиПредставления = Новый Массив;
		СтрокиПредставления.Добавить(НСтр("ru = 'Пакет ЭДО ('"));
		
		ВыборкаДокументовПакета = ВыборкаПакетов.Выбрать();
		ВсегоДокументовВПакете = ВыборкаДокументовПакета.Количество();
		
		ВыведеноВЗаголовокДокуентов = 0;
		
		Пока ВыборкаДокументовПакета.Следующий() И ВыведеноВЗаголовокДокуентов < 2 Цикл
			
			Если ВыведеноВЗаголовокДокуентов > 0 Тогда
				СтрокиПредставления.Добавить(", ");
			КонецЕсли;
			
			СтрокиПредставления.Добавить(Строка(ВыборкаДокументовПакета.Документ));
			
			ВыведеноВЗаголовокДокуентов = ВыведеноВЗаголовокДокуентов + 1;
			
		КонецЦикла;
		
		Если ВсегоДокументовВПакете - ВыведеноВЗаголовокДокуентов > 0 Тогда
			СтрокиПредставления.Добавить(НСтр("ru = ' и еще '"));
			СтрокиПредставления.Добавить(
				СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
					НСтр("ru = ';%1 документ;;%1 документа;%1 документов;%1 документов'"),
					ВсегоДокументовВПакете - ВыведеноВЗаголовокДокуентов));
		КонецЕсли;
		
		СтрокиПредставления.Добавить(")");
		
		ПредставленияПакетов.Вставить(ВыборкаПакетов.ИдентификаторПакета,
			СтрСоединить(СтрокиПредставления, ""));
		
	КонецЦикла;
	
	Возврат ПредставленияПакетов;
	
КонецФункции

#КонецОбласти
