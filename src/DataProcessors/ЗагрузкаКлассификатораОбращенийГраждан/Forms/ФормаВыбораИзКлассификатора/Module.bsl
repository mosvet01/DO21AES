
#Область Обработчики_событий_формы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Обработки.ЗагрузкаКлассификатораОбращенийГраждан.ЗаполнитьТаблицыФормыДаннымиКлассификатора(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьОформлениеЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Оповестить("Выполнена_загрузка_классификатора_обращений");
	
КонецПроцедуры

#КонецОбласти

#Область Обработчики_команд_формы

&НаКлиенте
Процедура Сохранить(Команда)
	
	Если СохранитьДанныеНаСервере() Тогда 
		ПоказатьВопрос(
			Новый ОписаниеОповещения("ПродолжениеПослеОтветаНаВопросПользователем", ЭтотОбъект),
			НСтр("ru = 'Данные классификатора обращений граждан успешно загружены.
				|Закрыть форму загрузки?'"),
			РежимДиалогаВопрос.ДаНет,
			,
			КодВозвратаДиалога.Нет,
			Нстр("ru = 'Загрузка классификатора обращений граждан.'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтметки(Команда)
	
	УстановитьЗначениеОтметки(СтрЗаменить(Элементы.ГруппаЭлементыКлассификатора.ТекущаяСтраница.Имя, "Страница", ""), Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсеОтметки(Команда)
	
	УстановитьЗначениеОтметки(СтрЗаменить(Элементы.ГруппаЭлементыКлассификатора.ТекущаяСтраница.Имя, "Страница", ""), Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ИнвертироватьОтметки(Команда)
	
	УстановитьЗначениеОтметки(СтрЗаменить(Элементы.ГруппаЭлементыКлассификатора.ТекущаяСтраница.Имя, "Страница", ""));
	
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	
	ВыполнитьСменуСтраницыФормы(-1);
	
КонецПроцедуры

&НаКлиенте
Процедура Вперед(Команда)
	
	ВыполнитьСменуСтраницыФормы(1);
	
КонецПроцедуры

#КонецОбласти

#Область Обработчики_команд_таблиц_формы

&НаКлиенте
Процедура ТематикиИТемыВыбранПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ТематикиИТемы.ТекущаяСтрока;
	ТекущаяСтрока = ?(Не ТекущаяСтрока = Неопределено,
		ТематикиИТемы.НайтиПоИдентификатору(ТекущаяСтрока),
		Неопределено);
		
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока.Выбран = ?(ТекущаяСтрока.Выбран > 1, 0, ТекущаяСтрока.Выбран);
	Если ТекущаяСтрока.ПолучитьЭлементы().Количество() Тогда
		УстановитьЗначениеОтметкиДерева(ТекущаяСтрока, ТекущаяСтрока.Выбран);
		ТекущаяСтрока.ЭлементовВыбрано = ?(ТекущаяСтрока.Выбран, ТекущаяСтрока.ЭлементовНаУровне, 0);
	Иначе
		Родитель = ТекущаяСтрока.ПолучитьРодителя();
		Если Не Родитель = Неопределено Тогда
			Родитель.ЭлементовВыбрано = 
				Родитель.ЭлементовВыбрано + ?(ТекущаяСтрока.Выбран = 0, -1, ТекущаяСтрока.Выбран);
			Родитель.Выбран = 
				?(Родитель.ЭлементовНаУровне = Родитель.ЭлементовВыбрано,
					1,				
					?(Родитель.ЭлементовВыбрано > 0, 2, 0));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Служебные_процедуры_и_функции

&НаКлиенте
Процедура УстановитьОформлениеЭлементовФормы()
	
	ЭлементыГруппыСтраниц = Элементы.ГруппаЭлементыКлассификатора.ПодчиненныеЭлементы;
	ТекущаяСтраница		  = Элементы.ГруппаЭлементыКлассификатора.ТекущаяСтраница;
	
	Заголовок = СтрШаблон(НСтр("ru = 'Загрузка классификатора обращений граждан (%1)'"), ТекущаяСтраница.Заголовок);
	Элементы.ДекорацияРасшифровкаСтраницы.Заголовок = ТекущаяСтраница.Подсказка;
	
	Элементы.Назад.Доступность = Не ЭлементыГруппыСтраниц.Индекс(ТекущаяСтраница) = 0;
	Элементы.Вперед.КнопкаПоУмолчанию = ЭлементыГруппыСтраниц.Индекс(ТекущаяСтраница) = ЭлементыГруппыСтраниц.Количество() - 1;
		
	Если ЭлементыГруппыСтраниц.Индекс(ТекущаяСтраница) >= ЭлементыГруппыСтраниц.Количество() - 1 Тогда
		Элементы.Вперед.Заголовок  = Нстр("ru = 'Сохранить'");
	Иначе
		Элементы.Вперед.Заголовок  = Нстр("ru = 'Вперед >'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьСменуСтраницыФормы(Направление)
	
	ЭлементыГруппыСтраниц = Элементы.ГруппаЭлементыКлассификатора.ПодчиненныеЭлементы;
	ТекущаяСтраница		  = Элементы.ГруппаЭлементыКлассификатора.ТекущаяСтраница;
	
	Если ЭлементыГруппыСтраниц.Индекс(ТекущаяСтраница) + Направление = ЭлементыГруппыСтраниц.Количество() Тогда
		Сохранить(Неопределено);
	Иначе
		Элементы.ГруппаЭлементыКлассификатора.ТекущаяСтраница =
			ЭлементыГруппыСтраниц[ЭлементыГруппыСтраниц.Индекс(ТекущаяСтраница) + Направление];
		УстановитьОформлениеЭлементовФормы();
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗначениеОтметки(Список, Режим = Неопределено)
	
	Если Элементы[Список].Отображение = ОтображениеТаблицы.Список Тогда
		Для Каждого СтрокаСписка Из ЭтотОбъект[Список] Цикл
			СтрокаСписка.Выбран = ?(Режим = Неопределено, Не СтрокаСписка.Выбран, Режим);
		КонецЦикла;
	Иначе
		УстановитьЗначениеОтметкиДерева(ЭтотОбъект[Список], Режим);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗначениеОтметкиДерева(Список, Режим)
	
	ТекущийУровень = Список.ПолучитьЭлементы();
	Для Каждого СтрокаСписка Из ТекущийУровень Цикл
		СтрокаСписка.Выбран = ?(Режим = Неопределено, Не СтрокаСписка.Выбран, Режим);
		Если СтрокаСписка.ПолучитьЭлементы().Количество() Тогда
			УстановитьЗначениеОтметкиДерева(СтрокаСписка, СтрокаСписка.Выбран);
			СтрокаСписка.ЭлементовВыбрано = ?(СтрокаСписка.Выбран, СтрокаСписка.ЭлементовНаУровне, 0);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция СохранитьДанныеНаСервере()
	
	Результат = Истина;
	
	Менеджер = Обработки.ЗагрузкаКлассификатораОбращенийГраждан;
	Попытка
		ВыбранныеРазделы  = Разделы.Выгрузить(Новый Структура("Выбран", Истина));
		ВыбранныеВопросы  = Вопросы.Выгрузить(Новый Структура("Выбран", Истина));
		ДанныеТематикИТем = Менеджер.ПодготовитьДанныеДереваТематикИТемКЗагрузке(ЭтаФорма);
	
		НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
		Результат = Менеджер.ЗаписатьДанныеКлассификатораВИнформационнуюБазу(
			ВыбранныеРазделы,
			"Справочник.РазделыОбращений");
		Результат = Менеджер.ЗаписатьДанныеКлассификатораВИнформационнуюБазу(
			ВыбранныеВопросы,
			"Справочник.ВопросыОбращений");
		Результат = Менеджер.ЗаписатьДанныеКлассификатораВИнформационнуюБазу(
			ДанныеТематикИТем.Тематики, 
			"Справочник.ТематикиОбращений");
		Результат = Менеджер.ЗаписатьДанныеКлассификатораВИнформационнуюБазу(
			ДанныеТематикИТем.Темы, 
			"Справочник.ТемыОбращений");
			
		Менеджер.УстановитьСоответствияВопросов();
		ЗафиксироватьТранзакцию();
	Исключение
		Если ТранзакцияАктивна() Тогда
			ОтменитьТранзакцию();
		КонецЕсли;
		
		Результат = Ложь;
		
		ВызватьИсключение КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;
		
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ПродолжениеПослеОтветаНаВопросПользователем(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
