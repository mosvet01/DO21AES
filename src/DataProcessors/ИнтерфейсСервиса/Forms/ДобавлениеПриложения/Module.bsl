
#Область ОбработчикиСобытийФормы
    
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

    Обработки.ИнтерфейсСервиса.ПроверитьВерсиюИнтерфейса(5, Отказ);
    Если Отказ Тогда
        Возврат;
    КонецЕсли; 
    
    Конфигурации = ПрограммныйИнтерфейсСервиса.Конфигурации();
    Для Каждого Конфигурация Из Конфигурации Цикл
        Элементы.КодКонфигурации.СписокВыбора.Добавить(Конфигурация.Код, Конфигурация.Наименование);	
    КонецЦикла;
    ПраваПользователей.Загрузить(ПрограммныйИнтерфейсСервиса.ПользователиАбонента());
    Для Каждого Пользователь Из ПраваПользователей Цикл
        Если Пользователь.РольПользователя = Перечисления.РолиПользователейАбонентов.ВладелецАбонента Тогда
            Пользователь.Право = Перечисления.ПраваПользователяПриложения.ЗапускИАдминистрирование;
        КонецЕсли; 	
    КонецЦикла; 
    ПриложенияАбонента.Загрузить(ПрограммныйИнтерфейсСервиса.Приложения());
    ЗаполнитьСписокЧасовыхПоясов();
    ЧасовойПояс = ЧасовойПоясСеанса();
    
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовШапкиФормы
    
&НаКлиенте
Процедура КодКонфигурацииПриИзменении(Элемент)
	
    УстановитьНаименование();
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
    
    СтандартнаяОбработка = Ложь;
    УстановитьНаименование();
    
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы
    
&НаКлиенте
Процедура Готово(Команда)
    
    Если ГотовоНаСервере() Тогда
        ПараметрыЗакрытия = Новый Структура("Результат, Код", Истина, Код);
        Закрыть(ПараметрыЗакрытия);
    КонецЕсли;
    
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
    
    Закрыть();
    
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции
    
&НаСервере
Процедура ЗаполнитьСписокЧасовыхПоясов()
	
	ЧасовыеПояса = Обработки.ИнтерфейсСервиса.ПолучитьЧасовыеПояса();
	Для каждого Строка Из ЧасовыеПояса Цикл
		Элементы.ЧасовойПояс.СписокВыбора.Добавить(Строка.Идентификатор, Строка.Представление);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ГотовоНаСервере()
    
    ПараметрыСоздания = ПрограммныйИнтерфейсСервиса.НовыйПараметрыСозданияПриложения();
    ПараметрыСоздания.Наименование = Наименование;
    ПараметрыСоздания.КодКонфигурации = КодКонфигурации;
    ПараметрыСоздания.ЧасовойПояс = ЧасовойПояс;
	КодОтвета = 0;
    Результат = ПрограммныйИнтерфейсСервиса.СоздатьПриложение(ПараметрыСоздания,,, КодОтвета);
	ПриложениеСоздано = (КодОтвета = 10200 Или КодОтвета = 10202);
    ПраваНазначены = Ложь;
    Если ПриложениеСоздано Тогда
		Код = Результат.Код;
		Для Каждого Строка Из ПраваПользователей Цикл
			Если ЗначениеЗаполнено(Строка.Право) Тогда
				ПараметрыДобавления = ПрограммныйИнтерфейсСервиса.НовыйПараметрыДобавленияПользователяВПриложение();
				ПараметрыДобавления.КодПриложения = Код;
				ПараметрыДобавления.Логин = Строка.Логин;
				ПараметрыДобавления.Право = Строка.Право;
				ПраваНазначены = ПрограммныйИнтерфейсСервиса.ДобавитьПользователяВПриложение(ПараметрыДобавления);
				Если Не ПраваНазначены Тогда
					Прервать;
				КонецЕсли;         
			КонецЕсли; 
		КонецЦикла;
    КонецЕсли; 

	Возврат ПриложениеСоздано И ПраваНазначены;
    
КонецФункции

&НаКлиенте
Процедура УстановитьНаименование()
    
    Если ЗначениеЗаполнено(КодКонфигурации) Тогда
        ПредставлениеПриложения = Элементы.КодКонфигурации.СписокВыбора.НайтиПоЗначению(КодКонфигурации).Представление;
        ДлинаПредставления = СтрДлина(ПредставлениеПриложения);
        МаксимальныйИндекс = 0;
        Для Каждого Строка Из ПриложенияАбонента Цикл
            Если Лев(Строка.Наименование, ДлинаПредставления) = ПредставлениеПриложения Тогда
                ИндекснаяЧасть = Сред(Строка.Наименование, ДлинаПредставления + 2);
                ЧислоИндекса = 0;
                Если Строка.Наименование = ПредставлениеПриложения Тогда
                    ЧислоИндекса = 1;
                ИначеЕсли СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ИндекснаяЧасть) Тогда
                    ЧислоИндекса = Число(ИндекснаяЧасть);
                КонецЕсли; 
                Если ЧислоИндекса > МаксимальныйИндекс Тогда
                    МаксимальныйИндекс = ЧислоИндекса;
                КонецЕсли; 
            КонецЕсли; 
        КонецЦикла;
        Если МаксимальныйИндекс >= 1 Тогда
            Наименование = ПредставлениеПриложения + " " + Формат(МаксимальныйИндекс + 1, "ЧГ=0");
        Иначе
            Наименование = ПредставлениеПриложения;
        КонецЕсли; 
    КонецЕсли;

КонецПроцедуры

#КонецОбласти 
