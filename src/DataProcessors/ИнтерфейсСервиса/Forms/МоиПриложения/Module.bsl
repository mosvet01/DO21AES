
#Область ОбработчикиСобытийФормы
    
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
    
    Обработки.ИнтерфейсСервиса.ПроверитьВерсиюИнтерфейса(5, Отказ);
    Если Отказ Тогда
        Возврат;
    КонецЕсли; 
    
    ЗаполнитьСписокПриложений();
    
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовТаблицыФормыПриложения
    
&НаКлиенте
Процедура ПриложенияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
    
    Отказ = Истина;
    Оповещение = Новый ОписаниеОповещения("ПослеИзмененияДобавления", ЭтотОбъект);
    ОткрытьФорму("Обработка.ИнтерфейсСервиса.Форма.ДобавлениеПриложения",, ЭтотОбъект,,,, Оповещение);
    
КонецПроцедуры

&НаКлиенте
Процедура ПриложенияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
    
    СтандартнаяОбработка = Ложь;
    ОткрытьЭлемент();

КонецПроцедуры

&НаКлиенте
Процедура ПриложенияПередНачаломИзменения(Элемент, Отказ)
    
    Отказ = Истина;
    ОткрытьЭлемент();
    
КонецПроцедуры

&НаКлиенте
Процедура ПриложенияПередУдалением(Элемент, Отказ)
    
    Отказ = Истина;
    
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Обновить(Команда)
    
    КлючТекущейСтроки = "";
    Если Элементы.Приложения.ТекущиеДанные <> Неопределено Тогда
        КлючТекущейСтроки = Элементы.Приложения.ТекущиеДанные.Код
    КонецЕсли; 
    ЗаполнитьСписокПриложений(КлючТекущейСтроки);
    
КонецПроцедуры
    
#КонецОбласти 

#Область СлужебныеПроцедурыИФункции
    
&НаСервере
Процедура ЗаполнитьСписокПриложений(КлючТекущейСтроки = 0)
    
    Приложения.Загрузить(ПрограммныйИнтерфейсСервиса.Приложения());
    
    // В качестве ключа текущей строки выступает код приложения (номер области).
    Если КлючТекущейСтроки <> 0 Тогда
        Поиск = Приложения.НайтиСтроки(Новый Структура("Код", КлючТекущейСтроки));
        Если Поиск.Количество() > 0 Тогда
            Элементы.Приложения.ТекущаяСтрока = Поиск[0].ПолучитьИдентификатор();
        КонецЕсли; 
    КонецЕсли; 

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЭлемент()
    
    ТекущиеДанные = Элементы.Приложения.ТекущиеДанные;
    
    ПараметрыФормы = Новый Структура("Код", ТекущиеДанные.Код);
    Оповещение = Новый ОписаниеОповещения("ПослеИзмененияДобавления", ЭтотОбъект);
    ОткрытьФорму("Обработка.ИнтерфейсСервиса.Форма.Приложение", ПараметрыФормы, ЭтаФорма,,,, Оповещение);

КонецПроцедуры

&НаКлиенте
Процедура ПослеИзмененияДобавления(Результат, ДопПараметры) Экспорт
	
    Если ТипЗнч(Результат) = Тип("Структура") И Результат.Результат Тогда
        ЗаполнитьСписокПриложений(Результат.Код);
    КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти 
