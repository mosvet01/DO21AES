
#Область ОбработчикиСобытийФормы
    
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
    
    Обработки.ИнтерфейсСервиса.ПроверитьВерсиюИнтерфейса(5, Отказ);
    Если Отказ Тогда
        Возврат;
    КонецЕсли; 
	
	СвойстваПриложения = ПрограммныйИнтерфейсСервиса.СвойстваПриложения(Параметры.Код);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, СвойстваПриложения, "Код, Наименование, КодАбонентаВладельца, АдресПриложения, ЧасовойПояс");
    Заголовок = Наименование;
    
    ПользователиПриложения = ПрограммныйИнтерфейсСервиса.ПользователиПриложения(Код);
    ПользователиПриложения.Индексы.Добавить("Логин");
    ПользователиАбонента = ПрограммныйИнтерфейсСервиса.ПользователиАбонента();
    
    Для Каждого Пользователь Из ПользователиАбонента Цикл
        НоваяСтрока = ПраваПользователей.Добавить();
        НоваяСтрока.Логин = Пользователь.Логин;
        ПользовательПриложения = ПользователиПриложения.Найти(Пользователь.Логин, "Логин");
        Если ЗначениеЗаполнено(ПользовательПриложения) Тогда
            НоваяСтрока.Право = ПользовательПриложения.Право;
            НоваяСтрока.ИсходноеПраво = ПользовательПриложения.Право;
        КонецЕсли; 
    КонецЦикла; 
    
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
    
    Если Модифицированность Тогда
        Оповещение = Новый ОписаниеОповещения("ОповещениеСохранитьИЗакрыть", ЭтаФорма);
        ТекстПредупреждения = "";
        ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(Оповещение, Отказ, ЗавершениеРаботы,, ТекстПредупреждения);
    КонецЕсли;
    
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы
    
&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
    
    ВыполнитьЗаписьИЗакрытьФорму();
    
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
    
    Модифицированность = Не ЗаписатьНаСервере();
    
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции
    
&НаСервере
Функция ЗаписатьНаСервере()

    Результат = Истина;
    Для Каждого Строка Из ПраваПользователей Цикл
        Если Строка.ИсходноеПраво <> Строка.Право Тогда
            Если ЗначениеЗаполнено(Строка.Право) Тогда
                ПараметрыДобавления = ПрограммныйИнтерфейсСервиса.НовыйПараметрыДобавленияПользователяВПриложение();
                ПараметрыДобавления.Логин = Строка.Логин;
                ПараметрыДобавления.Право = Строка.Право;
                ПараметрыДобавления.КодПриложения = Код;
                Результат = ПрограммныйИнтерфейсСервиса.ДобавитьПользователяВПриложение(ПараметрыДобавления);
            Иначе
                Результат = ПрограммныйИнтерфейсСервиса.УдалитьПользователяИзПриложения(Строка.Логин, Код);
            КонецЕсли; 
            Если Не Результат Тогда
                Прервать;
            КонецЕсли; 
        КонецЕсли; 
    КонецЦикла; 
    
    Возврат Результат;

КонецФункции

&НаКлиенте
Процедура ОповещениеСохранитьИЗакрыть(Ответ, ПараметрыОповещения) Экспорт
    
    ВыполнитьЗаписьИЗакрытьФорму();
    
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗаписьИЗакрытьФорму()
	
    Если ЗаписатьНаСервере() Тогда
        Модифицированность = Ложь;
        ПараметрыЗакрытия = Новый Структура("Результат, Код", Истина, Код);
        Закрыть(ПараметрыЗакрытия);
    КонецЕсли;
	
КонецПроцедуры
 

#КонецОбласти 

