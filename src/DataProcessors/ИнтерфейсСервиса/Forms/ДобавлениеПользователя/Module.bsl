
#Область ОбработчикиСобытийФормы
    
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
    
    Обработки.ИнтерфейсСервиса.ПроверитьВерсиюИнтерфейса(5, Отказ);
    Если Отказ Тогда
        Возврат;
    КонецЕсли; 
    
    ДоступКПриложениям.Загрузить(ПрограммныйИнтерфейсСервиса.Приложения());
    РольПользователя = Перечисления.РолиПользователейАбонентов.ПользовательАбонента;
    ЧасовойПояс = ЧасовойПоясСеанса();
    
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
    
    Если РольПользователя = Перечисления.РолиПользователейАбонентов.ВладелецАбонента Тогда
        ПроверяемыеРеквизиты.Добавить("Почта");
    КонецЕсли; 
    
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовШапкиФормы
    
&НаКлиенте
Процедура ПочтаПриИзменении(Элемент)
    Если Не ЗначениеЗаполнено(Логин) Тогда
        Логин = Почта;
    КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура ПарольНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
    
    СтандартнаяОбработка = Ложь;
    Элементы.ПарольСкрытый.Видимость = Истина;
    Элементы.Пароль.Видимость = Не Элементы.ПарольСкрытый.Видимость;
    
КонецПроцедуры

&НаКлиенте
Процедура ПарольСкрытыйНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
    
    СтандартнаяОбработка = Ложь;
    Элементы.ПарольСкрытый.Видимость = Ложь;
    Элементы.Пароль.Видимость = Не Элементы.ПарольСкрытый.Видимость;
    
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы
    
&НаКлиенте
Процедура Готово(Команда)
    
    Если ГотовоНаСервере() Тогда
        ПараметрыВозврата = Новый Структура("Результат, Логин", Истина, Логин);
        Закрыть(ПараметрыВозврата);
    КонецЕсли;
    
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
    Закрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
    
&НаСервере
Функция ГотовоНаСервере()
    
    ПараметрыСоздания = ПрограммныйИнтерфейсСервиса.НовыйПараметрыСозданияПользователя();
    ПараметрыСоздания.Логин = Логин;
    ПараметрыСоздания.Пароль = Пароль;
    ПараметрыСоздания.ПочтаОбязательна = ЗначениеЗаполнено(Почта);
    ПараметрыСоздания.Почта = Почта;
    ПараметрыСоздания.РольПользователя = РольПользователя;
    ПараметрыСоздания.ПолноеИмя = ПолноеИмя;
    ПараметрыСоздания.Телефон = Телефон;
    ПараметрыСоздания.ЧасовойПояс = ЧасовойПоясСеанса();
    
    ПользовательСоздан = ПрограммныйИнтерфейсСервиса.СоздатьПользователяАбонента(ПараметрыСоздания);
    
    ПраваНазначены = Ложь;
    Если ПользовательСоздан Тогда
        Для Каждого Строка Из ДоступКПриложениям Цикл
            Если ЗначениеЗаполнено(Строка.Право) Тогда
                ПараметрыДобавления = ПрограммныйИнтерфейсСервиса.НовыйПараметрыДобавленияПользователяВПриложение();
                ПараметрыДобавления.КодПриложения = Строка.Код;
                ПараметрыДобавления.Логин = Логин;
                ПараметрыДобавления.Право = Строка.Право;
                ПраваНазначены = ПрограммныйИнтерфейсСервиса.ДобавитьПользователяВПриложение(ПараметрыДобавления);
                Если Не ПраваНазначены Тогда
                    Прервать;
                КонецЕсли; 
            КонецЕсли; 
        КонецЦикла; 
    КонецЕсли; 
    
    Возврат ПользовательСоздан И ПраваНазначены;
    
КонецФункции

#КонецОбласти 
