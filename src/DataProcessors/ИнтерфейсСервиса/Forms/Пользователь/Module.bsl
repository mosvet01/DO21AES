
#Область ОбработчикиСобытийФормы
    
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
    
    Обработки.ИнтерфейсСервиса.ПроверитьВерсиюИнтерфейса(5, Отказ);
    Если Отказ Тогда
        Возврат;
    КонецЕсли; 
    СвойстваПользователя = ПрограммныйИнтерфейсСервиса.СвойстваПользователяАбонента(Параметры.Логин);
    ЗаполнитьЗначенияСвойств(ЭтотОбъект, СвойстваПользователя, "Логин, ПолноеИмя, Почта, РольПользователя");
    Заголовок = ПолноеИмя;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
    
    Если Модифицированность Тогда
        Оповещение = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтаФорма);
        ТекстПредупреждения = "";
        ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(Оповещение, Отказ, ЗавершениеРаботы,, ТекстПредупреждения);
    КонецЕсли;
    
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы
    
&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
    
    Если ЗаписатьНаСервере() Тогда
        Модифицированность = Ложь;
        ПараметрыВозврата = Новый Структура("Результат, Логин", Истина, Логин);
        Закрыть(ПараметрыВозврата);
    КонецЕсли;
    
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
    
    Модифицированность = Не ЗаписатьНаСервере();
    
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции
    
&НаСервере
Функция ЗаписатьНаСервере()
    
    Возврат ПрограммныйИнтерфейсСервиса.УстановитьРольПользователяАбонента(Логин, РольПользователя);
    
КонецФункции

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Ответ, ПараметрыОповещения) Экспорт
	
    Если ЗаписатьНаСервере() Тогда
        Модифицированность = Ложь;
        Закрыть();
    КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 


