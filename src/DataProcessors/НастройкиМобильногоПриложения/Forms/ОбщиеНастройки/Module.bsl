
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.Свойство("ТекущийПользователь", ТекущийПользователь);
	
	Если Не ЗначениеЗаполнено(ТекущийПользователь) Тогда
		ТекущийПользователь = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
	ЗагрузитьНастройки();

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Заголовок = СтрШаблон("%1: %2", Заголовок, ТекущийПользователь);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ФильтроватьПередаваемыеНаКлиентФайлыПриИзменении(Элемент)

	Элементы.ФорматыПередаваемыхФайлов.Доступность = ФильтроватьПередаваемыеНаКлиентФайлы;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)

	Если Не ЗначениеЗаполнено(ТекущийПользователь) Тогда

		Сообщение = Новый СообщениеПользователю();
		Сообщение.Поле  = "ТекущийПользователь";
		Сообщение.Текст = "Не выбран пользователь";
		Сообщение.Сообщить();

		Возврат;

	КонецЕсли;

	ЗаписатьОбщиеНастройкиСинхронизации();
	ЗаписатьНастройкиПередачиФайлов();

	Закрыть(НастройкиИзменены);

КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбщиеНастройкиСинхронизации

&НаСервере
Процедура ЗагрузитьНастройки()

	ЗагрузитьОбщиеНастройкиСинхронизации();
	ЗагрузитьНастройкиПередачиФайлов();

КонецПроцедуры 

&НаСервере
Процедура ЗагрузитьОбщиеНастройкиСинхронизации()

	МаксимальныйРазмерПередаваемогоФайла = 
		РегистрыСведений.ОбменСМобильнымиНастройкиПользователей.ПолучитьНастройку(
			ТекущийПользователь,
			Перечисления.ОбменСМобильнымиТипыНастроекПользователей.МаксимальныйРазмерФайлов);

	МаксимальныйРазмерПередаваемогоФайлаПриОткрытии = МаксимальныйРазмерПередаваемогоФайла;
	
	СрокУстареванияДанных = РегистрыСведений.ОбменСМобильнымиНастройкиПользователей.ПолучитьНастройку(
		ТекущийПользователь,
		Перечисления.ОбменСМобильнымиТипыНастроекПользователей.СрокУстареванияДанных);

	СрокУстареванияДанныхПриОткрытии = СрокУстареванияДанных;

	ПодробныйПротоколОбменаСУстройством = РегистрыСведений.ОбменСМобильнымиНастройкиПользователей.ПолучитьНастройку(
		ТекущийПользователь,
		Перечисления.ОбменСМобильнымиТипыНастроекПользователей.ПодробныйПротоколОбменаСМобильнымУстройством);

	ПодробныйПротоколОбменаСУстройствомПриОткрытии = ПодробныйПротоколОбменаСУстройством;

КонецПроцедуры 

&НаСервере
Процедура ЗаписатьОбщиеНастройкиСинхронизации()
	
	НастройкиИзменены = Ложь;
	
	РегистрыСведений.ОбменСМобильнымиНастройкиПользователей.ЗаписатьНастройку(
		ТекущийПользователь,
		Перечисления.ОбменСМобильнымиТипыНастроекПользователей.МаксимальныйРазмерФайлов,
		МаксимальныйРазмерПередаваемогоФайла);

	Если МаксимальныйРазмерПередаваемогоФайлаПриОткрытии <> МаксимальныйРазмерПередаваемогоФайла Тогда
		РегистрыСведений.ИзмененныеНастройкиСинхронизацииСМобильнымКлиентом.ДобавитьЗапись(
			ТекущийПользователь,
			Перечисления.ВидыНастроекОбменаСМобильнымКлиентом.МаксимальныйРазмерФайла);
		НастройкиИзменены = Истина;
	КонецЕсли;

	РегистрыСведений.ОбменСМобильнымиНастройкиПользователей.ЗаписатьНастройку(
		ТекущийПользователь,
		Перечисления.ОбменСМобильнымиТипыНастроекПользователей.СрокУстареванияДанных,
		СрокУстареванияДанных);

	Если СрокУстареванияДанных <> СрокУстареванияДанныхПриОткрытии Тогда
		// добавление записи об изменившейся настройке срока устаревания данных
		РегистрыСведений.ИзмененныеНастройкиСинхронизацииСМобильнымКлиентом.ДобавитьЗапись(
			ТекущийПользователь,
			Перечисления.ВидыНастроекОбменаСМобильнымКлиентом.СрокУстареванияДанных);
		НастройкиИзменены = Истина;
		
	КонецЕсли;

	Если ФильтроватьПередаваемыеНаКлиентФайлы <> ФильтроватьПередаваемыеНаКлиентФайлыПриОткрытии Тогда
		// добавление записи об изменившейся настройке срока устаревания данных
		РегистрыСведений.ИзмененныеНастройкиСинхронизацииСМобильнымКлиентом.ДобавитьЗапись(
			ТекущийПользователь,
			Перечисления.ВидыНастроекОбменаСМобильнымКлиентом.СрокУстареванияДанных);
		НастройкиИзменены = Истина;
	КонецЕсли;
	
	РегистрыСведений.ОбменСМобильнымиНастройкиПользователей.ЗаписатьНастройку(
		ТекущийПользователь,
		Перечисления.ОбменСМобильнымиТипыНастроекПользователей.ПодробныйПротоколОбменаСМобильнымУстройством,
		ПодробныйПротоколОбменаСУстройством);

КонецПроцедуры

#КонецОбласти

#Область НастройкиСинхронизацииФайлов

&НаСервере
Процедура ЗагрузитьНастройкиПередачиФайлов()

	ФильтроватьПередаваемыеНаКлиентФайлы = РегистрыСведений.ОбменСМобильнымиНастройкиПользователей.ПолучитьНастройку(
			ТекущийПользователь,
			Перечисления.ОбменСМобильнымиТипыНастроекПользователей.ОграничениеФорматовПередаваемыхФайлов);
	ФильтроватьПередаваемыеНаКлиентФайлыПриОткрытии = ФильтроватьПередаваемыеНаКлиентФайлы;
	
	ФорматыПередаваемыхФайлов = ВРег(РегистрыСведений.ОбменСМобильнымиНастройкиПользователей.ПолучитьНастройку(
			ТекущийПользователь,
			Перечисления.ОбменСМобильнымиТипыНастроекПользователей.ФорматыПередаваемыхФайлов));
			
	ФорматыПередаваемыхФайловПриОткрытии = ФорматыПередаваемыхФайлов;
	
	Элементы.ФорматыПередаваемыхФайлов.Доступность = ФильтроватьПередаваемыеНаКлиентФайлы;

КонецПроцедуры

&НаСервере
Процедура ЗаписатьНастройкиПередачиФайлов()

	Если ФильтроватьПередаваемыеНаКлиентФайлы <> ФильтроватьПередаваемыеНаКлиентФайлыПриОткрытии Тогда
		
		РегистрыСведений.ОбменСМобильнымиНастройкиПользователей.ЗаписатьНастройку(
			ТекущийПользователь,
			Перечисления.ОбменСМобильнымиТипыНастроекПользователей.ОграничениеФорматовПередаваемыхФайлов,
			ФильтроватьПередаваемыеНаКлиентФайлы);
			
		РегистрыСведений.ИзмененныеНастройкиСинхронизацииСМобильнымКлиентом.ДобавитьЗапись(
			ТекущийПользователь,
			Перечисления.ОбменСМобильнымиТипыНастроекПользователей.ОграничениеФорматовПередаваемыхФайлов);
			
	КонецЕсли;

	Если ФорматыПередаваемыхФайлов <> ФорматыПередаваемыхФайловПриОткрытии Тогда
		
		РегистрыСведений.ОбменСМобильнымиНастройкиПользователей.ЗаписатьНастройку(
			ТекущийПользователь,
			Перечисления.ОбменСМобильнымиТипыНастроекПользователей.ФорматыПередаваемыхФайлов,
			ФорматыПередаваемыхФайлов);
			
		РегистрыСведений.ИзмененныеНастройкиСинхронизацииСМобильнымКлиентом.ДобавитьЗапись(
			ТекущийПользователь,
			Перечисления.ОбменСМобильнымиТипыНастроекПользователей.ФорматыПередаваемыхФайлов);
			
	КонецЕсли;

КонецПроцедуры 

#КонецОбласти

#КонецОбласти

