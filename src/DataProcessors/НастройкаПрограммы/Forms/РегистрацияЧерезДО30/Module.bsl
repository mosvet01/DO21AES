#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВладелецХранилища = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
		Метаданные.Константы.ИмяПользователя1СДО30);
	УстановитьПривилегированныйРежим(Истина);
	ПарольПользователяВебСервиса = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(
		ВладелецХранилища);
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ВладелецХранилища = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(
		Метаданные.Константы.ИмяПользователя1СДО30);
	УстановитьПривилегированныйРежим(Истина);
	ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(ВладелецХранилища, ПарольПользователяВебСервиса);
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.Свойство("Закрыть") Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИнтеграцияС1САрхивомПриИзменении(Элемент)
	
	УстановитьДоступность();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаЗаписатьИЗакрыть(Команда)
	
	ПараметрыЗаписи = Новый Структура;
	ПараметрыЗаписи.Вставить("Закрыть", Истина);
	Записать(ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВебСервис(Команда)
	
	ПроверитьВебСервисНаСервере();
	ПоказатьПредупреждение(, НСтр("ru = 'Подключение выполнено'"));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьДоступность()
	
	Элементы.ГруппаСдвигВправо.Доступность = НаборКонстант.РегистрацияВ1СДО30;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьВебСервисНаСервере()
	
	АдресПубликации = НаборКонстант.АдресПубликации1СДО30;
	ИмяПользователя = НаборКонстант.ИмяПользователя1СДО30;
	Пароль = ПарольПользователяВебСервиса;
	
	Определение = Новый WSОпределения(АдресПубликации + "/ws/DMReg.1cws?wsdl", ИмяПользователя, Пароль);
	Прокси = Новый WSПрокси(Определение, "http://www.1c.ru/DMReg", "DMReg", "DMRegSoap");
	
КонецПроцедуры

#КонецОбласти
