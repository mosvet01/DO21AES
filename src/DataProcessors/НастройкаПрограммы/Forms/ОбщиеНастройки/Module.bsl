#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПолнотекстовыйПоискИзменен = Ложь;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьБизнесСобытия") Тогда 
		Элементы.ГруппаСрокХраненияБизнесСобытий.Доступность = Ложь;
		Элементы.ИспользоватьУведомления.Доступность = Ложь;
	КонецЕсли;
	
	Элементы.НастроитьУведомления.Доступность = НаборКонстант.ИспользоватьУведомления;
	Элементы.ИспользоватьБронированиеПомещений.Доступность = НаборКонстант.ИспользоватьСхемыПомещений;
	Элементы.НастроитьВерсионирование.Доступность = НаборКонстант.ИспользоватьВерсионированиеОбъектов;
	
	ЭтоФайловаяБаза = ОбщегоНазначения.ИнформационнаяБазаФайловая();
	
	ИспользоватьРабочийКалендарь = НаборКонстант.ИспользоватьРабочийКалендарь;
	ИспользоватьУправлениеМероприятиями = НаборКонстант.ИспользоватьУправлениеМероприятиями;
	ИспользоватьОтсутствия = НаборКонстант.ИспользоватьОтсутствия;
	Элементы.ИспользоватьНапоминанияПользователя.Доступность = НаборКонстант.ИспользоватьРабочийКалендарь;
	НастроитьЭлементИспользоватьСинхронизациюКалендарей(
		Элементы.ГруппаИспользоватьСинхронизациюКалендарей,
		Элементы.ГруппаИспользоватьСинхронизациюКалендарейНастроить,
		НаборКонстант.ИспользоватьРабочийКалендарь,
		НаборКонстант.ИспользоватьСинхронизациюКалендарей);
	ИспользоватьРаздельноеИсполнениеПунктовПротоколаМероприятия =
		НаборКонстант.ИспользоватьРаздельноеИсполнениеПунктовПротоколаМероприятия;
	Элементы.ИспользоватьРаздельноеИсполнениеПунктовПротоколаМероприятия.Доступность =
		НаборКонстант.ИспользоватьУправлениеМероприятиями;

	// Инструкции
	Элементы.ЗагрузитьТиповыеИнструкции.Доступность = НаборКонстант.ИспользоватьИнструкции;
	
	// ЭП
	Элементы.НастроитьЭП.Доступность = НаборКонстант.ИспользоватьЭлектронныеПодписи;
	Элементы.МашиночитаемыеДоверенности.Доступность = НаборКонстант.ИспользоватьЭлектронныеПодписи;
	Элементы.СертификатыИКриптопровайдеры.Доступность = НаборКонстант.ИспользоватьЭлектронныеПодписи
		Или НаборКонстант.ИспользоватьШифрование;
		
	// Защита персональных данных	
	Элементы.СобытияДоступаКПерсональнымДанным.Доступность =
		НаборКонстант.ИспользоватьУчетДоступаКПерсональнымДанным;
	
	Элементы.НастройкиРегистрацииСобытийДоступаКПерсональнымДанным.Доступность =
		НаборКонстант.ИспользоватьУчетДоступаКПерсональнымДанным;
		
	Элементы.НастройкиЧатБота.Доступность = НаборКонстант.ИспользоватьЧатБота;
	ЧатБотПриОткрытии = Константы.ЧатБот.Получить();
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		Элементы.ОбсужденияПодключены.Доступность = Ложь;
		Элементы.ПараметрыПрокси.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ТекущийЭлемент = Элементы.ПараметрыПрокси;
	
	ПриИзмененииСостоянияПодключенияОбсуждений();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	// Включен рабочий календарь и управление мероприятиями
	ВключенРабочийКалендарь = Не ИспользоватьРабочийКалендарь И НаборКонстант.ИспользоватьРабочийКалендарь;
	ВключеноУправлениеМероприятиями = Не ИспользоватьУправлениеМероприятиями И НаборКонстант.ИспользоватьУправлениеМероприятиями;
	Если НаборКонстант.ИспользоватьРабочийКалендарь И НаборКонстант.ИспользоватьУправлениеМероприятиями
		И (ВключенРабочийКалендарь Или ВключеноУправлениеМероприятиями) Тогда
		
		Состояние(НСтр("ru = 'Выполняется заполнение рабочего календаря мероприятиями. Пожалуйста, подождите...'"));
		ЗаполнитьРабочийКалендарьМероприятиями();
		Состояние(НСтр("ru = 'Рабочий календарь заполнен мероприятиями.'"));
		
	КонецЕсли;
	
	// Включен рабочий календарь и управление отсутствия.
	ВключенРабочийКалендарь = Не ИспользоватьРабочийКалендарь И НаборКонстант.ИспользоватьРабочийКалендарь;
	ВключеныОтсутствия = Не ИспользоватьОтсутствия И НаборКонстант.ИспользоватьОтсутствия;
	Если НаборКонстант.ИспользоватьРабочийКалендарь И НаборКонстант.ИспользоватьОтсутствия
		И (ВключенРабочийКалендарь Или ВключеныОтсутствия) Тогда
		
		Состояние(НСтр("ru = 'Выполняется заполнение рабочего календаря отсутствиями. Пожалуйста, подождите...'"));
		ЗаполнитьРабочийКалендарьОтсутствиями();
		Состояние(НСтр("ru = 'Рабочий календарь заполнен отсутствиями.'"));
		
	КонецЕсли;
	
	// Включено раздельное исполнение пунктов протокола мероприятий
	Если Не ИспользоватьРаздельноеИсполнениеПунктовПротоколаМероприятия
		И НаборКонстант.ИспользоватьРаздельноеИсполнениеПунктовПротоколаМероприятия Тогда
		
		Состояние(НСтр("ru = 'Выполняется заполнение состояний протоколов мероприятий. Пожалуйста, подождите...'"));
		ЗаполнитьСостоянияПротоколовМероприятий();
		Состояние(НСтр("ru = 'Состояния протоколов мероприятий заполнены.'"));
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ПараметрыЗаписи.Вставить("ИспользоватьЧатБота", Константы.ИспользоватьЧатБота.Получить());
	ИспользоватьБизнесСобытияВключено = ТекущийОбъект.ИспользоватьБизнесСобытия И НЕ Константы.ИспользоватьБизнесСобытия.Получить();
	
	ХранитьФайлыВТомахНаДиске = Константы.ХранитьФайлыВТомахНаДиске.Получить();
	
	ПолнотекстовыйПоискИзменен = (ТекущийОбъект.ИспользоватьПолнотекстовыйПоиск <> Константы.ИспользоватьПолнотекстовыйПоиск.Получить());
	
	// Регистрация событий доступа к персональным данным
	СтароеЗначение = Константы.ИспользоватьУчетДоступаКПерсональнымДанным.Получить();
	НовоеЗначение = НаборКонстант.ИспользоватьУчетДоступаКПерсональнымДанным;
	Если СтароеЗначение <> НовоеЗначение Тогда
		
		Если НовоеЗначение Тогда
			
			Запрос = Новый Запрос(
				"ВЫБРАТЬ
				|	ОбластиПерсональныхДанных.ИмяОбласти
				|ИЗ
				|	РегистрСведений.ОбластиПерсональныхДанных КАК ОбластиПерсональныхДанных
				|ГДЕ
				|	ОбластиПерсональныхДанных.ИспользованиеСобытийЖурналаРегистрации");
				
			ВыбранныеОбласти = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ИмяОбласти");
			ЗащитаПерсональныхДанных.УстановитьИспользованиеСобытияДоступ(Истина, ВыбранныеОбласти);
			
		Иначе
			
			ЗащитаПерсональныхДанных.УстановитьИспользованиеСобытияДоступ(Ложь);
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Приглашение на мероприятие
	ТекущийОбъект.ИспользоватьПриглашениеНаМероприятие =
		ТекущийОбъект.ИспользоватьБизнесПроцессыИЗадачи И 
		ТекущийОбъект.ИспользоватьУправлениеМероприятиями;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ОбновитьПовторноИспользуемыеЗначения();
	ОбновитьИнтерфейс();
	
	Если ПараметрыЗаписи.Свойство("ИспользоватьЧатБота")
		И ПараметрыЗаписи.ИспользоватьЧатБота 
			<> НаборКонстант.ИспользоватьЧатБота Тогда
			
		Оповестить("ИзменилосьИспользованиеЧатБота", НаборКонстант.ИспользоватьЧатБота);
	
	КонецЕсли;
	
	Если ПолнотекстовыйПоискИзменен Тогда
		ПолнотекстовыйПоискИзменен = Ложь;
		Оповестить("ПолнотекстовыйПоискИзменен");
	КонецЕсли;
	
	Если ИспользоватьБизнесСобытияВключено Тогда
		
		Если СтандартныеПодсистемыКлиент.ПараметрыРаботыКлиента().ИнформационнаяБазаФайловая Тогда
			Если Не ПараметрыЗаписи.Свойство("ПоказаноПредупреждение4") Тогда
				ОписаниеОповещения = Новый ОписаниеОповещения(
					"ПослеЗаписиПродолжение4",
					ЭтотОбъект,
					ПараметрыЗаписи);
				ПоказатьПредупреждение(
					ОписаниеОповещения,
					НСтр("ru = 'Включено использование бизнес-событий.
                          |В режиме файловой информационной базы регламентное задание
                          |""Обработка произошедших бизнес-событий""
                          |необходимо выполнять вручную через консоль регламентных заданий
                          |(например, запустив отдельный сеанс для выполнения регламентных заданий).'"));
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПараметрыЗаписи.Свойство("Закрыть") Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписиПродолжение1(ПараметрыЗаписи) Экспорт
	
	ПараметрыЗаписи.Вставить("ПоказаноПредупреждение1", Истина);
	ПослеЗаписи(ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписиПродолжение4(ПараметрыЗаписи) Экспорт
	
	ПараметрыЗаписи.Вставить("ПоказаноПредупреждение4", Истина);
	ПослеЗаписи(ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если ПолнотекстовыйПоискИзменен 
		И Не ОбщегоНазначения.РазделениеВключено() Тогда
		Разрешен = Константы.ИспользоватьПолнотекстовыйПоиск.Получить();
		
		Если Разрешен Тогда
			ПолнотекстовыйПоиск.УстановитьРежимПолнотекстовогоПоиска(РежимПолнотекстовогоПоиска.Разрешить);
		Иначе
			ПолнотекстовыйПоиск.УстановитьРежимПолнотекстовогоПоиска(РежимПолнотекстовогоПоиска.Запретить);
		КонецЕсли;
			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбсужденияПодключены" Тогда 
		ПриИзмененииСостоянияПодключенияОбсуждений(Параметр);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИспользоватьРаздельноеИсполнениеПунктовПротоколаМероприятияПриИзменении(Элемент)
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ИспользоватьРаздельноеИсполнениеПунктовПротоколаМероприятияПриИзмененииПродолжение",
		ЭтотОбъект,
		Новый Структура("Отказ", Ложь));
	
	ВключенРабочийКалендарь = Не ИспользоватьРабочийКалендарь И НаборКонстант.ИспользоватьРабочийКалендарь;
	ВключеноУправлениеМероприятиями = Не ИспользоватьУправлениеМероприятиями И НаборКонстант.ИспользоватьУправлениеМероприятиями;
	Если Не ИспользоватьРаздельноеИсполнениеПунктовПротоколаМероприятия
		И НаборКонстант.ИспользоватьРаздельноеИсполнениеПунктовПротоколаМероприятия Тогда
		
		Текст = НСтр("ru = 'Включено использование раздельного исполнения пунктов протокола мероприятия.
					|Для работы необходимо заполнить состояние протоколов мероприятий.
					|При сохранении настроек будет произведено заполнение состояний протоколов мероприятий. Продолжить?'");
		
		ОбщегоНазначенияДокументооборотКлиент.ПоказатьВопросДаНет(ОписаниеОповещения, Текст);
	Иначе 
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьРаздельноеИсполнениеПунктовПротоколаМероприятияПриИзмененииПродолжение(Результат, Параметры) Экспорт
	
	Отказ = Параметры.Отказ;
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Если Отказ Тогда
		НаборКонстант.ИспользоватьРаздельноеИсполнениеПунктовПротоколаМероприятия =
			Не НаборКонстант.ИспользоватьРаздельноеИсполнениеПунктовПротоколаМероприятия;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьУправлениеМероприятиямиПриИзменении(Элемент)
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ИспользоватьУправлениеМероприятиямиПриИзмененииПродолжение",
		ЭтотОбъект,
		Новый Структура("Отказ", Ложь));
	
	ВключенРабочийКалендарь = Не ИспользоватьРабочийКалендарь И НаборКонстант.ИспользоватьРабочийКалендарь;
	ВключеноУправлениеМероприятиями = Не ИспользоватьУправлениеМероприятиями И НаборКонстант.ИспользоватьУправлениеМероприятиями;
	Если НаборКонстант.ИспользоватьРабочийКалендарь И НаборКонстант.ИспользоватьУправлениеМероприятиями
		И (ВключенРабочийКалендарь Или ВключеноУправлениеМероприятиями) Тогда
		
		Текст = НСтр("ru = 'Включено использование рабочего календаря и управление мероприятиями. 
                      |Для работы необходимо заполнить календарь данными мероприятий.
                      |При сохранении настроек будет произведено заполнение календаря. Продолжить?'");
		
		ПоказатьВопрос(ОписаниеОповещения, Текст, РежимДиалогаВопрос.ДаНет);
	Иначе 
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьУправлениеМероприятиямиПриИзмененииПродолжение(Результат, Параметры) Экспорт 
	
	Отказ = Параметры.Отказ;
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Если Отказ Тогда
		НаборКонстант.ИспользоватьУправлениеМероприятиями = Не НаборКонстант.ИспользоватьУправлениеМероприятиями;
		Возврат;
	КонецЕсли;
	
	Элементы.ИспользоватьРаздельноеИсполнениеПунктовПротоколаМероприятия.Доступность =
		НаборКонстант.ИспользоватьУправлениеМероприятиями;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьУведомленияПриИзменении(Элемент)
	
	Элементы.НастроитьУведомления.Доступность = НаборКонстант.ИспользоватьУведомления;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если НаборКонстант.ИспользоватьБизнесСобытия Тогда
		
		Если НаборКонстант.СрокХраненияБизнесСобытий = 0 Тогда
			
			ТекстОшибки = НСтр("ru = 'Укажите срок хранения бизнес-событий'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, , "НаборКонстант.СрокХраненияБизнесСобытий", , Отказ);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьБизнесСобытияПриИзменении(Элемент)
	
	Элементы.ГруппаСрокХраненияБизнесСобытий.Доступность = НаборКонстант.ИспользоватьБизнесСобытия;
	Элементы.ИспользоватьУведомления.Доступность = НаборКонстант.ИспользоватьБизнесСобытия;
	
	Если Не НаборКонстант.ИспользоватьБизнесСобытия Тогда
		НаборКонстант.ИспользоватьУведомления = Ложь;
		Элементы.НастроитьУведомления.Доступность = НаборКонстант.ИспользоватьУведомления;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьРабочийКалендарьПриИзменении(Элемент)
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ИспользоватьРабочийКалендарьПриИзмененииПродолжение",
		ЭтотОбъект,
		Новый Структура("Отказ", Ложь));
	
	ВключенРабочийКалендарь = Не ИспользоватьРабочийКалендарь И НаборКонстант.ИспользоватьРабочийКалендарь;
	ВключеноУправлениеМероприятиями = Не ИспользоватьУправлениеМероприятиями И НаборКонстант.ИспользоватьУправлениеМероприятиями;
	Если НаборКонстант.ИспользоватьРабочийКалендарь И НаборКонстант.ИспользоватьУправлениеМероприятиями
		И (ВключенРабочийКалендарь Или ВключеноУправлениеМероприятиями) Тогда
		
		Текст = НСтр("ru = 'Включено использование рабочего календаря и управление мероприятиями. 
                      |Для работы необходимо заполнить календарь данными мероприятий.
                      |При сохранении настроек будет произведено заполнение календаря. Продолжить?'");
		
		ПоказатьВопрос(ОписаниеОповещения, Текст, РежимДиалогаВопрос.ДаНет);
	Иначе
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьРабочийКалендарьПриИзмененииПродолжение(Результат, Параметры) Экспорт 
	
	Отказ = Параметры.Отказ;
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Отказ = Истина;
	КонецЕсли;

	Если Отказ Тогда
		НаборКонстант.ИспользоватьРабочийКалендарь = Не НаборКонстант.ИспользоватьРабочийКалендарь;
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ИспользоватьРабочийКалендарьПриИзмененииЗавершение",
		ЭтотОбъект,
		Новый Структура("Отказ", Ложь));
	
	ВключенРабочийКалендарь = Не ИспользоватьРабочийКалендарь И НаборКонстант.ИспользоватьРабочийКалендарь;
	ВключеныОтсутствия = Не ИспользоватьОтсутствия И НаборКонстант.ИспользоватьОтсутствия;
	Если НаборКонстант.ИспользоватьРабочийКалендарь И НаборКонстант.ИспользоватьОтсутствия
		И (ВключенРабочийКалендарь Или ВключеныОтсутствия) Тогда
		
		Текст = НСтр("ru = 'Включено использование рабочего календаря и отсутствий. 
                      |Для работы необходимо заполнить календарь данными отсутствий.
                      |При сохранении настроек будет произведено заполнение календаря. Продолжить?'");
		
		ПоказатьВопрос(ОписаниеОповещения, Текст, РежимДиалогаВопрос.ДаНет);
	Иначе
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьРабочийКалендарьПриИзмененииЗавершение(Результат, Параметры) Экспорт 
	
	Отказ = Параметры.Отказ;
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Если Отказ Тогда
		НаборКонстант.ИспользоватьРабочийКалендарь = Не НаборКонстант.ИспользоватьРабочийКалендарь;
	Иначе
		НаборКонстант.ИспользоватьНапоминанияПользователя = НаборКонстант.ИспользоватьРабочийКалендарь;
		Элементы.ИспользоватьНапоминанияПользователя.Доступность = НаборКонстант.ИспользоватьРабочийКалендарь;
		Если Не НаборКонстант.ИспользоватьРабочийКалендарь Тогда
			НаборКонстант.ИспользоватьСинхронизациюКалендарей = Ложь;
		КонецЕсли;
		НастроитьЭлементИспользоватьСинхронизациюКалендарей(
			Элементы.ГруппаИспользоватьСинхронизациюКалендарей,
			Элементы.ГруппаИспользоватьСинхронизациюКалендарейНастроить,
			НаборКонстант.ИспользоватьРабочийКалендарь,
			НаборКонстант.ИспользоватьСинхронизациюКалендарей);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьИнструкцииПриИзменении(Элемент)
	
	Элементы.ЗагрузитьТиповыеИнструкции.Доступность = НаборКонстант.ИспользоватьИнструкции;

КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьЭлектронноЦифровыеПодписиПриИзменении(Элемент)
	
	Элементы.НастроитьЭП.Доступность = НаборКонстант.ИспользоватьЭлектронныеПодписи;
	Элементы.МашиночитаемыеДоверенности.Доступность = НаборКонстант.ИспользоватьЭлектронныеПодписи;
	Элементы.СертификатыИКриптопровайдеры.Доступность = НаборКонстант.ИспользоватьЭлектронныеПодписи
		Или НаборКонстант.ИспользоватьШифрование;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьШифрованиеПриИзменении(Элемент)
	
	Элементы.НастроитьЭП.Доступность = НаборКонстант.ИспользоватьЭлектронныеПодписи;
	Элементы.МашиночитаемыеДоверенности.Доступность = НаборКонстант.ИспользоватьЭлектронныеПодписи;
	Элементы.СертификатыИКриптопровайдеры.Доступность = НаборКонстант.ИспользоватьЭлектронныеПодписи
		Или НаборКонстант.ИспользоватьШифрование;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьСхемыПомещенийПриИзменении(Элемент)
	
	Элементы.ИспользоватьБронированиеПомещений.Доступность = НаборКонстант.ИспользоватьСхемыПомещений;
	
	Если Не НаборКонстант.ИспользоватьСхемыПомещений Тогда
		НаборКонстант.ИспользоватьБронированиеПомещений = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьВерсионированиеНажатие(Элемент)
	
	ОткрытьФорму("РегистрСведений.НастройкиВерсионированияОбъектов.Форма.НастройкиХраненияИстории");
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьВерсионированиеОбъектовПриИзменении(Элемент)
	
	Элементы.НастроитьВерсионирование.Доступность = НаборКонстант.ИспользоватьВерсионированиеОбъектов;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьУчетДоступаКПерсональнымДаннымПриИзменении(Элемент)
	
	Элементы.СобытияДоступаКПерсональнымДанным.Доступность =
		НаборКонстант.ИспользоватьУчетДоступаКПерсональнымДанным;
	
	Элементы.НастройкиРегистрацииСобытийДоступаКПерсональнымДанным.Доступность =
		НаборКонстант.ИспользоватьУчетДоступаКПерсональнымДанным;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьОтсутствияПриИзменении(Элемент)
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ИспользоватьОтсутствияПриИзмененииПродолжение",
		ЭтотОбъект,
		Новый Структура("Отказ", Ложь));
	
	ВключенРабочийКалендарь = Не ИспользоватьРабочийКалендарь И НаборКонстант.ИспользоватьРабочийКалендарь;
	ВключеныОтсутствия = Не ИспользоватьОтсутствия И НаборКонстант.ИспользоватьОтсутствия;
	Если НаборКонстант.ИспользоватьРабочийКалендарь И НаборКонстант.ИспользоватьОтсутствия
		И (ВключенРабочийКалендарь Или ВключеныОтсутствия) Тогда
		
		Текст = НСтр("ru = 'Включено использование рабочего календаря и отсутствий. 
                      |Для работы необходимо заполнить календарь данными отсутствий.
                      |При сохранении настроек будет произведено заполнение календаря. Продолжить?'");
		
		ПоказатьВопрос(ОписаниеОповещения, Текст, РежимДиалогаВопрос.ДаНет);
	Иначе
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьОтсутствияПриИзмененииПродолжение(Результат, Параметры) Экспорт 

	Отказ = Параметры.Отказ;
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Отказ = Истина;
	КонецЕсли;

	Если Отказ Тогда
		НаборКонстант.ИспользоватьОтсутствия = Не НаборКонстант.ИспользоватьОтсутствия;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбсужденияПодключеныПриИзменении(Элемент)
	
	ОбсужденияПодключены = Не ОбсужденияПодключены;
	Если ОбсужденияСлужебныйКлиент.Подключены() Тогда
		ОбсужденияСлужебныйКлиент.ПоказатьОтключение();
	Иначе 
		ОбсужденияСлужебныйКлиент.ПоказатьПодключение();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьСинхронизациюКалендарейПриИзменении(Элемент, ИспользоватьРабочийКалендарь, ИспользоватьСинхронизациюКалендарей)
	
	НастроитьЭлементИспользоватьСинхронизациюКалендарей(
		Элементы.ГруппаИспользоватьСинхронизациюКалендарей,
		Элементы.ГруппаИспользоватьСинхронизациюКалендарейНастроить,
		НаборКонстант.ИспользоватьРабочийКалендарь,
		НаборКонстант.ИспользоватьСинхронизациюКалендарей);
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьЧатБотаПриИзменении(Элемент)
	
	Элементы.НастройкиЧатБота.Доступность = НаборКонстант.ИспользоватьЧатБота;
	
	Если НаборКонстант.ИспользоватьЧатБота И Не ЗначениеЗаполнено(ЧатБотПриОткрытии) Тогда
		Состояние(НСтр("ru = 'Создаем пользователя чат-бота. Пожалуйста, подождите...'"));
		ПользовательСсылка = СоздатьПользователя("Ася (бот в помощь)");
		Записать();
		ЧатБотПриОткрытии = ПользовательСсылка;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НастройкиЭП(Команда)
	
	Записать();
	ОткрытьФорму("Обработка.НастройкаПрограммы.Форма.НастройкиЭлектронныхПодписей");
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатыИКриптопровайдеры(Команда)
	
	Записать();
	ЭлектроннаяПодписьКлиент.ОткрытьНастройкиЭлектроннойПодписиИШифрования("Программы");
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаОбсуждений(Команда)
	
	ОткрытьФорму("Обработка.НастройкаПрограммы.Форма.НастройкаОбсуждений");
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаУведомлений(Команда)
	
	ОткрытьФорму("ОбщаяФорма.НастройкаУведомлений");
	
КонецПроцедуры

&НаКлиенте
Процедура ПараметрыПрокси(Команда)
	
	ОткрытьФорму("Общаяформа.ПараметрыПроксиСервера",,,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьТиповыеИнструкции(Команда)
	
	ОткрытьФорму("Обработка.ЗагрузкаТиповыхИнструкций.Форма.Форма", , ЭтаФорма,,,,,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиЧатБота(Команда)
	
	Если Модифицированность Тогда 
		Записать();
	КонецЕсли;
	
	ОткрытьФорму("Обработка.НастройкаПрограммы.Форма.НастройкиЧатБота");
	
КонецПроцедуры

&НаКлиенте
Процедура МашиночитаемыеДоверенности(Команда)
	
	МашиночитаемыеДоверенностиКлиент.ОткрытьОбщуюФормуСписковМЧД();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьРабочийКалендарьМероприятиями()
	
	НачатьТранзакцию();
	Попытка
		
		
		ВыборкаМероприятия = Справочники.Мероприятия.Выбрать();
		
		Пока ВыборкаМероприятия.Следующий() Цикл
			
			Если ВыборкаМероприятия.ПометкаУдаления Тогда
				Продолжить;
			КонецЕсли;
			
			СостоянияЗаписейКалендаря = Справочники.Мероприятия.ПолучитьСостоянияЗаписейКалендаря(ВыборкаМероприятия.Ссылка);
			Справочники.Мероприятия.ОбновитьСостоянияЗаписейКалендаря(ВыборкаМероприятия.Ссылка, СостоянияЗаписейКалендаря);
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРабочийКалендарьОтсутствиями()
	
	Документы.Отсутствие.ЗаполнитьРабочийКалендарь();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗаписатьИЗакрыть(Команда)
	
	ПараметрыЗаписи = Новый Структура;
	ПараметрыЗаписи.Вставить("Закрыть", Истина);
	Записать(ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСостоянияПротоколовМероприятий()
	
	ТекущийПользователь = ПользователиКлиентСервер.ТекущийПользователь();
	
	ВыборкаМероприятия = Справочники.Мероприятия.Выбрать();
	Пока ВыборкаМероприятия.Следующий() Цикл
		
		// Отбор пунктов протокола.
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ПротоколыМероприятий.Ссылка,
			|	ПротоколыМероприятий.Исполнение
			|ИЗ
			|	Справочник.ПротоколыМероприятий КАК ПротоколыМероприятий
			|ГДЕ
			|	ПротоколыМероприятий.Владелец = &Владелец";
		Запрос.УстановитьПараметр("Владелец", ВыборкаМероприятия.Ссылка);
		Результат = Запрос.Выполнить();
		Если Результат.Пустой() Тогда
			Продолжить;
		КонецЕсли;
		
		// Состояние мероприятия.
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	Мероприятия.Ссылка КАК Ссылка,
			|	НЕ СостоянияМероприятийИсполнено.Состояние ЕСТЬ NULL  КАК Исполнено,
			|	НЕ СостоянияМероприятийНаИсполнении.Состояние ЕСТЬ NULL  КАК НаИсполнении,
			|	СостоянияМероприятийИсполнено.Установил КАК ИсполненоУстановил,
			|	СостоянияМероприятийНаИсполнении.Установил КАК НаИсполненииУстановил
			|ИЗ
			|	Справочник.Мероприятия КАК Мероприятия
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияМероприятий КАК СостоянияМероприятийИсполнено
			|		ПО Мероприятия.Ссылка = СостоянияМероприятийИсполнено.Мероприятие
			|			И (СостоянияМероприятийИсполнено.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияМероприятий.ПротоколИсполнен))
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияМероприятий КАК СостоянияМероприятийНаИсполнении
			|		ПО Мероприятия.Ссылка = СостоянияМероприятийНаИсполнении.Мероприятие
			|			И (СостоянияМероприятийНаИсполнении.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияМероприятий.ПротоколНаИсполнении))
			|ГДЕ
			|	Мероприятия.Ссылка = &Ссылка";
		Запрос.УстановитьПараметр("Ссылка", ВыборкаМероприятия.Ссылка);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Исполнено = Выборка.Исполнено;
			НаИсполнении = Выборка.НаИсполнении;
			ИсполненоУстановил = Выборка.ИсполненоУстановил;
			НаИсполненииУстановил = Выборка.НаИсполненииУстановил;
		Иначе
			Исполнено = Ложь;
			НаИсполнении = Ложь;
			ИсполненоУстановил = Неопределено;
			НаИсполненииУстановил = Неопределено;
		КонецЕсли;
		Если Исполнено Тогда
			СостояниеИсполнения = Перечисления.СостоянияПротоколовМероприятий.Исполнен;
			СостояниеИсполненияУстановил = ИсполненоУстановил;
		ИначеЕсли Выборка.НаИсполнении Тогда
			СостояниеИсполнения = Перечисления.СостоянияПротоколовМероприятий.НаИсполнении;
			СостояниеИсполненияУстановил = НаИсполненииУстановил;
		Иначе
			СостояниеИсполнения = Перечисления.СостоянияПротоколовМероприятий.ТребуетсяИсполнение;
			СостояниеИсполненияУстановил = ТекущийПользователь;
		КонецЕсли;
		
		// Обход пунктов протокола.
		ВыборкаПротоколМероприятия = Результат.Выбрать();
		Пока ВыборкаПротоколМероприятия.Следующий() Цикл
			ПунктПротокола = ВыборкаПротоколМероприятия.Ссылка.ПолучитьОбъект();
			Если ПунктПротокола.СостояниеИсполнения = СостояниеИсполнения
				И ПунктПротокола.СостояниеИсполненияУстановил = СостояниеИсполненияУстановил Тогда
				Продолжить;
			КонецЕсли;
			ПунктПротокола.СостояниеИсполнения = СостояниеИсполнения;
			ПунктПротокола.СостояниеИсполненияУстановил = СостояниеИсполненияУстановил;
			ПунктПротокола.Записать();
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьЭлементИспользоватьСинхронизациюКалендарей(ГруппаСинхронизация, ГруппаНастройкиСинхронизации, ИспользоватьРабочийКалендарь, ИспользоватьСинхронизациюКалендарей)
	
	ГруппаСинхронизация.Доступность = ИспользоватьРабочийКалендарь;
	ГруппаНастройкиСинхронизации.Доступность = ИспользоватьСинхронизациюКалендарей;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииСостоянияПодключенияОбсуждений(ПодключениеВыполнено = Неопределено)
	
	Если ПодключениеВыполнено = Неопределено Тогда 
		ОбсужденияПодключены = ОбсужденияСлужебныйКлиент.Подключены();
	Иначе
		ОбсужденияПодключены = ПодключениеВыполнено;
	КонецЕсли;
	
	Элементы.НастроитьОбсуждения.Доступность = ОбсужденияПодключены;
	Элементы.ГруппаЧатБот.Доступность = ОбсужденияПодключены;
	
КонецПроцедуры

&НаСервере
Функция СоздатьПользователя(ИмяПользователя)
	
	ПользовательСсылка = Справочники.Пользователи.НайтиПоНаименованию(ИмяПользователя, Истина);
	Если ЗначениеЗаполнено(ПользовательСсылка) Тогда
		Возврат ПользовательСсылка;
	КонецЕсли;	
	
	НовыйПользователь = Справочники.Пользователи.СоздатьЭлемент();
	НовыйПользователь.Наименование = ИмяПользователя;
	НовыйПользователь.ПредставлениеВПерепискеСРангом = ИмяПользователя;
	НовыйПользователь.Недействителен = Ложь;
	
	НовыйПароль = НовыйПароль();
	ОписаниеПользователяИБ = Пользователи.НовоеОписаниеПользователяИБ();
	ОписаниеПользователяИБ.Имя = ИмяПользователя;
	ОписаниеПользователяИБ.ПолноеИмя = ИмяПользователя;
	ОписаниеПользователяИБ.АутентификацияСтандартная = Ложь;
	ОписаниеПользователяИБ.ПоказыватьВСпискеВыбора = Ложь;
	ОписаниеПользователяИБ.Пароль = НовыйПароль;
	ОписаниеПользователяИБ.Вставить("Действие", "Записать");
	НовыйПользователь.ДополнительныеСвойства.Вставить("ОписаниеПользователяИБ", ОписаниеПользователяИБ);
	НовыйПользователь.Записать();
	
	ДвДанные = БиблиотекаКартинок.Чатбот.ПолучитьДвоичныеДанные();
	АдресВременногоХранилищаФайла = ПоместитьВоВременноеХранилище(ДвДанные);
	
	РаботаСФотографиями.ЗаписатьИзображение(АдресВременногоХранилищаФайла, 
		Новый УникальныйИдентификатор, 
		НовыйПользователь.Ссылка, ИмяПользователя);
		
	УстановитьПривилегированныйРежим(Истина);
	Константы.ЧатБот.Установить(НовыйПользователь.Ссылка);
	
	СВПодключено = ОбсужденияСлужебныйВызовСервера.Подключены();
	
	Если СВПодключено Тогда
		
		ПользовательИБ = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(
			НовыйПользователь.ИдентификаторПользователяИБ);
		ПользовательСВ = СистемаВзаимодействия.СоздатьПользователя(ПользовательИБ);
		ПользовательСВ.Картинка = БиблиотекаКартинок.Чатбот;
		ПользовательСВ.Записать();
	КонецЕсли;
	
	Возврат НовыйПользователь.Ссылка;
	
КонецФункции

&НаСервере
Функция НовыйПароль()
	
	МинимальнаяДлинаПароля = ПолучитьМинимальнуюДлинуПаролейПользователей();
	Если МинимальнаяДлинаПароля <= 8 Тогда
		МинимальнаяДлинаПароля = 8;
	КонецЕсли;
	ПараметрыПароля = ПользователиСлужебный.ПараметрыПароля(МинимальнаяДлинаПароля, Истина);
	Возврат ПользователиСлужебный.СоздатьПароль(ПараметрыПароля);
	
КонецФункции 

#КонецОбласти
