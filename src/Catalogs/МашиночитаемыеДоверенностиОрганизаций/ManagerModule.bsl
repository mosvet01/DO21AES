#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает сведения МЧД.
// 
// Параметры:
//  МЧД - СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций
// 
// Возвращаемое значение:
//  См. МашиночитаемыеДоверенности.НовыеСведенияМЧД
//
Функция СведенияМЧД(МЧД) Экспорт

	Реквизиты = "ДатаВыдачи, ДатаОкончания, ДоверительЮЛ_ИНН,
				|ДоверительФЛ_ИНН, ПредставительФЛ_ИНН, ДатаОбновленияСтатуса, СтатусВРеестреФНС,
				|Подписана, Верна, Отозвана, ДатаОтзыва";
	РеквизитыМЧД = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(МЧД, Реквизиты);
	СведенияМЧД = МашиночитаемыеДоверенности.НовыеСведенияМЧД();
	СведенияМЧД.Ссылка = МЧД;
	ЗаполнитьЗначенияСвойств(СведенияМЧД, РеквизитыМЧД,
		"ДатаВыдачи, ДатаОкончания, СтатусВРеестреФНС, Подписана, Верна, Отозвана, ДатаОтзыва");
	СведенияМЧД.ДатаПолученияСведений = РеквизитыМЧД.ДатаОбновленияСтатуса;
	СведенияМЧД.ИННПредставителя = РеквизитыМЧД.ПредставительФЛ_ИНН;
	СведенияМЧД.ИННДоверителя = ?(ПустаяСтрока(РеквизитыМЧД.ДоверительЮЛ_ИНН), РеквизитыМЧД.ДоверительФЛ_ИНН,
		РеквизитыМЧД.ДоверительЮЛ_ИНН);
	Возврат СведенияМЧД;

КонецФункции

// Получает данные МЧД.
// 
// Параметры:
//  МЧД - СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций
// 
// Возвращаемое значение:
//  См. МашиночитаемыеДоверенности.НовыеДанныеДоверенности
//
Функция ПолучитьДанныеМЧД(МЧД) Экспорт

	РеквизитыМЧД = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(МЧД,
		"НомерДоверенности, ДоверительЮЛ_ИНН, ДоверительФЛ_ИНН");
	ДанныеМЧД = МашиночитаемыеДоверенности.НовыеДанныеДоверенности();
	ДанныеМЧД.НомерДоверенности = РеквизитыМЧД.НомерДоверенности;
	ДанныеМЧД.ИННДоверителя = ?(ПустаяСтрока(РеквизитыМЧД.ДоверительЮЛ_ИНН), РеквизитыМЧД.ДоверительФЛ_ИНН,
		РеквизитыМЧД.ДоверительЮЛ_ИНН);

	Возврат ДанныеМЧД;

КонецФункции

// Заполняет элемент справочника МЧД сведениями.
// 
// Параметры:
//  МЧД - СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций
//  Сведения - См. МашиночитаемыеДоверенности.ПолучитьСведенияДоверенностиНаСервереМЧД
//
// Возвращаемое значение:
//  См. МашиночитаемыеДоверенности.НовыеДанныеСтатусаМЧД
//
Функция ЗаполнитьМЧД(МЧД, Сведения) Экспорт

	ТребуетсяПроверкаМЧДНаКлиенте = Ложь;
	Результат = МашиночитаемыеДоверенности.НовыеДанныеСтатусаМЧД();
	
	ДанныеДляПроверки = МашиночитаемыеДоверенностиКлиентСервер.НовыеДанныеДляПроверкиМЧД();
	ДанныеДляПроверки.ДанныеДоверенности = Сведения.ПолныеДанные.ДанныеВыгрузки;
	ДанныеДляПроверки.ДанныеПодписи = Сведения.ПолныеДанные.ДанныеПодписи;
	
	ДополнительныеСведения = Новый Структура;
	СтатусДоверенности = МашиночитаемыеДоверенностиКлиентСервер.СтатусВРеестреФНС(Сведения.ЧастичныеДанные.СтатусДоверенности);
	ДополнительныеСведения.Вставить("СтатусВРеестреФНС", СтатусДоверенности);
	ДополнительныеСведения.Вставить("ДатаОбновленияСтатуса", Сведения.ДатаЗагрузкиИзРеестра);
	РезультатЗагрузки = 
		ЗагрузитьЭлементИзФайлаОбмена(ДанныеДляПроверки,
			ТребуетсяПроверкаМЧДНаКлиенте, Истина, ДополнительныеСведения);
	Результат.ТребуетсяПроверкаМЧДНаКлиенте = ТребуетсяПроверкаМЧДНаКлиенте;
	
	МЧД = РезультатЗагрузки.Ссылка;
	
	Если НЕ ПустаяСтрока(РезультатЗагрузки.Ошибка) ИЛИ НЕ ЗначениеЗаполнено(МЧД) Тогда

		Результат.Ошибка = Истина;
		Результат.ОписаниеОшибки = РезультатЗагрузки.Ошибка;
		Возврат Результат;
			
	КонецЕсли;
	
	Реквизиты = "ДатаВыдачи, ДатаОкончания, ДоверительЮЛ_ИНН,
		|ДоверительФЛ_ИНН, ПредставительФЛ_ИНН, ДатаОбновленияСтатуса, СтатусВРеестреФНС,
		|Подписана, Верна, Отозвана, ДатаОтзыва";
	РеквизитыМЧД = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(МЧД, Реквизиты);
	СведенияМЧД = МашиночитаемыеДоверенности.НовыеСведенияМЧД();
	СведенияМЧД.Ссылка = МЧД;
	ЗаполнитьЗначенияСвойств(СведенияМЧД, РеквизитыМЧД,
		"ДатаВыдачи, ДатаОкончания, СтатусВРеестреФНС, Подписана, Верна, Отозвана, ДатаОтзыва");
	СведенияМЧД.ДатаПолученияСведений = РеквизитыМЧД.ДатаОбновленияСтатуса;
	СведенияМЧД.ИННПредставителя = РеквизитыМЧД.ПредставительФЛ_ИНН;
	СведенияМЧД.ИННДоверителя =
		?(ПустаяСтрока(РеквизитыМЧД.ДоверительЮЛ_ИНН), РеквизитыМЧД.ДоверительФЛ_ИНН, РеквизитыМЧД.ДоверительЮЛ_ИНН);
	Результат.Сведения = СведенияМЧД;
	
	Возврат Результат;
	
КонецФункции

// Ищет МЧД контрагента, а в случае неудачного поиска создает новую МЧД
// 
// Параметры:
//  ДанныеДоверенности - см. МашиночитаемыеДоверенности.НовыеДанныеДоверенности
// 
// Возвращаемое значение:
//  Структура:
//  * Ссылка - СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций
//  * Ошибка - Булево
//  * ОписаниеОшибки - Строка
//  
Функция НайтиСоздатьМЧД(ДанныеДоверенности) Экспорт

	Результат = Новый Структура;
	Результат.Вставить("Ссылка", Справочники.МашиночитаемыеДоверенностиОрганизаций.ПустаяСсылка());
	Результат.Вставить("Ошибка", Ложь);
	Результат.Вставить("ОписаниеОшибки", "");

	МЧД = Справочники.МашиночитаемыеДоверенностиОрганизаций.НайтиПоРеквизиту(
		"НомерДоверенности", ДанныеДоверенности.НомерДоверенности);

	Если ЗначениеЗаполнено(МЧД) Тогда
		Результат.Ссылка = МЧД;
	Иначе

		НачатьТранзакцию();

		Попытка

			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("Справочник.МашиночитаемыеДоверенностиОрганизаций");
			ЭлементБлокировки.УстановитьЗначение("НомерДоверенности", ДанныеДоверенности.НомерДоверенности);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			Блокировка.Заблокировать();

			МЧД = Справочники.МашиночитаемыеДоверенностиОрганизаций.НайтиПоРеквизиту(
				"НомерДоверенности", ДанныеДоверенности.НомерДоверенности);

			Если ЗначениеЗаполнено(МЧД) Тогда
				Результат.Ссылка = МЧД;
			Иначе

				МЧД = Справочники.МашиночитаемыеДоверенностиОрганизаций.СоздатьЭлемент();
				МЧД.НомерДоверенности = ДанныеДоверенности.НомерДоверенности;
				Если СтрДлина(ДанныеДоверенности.ИННДоверителя) < 12 Тогда
					МЧД.ДоверительЮЛ_ИНН = ДанныеДоверенности.ИННДоверителя;
				Иначе
					МЧД.ДоверительФЛ_ИНН = ДанныеДоверенности.ИННДоверителя;
				КонецЕсли;
				МЧД.Записать();
				Результат.Ссылка = МЧД.Ссылка;

			КонецЕсли;

			ЗафиксироватьТранзакцию();

		Исключение

			ОтменитьТранзакцию();
			Результат.Ошибка = Истина;
			Результат.ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());

		КонецПопытки;

	КонецЕсли;

	Возврат Результат;

КонецФункции

// Возвращает идентификатор машиночитаемой доверенности.
// 
// Параметры:
//  Ссылка - СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций
// 
// Возвращаемое значение:
//  Строка - Идентификатор машиночитаемой доверенности.
//
Функция ПолучитьИдентификаторМЧД(Ссылка) Экспорт
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "НомерДоверенности");
КонецФункции

// Возвращает имя файла машиночитаемой доверенности.
// 
// Параметры:
//  Ссылка - СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций
// 
// Возвращаемое значение:
//  Строка - Имя файла машиночитаемой доверенности.
//
Функция ПолучитьИмяФайлаМЧД(Ссылка) Экспорт

	Реквизиты = "ДатаОтправки, НомерДоверенности";
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, Реквизиты);

	ЭлементыИмениФайла = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФорматыЭДО_ФНС.ПространствоИмен_МЧД());
	ДатаФайла = ?(ЗначениеЗаполнено(ЗначенияРеквизитов.ДатаОтправки), ЗначенияРеквизитов.ДатаОтправки,
		ТекущаяДатаСеанса());
	ЭлементыИмениФайла.Добавить(Формат(ДатаФайла, "ДФ=ггггММдд;"));
	ЭлементыИмениФайла.Добавить(ЗначенияРеквизитов.НомерДоверенности);

	Возврат СтрСоединить(ЭлементыИмениФайла, "_");

КонецФункции

// Возвращает двоичные данные машиночитаемой доверенности.
// 
// Параметры:
//  Ссылка - СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций
// 
// Возвращаемое значение:
//  ДвоичныеДанные - Двоичные данные машиночитаемой доверенности.
//
Функция ПолучитьДвоичныеДанныеМЧД(Ссылка) Экспорт

	СтатусВРеестреФНС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "СтатусВРеестреФНС");
	
	Если ЗначениеЗаполнено(СтатусВРеестреФНС) Тогда
		НомерДоверенности = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "НомерДоверенности");
		РезультатВыгрузки = МашиночитаемыеДоверенности.ВыгрузитьЗаявлениеНаОтменуМЧД(НомерДоверенности, "Отзыв");
		СтрокаXML = РезультатВыгрузки.Содержимое;
		Возврат ПолучитьДвоичныеДанныеИзСтроки(СтрокаXML, "windows-1251");
	Иначе
		Возврат СформироватьДвоичныеДанныеXML(Ссылка);
	КонецЕсли;

КонецФункции

// Загружает в элемент справочника данные из архива с файлом МЧД и подписью.
// Если доверенности с таким номером нет, то создает новую, иначе перезаполняет существующую.
//
// Параметры:
//  ДанныеФайла - ДвоичныеДанные, Строка - Двоичные данные архива или адрес во временном хранилище.
//  МЧД			- Неопределено
//  			- СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций
//
// Возвращаемое значение:
//  Структура:
//  * МЧД - СправочникСсылка.МашиночитаемыеДоверенностиКонтрагентов - Ссылка на МЧД контрагента.
//  * ТребуетсяПроверкаМЧДНаКлиенте - Булево - Флаг устанавливается в Истина, 
//                                             если отсутствует возможность проверить подпись МЧД на сервере 
//                                             (в настройках установлена проверка подписи на клиенте) 
//                                             и необходимо выполнить проверку на клиенте.
//  * ДанныеДляПроверки - Неопределено, См. МашиночитаемыеДоверенностиКлиентСервер.НовыеДанныеДляПроверкиМЧД
//
Функция ЗагрузитьМЧДИзФайла(ДанныеФайла, МЧД = Неопределено) Экспорт

	ТребуетсяПроверкаМЧДНаКлиенте = Ложь;

	Результат = Новый Структура;
	Результат.Вставить("МЧД", Справочники.МашиночитаемыеДоверенностиОрганизаций.ПустаяСсылка());
	Результат.Вставить("ТребуетсяПроверкаМЧДНаКлиенте", ТребуетсяПроверкаМЧДНаКлиенте);
	Результат.Вставить("ДанныеДляПроверки", Неопределено);

	ДанныеАрхива = МашиночитаемыеДоверенности.ПрочитатьАрхивСДоверенностьюИПодписью(ДанныеФайла);
	Если ДанныеАрхива = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;

	ДанныеДляПроверки = МашиночитаемыеДоверенностиКлиентСервер.НовыеДанныеДляПроверкиМЧД();
	ДанныеДляПроверки.ДанныеДоверенности = ДанныеАрхива.ДанныеМЧД;
	ДанныеДляПроверки.ДанныеПодписи = ДанныеАрхива.ДанныеПодписи;

	Результат.ДанныеДляПроверки = ДанныеДляПроверки;

	ВидОперации = НСтр("ru = 'Загрузка машиночитаемой доверенности из файла.'");

	Попытка
		МашиночитаемыеДоверенности.ПрочитатьДанныеXML(ДанныеДляПроверки.ДанныеДоверенности);
	Исключение
		ТекстОшибки = НСтр("ru = 'Ошибка при чтении файла доверенности: файл не соответствует формату ФНС.'");
		ПодробныйТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЭлектронноеВзаимодействие.ОбработатьОшибку(ВидОперации, ПодробныйТекстОшибки, ТекстОшибки);
		Возврат Результат;
	КонецПопытки;

	ДополнительныеСведения = Новый Структура;
	ДополнительныеСведения.Вставить("ДатаЗагрузки", ТекущаяДатаСеанса());
	ДополнительныеСведения.Вставить(
		"СтатусВРеестреФНС", Перечисления.СтатусыМашиночитаемойДоверенностиВРеестреФНС.ПустаяСсылка());

	РезультатЗагрузки = ЗагрузитьЭлементИзФайлаОбмена(ДанныеДляПроверки, ТребуетсяПроверкаМЧДНаКлиенте, Истина,
		ДополнительныеСведения, МЧД);
	Результат.ТребуетсяПроверкаМЧДНаКлиенте = ТребуетсяПроверкаМЧДНаКлиенте;

	Если Не РезультатЗагрузки.Выполнено Тогда
		ТекстОшибки = НСтр("ru = 'Ошибка при заполнении доверенности из файла:'") + Символы.ПС
			+ РезультатЗагрузки.Ошибка;
		ЭлектронноеВзаимодействие.ОбработатьОшибку(ВидОперации, ТекстОшибки, ТекстОшибки);
		Возврат Результат;
	КонецЕсли;

	Результат.МЧД = РезультатЗагрузки.Ссылка;

	Возврат Результат;

КонецФункции

// Возвращает преобразованные данные доверенности в строку xml.
// 
// Параметры:
//  Ссылка - СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций
// 
// Возвращаемое значение:
//  Строка - доверенность в формате xml.
Функция СформироватьXML(Ссылка) Экспорт

	ДанныеДоверенности = ДанныеИзИнформационнойБазы(Ссылка);

	ДоверенностьXML = ДанныеДоверенностиВСтрокуXML(ДанныеДоверенности);

	Возврат ДоверенностьXML;

КонецФункции

// Возвращает двоичные данные xml в кодировке windows-1251.
// 
// Параметры:
//  Ссылка - СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций
// 
// Возвращаемое значение:
//  ДвоичныеДанные
Функция СформироватьДвоичныеДанныеXML(Ссылка) Экспорт

	ДоверенностьXML = СформироватьXML(Ссылка);

	ДвоичныеДанныеXML = ПолучитьДвоичныеДанныеИзСтроки(ДоверенностьXML, "windows-1251");

	Возврат ДвоичныеДанныеXML;

КонецФункции

// Определяет наличие у пользователя прав на изменение машиночитаемых доверенностей организаций.
//
// Возвращаемое значение:
//  Булево - Истина, если у пользователя есть право на изменение, иначе Ложь.
//
Функция ЕстьПравоИзменения() Экспорт

	Возврат ПравоДоступа("Изменение", Метаданные.Справочники.МашиночитаемыеДоверенностиОрганизаций);

КонецФункции

// Обновить статус МЧДИз реестра ФНС.
// 
// Параметры:
//  ДанныеДоверенности - Структура:
//  * НомерДоверенности - Строка
//  * СтатусВРеестреФНС - Строка
//  * ИдентификаторТранзакции - Строка
// 
// Возвращаемое значение:
// - Неопределено
// - Структура - Результат из реестра ФНС:
//    * СтраницаНеНайдена - Булево - Не удалось найти страницу ошибка 404
//    * СостояниеИзменено - Булево - Состояние изменено
//    * СтатусВРеестреФНС - Строка - Статус в реестре ФНС
//    * НомерДоверенности - Строка - Номер доверенности
//    * ДатаВыдачи - Неопределено, Дата - Дата выдачи доверенности
//    * ДатаОкончания - Неопределено, Дата - Дата окончания доверенности
//    * ДатаПроверки - Дата - Дата получения статуса 
Функция ОбновитьСтатусМЧДИзРеестраФНС(ДанныеДоверенности) Экспорт
	
	Если ДанныеДоверенности.СтатусВРеестреФНС =
		ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенностиВРеестреФНС.ПустаяСсылка") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("СтраницаНеНайдена", 	Ложь);
	Результат.Вставить("СостояниеИзменено", 	Ложь);
	Результат.Вставить("СтатусВРеестреФНС", 	"");
	Результат.Вставить("НомерДоверенности", 	"");
	Результат.Вставить("ДатаВыдачи", 			Неопределено);
	Результат.Вставить("ДатаОкончания", 		Неопределено);
	Результат.Вставить("ДатаПроверки", 		    '00010101');
	
	ТокенДоступа = "";
	СтраницаНеНайдена = Ложь;
	
	Если ДанныеДоверенности.СтатусВРеестреФНС = Перечисления.СтатусыМашиночитаемойДоверенностиВРеестреФНС.Отправлено Тогда
		
		СведенияСтатусаТранзакции = МашиночитаемыеДоверенностиВызовСервера.ПолучитьСтатусТранзакцииМЧД(
			ДанныеДоверенности.ИдентификаторТранзакции,
			ТокенДоступа,
			ДанныеДоверенности.НомерДоверенности);
		
		Если СведенияСтатусаТранзакции.СтатусТранзакции = "SUCCESS" Тогда
			
			Результат.СтатусВРеестреФНС = Перечисления.СтатусыМашиночитаемойДоверенностиВРеестреФНС.Зарегистрировано;	
			
		ИначеЕсли СведенияСтатусаТранзакции.СтатусТранзакции = "FAILURE" Тогда
			
			Результат.СтатусВРеестреФНС = Перечисления.СтатусыМашиночитаемойДоверенностиВРеестреФНС.ОшибкаРегистрации;
				
		ИначеЕсли СведенияСтатусаТранзакции.СтатусТранзакции = 404 ИЛИ СведенияСтатусаТранзакции.СтатусТранзакции = 500 Тогда
			СтраницаНеНайдена = Истина;
			Результат.СтраницаНеНайдена = СтраницаНеНайдена;
		КонецЕсли;
		
	КонецЕсли;
	
	Если (ДанныеДоверенности.СтатусВРеестреФНС <> Перечисления.СтатусыМашиночитаемойДоверенностиВРеестреФНС.Отправлено
		И ДанныеДоверенности.СтатусВРеестреФНС <> Перечисления.СтатусыМашиночитаемойДоверенностиВРеестреФНС.ОшибкаРегистрации)
		ИЛИ СтраницаНеНайдена Тогда
		
		СведенияСтатусаДоверенности = МашиночитаемыеДоверенностиВызовСервера.ПолучитьЧастичныеДанныеДоверенностиНаСервереМЧД(
			ДанныеДоверенности.НомерДоверенности,
			ТокенДоступа);
		
		Если СведенияСтатусаДоверенности.СтатусДоверенности = "CREATED"
			И ДанныеДоверенности.СтатусВРеестреФНС <> Перечисления.СтатусыМашиночитаемойДоверенностиВРеестреФНС.ДатаНачалаДействияНеНаступила Тогда
			
			Результат.СтатусВРеестреФНС = Перечисления.СтатусыМашиночитаемойДоверенностиВРеестреФНС.ДатаНачалаДействияНеНаступила;
			Результат.СостояниеИзменено = Истина;
			
		ИначеЕсли СведенияСтатусаДоверенности.СтатусДоверенности = "ACTIVE"
			И ДанныеДоверенности.СтатусВРеестреФНС <> Перечисления.СтатусыМашиночитаемойДоверенностиВРеестреФНС.Зарегистрировано
			И ДанныеДоверенности.СтатусВРеестреФНС <> Перечисления.СтатусыМашиночитаемойДоверенностиВРеестреФНС.ОтправленоЗаявлениеНаОтзыв
			И ДанныеДоверенности.СтатусВРеестреФНС <> Перечисления.СтатусыМашиночитаемойДоверенностиВРеестреФНС.ОшибкаОтзыва Тогда
			
			Результат.СтатусВРеестреФНС = Перечисления.СтатусыМашиночитаемойДоверенностиВРеестреФНС.Зарегистрировано;
			Результат.СостояниеИзменено = Истина;
			
		ИначеЕсли СведенияСтатусаДоверенности.СтатусДоверенности = "EXPIRED"
			И ДанныеДоверенности.СтатусВРеестреФНС <> Перечисления.СтатусыМашиночитаемойДоверенностиВРеестреФНС.ИстекСрокДействия Тогда
			
			Результат.СтатусВРеестреФНС = Перечисления.СтатусыМашиночитаемойДоверенностиВРеестреФНС.ИстекСрокДействия;
			Результат.СостояниеИзменено = Истина;
			
		ИначеЕсли (СведенияСтатусаДоверенности.СтатусДоверенности = "DECLINED"
			ИЛИ СведенияСтатусаДоверенности.СтатусДоверенности = "REVOKED")
			И ДанныеДоверенности.СтатусВРеестреФНС <> Перечисления.СтатусыМашиночитаемойДоверенностиВРеестреФНС.Отозвано Тогда
			
			Результат.СтатусВРеестреФНС = Перечисления.СтатусыМашиночитаемойДоверенностиВРеестреФНС.Отозвано;
			Результат.СостояниеИзменено = Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Результат.ДатаПроверки = ТекущаяДатаСеанса();
	
	Возврат Результат;
		
КонецФункции

#Область ЗагрузкаЭлементаСправочника


// Загружает элемент справочника из файла обмена.
// 
// Параметры:
//  ДанныеДляПроверки - см. МашиночитаемыеДоверенностиКлиентСервер.НовыеДанныеДляПроверкиМЧД
//  ТребуетсяПроверкаМЧДНаКлиенте - Булево - Флаг устанавливается в Истина, 
//                                             если отсутствует возможность проверить подпись МЧД на сервере 
//                                             (в настройках установлена проверка подписи на клиенте) 
//                                             и необходимо выполнить проверку на клиенте.
//  ОбновлятьСуществующий - Булево - Если Истина, то будет обновлен существующий элемент, если он найден.
//  ДополнительныеСведения - Неопределено, Структура - Если переданы, то будут заполнены в элементе справочника.
//  МЧД			- Неопределено
//  			- СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций
// 
// Возвращаемое значение:
//  Структура - Результат загрузки:
//   * Выполнено - Булево - Признак успешности выполнения загрузки.
//   * Ссылка - Неопределено, СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций - Ссылка на элемент справочника.
//   * Ошибка - Строка - Текст ошибки, если не удалось загрузить элемент.
//   * ТребуетсяПроверкаМЧДНаКлиенте - Булево - Флаг устанавливается в Истина, 
//                                             если отсутствует возможность проверить подпись МЧД на сервере 
//                                             (в настройках установлена проверка подписи на клиенте) 
//                                             и необходимо выполнить проверку на клиенте.
//
Функция ЗагрузитьЭлементИзФайлаОбмена(ДанныеДляПроверки, ТребуетсяПроверкаМЧДНаКлиенте, ОбновлятьСуществующий = Ложь,
	ДополнительныеСведения = Неопределено, МЧД = Неопределено) Экспорт

	Результат = Новый Структура;
	Результат.Вставить("Выполнено", Ложь);
	Результат.Вставить("Ссылка", Неопределено);
	Результат.Вставить("Ошибка", "");
	Результат.Вставить("ТребуетсяПроверкаМЧДНаКлиенте", Ложь);

	Попытка
		ДанныеДоверенности = МашиночитаемыеДоверенности.ДанныеИзФайлаОбмена(ДанныеДляПроверки.ДанныеДоверенности);
	Исключение
		Результат.Ошибка = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Возврат Результат;
	КонецПопытки;

	РезультатПроверки = МашиночитаемыеДоверенности.ПроверитьКлючевыеРеквизитыДанныхФайлаДоверенности(
		ДанныеДоверенности, Истина);
	Если РезультатПроверки.ЕстьОшибки Тогда
		Результат.Ошибка = РезультатПроверки.ТекстОшибки;
		Возврат Результат;
	КонецЕсли;

	Если ЗначениеЗаполнено(ДополнительныеСведения) Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ДанныеДоверенности, ДополнительныеСведения, Истина);
	КонецЕсли;

	ТекущийЭлемент = ЗаписатьЭлементСправочника(ДанныеДоверенности, ОбновлятьСуществующий, ДанныеДляПроверки,
		ТребуетсяПроверкаМЧДНаКлиенте, МЧД);
	Результат.Ссылка = ТекущийЭлемент;
	Результат.ТребуетсяПроверкаМЧДНаКлиенте = ТребуетсяПроверкаМЧДНаКлиенте;
	Если ЗначениеЗаполнено(ТекущийЭлемент) Тогда
		Результат.Выполнено = Истина;
	Иначе
		Результат.Ошибка = НСтр("ru = 'Не удалось записать элемент справочника. Подробности в журнале регистрации.'");
	КонецЕсли;

	Возврат Результат;

КонецФункции

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы = "ФормаОбъекта"
		И Параметры.Свойство("Ключ") Тогда
		ДоверенностьПодписана = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.Ключ, "Подписана");
		Если ДоверенностьПодписана Тогда
			ВыбраннаяФорма = "Справочник.МашиночитаемыеДоверенностиОрганизаций.Форма.ФормаПросмотра";
			СтандартнаяОбработка = Ложь;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ЗагрузкаЭлементаСправочника

// Заполняет и записывает элемент справочника.
//
// Параметры:
//  ДанныеДоверенности - см. МашиночитаемыеДоверенности.ДанныеИзФайлаОбмена
//  ОбновлятьСуществующий - Булево - Обновлять существующий
//  ДанныеДляПроверки - см. МашиночитаемыеДоверенностиКлиентСервер.НовыеДанныеДляПроверкиМЧД;
//  ТребуетсяПроверкаМЧДНаКлиенте - Булево - Флаг устанавливается в Истина, 
//                                             если отсутствует возможность проверить подпись МЧД на сервере 
//                                             (в настройках установлена проверка подписи на клиенте) 
//                                             и необходимо выполнить проверку на клиенте.
//  МЧД			- Неопределено
//  			- СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций
// 
//  Возвращаемое значение:
//   СправочникСсылка.МашиночитаемыеДоверенностиКонтрагентов - Ссылка на элемент справочника.
//
Функция ЗаписатьЭлементСправочника(ДанныеДоверенности, ОбновлятьСуществующий, ДанныеДляПроверки,
	ТребуетсяПроверкаМЧДНаКлиенте, МЧД = Неопределено)
	
	Результат = Справочники.МашиночитаемыеДоверенностиОрганизаций.ПустаяСсылка();
	НашлиЭлемент = Неопределено;
	
	Если ЗначениеЗаполнено(МЧД) Тогда
		НашлиЭлемент = МЧД;
	КонецЕсли;
	
	Если ОбновлятьСуществующий И Не ЗначениеЗаполнено(НашлиЭлемент) Тогда
		НашлиЭлемент = Справочники.МашиночитаемыеДоверенностиОрганизаций.НайтиПоРеквизиту(
			"НомерДоверенности", ДанныеДоверенности.НомерДоверенности);
	КонецЕсли;

	Если Не ЗначениеЗаполнено(НашлиЭлемент) Тогда
		ОбъектСправочника = Справочники.МашиночитаемыеДоверенностиОрганизаций.СоздатьЭлемент();
	Иначе
		ОбъектСправочника = НашлиЭлемент.ПолучитьОбъект();
	КонецЕсли;

	ДанныеОбъектаДляПроверки = МашиночитаемыеДоверенностиКлиентСервер.НовыеДанныеДляПроверкиМЧД();	
	ДанныеОбъектаДляПроверки.ДанныеДоверенности = ОбъектСправочника.XMLфайлМЧД.Получить();
	ДанныеОбъектаДляПроверки.ДанныеПодписи = ОбъектСправочника.ЭлектроннаяПодпись.Получить();
	
	Если МашиночитаемыеДоверенности.ТребуетсяПерезаполнениеМЧД(ДанныеОбъектаДляПроверки, ДанныеДляПроверки) Тогда
			
		ПрефиксыРеквизитовДляОчистки = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(
			"Доверитель, ЛицоБезДов, Представитель");
		Для Каждого СтрокаРеквизита Из ОбъектСправочника.Метаданные().Реквизиты Цикл
			Для каждого ПрефиксРеквизитаДляОчистки Из ПрефиксыРеквизитовДляОчистки Цикл
				Если СтрНачинаетсяС(СтрокаРеквизита.Имя, ПрефиксРеквизитаДляОчистки) Тогда
					ОбъектСправочника[СтрокаРеквизита.Имя] = Неопределено;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
		// здесь переносим значения реквизитов
		ЗаполнитьЗначенияСвойств(ОбъектСправочника, ДанныеДоверенности);
		МашиночитаемыеДоверенности.ЗаполнитьПодписанаВерна(ОбъектСправочника, ДанныеДляПроверки, ТребуетсяПроверкаМЧДНаКлиенте);
	
		ТабличныеЧасти = Новый Структура;
		ТабличныеЧасти.Вставить("ФИО", ДанныеДоверенности.ФИО);
		ТабличныеЧасти.Вставить("ПолномочияПредставителя", ДанныеДоверенности.ПолномочияПредставителя);
		ТабличныеЧасти.Вставить("УдостоверенияЛичности", ДанныеДоверенности.УдостоверенияЛичности);
		
		Для Каждого СтрокаТЧ Из ТабличныеЧасти Цикл
			ТекущаяТЧ = ОбъектСправочника[СтрокаТЧ.Ключ];
			ТекущаяТЧ.Очистить();
			Для Каждого НоваяСтрока Из СтрокаТЧ.Значение Цикл
				ДобавленнаяСтрока = ТекущаяТЧ.Добавить();
				ЗаполнитьЗначенияСвойств(ДобавленнаяСтрока, НоваяСтрока);
			КонецЦикла;
		КонецЦикла;
		
	Иначе
		Если (ДанныеДоверенности.Свойство("СтатусВРеестреФНС")
			И ДанныеДоверенности.СтатусВРеестреФНС <> ОбъектСправочника.СтатусВРеестреФНС) Тогда
			ОбъектСправочника.СтатусВРеестреФНС = ДанныеДоверенности.СтатусВРеестреФНС;
		КонецЕсли;
	КонецЕсли;

	ОтсутствуетВозможностьПроверитьНаСервере = Не ЭлектроннаяПодпись.ПроверятьЭлектронныеПодписиНаСервере();
	ЭтоПровереннаяРеестроваяМЧД =
		МашиночитаемыеДоверенности.ЭтоПровереннаяРеестроваяМЧД(ДанныеДляПроверки, ДанныеДоверенности);
	
	Если ЭтоПровереннаяРеестроваяМЧД И ОтсутствуетВозможностьПроверитьНаСервере Тогда
		ОбъектСправочника.Верна = Истина;
	КонецЕсли;
	
	Удачно = Ложь;
	
	Если ОбъектСправочника.ПроверитьЗаполнение() Тогда
	
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Справочник.МашиночитаемыеДоверенностиОрганизаций");
		Если Не ОбъектСправочника.ЭтоНовый() Тогда
			ЭлементБлокировки.УстановитьЗначение("Ссылка", ОбъектСправочника.Ссылка);
		КонецЕсли;
	
		Удачно = Истина;
	
		НачатьТранзакцию();
	
		Попытка
			Блокировка.Заблокировать();
			ОбъектСправочника.Записать();
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ИмяСобытия = НСтр("ru = 'Ошибка изменения МЧД.'", ОбщегоНазначения.КодОсновногоЯзыка());
			ДанныеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЗаписьЖурналаРегистрации(
				ИмяСобытия, УровеньЖурналаРегистрации.Ошибка,,, ДанныеОшибки);
			Удачно = Ложь;
		КонецПопытки;
	
	КонецЕсли;
	
	Если Удачно Тогда
		Результат = ОбъектСправочника.Ссылка;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ВыгрузкаЭлементаСправочника

// Возвращает данные доверенности из информационной базы.
// 
// Параметры:
//  СправочникСсылка - СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций
// 
// Возвращаемое значение:
//  Структура:
// * ОКУД - Строка
// * ИдентификаторФайла - Строка
// * СведенияОбИнформационнойСистеме - Строка
// * Номер - Строка
// * ДатаВыдачи - Дата
// * ДатаОкончания - Дата
// * СрокДействия - Дата
// * ВозможноПередоверие - Булево
// * ПредыдущийНомер - Строка
// * ИностраннаяОрганизация - Булево
// * ДоверительЮЛ_ИНН - Строка
// * ДоверительЮЛ_КПП - Строка
// * ДоверительЮЛ_ОГРН - Строка
// * ДоверительЮЛ_СтрРег - СправочникСсылка.СтраныМира
// * ДоверительЮЛ_НаимРегОрг - Строка
// * ДоверительЮЛ_РегНомер - Строка
// * ДоверительЮЛ_Адр - Строка
// * ДоверительФЛ_ИНН - Строка
// * ДоверительФЛ_ОГРН - Строка
// * ДоверительФЛ_СНИЛС - Строка
// * ДоверительФЛ_Гражданство - СправочникСсылка.СтраныМира
// * ДоверительФЛ_Пол - Строка
// * ДоверительФЛ_ДатаРождения - Дата
// * ДоверительФЛ_МестоРожд - Строка
// * ДоверительЮЛ_НаимОрг - Строка
// * ДоверительЮЛ_ТипОрганизации - Строка
// * ЛицоБезДовЮЛ_НаимОрг - Строка
// * ЛицоБезДовЮЛ_ИНН - Строка
// * ЛицоБезДовЮЛ_КПП - Строка
// * ЛицоБезДовЮЛ_ОГРН - Строка
// * ЛицоБезДовФЛ_ИНН - Строка
// * ЛицоБезДовФЛ_СНИЛС - Строка
// * ЛицоБезДовФЛ_Гражданство - СправочникСсылка.СтраныМира
// * ЛицоБезДовФЛ_ДатаРождения - Дата
// * ЛицоБезДовФЛ_Должность - Строка
// * ЛицоБезДовФЛ_ОснованиеПолномочий - Строка
// * ЛицоБезДовФЛ_Имя - Строка
// * ЛицоБезДовФЛ_Отчество - Строка
// * ЛицоБезДовФЛ_Фамилия - Строка
// * ЛицоБезДовФЛ_ВидДок - Строка
// * ЛицоБезДовФЛ_СерДок - Строка
// * ЛицоБезДовФЛ_НомДок - Строка
// * ЛицоБезДовФЛ_ДатаДок - Строка
// * ЛицоБезДовФЛ_ВыдДок - Строка
// * ЛицоБезДовФЛ_КодВыдДок - Строка
// * ПредставительЮЛ_НаимОрг - Строка
// * ПредставительЮЛ_ИНН - Строка
// * ПредставительЮЛ_КПП - Строка
// * ПредставительЮЛ_ОГРН - Строка
// * ПредставительФЛ_ИНН - Строка
// * ПредставительФЛ_ОГРН - Строка
// * ПредставительФЛ_СНИЛС - Строка
// * ПредставительФЛ_Гражданство - СправочникСсылка.СтраныМира
// * ПредставительФЛ_ДатаРождения - Дата
// * ПредставительЮЛ_Полномочия - Массив из Строка
// * ПредставительФЛ_Имя - Строка
// * ПредставительФЛ_Отчество - Строка
// * ПредставительФЛ_Фамилия - Строка
// * ПредставительФЛ_ВидДок - Строка
// * ПредставительФЛ_СерДок - Строка
// * ПредставительФЛ_НомДок - Строка
// * ПредставительФЛ_ДатаДок - Строка
// * ПредставительФЛ_ВыдДок - Строка
// * ПредставительФЛ_КодВыдДок - Строка
// * ИныеСведения - Строка
Функция ДанныеИзИнформационнойБазы(СправочникСсылка)

	Результат = Новый Структура;
	Результат.Вставить("ОКУД", "0251222");

	ИдентификаторФайла = Справочники.МашиночитаемыеДоверенностиОрганизаций.ПолучитьИмяФайлаМЧД(СправочникСсылка);
	Результат.Вставить("ИдентификаторФайла", ИдентификаторФайла);

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	МашиночитаемыеДоверенностиПолномочияПредставителя.Ссылка КАК Доверенность,
	|	МашиночитаемыеДоверенностиПолномочияПредставителя.ПризнакПолныеПолномочия КАК ПризнакПолныеПолномочия,
	|	&ТекстЗапросаРеквизитовПолномочий
	|ИЗ
	|	Справочник.МашиночитаемыеДоверенностиОрганизаций.ПолномочияПредставителя КАК
	|		МашиночитаемыеДоверенностиПолномочияПредставителя
	|ГДЕ
	|	МашиночитаемыеДоверенностиПолномочияПредставителя.Ссылка = &СправочникСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МашиночитаемыеДоверенностиФИО.Владелец КАК Владелец,
	|	МашиночитаемыеДоверенностиФИО.Фамилия КАК Фамилия,
	|	МашиночитаемыеДоверенностиФИО.Имя КАК Имя,
	|	МашиночитаемыеДоверенностиФИО.Отчество КАК Отчество
	|ИЗ
	|	Справочник.МашиночитаемыеДоверенностиОрганизаций.ФИО КАК МашиночитаемыеДоверенностиФИО
	|ГДЕ
	|	МашиночитаемыеДоверенностиФИО.Ссылка = &СправочникСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МашиночитаемыеДоверенности.Организация КАК ОрганизацияМЧД,
	|	МашиночитаемыеДоверенности.НомерДоверенности КАК НомерДоверенности,
	|	МашиночитаемыеДоверенности.ДатаВыдачи КАК ДатаВыдачи,
	|	МашиночитаемыеДоверенности.ДатаОкончания КАК ДатаОкончания,
	|	МашиночитаемыеДоверенности.СрокДействия КАК СрокДействия,
	|	МашиночитаемыеДоверенности.ВозможноПередоверие КАК ВозможноПередоверие,
	|	МашиночитаемыеДоверенности.НомерРодительскойДоверенности КАК НомерРодительскойДоверенности,
	|	МашиночитаемыеДоверенности.ДоверительЮЛ_ИностраннаяОрганизация КАК ДоверительЮЛ_ИностраннаяОрганизация,
	|	МашиночитаемыеДоверенности.ДоверительЮЛ_ИНН КАК ДоверительЮЛ_ИНН,
	|	МашиночитаемыеДоверенности.ДоверительЮЛ_КПП КАК ДоверительЮЛ_КПП,
	|	МашиночитаемыеДоверенности.ДоверительЮЛ_ОГРН КАК ДоверительЮЛ_ОГРН,
	|	МашиночитаемыеДоверенности.ДоверительЮЛ_СтрРег КАК ДоверительЮЛ_СтрРег,
	|	МашиночитаемыеДоверенности.ДоверительЮЛ_НаимРегОрг КАК ДоверительЮЛ_НаимРегОрг,
	|	МашиночитаемыеДоверенности.ДоверительЮЛ_РегНомер КАК ДоверительЮЛ_РегНомер,
	|	МашиночитаемыеДоверенности.ДоверительЮЛ_Адр КАК ДоверительЮЛ_Адр,
	|	МашиночитаемыеДоверенности.ДоверительФЛ_ИНН КАК ДоверительФЛ_ИНН,
	|	МашиночитаемыеДоверенности.ДоверительФЛ_ОГРН КАК ДоверительФЛ_ОГРН,
	|	МашиночитаемыеДоверенности.ДоверительФЛ_СНИЛС КАК ДоверительФЛ_СНИЛС,
	|	МашиночитаемыеДоверенности.ДоверительФЛ_Гражданство КАК ДоверительФЛ_Гражданство,
	|	МашиночитаемыеДоверенности.ДоверительФЛ_Пол КАК ДоверительФЛ_Пол,
	|	МашиночитаемыеДоверенности.ДоверительФЛ_ДатаРождения КАК ДоверительФЛ_ДатаРождения,
	|	МашиночитаемыеДоверенности.ДоверительФЛ_МестоРожд КАК ДоверительФЛ_МестоРожд,
	|	МашиночитаемыеДоверенности.ДоверительЮЛ_НаимОрг КАК ДоверительЮЛ_НаимОрг,
	|	МашиночитаемыеДоверенности.ЛицоБезДовЮЛ_НаимОрг КАК ЛицоБезДовЮЛ_НаимОрг,
	|	МашиночитаемыеДоверенности.ЛицоБезДовЮЛ_ИНН КАК ЛицоБезДовЮЛ_ИНН,
	|	МашиночитаемыеДоверенности.ЛицоБезДовЮЛ_КПП КАК ЛицоБезДовЮЛ_КПП,
	|	МашиночитаемыеДоверенности.ЛицоБезДовЮЛ_ОГРН КАК ЛицоБезДовЮЛ_ОГРН,
	|	МашиночитаемыеДоверенности.ЛицоБезДовФЛ_ИНН КАК ЛицоБезДовФЛ_ИНН,
	|	МашиночитаемыеДоверенности.ЛицоБезДовФЛ_СНИЛС КАК ЛицоБезДовФЛ_СНИЛС,
	|	МашиночитаемыеДоверенности.ЛицоБезДовФЛ_Гражданство КАК ЛицоБезДовФЛ_Гражданство,
	|	МашиночитаемыеДоверенности.ЛицоБезДовФЛ_ДатаРождения КАК ЛицоБезДовФЛ_ДатаРождения,
	|	МашиночитаемыеДоверенности.ЛицоБезДовФЛ_Должность КАК ЛицоБезДовФЛ_Должность,
	|	МашиночитаемыеДоверенности.ЛицоБезДовФЛ_ОснованиеПолномочий КАК ЛицоБезДовФЛ_ОснованиеПолномочий,
	|	МашиночитаемыеДоверенности.ПредставительЮЛ_НаимОрг КАК ПредставительЮЛ_НаимОрг,
	|	МашиночитаемыеДоверенности.ПредставительЮЛ_ИНН КАК ПредставительЮЛ_ИНН,
	|	МашиночитаемыеДоверенности.ПредставительЮЛ_КПП КАК ПредставительЮЛ_КПП,
	|	МашиночитаемыеДоверенности.ПредставительЮЛ_ОГРН КАК ПредставительЮЛ_ОГРН,
	|	МашиночитаемыеДоверенности.ПредставительФЛ_ИНН КАК ПредставительФЛ_ИНН,
	|	МашиночитаемыеДоверенности.ПредставительФЛ_ОГРН КАК ПредставительФЛ_ОГРН,
	|	МашиночитаемыеДоверенности.ПредставительФЛ_СНИЛС КАК ПредставительФЛ_СНИЛС,
	|	МашиночитаемыеДоверенности.ПредставительФЛ_Гражданство КАК ПредставительФЛ_Гражданство,
	|	МашиночитаемыеДоверенности.ПредставительФЛ_ДатаРождения КАК ПредставительФЛ_ДатаРождения,
	|	МашиночитаемыеДоверенности.СведенияОбИнформационнойСистеме КАК СведенияОбИнформационнойСистеме
	|ИЗ
	|	Справочник.МашиночитаемыеДоверенностиОрганизаций КАК МашиночитаемыеДоверенности
	|ГДЕ
	|	МашиночитаемыеДоверенности.Ссылка = &СправочникСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МашиночитаемыеДоверенностиУдостоверенияЛичности.Владелец КАК Владелец,
	|	МашиночитаемыеДоверенностиУдостоверенияЛичности.ВидДок КАК ВидДок,
	|	МашиночитаемыеДоверенностиУдостоверенияЛичности.СерДок КАК СерДок,
	|	МашиночитаемыеДоверенностиУдостоверенияЛичности.НомДок КАК НомДок,
	|	МашиночитаемыеДоверенностиУдостоверенияЛичности.ДатаДок КАК ДатаДок,
	|	МашиночитаемыеДоверенностиУдостоверенияЛичности.ВыдДок КАК ВыдДок,
	|	МашиночитаемыеДоверенностиУдостоверенияЛичности.КодВыдДок КАК КодВыдДок
	|ИЗ
	|	Справочник.МашиночитаемыеДоверенностиОрганизаций.УдостоверенияЛичности КАК
	|		МашиночитаемыеДоверенностиУдостоверенияЛичности
	|ГДЕ
	|	МашиночитаемыеДоверенностиУдостоверенияЛичности.Ссылка = &СправочникСсылка";

	ПоляВидовПолномочий = Новый Массив;
	Для ВидПолномочия = 1 По КоличествоВидовПолномочийПредставителя() Цикл
		ПолеВидаПолномочия = СтрШаблон("МашиночитаемыеДоверенностиПолномочияПредставителя.%1 КАК %1",
			ПредставлениеВидаПолномочия(ВидПолномочия));
		ПолеВидаПолномочия = ПолеВидаПолномочия + ?(ВидПолномочия = КоличествоВидовПолномочийПредставителя(), "", ",");
		ПоляВидовПолномочий.Добавить(ПолеВидаПолномочия);
	КонецЦикла;

	ТекстЗапросаРеквизитовПолномочий = СтрСоединить(ПоляВидовПолномочий, Символы.ПС);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаРеквизитовПолномочий", ТекстЗапросаРеквизитовПолномочий);

	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("СправочникСсылка", СправочникСсылка);

	УстановитьПривилегированныйРежим(Истина);
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	УстановитьПривилегированныйРежим(Ложь);

	ВыборкаПолномочий = ПакетЗапросов[0].Выгрузить();

	ПолномочияПредставителя = Новый Массив;
	Если ВыборкаПолномочий.Количество() = 0 Или ВыборкаПолномочий[0].ПризнакПолныеПолномочия Тогда
		ПолномочияПредставителя.Добавить("99");
	Иначе
		Для ВидПолномочия = 1 По КоличествоВидовПолномочийПредставителя() Цикл
			ПредставлениеВида = ПредставлениеВидаПолномочия(ВидПолномочия);
			Если ВыборкаПолномочий[0][ПредставлениеВида] Тогда
				ПолномочияПредставителя.Добавить(ВидПолномочияСтрокой(ВидПолномочия));
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	ВыборкаПодписантов = ПакетЗапросов[1].Выгрузить();
	ВыборкаДокументов = ПакетЗапросов[3].Выгрузить();

	ПустойПодписант = Новый Структура("Имя, Отчество, Фамилия", "", "", "");

	ПустойДокумент = Новый Структура;
	ПустойДокумент.Вставить("ВидДок", "");
	ПустойДокумент.Вставить("СерДок", "");
	ПустойДокумент.Вставить("НомДок", "");
	ПустойДокумент.Вставить("ДатаДок", "");
	ПустойДокумент.Вставить("ВыдДок", "");
	ПустойДокумент.Вставить("КодВыдДок", "");

	ВыборкаИнформации = ПакетЗапросов[2].Выбрать();

	Если ВыборкаИнформации.Следующий() Тогда

		Результат.Вставить("СведенияОбИнформационнойСистеме", ВыборкаИнформации.СведенияОбИнформационнойСистеме);

		Результат.Вставить("Номер", ВыборкаИнформации.НомерДоверенности);
		Результат.Вставить("ДатаВыдачи", ВыборкаИнформации.ДатаВыдачи);
		Результат.Вставить("ДатаОкончания", ВыборкаИнформации.ДатаОкончания);
		Результат.Вставить("СрокДействия", ВыборкаИнформации.СрокДействия);
		Результат.Вставить("ВозможноПередоверие", ВыборкаИнформации.ВозможноПередоверие);
		Результат.Вставить("ПредыдущийНомер", ВыборкаИнформации.НомерРодительскойДоверенности);		
		
		// информация об доверителе
		Результат.Вставить("ИностраннаяОрганизация", ВыборкаИнформации.ДоверительЮЛ_ИностраннаяОрганизация);
		Результат.Вставить("ДоверительЮЛ_ИНН", ВыборкаИнформации.ДоверительЮЛ_ИНН);
		Результат.Вставить("ДоверительЮЛ_КПП", ВыборкаИнформации.ДоверительЮЛ_КПП);
		Результат.Вставить("ДоверительЮЛ_ОГРН", ВыборкаИнформации.ДоверительЮЛ_ОГРН);
		Результат.Вставить("ДоверительЮЛ_СтрРег", ВыборкаИнформации.ДоверительЮЛ_СтрРег);
		Результат.Вставить("ДоверительЮЛ_НаимРегОрг", ВыборкаИнформации.ДоверительЮЛ_НаимРегОрг);
		Результат.Вставить("ДоверительЮЛ_РегНомер", ВыборкаИнформации.ДоверительЮЛ_РегНомер);
		Результат.Вставить("ДоверительЮЛ_Адр", ВыборкаИнформации.ДоверительЮЛ_Адр);
		Результат.Вставить("ДоверительФЛ_ИНН", ВыборкаИнформации.ДоверительФЛ_ИНН);
		Результат.Вставить("ДоверительФЛ_ОГРН", ВыборкаИнформации.ДоверительФЛ_ОГРН);
		Результат.Вставить("ДоверительФЛ_СНИЛС", ВыборкаИнформации.ДоверительФЛ_СНИЛС);
		Результат.Вставить("ДоверительФЛ_Гражданство", ВыборкаИнформации.ДоверительФЛ_Гражданство);
		Результат.Вставить("ДоверительФЛ_Пол", ВыборкаИнформации.ДоверительФЛ_Пол);
		Результат.Вставить("ДоверительФЛ_ДатаРождения", ВыборкаИнформации.ДоверительФЛ_ДатаРождения);
		Результат.Вставить("ДоверительФЛ_МестоРожд", ВыборкаИнформации.ДоверительФЛ_МестоРожд);
		Результат.Вставить("ДоверительЮЛ_НаимОрг", ВыборкаИнформации.ДоверительЮЛ_НаимОрг);

		ТипОрганизации = "";			
		Если Результат.ИностраннаяОрганизация Тогда
			ТипОрганизации = "ИО";
		Иначе				
			Если ИнтеграцияЭДО.ЭтоФизЛицо(ВыборкаИнформации.ОрганизацияМЧД) Тогда
				Если ПустаяСтрока(ВыборкаИнформации.ДоверительФЛ_ОГРН) Тогда
					ТипОрганизации = "ФЛ";
				Иначе	
					ТипОрганизации = "ИП";
				КонецЕсли;
			Иначе
				ТипОрганизации = "ЮЛ";
			КонецЕсли;
		КонецЕсли;
		Результат.Вставить("ДоверительЮЛ_ТипОрганизации", ТипОрганизации);			
		
		// информация об подписанте
		Результат.Вставить("ЛицоБезДовЮЛ_НаимОрг", ВыборкаИнформации.ЛицоБезДовЮЛ_НаимОрг);
		Результат.Вставить("ЛицоБезДовЮЛ_ИНН", ВыборкаИнформации.ЛицоБезДовЮЛ_ИНН);
		Результат.Вставить("ЛицоБезДовЮЛ_КПП", ВыборкаИнформации.ЛицоБезДовЮЛ_КПП);
		Результат.Вставить("ЛицоБезДовЮЛ_ОГРН", ВыборкаИнформации.ЛицоБезДовЮЛ_ОГРН);
		Результат.Вставить("ЛицоБезДовФЛ_ИНН", ВыборкаИнформации.ЛицоБезДовФЛ_ИНН);
		Результат.Вставить("ЛицоБезДовФЛ_СНИЛС", ВыборкаИнформации.ЛицоБезДовФЛ_СНИЛС);
		Результат.Вставить("ЛицоБезДовФЛ_Гражданство", ВыборкаИнформации.ЛицоБезДовФЛ_Гражданство);
		Результат.Вставить("ЛицоБезДовФЛ_ДатаРождения", ВыборкаИнформации.ЛицоБезДовФЛ_ДатаРождения);
		Результат.Вставить("ЛицоБезДовФЛ_Должность", ВыборкаИнформации.ЛицоБезДовФЛ_Должность);
		Результат.Вставить("ЛицоБезДовФЛ_ОснованиеПолномочий", ВыборкаИнформации.ЛицоБезДовФЛ_ОснованиеПолномочий);

		Если Результат.ДоверительЮЛ_ТипОрганизации = "ФЛ" ИЛИ Результат.ДоверительЮЛ_ТипОрганизации = "ИП" Тогда
			НашлиСтроки = ВыборкаПодписантов.НайтиСтроки(Новый Структура("Владелец",
				Перечисления.СубъектыДоверенности.ДоверительФЛ));
		Иначе
			НашлиСтроки = ВыборкаПодписантов.НайтиСтроки(Новый Структура("Владелец",
				Перечисления.СубъектыДоверенности.ДоверительРук));
		КонецЕсли;

		Если НашлиСтроки.Количество() = 0 Тогда
			НашлиФИО = ПустойПодписант;
		Иначе
			НашлиФИО = НашлиСтроки[0];
		КонецЕсли;

		Результат.Вставить("ЛицоБезДовФЛ_Имя", НашлиФИО.Имя);
		Результат.Вставить("ЛицоБезДовФЛ_Отчество", НашлиФИО.Отчество);
		Результат.Вставить("ЛицоБезДовФЛ_Фамилия", НашлиФИО.Фамилия);

		НашлиСтроки = ВыборкаДокументов.НайтиСтроки(Новый Структура("Владелец",
			Перечисления.СубъектыДоверенности.ДоверительФЛ));
		Если НашлиСтроки.Количество() = 0 Тогда
			НашлиДок = ПустойДокумент;
		Иначе
			НашлиДок = НашлиСтроки[0];
		КонецЕсли;

		Результат.Вставить("ЛицоБезДовФЛ_ВидДок", НашлиДок.ВидДок);
		Результат.Вставить("ЛицоБезДовФЛ_СерДок", НашлиДок.СерДок);
		Результат.Вставить("ЛицоБезДовФЛ_НомДок", НашлиДок.НомДок);
		Результат.Вставить("ЛицоБезДовФЛ_ДатаДок", НашлиДок.ДатаДок);
		Результат.Вставить("ЛицоБезДовФЛ_ВыдДок", НашлиДок.ВыдДок);
		Результат.Вставить("ЛицоБезДовФЛ_КодВыдДок", НашлиДок.КодВыдДок);
		
		// информация о представителе
		Результат.Вставить("ПредставительЮЛ_НаимОрг", ВыборкаИнформации.ПредставительЮЛ_НаимОрг);
		Результат.Вставить("ПредставительЮЛ_ИНН", ВыборкаИнформации.ПредставительЮЛ_ИНН);
		Результат.Вставить("ПредставительЮЛ_КПП", ВыборкаИнформации.ПредставительЮЛ_КПП);
		Результат.Вставить("ПредставительЮЛ_ОГРН", ВыборкаИнформации.ПредставительЮЛ_ОГРН);
		Результат.Вставить("ПредставительФЛ_ИНН", ВыборкаИнформации.ПредставительФЛ_ИНН);
		Результат.Вставить("ПредставительФЛ_ОГРН", ВыборкаИнформации.ПредставительФЛ_ОГРН);
		Результат.Вставить("ПредставительФЛ_СНИЛС", ВыборкаИнформации.ПредставительФЛ_СНИЛС);
		Результат.Вставить("ПредставительФЛ_Гражданство", ВыборкаИнформации.ПредставительФЛ_Гражданство);
		Результат.Вставить("ПредставительФЛ_ДатаРождения", ВыборкаИнформации.ПредставительФЛ_ДатаРождения);
		Результат.Вставить("ПредставительЮЛ_Полномочия", ПолномочияПредставителя);

		ТипВладельца = Перечисления.СубъектыДоверенности.ПредставительФЛ;
		НашлиСтроки = ВыборкаПодписантов.НайтиСтроки(Новый Структура("Владелец", ТипВладельца));

		Если НашлиСтроки.Количество() = 0 Тогда
			НашлиФИО = ПустойПодписант;
		Иначе
			НашлиФИО = НашлиСтроки[0];
		КонецЕсли;

		Результат.Вставить("ПредставительФЛ_Имя", НашлиФИО.Имя);
		Результат.Вставить("ПредставительФЛ_Отчество", НашлиФИО.Отчество);
		Результат.Вставить("ПредставительФЛ_Фамилия", НашлиФИО.Фамилия);

		НашлиСтроки = ВыборкаДокументов.НайтиСтроки(Новый Структура("Владелец", ТипВладельца));

		Если НашлиСтроки.Количество() = 0 Тогда
			НашлиДок = ПустойДокумент;
		Иначе
			НашлиДок = НашлиСтроки[0];
		КонецЕсли;

		Результат.Вставить("ПредставительФЛ_ВидДок", НашлиДок.ВидДок);
		Результат.Вставить("ПредставительФЛ_СерДок", НашлиДок.СерДок);
		Результат.Вставить("ПредставительФЛ_НомДок", НашлиДок.НомДок);
		Результат.Вставить("ПредставительФЛ_ДатаДок", НашлиДок.ДатаДок);
		Результат.Вставить("ПредставительФЛ_ВыдДок", НашлиДок.ВыдДок);
		Результат.Вставить("ПредставительФЛ_КодВыдДок", НашлиДок.КодВыдДок);

		ТипПредставителя = "ФЛ";
		Если Не ПустаяСтрока(Результат.ПредставительЮЛ_ИНН) Тогда
			СведенияЮрЛица = Новый Структура;
			СведенияЮрЛица.Вставить("ИНН", Результат.ПредставительЮЛ_ИНН);
			СведенияЮрЛица.Вставить("КПП", Результат.ПредставительЮЛ_КПП);
			СведенияЮрЛица.Вставить("ОГРН", Результат.ПредставительЮЛ_ОГРН);
			ПредставительОрганизация = ЭлектронноеВзаимодействиеПереопределяемый.ПолучитьОрганизациюМЧД(
				СведенияЮрЛица);				
			Если ИнтеграцияЭДО.ЭтоФизЛицо(ПредставительОрганизация) Тогда
				Если ПустаяСтрока(Результат.ПредставительЮЛ_ОГРН) Тогда
					ТипПредставителя = "ФЛ";
				Иначе	
					ТипПредставителя = "ИП";
				КонецЕсли;	
			Иначе
				ТипПредставителя = "ЮЛ";
			КонецЕсли;			
		КонецЕсли;
		Результат.Вставить("ТипПредставителя", ТипПредставителя);

	КонецЕсли;

	Результат.Вставить("ИныеСведения", "");

	Возврат Результат;

КонецФункции

Функция КоличествоВидовПолномочийПредставителя()

	Возврат 25;

КонецФункции

Функция ПредставлениеВидаПолномочия(ВидПолномочия)

	Возврат "Признак" + ВидПолномочияСтрокой(ВидПолномочия);

КонецФункции

Функция ВидПолномочияСтрокой(ВидПолномочия)

	Возврат Формат(ВидПолномочия, "ЧЦ=2; ЧВН=");

КонецФункции

// Возвращает сконвертированные данные доверенности в строку XML.
// 
// Параметры:
//  ДанныеДоверенности - См. ДанныеИзИнформационнойБазы
// 
// Возвращаемое значение:
//  Строка - строка XML из данных XML
Функция ДанныеДоверенностиВСтрокуXML(ДанныеДоверенности)

	Ошибки = Неопределено;
	ПространствоИмен = МашиночитаемыеДоверенности.ФорматМЧД_2022();	

	Доверенность = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("Доверенность", ПространствоИмен);
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(Доверенность, "ВерсФорм", "001", Истина, Ошибки);

	Документ = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("Доверенность.Документ", ПространствоИмен);
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(Документ, "ОКУД", ДанныеДоверенности.ОКУД, Истина, Ошибки);
	
	// формирование раздела Сведения о доверенности
	СвДов = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("Доверенность.Документ.СвДов", ПространствоИмен);
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвДов, "НомДовер", ДанныеДоверенности.Номер, Истина, Ошибки);
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвДов, "ДатаВыдДовер", ДанныеДоверенности.ДатаВыдачи, Истина, Ошибки);
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвДов, "ДатаКонДовер", ДанныеДоверенности.ДатаОкончания, Истина, Ошибки);
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвДов, "ПрПередов", ?(ДанныеДоверенности.ВозможноПередоверие, "1", "2"),
		Истина, Ошибки);
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвДов, "ПрСовмПолн", "1", Истина, Ошибки); // полномочия совместные	
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвДов, "ВнНомДовер", ДанныеДоверенности.Номер, Истина, Ошибки);
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвДов, "ДатаВнРегДовер", ДанныеДоверенности.ДатаВыдачи, Истина, Ошибки);

	Безотзыв = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("Доверенность.Документ.СвДов.Безотзыв", ПространствоИмен);
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(Безотзыв, "ПрБезотзыв", "1", Истина, Ошибки); // отзыв возможен
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(Безотзыв, "УслОтзыва", "1", Истина, Ошибки); // по истечению срока действия
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвДов, "Безотзыв", Безотзыв, Истина, Ошибки);
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвДов, "СведСистОтм", ДанныеДоверенности.СведенияОбИнформационнойСистеме,
		Истина, Ошибки);
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(Документ, "СвДов", СвДов, Ложь, Ошибки);
	
	// формирование раздела Сведения о доверителе
	СвДоверит = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("Доверенность.Документ.СвДоверит", ПространствоИмен);
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвДоверит, "ТипДовер", ДанныеДоверенности.ДоверительЮЛ_ТипОрганизации,
		Истина, Ошибки);

	ЛицоБезДовФЛ_Фамилия = ?(ПустаяСтрока(ДанныеДоверенности.ЛицоБезДовФЛ_Фамилия), "_", 
		ДанныеДоверенности.ЛицоБезДовФЛ_Фамилия);
	ЛицоБезДовФЛ_Имя = ?(ПустаяСтрока(ДанныеДоверенности.ЛицоБезДовФЛ_Имя), "_",
		ДанныеДоверенности.ЛицоБезДовФЛ_Имя);
	ЛицоБезДовФЛ_Отчество = ?(ПустаяСтрока(ДанныеДоверенности.ЛицоБезДовФЛ_Отчество), "_",
		ДанныеДоверенности.ЛицоБезДовФЛ_Отчество);

	Если ДанныеДоверенности.ДоверительЮЛ_ТипОрганизации = "ИО" Тогда

		ИнОргДовер = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("Доверенность.Документ.СвДоверит.ИнОргДовер", 
			ПространствоИмен);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(ИнОргДовер, "НаимИО", ДанныеДоверенности.ДоверительЮЛ_НаимОрг, 
			Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(ИнОргДовер, "ИННЮЛ", ДанныеДоверенности.ДоверительЮЛ_ИНН, 
			Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(ИнОргДовер, "КПП", ДанныеДоверенности.ДоверительЮЛ_КПП, 
			Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(ИнОргДовер, "СтрРег", ДанныеДоверенности.ДоверительЮЛ_СтрРег, 
			Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(ИнОргДовер, "НаимРегОрг", ДанныеДоверенности.ДоверительЮЛ_НаимРегОрг,
			Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(ИнОргДовер, "РегНомер", ДанныеДоверенности.ДоверительЮЛ_РегНомер,
			Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(ИнОргДовер, "АдрСтрРег", ДанныеДоверенности.ДоверительЮЛ_Адр, 
			Истина, Ошибки);

		СвРукОП = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("Доверенность.Документ.СвДоверит.ИнОргДовер.СвРукОП",
			ПространствоИмен);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвРукОП, "ИННФЛ", ДанныеДоверенности.ДоверительФЛ_ИНН, Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвРукОП, "НаимДокПолн", "_", Истина, Ошибки);

		СведФЛ = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("СведФЛТип", ПространствоИмен);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "ДатаРожд", ДанныеДоверенности.ДоверительФЛ_ДатаРождения,
			Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "МестоРожд", ДанныеДоверенности.ДоверительФЛ_МестоРожд, 
			Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "Пол", ДанныеДоверенности.ДоверительФЛ_Пол, Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "КонтактТлф", "_", Истина, Ошибки);

		Если ДанныеДоверенности.ДоверительФЛ_Гражданство = Справочники.СтраныМира.Россия Тогда
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "ПрГражд", "1", Истина, Ошибки);
		ИначеЕсли ЗначениеЗаполнено(ДанныеДоверенности.ДоверительФЛ_Гражданство) Тогда
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "ПрГражд", "2", Истина, Ошибки);
		Иначе
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "ПрГражд", "3", Истина, Ошибки);
		КонецЕсли;

		ДоверительФЛ_Гражданство = "";
		Если ЗначениеЗаполнено(ДанныеДоверенности.ДоверительФЛ_Гражданство) Тогда
			ДоверительФЛ_Гражданство = ДанныеДоверенности.ДоверительФЛ_Гражданство.Код;
		КонецЕсли;
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "Гражданство", ДоверительФЛ_Гражданство, Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвРукОП, "СведФЛ", СведФЛ, Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(ИнОргДовер, "СвРукОП", СвРукОП, Истина, Ошибки);
		СвДоверит.ИнОргДовер.Добавить(ИнОргДовер);

	ИначеЕсли ДанныеДоверенности.ДоверительЮЛ_ТипОрганизации = "ЮЛ" Тогда

		РосОргДовер = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("Доверенность.Документ.СвДоверит.РосОргДовер", 
			ПространствоИмен);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(РосОргДовер, "НаимОрг", ДанныеДоверенности.ДоверительЮЛ_НаимОрг,
			Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(РосОргДовер, "ОГРН", ДанныеДоверенности.ДоверительЮЛ_ОГРН, 
			Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(РосОргДовер, "ИННЮЛ", ДанныеДоверенности.ДоверительЮЛ_ИНН, 
			Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(РосОргДовер, "КПП", ДанныеДоверенности.ДоверительЮЛ_КПП, 
			Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(РосОргДовер, "АдрРФ", ДанныеДоверенности.ДоверительЮЛ_Адр, 
			Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(РосОргДовер, "АдрМНФакт", "_", 
			Истина, Ошибки);	
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(РосОргДовер, "НаимУчрДок", "_", 
			Истина, Ошибки);	
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(РосОргДовер, "Тлф", "_", 
			Истина, Ошибки);

		ЛицоБезДов = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("Доверенность.Документ.СвДоверит.РосОргДовер.ЛицоБезДов",
			ПространствоИмен);

		Если Не ПустаяСтрока(ДанныеДоверенности.ЛицоБезДовЮЛ_ИНН) Тогда
			СвОрг = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("СвОргТип", ПространствоИмен);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвОрг, "НаимОрг", ДанныеДоверенности.ЛицоБезДовЮЛ_НаимОрг, 
				Истина, Ошибки);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвОрг, "ИННЮЛ", ДанныеДоверенности.ЛицоБезДовЮЛ_ИНН, 
				Истина, Ошибки);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвОрг, "КПП", ДанныеДоверенности.ЛицоБезДовЮЛ_КПП, 
				Истина, Ошибки);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвОрг, "ОГРН", ДанныеДоверенности.ЛицоБезДовЮЛ_ОГРН, 
				Истина, Ошибки);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвОрг, "НаимУчрДок", "_", Истина, Ошибки);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвОрг, "КонтактТлф", "_", Истина, Ошибки);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвОрг, "РегНомер", "_", Истина, Ошибки);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(ЛицоБезДов, "СвОрг", СвОрг, Истина, Ошибки);
		КонецЕсли;

		СвФЛ = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("Доверенность.Документ.СвДоверит.РосОргДовер.ЛицоБезДов.СвФЛ",
			ПространствоИмен);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвФЛ, "ИННФЛ", ДанныеДоверенности.ЛицоБезДовФЛ_ИНН, 
			Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвФЛ, "СНИЛС", ДанныеДоверенности.ЛицоБезДовФЛ_СНИЛС, 
			Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвФЛ, "Должность", ДанныеДоверенности.ЛицоБезДовФЛ_Должность, 
			Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвФЛ, "НаимДокПолн",
			ДанныеДоверенности.ЛицоБезДовФЛ_ОснованиеПолномочий, Истина, Ошибки);

		СведФЛ = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("СведФЛТип", ПространствоИмен);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "ДатаРожд", ДанныеДоверенности.ЛицоБезДовФЛ_ДатаРождения,
			Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "МестоРожд", "_", Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "КонтактТлф", "_", Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "Пол", "1", Истина, Ошибки);

		Если ДанныеДоверенности.ЛицоБезДовФЛ_Гражданство = Справочники.СтраныМира.Россия Тогда
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "ПрГражд", "1", Истина, Ошибки);
		ИначеЕсли ЗначениеЗаполнено(ДанныеДоверенности.ЛицоБезДовФЛ_Гражданство) Тогда
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "ПрГражд", "2", Истина, Ошибки);
		Иначе
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "ПрГражд", "3", Истина, Ошибки);
		КонецЕсли;

		ЛицоБезДовФЛ_Гражданство = "";
		Если ЗначениеЗаполнено(ДанныеДоверенности.ЛицоБезДовФЛ_Гражданство) Тогда
			ЛицоБезДовФЛ_Гражданство = ДанныеДоверенности.ЛицоБезДовФЛ_Гражданство.Код;
		КонецЕсли;
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "Гражданство", ЛицоБезДовФЛ_Гражданство, Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвФЛ, "СведФЛ", СведФЛ, Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(ЛицоБезДов, "СвФЛ", СвФЛ, Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(РосОргДовер, "ЛицоБезДов", ЛицоБезДов, Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвДоверит, "РосОргДовер", РосОргДовер, Истина, Ошибки);

	ИначеЕсли ДанныеДоверенности.ДоверительЮЛ_ТипОрганизации = "ИП" Тогда

		ИПДовер = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("Доверенность.Документ.СвДоверит.ИПДовер", ПространствоИмен);
		ЧастиФИО = Новый Массив;
		Если Не ПустаяСтрока(ДанныеДоверенности.ЛицоБезДовФЛ_Фамилия) Тогда
			ЧастиФИО.Добавить(ДанныеДоверенности.ЛицоБезДовФЛ_Фамилия);
		КонецЕсли;
		Если Не ПустаяСтрока(ДанныеДоверенности.ЛицоБезДовФЛ_Имя) Тогда
			ЧастиФИО.Добавить(ДанныеДоверенности.ЛицоБезДовФЛ_Имя);
		КонецЕсли;
		Если Не ПустаяСтрока(ДанныеДоверенности.ЛицоБезДовФЛ_Отчество) Тогда
			ЧастиФИО.Добавить(ДанныеДоверенности.ЛицоБезДовФЛ_Отчество);
		КонецЕсли;
		НаименованиеИП = СтрСоединить(ЧастиФИО, " ");
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(ИПДовер, "НаимИП", НаименованиеИП, 
			Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(ИПДовер, "ИННФЛ", ДанныеДоверенности.ДоверительФЛ_ИНН, 
			Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(ИПДовер, "ОГРНИП", ДанныеДоверенности.ДоверительФЛ_ОГРН, 
			Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(ИПДовер, "СНИЛС", ДанныеДоверенности.ДоверительФЛ_СНИЛС, 
			Истина, Ошибки);

		СведФЛ = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("СведФЛТип", ПространствоИмен);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "ДатаРожд", ДанныеДоверенности.ДоверительФЛ_ДатаРождения,
			Истина, Ошибки);
		Если Не ПустаяСтрока(ДанныеДоверенности.ДоверительФЛ_МестоРожд) Тогда
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "МестоРожд", ДанныеДоверенности.ДоверительФЛ_МестоРожд, 
				Истина, Ошибки);
		КонецЕсли;

		Если ДанныеДоверенности.ДоверительФЛ_Гражданство = Справочники.СтраныМира.Россия Тогда
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "ПрГражд", "1", Истина, Ошибки);
		ИначеЕсли ЗначениеЗаполнено(ДанныеДоверенности.ДоверительФЛ_Гражданство) Тогда
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "ПрГражд", "2", Истина, Ошибки);
		Иначе
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "ПрГражд", "3", Истина, Ошибки);
		КонецЕсли;

		ДоверительФЛ_Гражданство = "";
		Если ЗначениеЗаполнено(ДанныеДоверенности.ДоверительФЛ_Гражданство) Тогда
			ДоверительФЛ_Гражданство = ДанныеДоверенности.ДоверительФЛ_Гражданство.Код;
		КонецЕсли;
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "Гражданство", ДоверительФЛ_Гражданство, Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(ИПДовер, "СведФЛ", СведФЛ, Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвДоверит, "ИПДовер", ИПДовер, Истина, Ошибки);

	Иначе

		ФЛДовер = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("Доверенность.Документ.СвДоверит.ФЛДовер", ПространствоИмен);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(ФЛДовер, "ПрДеесп", "1", Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(ФЛДовер, "ИННФЛ", ДанныеДоверенности.ДоверительФЛ_ИНН, Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(ФЛДовер, "СНИЛС", ДанныеДоверенности.ДоверительФЛ_СНИЛС, Истина, Ошибки);

		СведФЛ = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("СведФЛТип", ПространствоИмен);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "ДатаРожд", ДанныеДоверенности.ДоверительФЛ_ДатаРождения,
			Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "МестоРожд", ДанныеДоверенности.ДоверительФЛ_МестоРожд, 
			Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "КонтактТлф", "_", Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "Пол", "1", Истина, Ошибки);

		Если ДанныеДоверенности.ДоверительФЛ_Гражданство = Справочники.СтраныМира.Россия Тогда
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "ПрГражд", "1", Истина, Ошибки);
		ИначеЕсли ЗначениеЗаполнено(ДанныеДоверенности.ДоверительФЛ_Гражданство) Тогда
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "ПрГражд", "2", Истина, Ошибки);
		Иначе
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "ПрГражд", "3", Истина, Ошибки);
		КонецЕсли;

		ДоверительФЛ_Гражданство = "";
		Если ЗначениеЗаполнено(ДанныеДоверенности.ДоверительФЛ_Гражданство) Тогда
			ДоверительФЛ_Гражданство = ДанныеДоверенности.ДоверительФЛ_Гражданство.Код;
		КонецЕсли;
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "Гражданство", ДоверительФЛ_Гражданство, Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(ФЛДовер, "СведФЛ", СведФЛ, Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвДоверит, "ФЛДовер", ФЛДовер, Истина, Ошибки);

	КонецЕсли;

	Подписант = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("ФИО", ПространствоИмен);
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(Подписант, "Фамилия", ЛицоБезДовФЛ_Фамилия, Истина, Ошибки);
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(Подписант, "Имя", ЛицоБезДовФЛ_Имя, Истина, Ошибки);
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(Подписант, "Отчество", ЛицоБезДовФЛ_Отчество, Истина, Ошибки);
	СвДоверит.Подписант.Добавить(Подписант);

	Документ.СвДоверит.Добавить(СвДоверит);
	
	// формирование раздела Сведения об уполномоченном представителе
	СвУпПред = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("Доверенность.Документ.СвУпПред", ПространствоИмен);
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвУпПред, "ТипПред", ДанныеДоверенности.ТипПредставителя, Истина, Ошибки);

	СвПред = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("Доверенность.Документ.СвУпПред.СвПред", ПространствоИмен);

	Если Не ПустаяСтрока(ДанныеДоверенности.ПредставительЮЛ_ИНН) Тогда

		Если ДанныеДоверенности.ТипПредставителя = "ИП" Тогда

			СведИП = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("Доверенность.Документ.СвУпПред.СвПред.СведИП", 
				ПространствоИмен);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведИП, "НаимИП", ДанныеДоверенности.ПредставительЮЛ_НаимОрг,
				Истина, Ошибки);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведИП, "ИННФЛ", ДанныеДоверенности.ПредставительЮЛ_ИНН, 
				Истина, Ошибки);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведИП, "ОГРНИП", ДанныеДоверенности.ПредставительЮЛ_ОГРН, 
				Истина, Ошибки);

			ПредставительФЛ_Фамилия = ?(ПустаяСтрока(ДанныеДоверенности.ПредставительФЛ_Фамилия), "_",
				ДанныеДоверенности.ПредставительФЛ_Фамилия);
			ПредставительФЛ_Имя = ?(ПустаяСтрока(ДанныеДоверенности.ПредставительФЛ_Имя), "_",
				ДанныеДоверенности.ПредставительФЛ_Имя);
			ПредставительФЛ_Отчество = ?(ПустаяСтрока(ДанныеДоверенности.ПредставительФЛ_Отчество), "_",
				ДанныеДоверенности.ПредставительФЛ_Отчество);

			ФИО = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("ФИО", ПространствоИмен);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(ФИО, "Фамилия", ПредставительФЛ_Фамилия, Истина, Ошибки);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(ФИО, "Имя", ПредставительФЛ_Имя, Истина, Ошибки);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(ФИО, "Отчество", ПредставительФЛ_Отчество, Истина, Ошибки);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведИП, "ФИО", ФИО, Истина, Ошибки);

			СведФЛ = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("СведФЛТип", ПространствоИмен);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "ДатаРожд", ДанныеДоверенности.ПредставительФЛ_ДатаРождения,
				Истина, Ошибки);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "МестоРожд", "_", Истина, Ошибки);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "КонтактТлф", "_", Истина, Ошибки);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "Пол", "1", Истина, Ошибки);

			Если ДанныеДоверенности.ПредставительФЛ_Гражданство = Справочники.СтраныМира.Россия Тогда
				РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "ПрГражд", "1", Истина, Ошибки);
			ИначеЕсли ЗначениеЗаполнено(ДанныеДоверенности.ПредставительФЛ_Гражданство) Тогда
				РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "ПрГражд", "2", Истина, Ошибки);
			Иначе
				РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "ПрГражд", "3", Истина, Ошибки);
			КонецЕсли;

			ПредставительФЛ_Гражданство = "";
			Если ЗначениеЗаполнено(ДанныеДоверенности.ПредставительФЛ_Гражданство) Тогда
				ПредставительФЛ_Гражданство = ДанныеДоверенности.ПредставительФЛ_Гражданство.Код;
			КонецЕсли;
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "Гражданство", ПредставительФЛ_Гражданство, Истина, Ошибки);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведИП, "СведФЛ", СведФЛ, Истина, Ошибки);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвПред, "СведИП", СведИП, Истина, Ошибки);

		Иначе

			СвОрг = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("СвОргТип", ПространствоИмен);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвОрг, "НаимОрг", ДанныеДоверенности.ПредставительЮЛ_НаимОрг,
				Истина, Ошибки);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвОрг, "ИННЮЛ", ДанныеДоверенности.ПредставительЮЛ_ИНН, 
				Истина, Ошибки);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвОрг, "КПП", ДанныеДоверенности.ПредставительЮЛ_КПП, 
				Истина, Ошибки);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвОрг, "ОГРН", ДанныеДоверенности.ПредставительЮЛ_ОГРН, 
				Истина, Ошибки);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвОрг, "НаимУчрДок", "_", Истина, Ошибки);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвОрг, "КонтактТлф", "_", Истина, Ошибки);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвОрг, "РегНомер", "_", Истина, Ошибки);
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвПред, "СвОрг", СвОрг, Истина, Ошибки);

		КонецЕсли;

	КонецЕсли;

	Если Не ПустаяСтрока(ДанныеДоверенности.ПредставительФЛ_ИНН) Тогда

		СведФизЛ = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("Доверенность.Документ.СвУпПред.СвПред.СведФизЛ", 
			ПространствоИмен);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФизЛ, "ИННФЛ", ДанныеДоверенности.ПредставительФЛ_ИНН, 
			Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФизЛ, "СНИЛС", ДанныеДоверенности.ПредставительФЛ_СНИЛС, 
			Истина, Ошибки);

		ПредставительФЛ_Фамилия = ?(ПустаяСтрока(ДанныеДоверенности.ПредставительФЛ_Фамилия), "_",
			ДанныеДоверенности.ПредставительФЛ_Фамилия);
		ПредставительФЛ_Имя = ?(ПустаяСтрока(ДанныеДоверенности.ПредставительФЛ_Имя), "_",
			ДанныеДоверенности.ПредставительФЛ_Имя);
		ПредставительФЛ_Отчество = ?(ПустаяСтрока(ДанныеДоверенности.ПредставительФЛ_Отчество), "_",
			ДанныеДоверенности.ПредставительФЛ_Отчество);

		ФИО = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("ФИО", ПространствоИмен);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(ФИО, "Фамилия", ПредставительФЛ_Фамилия, Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(ФИО, "Имя", ПредставительФЛ_Имя, Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(ФИО, "Отчество", ПредставительФЛ_Отчество, Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФизЛ, "ФИО", ФИО, Истина, Ошибки);

		СведФЛ = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("СведФЛТип", ПространствоИмен);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "ДатаРожд", ДанныеДоверенности.ПредставительФЛ_ДатаРождения,
			Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "МестоРожд", "_", Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "КонтактТлф", "_", Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "Пол", "1", Истина, Ошибки);

		Если ДанныеДоверенности.ПредставительФЛ_Гражданство = Справочники.СтраныМира.Россия Тогда
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "ПрГражд", "1", Истина, Ошибки);
		ИначеЕсли ЗначениеЗаполнено(ДанныеДоверенности.ПредставительФЛ_Гражданство) Тогда
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "ПрГражд", "2", Истина, Ошибки);
		Иначе
			РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "ПрГражд", "3", Истина, Ошибки);
		КонецЕсли;

		ПредставительФЛ_Гражданство = "";
		Если ЗначениеЗаполнено(ДанныеДоверенности.ПредставительФЛ_Гражданство) Тогда
			ПредставительФЛ_Гражданство = ДанныеДоверенности.ПредставительФЛ_Гражданство.Код;
		КонецЕсли;
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "Гражданство", ПредставительФЛ_Гражданство, Истина, Ошибки);

		УдЛичнФЛ = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("УдЛичнФЛТип", ПространствоИмен);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(УдЛичнФЛ, "КодВидДок", ДанныеДоверенности.ПредставительФЛ_ВидДок,
			Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(УдЛичнФЛ, "СерНомДок", СокрЛП(ДанныеДоверенности.ПредставительФЛ_СерДок
			+ " " + ДанныеДоверенности.ПредставительФЛ_НомДок), Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(УдЛичнФЛ, "ДатаДок", ДанныеДоверенности.ПредставительФЛ_ДатаДок,
			Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(УдЛичнФЛ, "ВыдДок", ДанныеДоверенности.ПредставительФЛ_ВыдДок, 
			Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(УдЛичнФЛ, "КодВыдДок", ДанныеДоверенности.ПредставительФЛ_КодВыдДок,
			Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФЛ, "УдЛичнФЛ", УдЛичнФЛ, Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СведФизЛ, "СведФЛ", СведФЛ, Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвПред, "СведФизЛ", СведФизЛ, Истина, Ошибки);

	КонецЕсли;

	СвУпПред.СвПред.Добавить(СвПред);
	Документ.СвУпПред.Добавить(СвУпПред);	
	
	// формирование раздела Сведения о полномочиях представителя (представителей)
	СвПолн = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("Доверенность.Документ.СвПолн", ПространствоИмен);
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(СвПолн, "ТекстПолн", "Все полномочия", Истина, Ошибки);
	Документ.СвПолн.Добавить(СвПолн);
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(Доверенность, "Документ", Документ, Истина, Ошибки);
	
	// Иные сведения по соглашению участников электронного взаимодействия
	// и по требованиям федеральных органов исполнительной власти.
	Если Не ПустаяСтрока(ДанныеДоверенности.ИныеСведения) Тогда
		ИнСвед = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML("Доверенность.ИнСвед", ПространствоИмен);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(ИнСвед, "ИнСвед", ДанныеДоверенности.ИныеСведения, Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(Доверенность, "ИнСвед", ИнСвед, Истина, Ошибки);
	КонецЕсли;

	СтрокаXML = "";

	Попытка

		Если ЗначениеЗаполнено(Ошибки) Тогда
			ТекстОшибки = ОбщегоНазначенияБЭД.СоединитьОшибки(Ошибки);
			ВызватьИсключение ТекстОшибки;
		КонецЕсли;

		ДвоичныеДанныеXML = РаботаСФайламиБЭД.XDTOВДвоичныеДанные(Доверенность, Ложь);
		ДвоичныеДанныеXML = ОбщегоНазначенияБЭД.УдалитьПространствоИмен(ДвоичныеДанныеXML, ПространствоИмен);
		СтрокаXML = ПолучитьСтрокуИзДвоичныхДанных(ДвоичныеДанныеXML, "windows-1251");

	Исключение

		ВидОперации = НСтр("ru = 'Формирование xml по данным МЧД.'");
		ПодробныйТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЭлектронноеВзаимодействие.ОбработатьОшибку(ВидОперации, ПодробныйТекстОшибки);
		ВызватьИсключение НСтр("ru = 'Ошибка формата МЧД. Проверьте введенные данные.'");

	КонецПопытки;

	Возврат СтрокаXML;

КонецФункции	

#КонецОбласти

#КонецОбласти

#КонецЕсли