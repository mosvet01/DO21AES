#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
	
	Если ПолучитьФункциональнуюОпцию("РаздельныйУчетДокументов") Тогда 
		ПроверяемыеРеквизиты.Добавить("ФормаДокумента");
	КонецЕсли;
	
	Если Не ЭтоГруппа И Не УчитыватьНедействующиеДокументы Тогда
		НепроверяемыеРеквизиты.Добавить("ВариантУказанияОтменяющегоДокумента");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ЭтоНовый() И Не ЭтоГруппа Тогда
		ФормаДокумента = Перечисления.ВариантыФормДокументов.БумажнаяИлиЭлектронная;
		УчитыватьМестоХранения = Перечисления.ВариантыИспользованияПолейХранения.НеУчитывать;
		УчитыватьОтветственногоЗаХранение = Перечисления.ВариантыИспользованияПолейХранения.НеУчитывать;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоГруппа Тогда 
		Возврат;
	КонецЕсли;
	
	ВключенУчетПоНоменклатуреДел = ВестиУчетПоНоменклатуреДел И Константы.ИспользоватьНоменклатуруДел.Получить();
	
	// Подсистема Свойства
	УправлениеСвойствами.ПередЗаписьюВидаОбъекта(ЭтотОбъект, "Справочник_ВнутренниеДокументы");
	
	ПредыдущаяПометкаУдаления = Ложь;
	Если ЗначениеЗаполнено(Ссылка) Тогда
		ПредыдущаяПометкаУдаления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ПометкаУдаления");
	КонецЕсли;
	
	Если ПометкаУдаления <> ПредыдущаяПометкаУдаления Тогда 
		Если ДокументооборотПраваДоступа.ПолучитьПраваПоОбъекту(Ссылка).Удаление Тогда 
			ШаблоныДокументов.ПометитьНаУдалениеШаблоныДокументов(Ссылка, ПометкаУдаления);
		Иначе
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'У вас нет права ""Пометка на удаление"" вида документа ""%1"".'"),
				Строка(Ссылка));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	ДокументооборотПраваДоступа.ПриЗаписиРазрезаДоступа(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Если ЭтоГруппа Тогда 
		Возврат;
	КонецЕсли;
	
	НаборСвойств = Неопределено;
	
КонецПроцедуры

#КонецЕсли