
Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	ИспользоватьСВД = ПолучитьФункциональнуюОпцию("ИспользоватьСВД");
	ИспользоватьМЭДО = ПолучитьФункциональнуюОпцию("ИспользоватьМЭДО");
	Если Не ИспользоватьСВД Или Не ИспользоватьМЭДО Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	СпособыДоставки.Ссылка,
			|	СпособыДоставки.Наименование
			|ИЗ
			|	Справочник.СпособыДоставки КАК СпособыДоставки
			|ГДЕ
			|	СпособыДоставки.Наименование ПОДОБНО &Текст
			|	И СпособыДоставки.Наименование Не В (&Массив)";
		
		Массив = Новый Массив;
		Если Не ИспользоватьСВД Тогда
			Массив.Добавить("СВД");
		КонецЕсли;
		Если Не ИспользоватьМЭДО Тогда
			Массив.Добавить("МЭДО");
		КонецЕсли;
		
		Запрос.УстановитьПараметр("Текст", Строка(Параметры.СтрокаПоиска) + "%");
		Запрос.УстановитьПараметр("Массив", Массив);
		
		ДанныеВыбора = Новый СписокЗначений;
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			
			ДанныеВыбора.Добавить(Выборка.Ссылка, Выборка.Наименование);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры
