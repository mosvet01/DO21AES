#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗагрузитьНастройки();
	
	Элементы.ПериодАвтообновления.Доступность = НастройкаАвтообновление;
	
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		НастроитьЭлементыФормыДляМобильногоУстройства();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтображатьВремяСПриИзменении(Элемент)
	
	ОтображатьВремяПоНормализованное = 24 - ОтображатьВремяПо;
	Если ОтображатьВремяС >= ОтображатьВремяПоНормализованное Тогда
		ОтображатьВремяПоНормализованное = ОтображатьВремяС + 1;
		ОтображатьВремяПо = 24 - ОтображатьВремяПоНормализованное;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтображатьВремяПоПриИзменении(Элемент)
	
	ОтображатьВремяПоНормализованное = 24 - ОтображатьВремяПо;
	Если ОтображатьВремяС >= ОтображатьВремяПоНормализованное Тогда
		ОтображатьВремяС = ОтображатьВремяПоНормализованное - 1;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АвтообновлениеПриИзменении(Элемент)
	
	Элементы.ПериодАвтообновления.Доступность = НастройкаАвтообновление;
	Если ПериодАвтообновления < 10 Тогда
		ПериодАвтообновления = 300; // Значение по умолчанию если период еще не задан
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодАвтообновленияПриИзменении(Элемент)
	
	Если ПериодАвтообновления < 10 Тогда
		ПериодАвтообновления = 10; // Не меньше 10 секунд
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	ГотовоНаСервере();
	Оповестить("Запись_НастройкиКалендаря");
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьДоступноеВремя(Команда)
	
	РаботаСРабочимКалендаремКлиент.ОткрытьФормуНастройкиДоступногоВремени();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ГотовоНаСервере()
	
	СохранитьНастройки();
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройки()
	
	РаботаСРабочимКалендаремСервер.УстановитьПерсональнуюНастройку(
		"СкрытьОтсутствияБудуРазбиратьЗадачи", СкрытьОтсутствияБудуРазбиратьЗадачи);
	РаботаСРабочимКалендаремСервер.УстановитьПерсональнуюНастройку(
		"ОтображатьВремяС", ОтображатьВремяС);
	РаботаСРабочимКалендаремСервер.УстановитьПерсональнуюНастройку(
		"ОтображатьВремяПо", ОтображатьВремяПо);
	РаботаСРабочимКалендаремСервер.УстановитьПерсональнуюНастройку(
		"ОтображениеВремениЭлементов", ОтображениеВремениЭлементов);
	РаботаСРабочимКалендаремСервер.УстановитьПерсональнуюНастройку(
		"ОтображатьЗанятость", ОтображатьЗанятость);
	РаботаСРабочимКалендаремСервер.УстановитьПерсональнуюНастройку(
		"ИспользоватьБыстроеРедактирование", ИспользоватьБыстроеРедактирование);
	РаботаСРабочимКалендаремСервер.УстановитьПерсональнуюНастройку(
		"ОтображатьПомеченныеНаУдаление", ОтображатьПомеченныеНаУдаление);
	РаботаСРабочимКалендаремСервер.УстановитьПерсональнуюНастройку(
		"РазмерЯчейкиВремени", РазмерЯчейкиВремени);
	РаботаСРабочимКалендаремСервер.УстановитьПерсональнуюНастройку(
		"ОтображатьОтклоненные", ОтображатьОтклоненные);
	РаботаСРабочимКалендаремСервер.УстановитьПерсональнуюНастройку(
		"ОтображатьКалендарьВЗаписиКалендаря", ОтображатьКалендарьВЗаписиКалендаря);
	РаботаСРабочимКалендаремСервер.УстановитьПерсональнуюНастройку(
		"ОтображатьЛегенду", ОтображатьЛегенду);
	
	МассивСтруктур = Новый Массив;
	// НапоминанияПользователя
	ДобавитьСтруктуруНастройки(МассивСтруктур, "НастройкиНапоминаний", "ИнтервалПроверкиНапоминаний",
		ИнтервалПроверкиНапоминаний);
	ДобавитьСтруктуруНастройки(МассивСтруктур, "НастройкиНапоминаний", "СрокНапоминанияПоУмолчанию",
		СрокНапоминанияПоУмолчанию);
	ДобавитьСтруктуруНастройки(МассивСтруктур, "НастройкиНапоминаний", "УстанавливатьНапоминаниеАвтоматически",
		УстанавливатьНапоминаниеАвтоматически);
	// Конец НапоминанияПользователя
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранитьМассив(МассивСтруктур);
	
	Результат = Новый Структура;
	Результат.Вставить("Автообновление", НастройкаАвтообновление);
	Результат.Вставить("ПериодАвтообновления", ПериодАвтообновления);
	АвтообновлениеВызовСервера.СохранитьНастройкиАвтообновленияФормы(
		"Справочник.ЗаписиРабочегоКалендаря.Форма.Календарь", Результат);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьНастройки()
	
	СкрытьОтсутствияБудуРазбиратьЗадачи = 
		РаботаСРабочимКалендаремСервер.ПолучитьПерсональнуюНастройку("СкрытьОтсутствияБудуРазбиратьЗадачи");
	ОтображатьВремяС = 
		РаботаСРабочимКалендаремСервер.ПолучитьПерсональнуюНастройку("ОтображатьВремяС");
	ОтображатьВремяПо =
		РаботаСРабочимКалендаремСервер.ПолучитьПерсональнуюНастройку("ОтображатьВремяПо");
	ОтображениеВремениЭлементов =
		РаботаСРабочимКалендаремСервер.ПолучитьПерсональнуюНастройку("ОтображениеВремениЭлементов");
	ОтображатьЗанятость =
		РаботаСРабочимКалендаремСервер.ПолучитьПерсональнуюНастройку("ОтображатьЗанятость");
	ИспользоватьБыстроеРедактирование =
		РаботаСРабочимКалендаремСервер.ПолучитьПерсональнуюНастройку("ИспользоватьБыстроеРедактирование");
	ОтображатьПомеченныеНаУдаление =
		РаботаСРабочимКалендаремСервер.ПолучитьПерсональнуюНастройку("ОтображатьПомеченныеНаУдаление");
	РазмерЯчейкиВремени =
		РаботаСРабочимКалендаремСервер.ПолучитьПерсональнуюНастройку("РазмерЯчейкиВремени");
	ОтображатьОтклоненные =
		РаботаСРабочимКалендаремСервер.ПолучитьПерсональнуюНастройку("ОтображатьОтклоненные");
	ОтображатьКалендарьВЗаписиКалендаря =
		РаботаСРабочимКалендаремСервер.ПолучитьПерсональнуюНастройку("ОтображатьКалендарьВЗаписиКалендаря");
	ОтображатьЛегенду =
		РаботаСРабочимКалендаремСервер.ПолучитьПерсональнуюНастройку("ОтображатьЛегенду");
	
	// НапоминанияПользователя
	Элементы.ГруппаНапоминания.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьНапоминанияПользователя");
	ИнтервалПроверкиНапоминаний = НапоминанияПользователяСлужебный.ПолучитьИнтервалПроверкиНапоминаний();
	СрокНапоминанияПоУмолчанию =
		ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"НастройкиНапоминаний",
			"СрокНапоминанияПоУмолчанию",
			15);
	УстанавливатьНапоминаниеАвтоматически =
		ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"НастройкиНапоминаний",
			"УстанавливатьНапоминаниеАвтоматически",
			Истина);
	// Конец НапоминанияПользователя
	
	НастройкиАвтообновления = Автообновление.ПолучитьНастройкиАвтообновленияФормы(
		"Справочник.ЗаписиРабочегоКалендаря.Форма.Календарь");
	НастройкаАвтообновление = НастройкиАвтообновления.Автообновление;
	ПериодАвтообновления = НастройкиАвтообновления.ПериодАвтообновления;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьСтруктуруНастройки(МассивСтруктур, Объект, Настройка, Значение)
	
	МассивСтруктур.Добавить(Новый Структура ("Объект, Настройка, Значение", Объект, Настройка, Значение));
	
КонецПроцедуры

&НаСервере
Процедура НастроитьЭлементыФормыДляМобильногоУстройства()
	
	ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиФормы.Верх;
	Элементы.ОтображатьВремяС.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
	Элементы.ОтображатьВремяС.Ширина = 0;
	Элементы.ОтображатьВремяПо.Ширина = 0;
	
	Элементы.ГруппаОтображатьВремя.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
	Элементы.ГруппаОтображатьВремя.Подсказка = Элементы.ОтображатьВремяС.Подсказка;
	
	Элементы.ОтображениеВремениЭлементов.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
	
	Элементы.РазмерЯчейкиВремени.Заголовок =
		СтрШаблон("%1 (%2)", Элементы.РазмерЯчейкиВремени.Заголовок, Элементы.ДекорацияМинРазмерЯчейкиВремени.Заголовок);
	Элементы.РазмерЯчейкиВремени.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
	Элементы.ДекорацияМинРазмерЯчейкиВремени.Видимость = Ложь;
	
	Элементы.ИнтервалПроверкиНапоминаний.Заголовок = НСтр("ru = 'Интервал проверки напоминаний (мин.)'");
	Элементы.ИнтервалПроверкиНапоминаний.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
	Элементы.ДекорацияМинИнтервалПроверки.Видимость = Ложь;
	
	Элементы.СрокНапоминанияПоУмолчанию.Заголовок = НСтр("ru = 'Срок напоминания по умолчанию (мин.)'");
	Элементы.СрокНапоминанияПоУмолчанию.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
	Элементы.СрокНапоминанияПоУмолчанию.Ширина = 0;
	Элементы.ДекорацияМинСрокНапоминания.Видимость = Ложь;
	
	Элементы.УстанавливатьНапоминаниеАвтоматически.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
	Элементы.ОтображатьКалендарьВЗаписиКалендаря.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
	Элементы.ОтображатьЛегенду.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
	Элементы.ОтображатьЗанятость.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
	Элементы.ИспользоватьБыстроеРедактирование.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
	Элементы.ОтображатьПомеченныеНаУдаление.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
	Элементы.ОтображатьОтклоненные.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
	Элементы.СкрытьРаботуИзДома.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
	
	Элементы.УстанавливатьНапоминаниеАвтоматически.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
	Элементы.ОтображатьЗанятость.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
	Элементы.ИспользоватьБыстроеРедактирование.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
	Элементы.ОтображатьПомеченныеНаУдаление.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
	
	Элементы.ИспользоватьАвтообновление.Заголовок = НСтр("ru = 'Автоматически обновлять (сек.)'");
	Элементы.ИспользоватьАвтообновление.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
	Элементы.НадписьСек.Видимость = Ложь;
	
КонецПроцедуры

#КонецОбласти
