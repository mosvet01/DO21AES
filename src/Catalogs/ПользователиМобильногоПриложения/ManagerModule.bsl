
Функция ПолучитьПриложение(КодПриложения, Пользователь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("КодПриложения", КодПриложения);
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПользователиМобильногоПриложения.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ПользователиМобильногоПриложения КАК ПользователиМобильногоПриложения
		|ГДЕ
		|	ПользователиМобильногоПриложения.Код = &КодПриложения";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		НовыйЭлемент = Справочники.ПользователиМобильногоПриложения.СоздатьЭлемент();
		НовыйЭлемент.Пользователь = Пользователь;
		НовыйЭлемент.Код = КодПриложения;
		НовыйЭлемент.ДатаСоздания = ТекущаяДатаСеанса();
		НовыйЭлемент.Записать();
		Ссылка = НовыйЭлемент.Ссылка;
		
		// Создание новой очереди сообщений для нового мобильного клиента
		НоваяОчередь = Справочники.ОчередиСообщенийИнтегрированныхСистем.СоздатьЭлемент();
		НоваяОчередь.Наименование = Строка(Пользователь);
		НоваяОчередь.Записать();
		
		// Привязка новой очереди сообщений и нового мобильного клиента
		МенеджерЗаписиОчередей = РегистрыСведений.ОчередиСообщенийОбменаСМобильнымиКлиентами.СоздатьМенеджерЗаписи();
		МенеджерЗаписиОчередей.МобильныйКлиент = Ссылка;
		МенеджерЗаписиОчередей.Очередь = НоваяОчередь.Ссылка;
		МенеджерЗаписиОчередей.Записать();
	Иначе
		Ссылка = РезультатЗапроса.Выгрузить()[0].Ссылка;
	КонецЕсли;
	
	Возврат Ссылка;

КонецФункции

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	МобильноеПриложениеСсылка = Данные.Ссылка;
	Если ЗначениеЗаполнено(МобильноеПриложениеСсылка) Тогда

		Сведения = РегистрыСведений.СведенияОМобильныхКлиентах.СведенияОМобильномПриложении(МобильноеПриложениеСсылка);

		Если Сведения <> Неопределено Тогда
			
			СтандартнаяОбработка = Ложь;
			
			Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1, акт. %2", Сведения.Описание,
				Сведения.ДатаПоследнейАктивности);
			
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

Функция ВсеПользователиМП() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПользователиМобильногоПриложения.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ПользователиМобильногоПриложения КАК ПользователиМобильногоПриложения
		|ГДЕ
		|	Не ПользователиМобильногоПриложения.ПометкаУдаления";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

// Позволяет узнать, использует ли пользователь мобильное приложение
//
// Параметры:
//  Пользователь - СправочникСсылка.Пользователи
//
// Возвращаемое значение:
//   Булево - Истина, если пользователь найден, Ложь в противном случае
//
Функция ЭтоПользовательМП(Пользователь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПользователиМобильногоПриложения.Пользователь КАК Пользователь
		|ИЗ
		|	Справочник.ПользователиМобильногоПриложения КАК ПользователиМобильногоПриложения
		|ГДЕ
		|	ПользователиМобильногоПриложения.Пользователь = &Пользователь";
	
	Возврат Не Запрос.Выполнить().Пустой();

КонецФункции