
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Проверить(Команда)
	
	ОчиститьСообщения();
	
	Если Не ЗначениеЗаполнено(Объект.Скрипт) Тогда
		
		ТекстОшибки = НСтр("ru = 'Скрипт не заполнен'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, "Скрипт", "Объект.Скрипт");
		Возврат;
	
	КонецЕсли;
	
	Если Модифицированность Тогда
	
		Записать();
	
	КонецЕсли;
	
	Если ПроверитьНаСервере() = Истина Тогда
		
		ПоказатьПредупреждение(, НСтр("ru = 'Выполнено успешно.'"));
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПроверитьНаСервере()
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		Для Каждого ИмяРазделителя Из РаботаВМоделиСервиса.РазделителиКонфигурации() Цикл
			УстановитьБезопасныйРежимРазделенияДанных(ИмяРазделителя, Истина);
		КонецЦикла;
		УстановитьБезопасныйРежим(Истина);
	КонецЕсли;
	
	Попытка
	
		ЗначениеПараметра = "";
		Выполнить(Объект.Скрипт);
		Сообщить(ЗначениеПараметра);
		Возврат Истина;
		
	Исключение
		
		ПредставлениеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		СообщениеПользователю = Нстр("ru = 'При выполнении кода возникли ошибки:'") 
			+ Символы.ПС
			+ ПредставлениеОшибки;
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеПользователю);
	
	КонецПопытки;
	
КонецФункции

#КонецОбласти
