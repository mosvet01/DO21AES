#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает список делегирующих пользователей свои полномочия
// переданному в качестве параметра пользователю.
//
// Параметры:
//  Делегат - СправочникСсылка.Пользователи
//
// Возвращаемое значение:
//  Массив
//   * СправочникСсылка.Пользователи
//
Функция СписокДелегирующих(Делегат) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДелегированиеПрав.ОтКого
		|ИЗ
		|	Справочник.ДелегированиеПрав КАК ДелегированиеПрав
		|ГДЕ
		|	ДелегированиеПрав.Кому = &Пользователь
		|	И ДелегированиеПрав.Действует = ИСТИНА";
		
	Запрос.УстановитьПараметр("Пользователь", Делегат);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ОтКого");
	
КонецФункции

// Возвращает список делегатов для уведомления по подписчику на событие.
//
// Параметры:
//  Подписчик - СправочникСсылка.Пользователь - Исходный подписчик на уведомление.
//  Объект - Произвольный - Объект, делегирование по которому учитывается.
// 
// Возвращаемое значение:
//  Массив - Делегаты подписчика для уведомления.
//
Функция ПолучитьДелегатовДляУведомления(Подписчик, Объект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДелегированиеПрав.Кому
		|ИЗ
		|	Справочник.ДелегированиеПрав КАК ДелегированиеПрав
		|ГДЕ
		|	ДелегированиеПрав.ОтКого = &Подписчик
		|	И ДелегированиеПрав.Действует
		|	И ДелегированиеПрав.ВариантДелегирования = ЗНАЧЕНИЕ(Перечисление.ВариантыДелегированияПрав.ВсеПрава)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДелегированиеПрав.Кому
		|ИЗ
		|	Справочник.ДелегированиеПрав.ОбластиДелегирования КАК ДелегированиеПравОбластиДелегирования
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОбластиДелегированияПрав.Состав КАК ОбластиДелегированияПравСостав
		|		ПО ДелегированиеПравОбластиДелегирования.ОбластьДелегирования = ОбластиДелегированияПравСостав.Ссылка
		|			И (ОбластиДелегированияПравСостав.ОбъектМетаданных = &ИдентификаторОбъектаМетаданных)
		|			И (НЕ ОбластиДелегированияПравСостав.Ссылка.ПометкаУдаления)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДелегированиеПрав КАК ДелегированиеПрав
		|		ПО ДелегированиеПравОбластиДелегирования.Ссылка = ДелегированиеПрав.Ссылка
		|ГДЕ
		|	ДелегированиеПрав.ОтКого = &Подписчик
		|	И ДелегированиеПрав.Действует
		|	И ДелегированиеПрав.ВариантДелегирования = ЗНАЧЕНИЕ(Перечисление.ВариантыДелегированияПрав.Выборочно)";
	
	Запрос.УстановитьПараметр("Подписчик", Подписчик);
	
	Если Объект <> Неопределено Тогда
		ИдентификаторОбъектаМетаданных = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Объект.Метаданные());
	Иначе
		ИдентификаторОбъектаМетаданных = Неопределено;
	КонецЕсли;
	Запрос.УстановитьПараметр("ИдентификаторОбъектаМетаданных", ИдентификаторОбъектаМетаданных);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Кому");
	
КонецФункции

#КонецОбласти

#КонецЕсли