#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
		
	//Проверка установки пометки удаления для предопределенного элемента.
	Если Предопределенный И ПометкаУдаления Тогда
		ОписаниеОшибки = НСтр("ru = 'Пометка на удаление предопределенного элемента справочника запрещена'");
		ВызватьИсключение ОписаниеОшибки;
	КонецЕсли;
	
	// Помечаем на удаление все контрольные точки в этой группе
	Если ПометкаУдаления = Истина Тогда
		
		СтараяПометкаУдаления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ПометкаУдаления");
		Если СтараяПометкаУдаления <> Истина Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
				"ВЫБРАТЬ
				|	ШаблоныКонтрольныхТочек.Ссылка
				|ИЗ
				|	Справочник.ШаблоныКонтрольныхТочек КАК ШаблоныКонтрольныхТочек
				|ГДЕ
				|	ШаблоныКонтрольныхТочек.ПометкаУдаления = Ложь
				|	И ШаблоныКонтрольныхТочек.ГруппаКТ = &ГруппаКТ";
			
			Запрос.УстановитьПараметр("ГруппаКТ", Ссылка);
			
			Выборка = Запрос.Выполнить().Выбрать();
			
			Пока Выборка.Следующий() Цикл
				
				КТ = Выборка.Ссылка.ПолучитьОбъект(); 
				КТ.УстановитьПометкуУдаления(Истина);
				
			КонецЦикла;
	
		КонецЕсли;
		
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли