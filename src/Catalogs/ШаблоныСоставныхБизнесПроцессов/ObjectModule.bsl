#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Проверяет что заполнены поля шаблона
Функция ПолучитьСписокНезаполненныхПолейНеобходимыхДляСтарта() Экспорт
	
	МассивПолей = Новый Массив;
	
	СтруктураРеквизитов = Новый Структура;
	Для Каждого Строка Из Шаблоны Цикл
		СтруктураРеквизитов.Вставить(Строка.Реквизит, Строка.Шаблон);
	КонецЦикла;	
	
	Попытка
		Ошибки = БизнесПроцессы[ТипШаблона].ПроверитьШаблон(СтруктураРеквизитов);
	Исключение
		МассивПолей.Добавить(НСтр("ru='Неверный тип процесса'"));
		Возврат МассивПолей;	
	КонецПопытки;	
	
	ЕстьОшибки = Ложь;
	Для Каждого Ошибка Из Ошибки Цикл
		МассивПолей.Добавить(Ошибка.Представление);
		ЕстьОшибки = Истина;
	КонецЦикла;	
	
	Если ЕстьОшибки Тогда
		Возврат МассивПолей;
	КонецЕсли;	
	
	Для Каждого Шаблон Из Шаблоны Цикл
		
		Если ЗначениеЗаполнено(Шаблон.Шаблон) Тогда
			
			ШаблонОбъект = Шаблон.Шаблон.ПолучитьОбъект();
			
			МассивШаблонаШаблона = ШаблонОбъект.ПолучитьСписокНезаполненныхПолейНеобходимыхДляСтарта();
			Если МассивШаблонаШаблона.Количество() <> 0 Тогда
				Для Каждого ИмяПоля Из МассивШаблонаШаблона Цикл
					МассивПолей.Добавить(ИмяПоля);
				КонецЦикла;
				Возврат МассивПолей;	
			КонецЕсли;	
			
		КонецЕсли;	
		
	КонецЦикла;		

	Возврат МассивПолей;
	
КонецФункции	

//Формирует текстовое представление бизнес-процесса, создаваемого по шаблону
Функция СформироватьСводкуПоШаблону() Экспорт
	
	Результат = ШаблоныБизнесПроцессов.ПолучитьОбщуюЧастьОписанияШаблона(Ссылка);
	
	Если Шаблоны.Количество() > 0 Тогда
		Результат = Результат + НСтр("ru = 'Этапы:'") + Символы.ПС;
		Для Каждого Шаблон Из Шаблоны Цикл
			Если ЗначениеЗаполнено(Шаблон.Шаблон) Тогда
				Результат = 
					Результат 
					+ Символы.Таб 
					+ Метаданные.БизнесПроцессы[ТипШаблона].Реквизиты[Шаблон.Реквизит].Синоним + ": " 
					+ Строка(Шаблон.Шаблон) 
					+ Символы.ПС;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ДлительностьПроцесса = СрокиИсполненияПроцессов.ДлительностьИсполненияПроцесса(ЭтотОбъект);
	ДлительностьПроцессаСтрокой = СрокиИсполненияПроцессовКлиентСервер.ПредставлениеДлительности(
		ДлительностьПроцесса.СрокИсполненияПроцессаДни,
		ДлительностьПроцесса.СрокИсполненияПроцессаЧасы,
		ДлительностьПроцесса.СрокИсполненияПроцессаМинуты);
		
	Если ЗначениеЗаполнено(ДлительностьПроцессаСтрокой) Тогда
		Результат = Результат + Нстр("ru = 'Срок'") + ": "
			+ ДлительностьПроцессаСтрокой;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Заполняет предопределенный шаблон по умолчанию.
// Предназначена для вызова из предопределенного объекта.
// Если объект не предопределенный, то вызов процедуры приведет к исключению.
//
Процедура ЗаполнитьШаблонПоУмолчанию() Экспорт
	
	Если Ссылка <> Справочники.ШаблоныСоставныхБизнесПроцессов.ОбработкаВнутреннегоДокументаПоУмолчанию
		И Ссылка <> Справочники.ШаблоныСоставныхБизнесПроцессов.ОбработкаВходящегоДокументаПоУмолчанию
		И Ссылка <> Справочники.ШаблоныСоставныхБизнесПроцессов.ОбработкаИсходящегоДокументаПоУмолчанию Тогда
		
		ВызватьИсключение НСтр("ru = 'Процедура ЗаполнитьШаблонПоУмолчанию предназначена для вызова из предопределенного шаблона.'");
	КонецЕсли;
	
	Автор = Справочники.Пользователи.ПустаяСсылка();
	Важность = Перечисления.ВариантыВажностиЗадачи.Обычная;
	ВладелецШаблона = Неопределено;
	ДобавлятьНаименованиеПредмета = Истина;
	ИсходныйШаблон = Справочники.ШаблоныСоставныхБизнесПроцессов.ПустаяСсылка();
	Комментарий = "";
	КомплексныйПроцесс = БизнесПроцессы.КомплексныйПроцесс.ПустаяСсылка();
	Наименование = НСтр("ru = 'По умолчанию'");
	
	Ответственный = ПользователиКлиентСервер.ТекущийПользователь();
	Если Не Пользователи.ЭтоПолноправныйПользователь(Ответственный) Тогда
		Ответственный = Пользователи.СсылкаНеуказанногоПользователя(Истина);
	КонецЕсли;
	
	СрокИсполненияПроцесса = Дата(1,1,1);
	
	СрокОтложенногоСтарта = 0;
	
	Если Ссылка = Справочники.ШаблоныСоставныхБизнесПроцессов.ОбработкаВнутреннегоДокументаПоУмолчанию Тогда
		ТипШаблона = Справочники.ШаблоныСоставныхБизнесПроцессов.ТипШаблонаОбработкаВнутреннегоДокумента();
		НаименованиеБизнесПроцесса = НСтр("ru = 'Обработка внутреннего '");
	ИначеЕсли Ссылка = Справочники.ШаблоныСоставныхБизнесПроцессов.ОбработкаВходящегоДокументаПоУмолчанию Тогда
		ТипШаблона = Справочники.ШаблоныСоставныхБизнесПроцессов.ТипШаблонаОбработкаВходящегоДокумента();
		НаименованиеБизнесПроцесса = НСтр("ru = 'Обработка входящего '");
	ИначеЕсли Ссылка = Справочники.ШаблоныСоставныхБизнесПроцессов.ОбработкаИсходящегоДокументаПоУмолчанию Тогда
		ТипШаблона = Справочники.ШаблоныСоставныхБизнесПроцессов.ТипШаблонаОбработкаИсходящегоДокумента();
		НаименованиеБизнесПроцесса = НСтр("ru = 'Обработка исходящего '");
	КонецЕсли;
	
	ШаблонВКомплексномПроцессе = Ложь;
	
	Предметы.Очистить();
	ПредметыЗадач.Очистить();
	
	Справочники.ШаблоныСоставныхБизнесПроцессов.ЗаполнитьТаблицуШаблоны(Шаблоны, ТипШаблона);
	
КонецПроцедуры


#КонецОбласти

#Область ПрограммныйИнтерфейс_ПоддержкаМеханизмаОтсутствий

// Получает исполнителей
Функция ПолучитьИсполнителей() Экспорт
	
	МассивИсполнителей = Новый Массив;
	
	Для Каждого СтрокаШаблон Из Шаблоны Цикл
		
		Шаблон = СтрокаШаблон.Шаблон;
		Если Шаблон = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ШаблонОбъект = Шаблон.ПолучитьОбъект();
		Если ШаблонОбъект = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ИсполнителиЭтапа = ШаблонОбъект.ПолучитьИсполнителей();
		Для Каждого Исполнитель Из ИсполнителиЭтапа Цикл
			МассивИсполнителей.Добавить(Исполнитель);
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат МассивИсполнителей;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ЭтоГруппа Тогда
		Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("ТипШаблона") Тогда
			ТипШаблона = ДанныеЗаполнения.ТипШаблона;
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	Если ЭтоНовый() Тогда
		ШаблоныБизнесПроцессов.НачальноеЗаполнениеШаблона(ЭтотОбъект, ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ШаблоныБизнесПроцессов.ШаблонПередЗаписью(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если Ссылка <> Справочники.ШаблоныСоставныхБизнесПроцессов.ОбработкаВнутреннегоДокументаПоУмолчанию
		И Ссылка <> Справочники.ШаблоныСоставныхБизнесПроцессов.ОбработкаВходящегоДокументаПоУмолчанию
		И Ссылка <> Справочники.ШаблоныСоставныхБизнесПроцессов.ОбработкаИсходящегоДокументаПоУмолчанию Тогда
		
		ПроверяемыеРеквизиты.Добавить("НаименованиеБизнесПроцесса");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли