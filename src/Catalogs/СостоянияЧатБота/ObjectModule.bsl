
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКопировании(ОбъектКопирования)
	
	КлючевыеСлова = "";
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Ссылка) Тогда
		ДополнительныеСвойства.Вставить("ЭтоНовый", Ложь);
		ПредыдущиеЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка,
			"Используется, Ссылка");
	Иначе
		ДополнительныеСвойства.Вставить("ЭтоНовый", Истина);
		ПредыдущиеЗначенияРеквизитов = Новый Структура;
		ПредыдущиеЗначенияРеквизитов.Вставить("Используется", Ложь);
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("ПредыдущийИспользуется", ПредыдущиеЗначенияРеквизитов.Используется);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ПредыдущийИспользуется") 
		И Используется <> ДополнительныеСвойства.ПредыдущийИспользуется Тогда 
		ОбновитьАктивностьЗависимыхСостояний(Ссылка, Родитель, Используется);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если Не РодительЗаданКорректно(Родитель) Тогда
		Отказ = Истина;
		ТекстПредупреждения = Нстр("ru = 'Недопустимо указывать предопределенный элемент в качестве родителя'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстПредупреждения,, "Родитель", "Объект");
	КонецЕсли; 
	
	Если ТипСостояния = Перечисления.ТипыСостоянийЧатБота.Действие
		И Не ЗначениеЗаполнено(Действие) Тогда
		ПроверяемыеРеквизиты.Добавить("Действие");
	КонецЕсли;
	
	Если ТипСостояния = Перечисления.ТипыСостоянийЧатБота.Высказывание
		И Не ЗначениеЗаполнено(Высказывание) Тогда
		ПроверяемыеРеквизиты.Добавить("Высказывание");
	КонецЕсли;
	
	РодительТип = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Родитель, "ТипСостояния");
	
	Если РодительТип = Перечисления.ТипыСостоянийЧатБота.Вопрос 
		И Не ЭтоЕдинственноеПодчиненноеСостояние(Ссылка, Родитель) Тогда
		Отказ = Истина;
		ТекстПредупреждения = НСтр("ru = 'Для типа состояния ""Вопрос"" уже задано подчиненное состояние'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстПредупреждения,, "ТипСостояния", "Объект");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбновитьАктивностьЗависимыхСостояний(Ссылка, Родитель, ЗначениеИспользуется)
	
	СостоянияДляОбновления = Новый ТаблицаЗначений();
	СостоянияДляОбновления.Колонки.Добавить("Ссылка");
	
	Если ЗначениеИспользуется Тогда
		Если ЗначениеЗаполнено(Родитель) Тогда 
			СостояниеДляОбновления = СостоянияДляОбновления.Добавить();
			СостояниеДляОбновления.Ссылка = Родитель;
		КонецЕсли;
		
	ИначеЕсли ЗначениеЗаполнено(Ссылка) Тогда
		
		Запрос = Новый Запрос();
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	СостоянияЧатБота.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.СостоянияЧатБота КАК СостоянияЧатБота
			|ГДЕ
			|	СостоянияЧатБота.Ссылка В ИЕРАРХИИ(&ТекущееСостояние)
			|	И СостоянияЧатБота.Используется = ИСТИНА
			|	И СостоянияЧатБота.Ссылка <> &ТекущееСостояние";
		Запрос.Параметры.Вставить("ТекущееСостояние", Ссылка);
		СостоянияДляОбновления = Запрос.Выполнить().Выгрузить();
		
	КонецЕсли;
	
	Для Каждого Состояние Из СостоянияДляОбновления Цикл
		СостояниеОбъект = Состояние.Ссылка.ПолучитьОбъект();
		СостояниеОбъект.Используется = Используется;
		СостояниеОбъект.Записать();
	КонецЦикла;
	
КонецПроцедуры

Функция РодительЗаданКорректно(Ссылка)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Предопределенный") <> Истина;
	
КонецФункции

Функция ЭтоЕдинственноеПодчиненноеСостояние(Ссылка, Родитель)
	
	Запрос = Новый Запрос();
	Запрос.Параметры.Вставить("Родитель", Родитель);
	Запрос.Параметры.Вставить("Ссылка", Ссылка);
	Запрос.Текст =
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(*) КАК Колво
		|ИЗ
		|	Справочник.СостоянияЧатБота КАК СостоянияЧатБота
		|ГДЕ
		|	СостоянияЧатБота.Родитель = &Родитель
		|	И СостоянияЧатБота.Ссылка <> &Ссылка
		|	И СостоянияЧатБота.ПометкаУдаления = ЛОЖЬ";
	Результат = Запрос.Выполнить();
	
	Возврат Результат.Пустой();
	
КонецФункции

#КонецОбласти

#КонецЕсли