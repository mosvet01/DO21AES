
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Делопроизводство.СписокДокументовУсловноеОформлениеПомеченныхНаУдаление(Список);
	
	ПоказатьУдаленные();
	ПоказатьПредопределенные();
	
	Если Не РольДоступна("ПолныеПрава") И Не РольДоступна("АдминистраторЧатБота") Тогда 
		Элементы.ФормаИспользуется.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказыватьУдаленные(Команда)
	
	ПоказыватьУдаленные = Не ПоказыватьУдаленные;
	ПоказатьУдаленные();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьПредопределенные(Команда)
	
	ПоказыватьПредопределенные = Не ПоказыватьПредопределенные;
	ПоказатьПредопределенные();
	
КонецПроцедуры

&НаКлиенте
Процедура Используется(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат
	КонецЕсли;
	
	Если ТипЗнч(Элементы.Список.ТекущаяСтрока) = Тип("СтрокаГруппировкиДинамическогоСписка")
		Или ТекущиеДанные.Предопределенный Тогда
		ТекстПредупреждения = НСтр("ru = 'Команда не может быть выполнена для указанного объекта.'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	ИспользуетсяНаСервере(ТекущиеДанные.Ссылка);
	Элементы.Список.Обновить();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПоказатьУдаленные()
	
	Если ПоказыватьУдаленные Тогда
	
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(Список, "ПометкаУдаления");
		
	Иначе
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ПометкаУдаления", Ложь);
	
	КонецЕсли;
	
	Элементы.ПоказыватьУдаленные.Пометка = ПоказыватьУдаленные;

КонецПроцедуры

&НаСервере
Процедура ПоказатьПредопределенные()
	
	Если ПоказыватьПредопределенные Тогда
	
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(Список, "Предопределенный");
		
	Иначе
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Предопределенный", Ложь);
	
	КонецЕсли;
	
	Элементы.ПоказыватьПредопределенные.Пометка = ПоказыватьПредопределенные;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	ПоказыватьУдаленные = Настройки["ПоказыватьУдаленные"];
	ПоказатьУдаленные();
	
	ПоказыватьПредопределенные = Настройки["ПоказыватьПредопределенные"];
	ПоказатьПредопределенные();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ИспользуетсяНаСервере(Состояние) 
	
	СостояниеОбъект = Состояние.ПолучитьОбъект();
	СостояниеОбъект.Используется = Не СостояниеОбъект.Используется;
	СостояниеОбъект.Записать();
	
КонецПроцедуры

#КонецОбласти


