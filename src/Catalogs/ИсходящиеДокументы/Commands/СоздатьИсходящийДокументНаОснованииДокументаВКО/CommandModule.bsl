&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Параметры = Новый Структура;
	Параметры.Вставить("ДокументОтчетности", ПараметрКоманды);
	Параметры.Вставить("ПерезаписатьСуществующий", Ложь);
	Параметры.Вставить("ИсходящийДокумент", Неопределено);
	СоздатьДокументНаОсновании(Параметры);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументНаОсновании(Параметры) Экспорт
	
	РезультатСоздания = ОтчетностьВКонтролирующиеОрганыВызовСервера.СоздатьИлиОбновитьИсходящийДокумент(
		Параметры.ДокументОтчетности, Параметры.ПерезаписатьСуществующий);
	
	Если РезультатСоздания.ДокументСуществует Тогда
		
		Параметры.ИсходящийДокумент = РезультатСоздания.ИсходящийДокумент;
		ОписаниеОповещения = Новый ОписаниеОповещения("СоздатьДокументНаОснованииЗавершение", ЭтотОбъект, Параметры);
		ТекстВопроса = НСтр("ru = 'Исходящий документ уже создан.
							|Вы хотите обновить его данными документа отчетности?'");
		ОбщегоНазначенияДокументооборотКлиент.ПоказатьВопросДаНет(
			ОписаниеОповещения,
			ТекстВопроса,
			НСтр("ru='Обновить и открыть'"),
			НСтр("ru='Открыть без обновления'"),
			КодВозвратаДиалога.Нет);
			
	ИначеЕсли ЗначениеЗаполнено(РезультатСоздания.ОписаниеОшибки) Тогда
		
		ПоказатьПредупреждение(, РезультатСоздания.ОписаниеОшибки);
		
	Иначе
		
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("ДокументОтчетности", Параметры.ДокументОтчетности);
		ПараметрыОповещения.Вставить("ДокументДокументооборота", РезультатСоздания.ИсходящийДокумент);
		Оповестить("СозданДокументДокументооборотаНаОснованииДокументаКО", ПараметрыОповещения);
		ОповеститьОбИзменении(РезультатСоздания.ИсходящийДокумент);
		ПоказатьДокумент(РезультатСоздания.ИсходящийДокумент);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьДокумент(ИсходящийДокумент)
	
	ПоказатьЗначение(, ИсходящийДокумент);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументНаОснованииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ДополнительныеПараметры.ПерезаписатьСуществующий = Истина;
		СоздатьДокументНаОсновании(ДополнительныеПараметры);
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		ПоказатьДокумент(ДополнительныеПараметры.ИсходящийДокумент);
	КонецЕсли;
	
КонецПроцедуры
