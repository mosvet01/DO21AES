#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Процедура - Делает первоначальное заполнение справочника, согласно Приложению
// Приказа Минцифры России о требованиях к обмену электронными документами
// Данные загружаются из макета
// https://gosedo.ru/законодательство/
//
Процедура ПервоначальноеЗаполнение() Экспорт
	
	ТекстовыйМакет = ПолучитьМакет("КлассификаторИзПриказаМинцифры667_233_От2020_12_04");
	
	Для НомерСтроки = 1 По ТекстовыйМакет.КоличествоСтрок() Цикл
		
		ТекущаяСтрока = ТекстовыйМакет.ПолучитьСтроку(НомерСтроки);
		МассивСтрок = СтрРазделить(ТекущаяСтрока, Символы.Таб, );
		Если МассивСтрок.Количество() <> 2 Тогда
			Продолжить;
		КонецЕсли;
		
		КодМЭДО = СокрЛП(МассивСтрок[0]);
		НаименованиеПоКлассификатору = СокрЛП(МассивСтрок[1]);
		Если СтрНайти(КодМЭДО, "00") = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Предмет = СоздатьЭлемент();
		ПредметСсылка = НайтиПоРеквизиту("КодМЭДО", КодМЭДО);
		Если ЗначениеЗаполнено(ПредметСсылка) Тогда
			Предмет = ПредметСсылка.ПолучитьОбъект();
		КонецЕсли;
		
		Предмет.КодМЭДО = КодМЭДО;
		Если Предмет.Наименование <> НаименованиеПоКлассификатору Тогда
			Предмет.Наименование = НаименованиеПоКлассификатору;
			Предмет.Записать();
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Найти вид документа МЭДО по коду из паспорта контейнера, или по наименованию.
// 
// Параметры:
//  КодМЭДО - Строка - Код МЭДО
//  НаименованиеМЭДО - Строка - Наименование МЭДО
// 
// Возвращаемое значение:
//  СправочникСсылка.МестаСоставленияДокументовМЭДО - Найденное место (область РФ), или пустая ссылка.
Функция НайтиМестоСоставления(КодМЭДО, НаименованиеМЭДО) Экспорт
	
	Если Не ЗначениеЗаполнено(КодМЭДО) Тогда
		Возврат НайтиПоНаименованию(НаименованиеМЭДО);
	КонецЕсли;
	
	Место = НайтиПоРеквизиту("КодМЭДО", КодМЭДО);
	Если Место.Пустая() Тогда
		// Не найдено по коду, может быть по наименованию:
		Место = НайтиПоНаименованию(НаименованиеМЭДО);
	КонецЕсли;
	
	Возврат Место;
	
КонецФункции

#КонецОбласти

#КонецЕсли