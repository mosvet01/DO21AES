&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;

	Список.Параметры.УстановитьЗначениеПараметра("Пользователь", ПользователиКлиентСервер.ТекущийПользователь());
	
	Если Параметры.Свойство("ОтображатьУдаленные") Тогда
		ПараметрОтображатьУдаленные = Параметры.ОтображатьУдаленные;
	Иначе
		ПараметрОтображатьУдаленные =
			ВстроеннаяПочтаСервер.ПолучитьПерсональнуюНастройку("ОтображатьУдаленныеПисьмаИПапки");
	КонецЕсли;
	
	Если Параметры.Свойство("РежимМоиПапки") Тогда
		ПараметрРежимМоиПапки = Параметры.РежимМоиПапки;
	Иначе
		ПараметрРежимМоиПапки = ВстроеннаяПочтаСервер.ПолучитьПерсональнуюНастройку("РежимМоиПапки");
	КонецЕсли;
		
	УстановитьРежимМоиПапки(ПараметрРежимМоиПапки);
	УстановитьРежимОтображатьУдаленные(ПараметрОтображатьУдаленные);
	
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		НастроитьЭлементыФормыДляМобильногоУстройства();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РежимМоиПапки(Команда)
	
	УстановитьРежимМоиПапки(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура РежимВсеПапки(Команда)
	
	УстановитьРежимМоиПапки(Ложь);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьРежимМоиПапки(НовыйРежимМоиПапки)
	
	РежимМоиПапки = НовыйРежимМоиПапки; 
	
	Элементы.ПапкиКонтекстноеМенюРежимМоиПапки.Пометка = НовыйРежимМоиПапки;
	Элементы.ПапкиКонтекстноеМенюРежимВсеПапки.Пометка = Не НовыйРежимМоиПапки;
	Элементы.ПапкиМенюРежимМоиПапки.Пометка = НовыйРежимМоиПапки;
	Элементы.ПапкиМенюРежимВсеПапки.Пометка = Не НовыйРежимМоиПапки;
		
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		Список.Отбор,
		"ВМоихПапках",
		Истина,
		ВидСравненияКомпоновкиДанных.Равно,
		,
		НовыйРежимМоиПапки);
		
КонецПроцедуры

&НаСервере
Процедура УстановитьРежимОтображатьУдаленные(НовымРежимОтображатьУдаленные)
	
	Элементы.СписокОтображатьПомеченныеНаУдаление.Пометка = НовымРежимОтображатьУдаленные;
	Элементы.СписокОтображатьПомеченныеНаУдалениеКонтекст.Пометка = НовымРежимОтображатьУдаленные;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		Список.Отбор,
		"ПометкаУдаления",
		Ложь,
		ВидСравненияКомпоновкиДанных.Равно,
		,
		Не НовымРежимОтображатьУдаленные);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтображатьПомеченныеНаУдаление(Команда)
	
	УстановитьРежимОтображатьУдаленные(Не Элементы.СписокОтображатьПомеченныеНаУдаление.Пометка);	
	
КонецПроцедуры

Процедура НастроитьЭлементыФормыДляМобильногоУстройства()
	
	// Отключим контекстное меню.
	Элементы.Список.КоманднаяПанель.Видимость = Ложь;
	Элементы.Просмотр.Видимость = Ложь;
	Элементы.СписокОтображатьПомеченныеНаУдалениеКонтекст.Видимость = Ложь;
	
	// Вклюим отображение количества писем в папках.
	Элементы.Количество.Видимость = Истина;
	
	НовоеУсловноеОформление = УсловноеОформление.Элементы.Добавить();
	
	ОформляемоеПоле = НовоеУсловноеОформление.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("Список");
	
	ГруппаОтбора = НовоеУсловноеОформление.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
	Отбор = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Отбор.ВидСравнения 		= ВидСравненияКомпоновкиДанных.Равно;
	Отбор.Использование 	= Истина;
	Отбор.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных("Список.ВариантОтображенияКоличестваПисем");
	Отбор.ПравоеЗначение 	= Перечисления.ВариантыОтображенияКоличестваПисемВПапке.Непрочтенные;
	
	Отбор = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Отбор.ВидСравнения 		= ВидСравненияКомпоновкиДанных.Больше;
	Отбор.Использование 	= Истина;
	Отбор.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных("Список.Количество");
	Отбор.ПравоеЗначение 	= 0;
	
	Оформление = НовоеУсловноеОформление.Оформление.Элементы.Найти("Шрифт");
	Оформление.Значение 		= Новый Шрифт(Оформление.Значение,,,Истина);
	Оформление.Использование 	= Истина;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	Папки = Неопределено;
	Для Каждого Строка Из Строки Цикл
		Если Строка.Значение.Данные.Свойство("Количество")
			И Строка.Значение.Данные.Свойство("Ссылка") Тогда
			Папки = Строки.ПолучитьКлючи();
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(Папки) Тогда 
		СведенияОПапках = ВстроеннаяПочтаСервер.ПолучитьСведенияОПапках(
			ПользователиКлиентСервер.ТекущийПользователь(),
			Ложь,
			Папки);
			
		Для Каждого КлючИЗначение Из СведенияОПапках Цикл
			
			Строка = Строки.Получить(КлючИЗначение.Ключ);
			Если Строка <> Неопределено Тогда
				Строка.Данные.Количество = КлючИЗначение.Значение.Количество;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры
