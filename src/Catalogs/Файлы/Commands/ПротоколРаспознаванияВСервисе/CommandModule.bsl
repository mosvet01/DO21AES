
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Группы", ГруппыЗаписейПоФайлу(ПараметрКоманды));
	
	ОткрытьФорму(
		"РегистрСведений.ПротоколРаботыССервисомРаспознавания.Форма.ЗапросыГрупп",
		ПараметрыФормы,
		ПараметрыВыполненияКоманды.Источник,
		ПараметрыВыполненияКоманды.Уникальность,
		ПараметрыВыполненияКоманды.Окно,
		ПараметрыВыполненияКоманды.НавигационнаяСсылка);
	
КонецПроцедуры

&НаСервере
Функция ГруппыЗаписейПоФайлу(Файл)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ГруппыЗаписей = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВерсииФайлов.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ВерсииФайлов КАК ВерсииФайлов
		|ГДЕ
		|	ВерсииФайлов.Владелец = &Владелец";
	Запрос.УстановитьПараметр("Владелец", Файл);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ГруппыЗаписей.Добавить(Строка(Выборка.Ссылка.УникальныйИдентификатор()));
	КонецЦикла;
	
	Возврат ГруппыЗаписей;
	
КонецФункции