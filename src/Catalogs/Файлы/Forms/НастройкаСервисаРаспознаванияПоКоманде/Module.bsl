
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЯзыкиРаспознавания = РаботаСФайламиВызовСервера.ЯзыкиРаспознаванияПрограммы(
		РаботаСФайламиВызовСервера.ПрограммаРаспознавания());
	Для Каждого Запись Из ЯзыкиРаспознавания Цикл
		Элементы.ЯзыкРаспознавания.СписокВыбора.Добавить(Запись.Language, Запись.Name);
	КонецЦикла;	
	
	Если Параметры.Свойство("ЯзыкРаспознавания") Тогда
		ЯзыкРаспознавания = Параметры.ЯзыкРаспознавания;
	Иначе	
		ЯзыкРаспознавания = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("Распознавание", "ЯзыкРаспознавания");
		Если ЯзыкРаспознавания = "" Тогда
			ЯзыкРаспознавания = РаботаСФайламиВызовСервера.ЯзыкРаспознаванияВСервисеПоУмолчанию();
		КонецЕсли;
	КонецЕсли;
	
	Если ЯзыкиРаспознавания.Найти(ЯзыкРаспознавания, "Language") = Неопределено Тогда
		ЯзыкРаспознавания = РаботаСФайламиВызовСервера.ЯзыкРаспознаванияВСервисеПоУмолчанию();
	КонецЕсли;
	
	Если Параметры.Свойство("СтратегияРаспознавания") Тогда
		СтратегияРаспознавания = Параметры.СтратегияРаспознавания;
	Иначе	
		СтратегияРаспознавания = Перечисления.СтратегииРаспознаванияТекста.ПоместитьТолькоВТекстовыйОбраз;
	КонецЕсли;
	
	Если Элементы.ЯзыкРаспознавания.СписокВыбора.Количество() = 1 Тогда
		
		Элементы.ЯзыкРаспознавания.Видимость = Ложь;
		
		Заголовок = НСтр("ru = 'Распознавание файла(ов)'");
		
		Элементы.ДекорацияЯзыкРаспознавания.Заголовок = Элементы.ДекорацияЯзыкРаспознавания.Заголовок
			+ Элементы.ЯзыкРаспознавания.СписокВыбора.НайтиПоЗначению(ЯзыкРаспознавания).Представление;
		
		Команды.ОК.Заголовок = НСтр("ru = 'Продолжить'");
		
		КлючСохраненияПоложенияОкна = "Вопрос";
		
	Иначе
		Элементы.ДекорацияИнфо.Видимость = Ложь;
		Элементы.ДекорацияЯзыкРаспознавания.Видимость = Ложь;
		
		КлючСохраненияПоложенияОкна = "Настройка";
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	СтруктураВозврата = Новый Структура;
	
	СтруктураВозврата.Вставить("ЯзыкРаспознавания", ЯзыкРаспознавания);
	СтруктураВозврата.Вставить("СтратегияРаспознавания", СтратегияРаспознавания);
	
	Закрыть(СтруктураВозврата);
	
КонецПроцедуры
