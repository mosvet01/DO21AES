&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Файл = Параметры.Файл;
	ДанныеФайла = Параметры.ДанныеФайла;
	ИмяОткрываемогоФайла = Параметры.ИмяОткрываемогоФайла;
	Если Параметры.Свойство("УникальныйИдентификатор") Тогда
		ИдентификаторРодительскойФормы = Параметры.УникальныйИдентификатор;
	КонецЕсли;
	
	ПредельныйРазмерФайла = 500000;
	
	КодФайла = Файл.Код;
	
	ЭтаФорма.АвтоНавигационнаяСсылка = Ложь;
	ЭтаФорма.НавигационнаяСсылка = ПолучитьНавигационнуюСсылку(Файл);
	
	Если ДанныеФайла.РедактируетТекущийПользователь Тогда
		РежимРедактирования = Истина;
	КонецЕсли;	
	
	Если ДанныеФайла.Версия <> ДанныеФайла.ТекущаяВерсия Тогда
		РежимРедактирования = Ложь;
	КонецЕсли;	
	
	Элементы.Текст.ТолькоПросмотр = Не РежимРедактирования;
	Элементы.ПоказатьОтличия.Видимость = Не ОбщегоНазначенияКлиентСервер.ЭтоLinuxКлиент();
	Элементы.Редактировать.Доступность = Не РежимРедактирования;
	Элементы.ЗакончитьРедактирование.Доступность = РежимРедактирования;
	Элементы.ЗаписатьИЗакрыть.Доступность = РежимРедактирования;
	Элементы.Записать.Доступность = РежимРедактирования;
	Элементы.Редактировать.Видимость = Не РежимРедактирования;
	
	Если ДанныеФайла.Версия <> ДанныеФайла.ТекущаяВерсия Тогда
		Элементы.Редактировать.Доступность = Ложь;
	КонецЕсли;	
	
	Права = ДокументооборотПраваДоступа.ПолучитьПраваПоОбъекту(Файл);
	Если Не Права.Изменение Тогда
		Элементы.Редактировать.Видимость = Ложь;
	КонецЕсли;	
	
	Если Не РежимРедактирования Тогда
		Элементы.ЗаписатьИЗакрыть.Видимость = Ложь;
		Элементы.Записать.Видимость = Ложь;
		Элементы.ЗакончитьРедактирование.Видимость = Ложь;
	КонецЕсли;	
	
	ЗаголовокСтрока = ФайловыеФункцииКлиентСервер.ПолучитьИмяСРасширением(ДанныеФайла.ПолноеНаименованиеВерсии, 
		ДанныеФайла.Расширение);
	Если Не РежимРедактирования Тогда
		ЗаголовокСтрока = ЗаголовокСтрока + НСтр("ru=' (только просмотр)'");
	КонецЕсли;
	Заголовок = ЗаголовокСтрока;
	
	Если ЗначениеЗаполнено(ДанныеФайла.Версия) Тогда
		КодировкаТекстаФайла = РаботаСФайламиВызовСервера.ПолучитьКодировкуВерсииФайла(ДанныеФайла.Версия);
		
		Если ЗначениеЗаполнено(КодировкаТекстаФайла) Тогда
			СписокКодировок = РаботаСоСтроками.ПолучитьСписокКодировок();
			ЭлементСписка = СписокКодировок.НайтиПоЗначению(КодировкаТекстаФайла);
			Если ЭлементСписка = Неопределено Тогда
				КодировкаПредставление = КодировкаТекстаФайла;
			Иначе	
				КодировкаПредставление = ЭлементСписка.Представление;
			КонецЕсли;
		Иначе	
			КодировкаПредставление = НСтр("ru='По умолчанию'");
		КонецЕсли;
		
	КонецЕсли;	
	
	НастроитьОтображениеКомандРаботыСПроцессамиИЗадачами();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	КлючУникальности = КодФайла;
	
	ПрочитатьТекст();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьHtml(Текст)
	
	ТекстHtml = РаботаС_HTML.ПолучитьHTMLИзТекста_ДляПросмотраТекста(Текст);
	РаботаС_HTML.ДобавитьТегиКСсылкам(ТекстHtml);
	
	Возврат ТекстHtml;
	
КонецФункции

&НаКлиенте
Процедура СохранитьКак(Команда)
	
	// выбираем путь к файлу на диске
	ВыборФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ВыборФайла.МножественныйВыбор = Ложь;
	ИмяСРасширением = ФайловыеФункцииКлиентСервер.ПолучитьИмяСРасширением(ДанныеФайла.ПолноеНаименованиеВерсии, 
		ДанныеФайла.Расширение);
	ВыборФайла.ПолноеИмяФайла = ИмяСРасширением;
	Фильтр = СтрШаблон(
		НСтр("ru = 'Все файлы (*.%1)|*.%1'"), ДанныеФайла.Расширение);
	ВыборФайла.Фильтр = Фильтр;
	
	Если ВыборФайла.Выбрать() Тогда
		
		ВыбранноеПолноеИмяФайла = ВыборФайла.ПолноеИмяФайла;
		
		КодировкаТекстаДляЗаписи = КодировкаТекстаФайла;
		Если Не ЗначениеЗаполнено(КодировкаТекстаДляЗаписи) Тогда
			КодировкаТекстаДляЗаписи = Неопределено;
		КонецЕсли;	
		
		Текст.Записать(ВыбранноеПолноеИмяФайла, КодировкаТекстаДляЗаписи);
		
		Состояние(НСтр("ru = 'Файл успешно сохранен'"), , ВыбранноеПолноеИмяФайла);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура Задачи(Команда)
	
	ОткрытьФорму("ОбщаяФорма.ПроцессыИЗадачи", 
		Новый Структура("Предмет, НезависимоеОкно", Файл, Истина));	
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКарточку(Команда)
	
	ПоказатьЗначение(,Файл);
	
КонецПроцедуры

&НаКлиенте
Процедура ВнешнийРедактор(Команда)
	
	ЗаписатьТекст();
	РаботаСФайламиКлиент.ВыполнитьЗапускПриложения(ИмяОткрываемогоФайла);
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Редактировать(Команда)
	
	КомандыРаботыСФайламиКлиент.Редактировать(Файл, ПолучитьУникальныйИдентификатор());
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаТекст;
	Элементы.ЗаписатьИЗакрыть.Видимость = Истина;
	Элементы.Записать.Видимость = Ложь;
	Элементы.ЗакончитьРедактирование.Видимость = Истина;
	Элементы.Редактировать.Видимость = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ФайлРедактировался" И Параметр = Файл Тогда
		РежимРедактирования = Истина;
		УстановитьДоступностьКоманд();
	КонецЕсли;
	
	Если ИмяСобытия = "Запись_Файл" И Параметр.Событие = "ДанныеФайлаИзменены" Тогда
		
		ФайлВОповещении = Неопределено;
		
		Если ТипЗнч(Параметр) = Тип("Структура") И Параметр.Свойство("Файл") Тогда
			ФайлВОповещении = Параметр.Файл;
		Иначе	
			ФайлВОповещении = Источник;
		КонецЕсли;	
		
		Если ФайлВОповещении = Файл Тогда
			
			ДанныеФайла = РаботаСФайламиВызовСервера.ДанныеФайла(Файл);
			
			РежимРедактирования = Ложь;
			
			Если ДанныеФайла.РедактируетТекущийПользователь Тогда
				РежимРедактирования = Истина;
			КонецЕсли;	
			
			Если ДанныеФайла.Версия <> ДанныеФайла.ТекущаяВерсия Тогда
				РежимРедактирования = Ложь;
			КонецЕсли;	
			
			УстановитьДоступностьКоманд();
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
	
	ЗаписатьТекст();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьТекст()
	
	Если Модифицированность Тогда
		
		КодировкаТекстаДляЗаписи = КодировкаТекстаФайла;
		Если Не ЗначениеЗаполнено(КодировкаТекстаДляЗаписи) Тогда
			КодировкаТекстаДляЗаписи = Неопределено;
		КонецЕсли;	
		
		Текст.Записать(ИмяОткрываемогоФайла, КодировкаТекстаДляЗаписи);
		Модифицированность = Ложь;
		
		Если ДанныеФайла.Размер < ПредельныйРазмерФайла Тогда
			ТекстHtml = ПолучитьHtml(Текст.ПолучитьТекст());
		КонецЕсли;
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьУникальныйИдентификатор()
	
	Если ЗначениеЗаполнено(ИдентификаторРодительскойФормы) Тогда
		Возврат ИдентификаторРодительскойФормы;
	КонецЕсли;
	
	Возврат УникальныйИдентификатор;
	
КонецФункции	

&НаКлиенте
Процедура ЗакончитьРедактирование(Команда)
	
	ЗаписатьТекст();
	
	Обработчик = Новый ОписаниеОповещения("УстановитьДоступностьФайловыхКоманд", ЭтотОбъект);
	
	ПараметрыОбновленияФайла = РаботаСФайламиКлиент.ПараметрыОбновленияФайла(Обработчик, Файл, 
		ПолучитьУникальныйИдентификатор());
	ПараметрыОбновленияФайла.Кодировка = КодировкаТекстаФайла;
	РаботаСФайламиКлиент.ЗакончитьРедактированиеСОповещением(ПараметрыОбновленияФайла);
		
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьФайловыхКоманд(Результат = Неопределено, ПараметрыВыполнения = Неопределено) Экспорт
	
	Если Результат = Истина Тогда
		РежимРедактирования = Ложь;
	КонецЕсли;	
	УстановитьДоступностьКоманд();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ОбщегоНазначенияДокументооборотКлиент.ПередЗакрытием(
		Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка, Модифицированность) Тогда
		Возврат;
	КонецЕсли;
	
	Если Модифицированность И Не БылЗаданВопросПередЗакрытием Тогда
		
		Отказ = Истина;
		
		ИмяИРасширение = ФайловыеФункцииКлиентСервер.ПолучитьИмяСРасширением(ДанныеФайла.ПолноеНаименованиеВерсии, 
			ДанныеФайла.Расширение);
			
		ТекстВопроса = СтрШаблон(
			НСтр("ru ='Файл ""%1"" был изменен.
			|Сохранить изменения?'"), ИмяИРасширение);
			
		Обработчик = Новый ОписаниеОповещения("ПослеВопросаПередЗакрытием", ЭтотОбъект);
		
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Сохранить'"));
		Кнопки.Добавить(КодВозвратаДиалога.Нет, НСтр("ru = 'Не сохранять'"));
		Кнопки.Добавить(КодВозвратаДиалога.Отмена);
		
		ПоказатьВопрос(Обработчик, ТекстВопроса, Кнопки);
		БылЗаданВопросПередЗакрытием = Истина;
		
	ИначеЕсли РежимРедактирования И Не БылЗаданВопросПередЗакрытием Тогда	
		
		Обработчик = Новый ОписаниеОповещения("УстановитьДоступностьФайловыхКоманд", ЭтотОбъект);
		
		ПараметрыОбновленияФайла = РаботаСФайламиКлиент.ПараметрыОбновленияФайла(Обработчик, Файл, 
			ПолучитьУникальныйИдентификатор());
		ПараметрыОбновленияФайла.СоздатьНовуюВерсию = ДанныеФайла.ХранитьВерсии;
		ПараметрыОбновленияФайла.Кодировка = КодировкаТекстаФайла;
		РаботаСФайламиКлиент.ЗакончитьРедактированиеСОповещением(ПараметрыОбновленияФайла);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВопросаПередЗакрытием(Ответ, ПараметрыВыполнения) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Нет Тогда 
		Закрыть();
	ИначеЕсли Ответ = КодВозвратаДиалога.Да Тогда 
		
		ЗаписатьТекст();

		Обработчик = Новый ОписаниеОповещения("УстановитьДоступностьФайловыхКоманд", ЭтотОбъект);
		
		ПараметрыОбновленияФайла = РаботаСФайламиКлиент.ПараметрыОбновленияФайла(Обработчик, Файл, 
			ПолучитьУникальныйИдентификатор());
		ПараметрыОбновленияФайла.Кодировка = КодировкаТекстаФайла;
		ПараметрыОбновленияФайла.СоздатьНовуюВерсию = ДанныеФайла.ХранитьВерсии;
		РаботаСФайламиКлиент.ЗакончитьРедактированиеСОповещением(ПараметрыОбновленияФайла);
		
		Модифицированность = Ложь;
		Закрыть();
		
	ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда 
		БылЗаданВопросПередЗакрытием = Ложь;
	КонецЕсли;	
	
КонецПроцедуры	

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ВРег(ИсточникВыбора.ИмяФормы) = ВРег("Справочник.ВерсииФайлов.Форма.ФормаВыбораСпособаСравненияВерсий") Тогда
		
		Если ВыбранноеЗначение <> КодВозвратаДиалога.ОК Тогда
			Возврат;
		КонецЕсли;
		
		// Повторное чтение настройки
		СпособСравненияВерсийФайлов = ФайловыеФункцииСлужебныйКлиентСервер.ПерсональныеНастройкиРаботыСФайлами().СпособСравненияВерсийФайлов;
		Если СпособСравненияВерсийФайлов = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ПродолжитьСравнениеВерсий(СпособСравненияВерсийФайлов);
		
	ИначеЕсли ВРег(ИсточникВыбора.ИмяФормы) = ВРег("Справочник.Файлы.Форма.ВыборКодировки") Тогда
		
		Если ТипЗнч(ВыбранноеЗначение) <> Тип("Структура") Тогда
			Возврат;
		КонецЕсли;
		
		КодировкаТекстаФайла = ВыбранноеЗначение.Значение;
		КодировкаПредставление = ВыбранноеЗначение.Представление;
		
		ПрочитатьТекст();
		
		РаботаСФайламиСлужебныйВызовСервера.ЗаписатьКодировкуВерсииФайлаИИзвлеченныйТекст(
			ДанныеФайла.Версия,
			КодировкаТекстаФайла,
			Текст.ПолучитьТекст());
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьОтличия(Команда)
	
	#Если НЕ ВебКлиент Тогда
		// Чтение настройки
		СпособСравненияВерсийФайлов = ФайловыеФункцииСлужебныйКлиентСервер.ПерсональныеНастройкиРаботыСФайлами().СпособСравненияВерсийФайлов;
		
		Если СпособСравненияВерсийФайлов = Неопределено Тогда 
			// Инициализация настройки
			ОткрытьФорму("Справочник.ВерсииФайлов.Форма.ФормаВыбораСпособаСравненияВерсий", , ЭтаФорма);
			Возврат;
		КонецЕсли;
		
		ПродолжитьСравнениеВерсий(СпособСравненияВерсийФайлов);
	#Иначе
		ПоказатьПредупреждение(, НСтр("ru = 'Сравнение версий в веб-клиенте не поддерживается.'"));
	#КонецЕсли

КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьКоманд()
	
	Элементы.Текст.ТолькоПросмотр = Не РежимРедактирования;
	Элементы.ПоказатьОтличия.Доступность = РежимРедактирования;
	Элементы.Редактировать.Доступность = Не РежимРедактирования;
	Элементы.ЗакончитьРедактирование.Доступность = РежимРедактирования;
	Элементы.ЗаписатьИЗакрыть.Доступность = РежимРедактирования;
	Элементы.Записать.Доступность = РежимРедактирования;
	
	ЗаголовокСтрока = ФайловыеФункцииКлиентСервер.ПолучитьИмяСРасширением(ДанныеФайла.ПолноеНаименованиеВерсии, 
		ДанныеФайла.Расширение);
	Если Не РежимРедактирования Тогда
		ЗаголовокСтрока = ЗаголовокСтрока + НСтр("ru=' (только просмотр)'");
	КонецЕсли;
	Заголовок = ЗаголовокСтрока;
	
	Если РежимРедактирования = Ложь И ДанныеФайла.Размер < ПредельныйРазмерФайла Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаHtml;
	Иначе
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаТекст;
	КонецЕсли;	
	
	Если Не РежимРедактирования Тогда
		Элементы.ЗаписатьИЗакрыть.Видимость = Ложь;
		Элементы.Записать.Видимость = Ложь;
		Элементы.ЗакончитьРедактирование.Видимость = Ложь;
		Элементы.Редактировать.Видимость = Истина;
	Иначе	
		Элементы.ЗаписатьИЗакрыть.Видимость = Истина;
		Элементы.Записать.Видимость = Ложь;
		Элементы.ЗакончитьРедактирование.Видимость = Истина;
		Элементы.Редактировать.Видимость = Ложь;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьТекст()
	
	КодировкаТекстаДляЧтения = КодировкаТекстаФайла;
	Если Не ЗначениеЗаполнено(КодировкаТекстаДляЧтения) Тогда
		КодировкаТекстаДляЧтения = Неопределено;
	КонецЕсли;	
	
	Текст.Прочитать(ИмяОткрываемогоФайла, КодировкаТекстаДляЧтения);
	
	Если ДанныеФайла.Размер < ПредельныйРазмерФайла Тогда
		ТекстHtml = ПолучитьHtml(Текст.ПолучитьТекст());
	КонецЕсли;	
	
	Если РежимРедактирования = Ложь И ДанныеФайла.Размер < ПредельныйРазмерФайла Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаHtml;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	Если Модифицированность Тогда
		
		ЗаписатьТекст();
		
		Обработчик = Новый ОписаниеОповещения("ЗакрытьФорму", ЭтотОбъект);
		
		ПараметрыОбновленияФайла = РаботаСФайламиКлиент.ПараметрыОбновленияФайла(Обработчик, Файл, 
			ПолучитьУникальныйИдентификатор());
		ПараметрыОбновленияФайла.Кодировка = КодировкаТекстаФайла;
		РаботаСФайламиКлиент.ЗакончитьРедактированиеСОповещением(ПараметрыОбновленияФайла);
		
		Возврат;
		
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(Результат = Неопределено, ПараметрыВыполнения = Неопределено) Экспорт
	
	Если Результат = Истина Тогда
		Закрыть();
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьОбъекты(Команда)
	
	ОбработчикОтветаНаВопрос = 
		Новый ОписаниеОповещения("ПродолжитьОтправкуПослеВопросОЗавершенииРедактирования", ЭтотОбъект);
	
	Если РежимРедактирования Тогда
		
		ТекстВопроса = НСтр("ru = 'Перед отправкой необходимо закончить редактирование файла.
			|
			|Закончить редактирование и отправить?'");
		
		ОбщегоНазначенияДокументооборотКлиент.ПоказатьВопросДаНет(
			ОбработчикОтветаНаВопрос, 
			ТекстВопроса,
			НСтр("ru = 'Да'"),
			НСтр("ru = 'Нет'"),
			КодВозвратаДиалога.Да);
			
		Возврат;
		
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ОбработчикОтветаНаВопрос, КодВозвратаДиалога.Да);
	
КонецПроцедуры

// Продолжение процедуры ОтправитьОбъекты
//
&НаКлиенте
Процедура ПродолжитьОтправкуПослеВопросОЗавершенииРедактирования(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Обработчик = Новый ОписаниеОповещения("ПродолжитьОтправкуПослеЗавершенияРедактирования", ЭтотОбъект);
	
	Если РежимРедактирования Тогда
		
		ЗаписатьТекст();
		
		ПараметрыОбновленияФайла = РаботаСФайламиКлиент.ПараметрыОбновленияФайла(Обработчик, Файл, 
			ПолучитьУникальныйИдентификатор());
		ПараметрыОбновленияФайла.Кодировка = КодировкаТекстаФайла;
		РаботаСФайламиКлиент.ЗакончитьРедактированиеСОповещением(ПараметрыОбновленияФайла);
		
		Возврат;
		
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Обработчик, Истина);
	
КонецПроцедуры

// Продолжение ПродолжитьОтправкуПослеВопросОЗавершенииРедактирования
//
&НаКлиенте
Процедура ПродолжитьОтправкуПослеЗавершенияРедактирования(
	Результат = Неопределено, ПараметрыВыполнения = Неопределено) Экспорт
	
	Если Результат <> Истина Тогда
		Возврат;
	КонецЕсли;
	
	ПомощникОтправитьКлиент.ОтправитьОбъекты(
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Файл), ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьКодировку(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТекущаяКодировка", КодировкаТекстаФайла);
	ОткрытьФорму("Справочник.Файлы.Форма.ВыборКодировки", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьСравнениеВерсий(СпособСравненияВерсийФайлов)
	#Если НЕ ВебКлиент Тогда
		
		ЗаписатьТекст();
		
		ДанныеФайлаДляСохранения = РаботаСФайламиВызовСервера.ДанныеФайлаДляСохранения(
			Файл, Неопределено, УникальныйИдентификатор);
		
		АдресФайла = ДанныеФайлаДляСохранения.НавигационнаяСсылкаТекущейВерсии;
		
		Если ДанныеФайла.ТекущаяВерсия <> ДанныеФайла.Версия Тогда
			АдресФайла = РаботаСФайламиСлужебныйВызовСервера.ПолучитьНавигационнуюСсылкуДляОткрытия(
				ДанныеФайла.Версия, УникальныйИдентификатор);
		КонецЕсли;
		
		ПутьКФайлу = ПолучитьИмяВременногоФайла(ДанныеФайла.Расширение);
		
		ПередаваемыеФайлы = Новый Массив;
		Описание = Новый ОписаниеПередаваемогоФайла(ПутьКФайлу, АдресФайла);
		ПередаваемыеФайлы.Добавить(Описание);
		
		// Сохраним Файл из БД на диск
		Если Не ПолучитьФайлы(ПередаваемыеФайлы,, ПутьКФайлу, Ложь) Тогда
			Возврат;
		КонецЕсли;
		
		РаботаСФайламиКлиент.СравнитьФайлы(ПутьКФайлу, ИмяОткрываемогоФайла, СпособСравненияВерсийФайлов);
		
	#КонецЕсли
КонецПроцедуры

&НаКлиенте
Процедура Печатать(Команда)
	
	#Если ВебКлиент Тогда 
		ПоказатьПредупреждение(, НСтр("ru = 'В Веб-клиенте печать файлов не поддерживается.'"));
		Возврат;
	#КонецЕсли
	
	#Если Не ВебКлиент Тогда
	
		СистемнаяИнфо = Новый СистемнаяИнформация;
		Если СистемнаяИнфо.ТипПлатформы <> ТипПлатформы.Windows_x86 
		   И СистемнаяИнфо.ТипПлатформы <> ТипПлатформы.Windows_x86_64 Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Печать файлов возможна только в Windows.'"));
			Возврат;
		КонецЕсли;
		
		Если РежимРедактирования Тогда
			ЗаписатьТекст();
		КонецЕсли;	
		
		ФайлНаДиске = Новый Файл(ИмяОткрываемогоФайла);
		ИмяВременногоФайла = ПолучитьИмяВременногоФайла(ФайлНаДиске.Расширение);
		Текст.Записать(ИмяВременногоФайла, "UTF-16");
		
		РаботаСФайламиКлиент.НапечататьИзПриложенияПоИмениФайла(ИмяВременногоФайла);
		
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстHtmlПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РаботаС_HTMLКлиент.ОткрытьСсылку(Элемент, ДанныеСобытия, 
		СтандартнаяОбработка, ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура НастроитьОтображениеКомандРаботыСПроцессамиИЗадачами()
	
	ДоступнаОтправкаФайла = 
		РаботаСФайламиВызовСервера.ДоступнаОтправкаФайлаИзВстроенногоРедактораФайла(ДанныеФайла);
	
	Элементы.ОтправитьОбъекты.Видимость = ДоступнаОтправкаФайла;
	Элементы.Задачи.Видимость = ДоступнаОтправкаФайла;
	
КонецПроцедуры