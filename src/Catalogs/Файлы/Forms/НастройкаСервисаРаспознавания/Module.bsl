
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("СтратегияРаспознавания") Тогда
		СтратегияРаспознавания = Параметры.СтратегияРаспознавания;
	Иначе	
		СтратегияРаспознавания = Перечисления.СтратегииРаспознаванияТекста.ПоместитьТолькоВТекстовыйОбраз;
	КонецЕсли;
	
	РаспознаватьФайлы = 
		(СтратегияРаспознавания = Перечисления.СтратегииРаспознаванияТекста.ПоместитьТолькоВТекстовыйОбраз);
	
	ЯзыкиРаспознавания = РаботаСФайламиВызовСервера.ЯзыкиРаспознаванияПрограммы(
		РаботаСФайламиВызовСервера.ПрограммаРаспознавания());
	Для Каждого Запись Из ЯзыкиРаспознавания Цикл
		Элементы.ЯзыкРаспознавания.СписокВыбора.Добавить(Запись.Language, Запись.Name);
	КонецЦикла;	
	
	Если Параметры.Свойство("ЯзыкРаспознавания") Тогда
		ЯзыкРаспознавания = Параметры.ЯзыкРаспознавания;
	Иначе
		
		ЯзыкРаспознавания = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("Распознавание", "ЯзыкРаспознавания");
		Если ЯзыкРаспознавания = "" Тогда
			ЯзыкРаспознавания = РаботаСФайламиВызовСервера.ЯзыкРаспознаванияВСервисеПоУмолчанию();
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЯзыкиРаспознавания.Найти(ЯзыкРаспознавания, "Language") = Неопределено Тогда
		ЯзыкРаспознавания = РаботаСФайламиВызовСервера.ЯзыкРаспознаванияВСервисеПоУмолчанию();
	КонецЕсли;
	
	Если Элементы.ЯзыкРаспознавания.СписокВыбора.Количество() = 1 Тогда
		Элементы.ЯзыкРаспознавания.Видимость = Ложь;
		
		Элементы.ДекорацияЯзыкРаспознавания.Видимость = Истина;
		Элементы.ДекорацияЯзыкРаспознавания.Заголовок = Элементы.ДекорацияЯзыкРаспознавания.Заголовок
			+ Элементы.ЯзыкРаспознавания.СписокВыбора.НайтиПоЗначению(ЯзыкРаспознавания).Представление;
	Иначе
		Элементы.ЯзыкРаспознавания.Видимость = Истина;
		Элементы.ДекорацияЯзыкРаспознавания.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РаспознаватьФайлыПриИзменении(Элемент)
	
	Если РаспознаватьФайлы Тогда
		СтратегияРаспознавания = ПредопределенноеЗначение(
			"Перечисление.СтратегииРаспознаванияТекста.ПоместитьТолькоВТекстовыйОбраз");
	Иначе
		СтратегияРаспознавания = ПредопределенноеЗначение(
			"Перечисление.СтратегииРаспознаванияТекста.НеРаспознавать");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	СтруктураВозврата = Новый Структура;
	
	СтруктураВозврата.Вставить("ЯзыкРаспознавания", ЯзыкРаспознавания);
	СтруктураВозврата.Вставить("СтратегияРаспознавания", СтратегияРаспознавания);
	
	Закрыть(СтруктураВозврата);
	
КонецПроцедуры

#КонецОбласти






