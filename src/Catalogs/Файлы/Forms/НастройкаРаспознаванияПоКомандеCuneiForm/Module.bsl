
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ПрограммаРаспознавания = РаботаСФайламиВызовСервера.ПрограммаРаспознавания();
	
	ЯзыкиРаспознавания = РаботаСФайламиВызовСервера.ЯзыкиРаспознаванияПрограммы(
		ПрограммаРаспознавания);
	Для Каждого Запись Из ЯзыкиРаспознавания Цикл
		Элементы.ЯзыкРаспознавания.СписокВыбора.Добавить(Запись.Language, Запись.Name);
	КонецЦикла;
	
	Если Параметры.Свойство("ЯзыкРаспознавания") Тогда
		ЯзыкРаспознавания = Параметры.ЯзыкРаспознавания;
	Иначе	
		ЯзыкРаспознавания = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"Распознавание", "ЯзыкРаспознавания");
		
		Если Не ЗначениеЗаполнено(ЯзыкРаспознавания) Тогда
			ЯзыкРаспознавания = РаботаСФайламиВызовСервера.ПолучитьЯзыкРаспознавания();
		КонецЕсли;
		
		Если ЯзыкиРаспознавания.Найти(ЯзыкРаспознавания, "Language") = Неопределено Тогда
			ЯзыкРаспознавания = РаботаСФайламиВызовСервера.ЯзыкРаспознаванияПрограммыПоУмолчанию(ПрограммаРаспознавания);
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.Свойство("СтратегияРаспознавания") Тогда
		СтратегияРаспознавания = Параметры.СтратегияРаспознавания;
	Иначе	
		СтратегияРаспознавания = Перечисления.СтратегииРаспознаванияТекста.ПоместитьТолькоВТекстовыйОбраз;
	КонецЕсли;
	
	Если Параметры.УточнитьСтратегию Тогда
		Элементы.СтратегияРаспознавания.Видимость = Истина;
		Элементы.ДекорацияОтступ.Видимость = Истина;
		КлючСохраненияПоложенияОкна = "";
	Иначе
		Элементы.СтратегияРаспознавания.Видимость = Ложь;
		Элементы.ДекорацияОтступ.Видимость = Ложь;
		Элементы.ЯзыкРаспознавания.Заголовок = НСтр("ru = 'Язык'");
		КлючСохраненияПоложенияОкна = "БезУточненияСтратегии";
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	СтруктураВозврата = Новый Структура;
	
	СтруктураВозврата.Вставить("ЯзыкРаспознавания", ЯзыкРаспознавания);
	СтруктураВозврата.Вставить("СтратегияРаспознавания", СтратегияРаспознавания);
	
	Закрыть(СтруктураВозврата);
КонецПроцедуры
