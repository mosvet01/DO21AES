
#Область ОписаниеПеременных

&НаКлиенте
Перем АвтораспознаваниеДоИзмененияРежима;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ПрочитатьПараметрыРаспознаванияВФорму();
	
	ЯзыкиРаспознавания = РаботаСФайламиВызовСервера.ЯзыкиРаспознаванияПрограммы(
		Перечисления.ПрограммыРаспознавания.СервисРаспознавания);
	Для Каждого Запись Из ЯзыкиРаспознавания Цикл
		Элементы.ЯзыкРаспознавания.СписокВыбора.Добавить(Запись.Language, Запись.Name);
	КонецЦикла;
	
	Если Элементы.ЯзыкРаспознавания.СписокВыбора.НайтиПоЗначению(ЯзыкРаспознавания) = Неопределено Тогда
		ЯзыкРаспознавания = РаботаСФайламиВызовСервера.ЯзыкРаспознаванияВСервисеПоУмолчанию();
	КонецЕсли;
	
	Если Элементы.ЯзыкРаспознавания.СписокВыбора.Количество() = 1 Тогда
		Элементы.ЯзыкРаспознавания.Видимость = Ложь;
		
		Элементы.ДекорацияЯзыкРаспознавания.Видимость = Истина;
		Элементы.ДекорацияЯзыкРаспознавания.Заголовок = Элементы.ДекорацияЯзыкРаспознавания.Заголовок
			+ Элементы.ЯзыкРаспознавания.СписокВыбора.НайтиПоЗначению(ЯзыкРаспознавания).Представление;
	Иначе
		Элементы.ЯзыкРаспознавания.Видимость = Истина;
		Элементы.ДекорацияЯзыкРаспознавания.Видимость = Ложь;
	КонецЕсли;
	
	Если ПользовательЗаданияРаспознавания.Пустая() Тогда
		МенятьАвтораНовогоФайлаИлиВерсии = Ложь;
		Элементы.ПользовательЗаданияРаспознавания.Доступность = Ложь;
	Иначе
		МенятьАвтораНовогоФайлаИлиВерсии = Истина;
	КонецЕсли;
	
	ОбновитьПараметрыАвторизации();
	ОбновитьОтображениеИнформацииОбАвторизации();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура МенятьАвтораНовогоФайлаИлиВерсииПриИзменении(Элемент)
	
	Если НЕ МенятьАвтораНовогоФайлаИлиВерсии Тогда
		ПользовательЗаданияРаспознавания = ПредопределенноеЗначение("Справочник.Пользователи.ПустаяСсылка");
		Элементы.ПользовательЗаданияРаспознавания.Доступность = Ложь;
	Иначе
		Элементы.ПользовательЗаданияРаспознавания.Доступность = Истина;
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормы_РежимыРаспознавания

&НаКлиенте
Процедура РежимыРаспознаванияРежимПриИзменении(Элемент)
	
	ВключеноАвтораспознавание = ВключеноАвтораспознавание();
	Если ВключеноАвтораспознавание И Не АвтораспознаваниеДоИзмененияРежима
		И Не ЗначениеЗаполнено(НачалоАвтоматическогоРаспознаванияФайлов) Тогда
		
		НачалоАвтоматическогоРаспознаванияФайлов = ТекущаяДата();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РежимыРаспознаванияРежимОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура РежимыРаспознаванияРежимНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	АвтораспознаваниеДоИзмененияРежима = ВключеноАвтораспознавание();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодключитьИнтернетПоддержку(Команда)
	
	ОбрабочтикПодключенияПоддержки = Новый ОписаниеОповещения(
		"ПослеПодключенияИнтернетПоддержки",
		ЭтотОбъект);
	
	ИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей(
		ОбрабочтикПодключенияПоддержки, ЭтаФорма);
	
КонецПроцедуры

// Продолжение ПодключитьИнтернетПоддержку.
//
&НаКлиенте
Процедура ПослеПодключенияИнтернетПоддержки(РезультатПодключения, ДополнительныеПараметры) Экспорт
	
	Если РезультатПодключения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыполнитьАвторизацию();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьСервисРаспознавания(Команда)
	
	ВыполнитьАвторизацию();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьСервисРаспознавания(Команда)
	
	УдалитьДанныеАвторизации();
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	ОбработчикОтвета = Новый ОписаниеОповещения("ОК_ПослеВопросаОПродолжении", ЭтотОбъект);
	
	Если ВключеноАвтораспознавание() Тогда
		ТекстВопроса = НСтр("ru = 'Внимание! Часть файлов распознается автоматически.
			|К ним будут применены единые правила тарификации, даже если язык файла отличается от указанного.
			|
			|Продолжить?'");
		ОбщегоНазначенияДокументооборотКлиент.ПоказатьВопросДаНет(
			ОбработчикОтвета,
			ТекстВопроса,
			НСтр("ru = 'Продолжить'"),
			НСтр("ru = 'Отмена'"));
		
		Возврат;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ОбработчикОтвета, КодВозвратаДиалога.Да);
	
КонецПроцедуры

// Продолжение ОК
//
&НаКлиенте
Процедура ОК_ПослеВопросаОПродолжении(КодОтвета, ДолнительныеПараметры) Экспорт
	
	Если КодОтвета <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписатьПараметрыРаспознавания();
	Закрыть();
	
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПараметрыРаспознавания

// Читает параметры распознавания в форму.
//
&НаСервере
Процедура ПрочитатьПараметрыРаспознаванияВФорму()
	
	ПараметрыРапознавания = РаботаСФайламиВызовСервера.ПараметрыРапознаванияВСервисе();
	АдресПараметровРапознавания = ПоместитьВоВременноеХранилище(ПараметрыРапознавания, УникальныйИдентификатор);
	
	РежимыРаспознавания.Очистить();
	ВладельцыФайлов = Новый Массив;
	Для Каждого РежимДляВладельца Из ПараметрыРапознавания.РежимыРаспознавания Цикл
		СтрокаТаблицы = РежимыРаспознавания.Добавить();
		СтрокаТаблицы.Владелец = РежимДляВладельца.Ключ;
		СтрокаТаблицы.Режим = РежимДляВладельца.Значение;
		ВладельцыФайлов.Добавить(СтрокаТаблицы.Владелец);
	КонецЦикла;
	
	ПредставленияВладельцев = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(ВладельцыФайлов, "Синоним");
	Для Каждого СтрокаТаблицы Из РежимыРаспознавания Цикл
		СтрокаТаблицы.Представление = ПредставленияВладельцев[СтрокаТаблицы.Владелец];
	КонецЦикла;
	РежимыРаспознавания.Сортировать("Представление");
	
	ЯзыкРаспознавания = ПараметрыРапознавания.ЯзыкРаспознавания;
	ПользовательЗаданияРаспознавания = ПараметрыРапознавания.ПользовательЗаданияРаспознавания;
	
	ГраницаОтключенияНачалаАвтораспознаванияВСервисе = 
		ПараметрыРапознавания.ГраницаОтключенияНачалаАвтораспознаванияВСервисе;
	
	СрокХраненияПротоколаРаботыССервисомРаспознавания =
		ПараметрыРапознавания.СрокХраненияПротоколаРаботыССервисомРаспознавания;
		
	НачалоАвтоматическогоРаспознаванияФайлов =
		ПараметрыРапознавания.НачалоАвтоматическогоРаспознаванияФайлов;
	
КонецПроцедуры

// Пишет параметры распознавания, установленные в форме.
//
&НаСервере
Процедура ЗаписатьПараметрыРаспознавания()
	
	ПараметрыРапознавания = ПолучитьИзВременногоХранилища(АдресПараметровРапознавания);
	
	ПараметрыРапознавания.РежимыРаспознавания.Очистить();
	Для Каждого СтрокаТаблицы Из РежимыРаспознавания Цикл
		ПараметрыРапознавания.РежимыРаспознавания[СтрокаТаблицы.Владелец] = СтрокаТаблицы.Режим;
	КонецЦикла;
	
	ПараметрыРапознавания.ЯзыкРаспознавания = ЯзыкРаспознавания;
	ПараметрыРапознавания.ПользовательЗаданияРаспознавания = ПользовательЗаданияРаспознавания;
	
	ПараметрыРапознавания.ГраницаОтключенияНачалаАвтораспознаванияВСервисе =
		ГраницаОтключенияНачалаАвтораспознаванияВСервисе;
	
	ПараметрыРапознавания.СрокХраненияПротоколаРаботыССервисомРаспознавания =
		СрокХраненияПротоколаРаботыССервисомРаспознавания;
	
	ПараметрыРапознавания.НачалоАвтоматическогоРаспознаванияФайлов =
		НачалоАвтоматическогоРаспознаванияФайлов;
	
	РаботаСФайламиВызовСервера.УстановитьПараметрыРаспознаванияВСервисе(ПараметрыРапознавания);
	
КонецПроцедуры

#КонецОбласти

#Область АвторизацияВСервисе

&НаСервере
Процедура ОбновитьПараметрыАвторизации()
	
	БазаПодключенаКИТС = 
		ИнтернетПоддержкаПользователей.ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки();
	
	БазаАвторизированаВСервисе = Распознавание.БазаАвторизированаВСервисеРаспознавания();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОтображениеИнформацииОбАвторизации()
	
	Если БазаАвторизированаВСервисе Тогда
		Элементы.ДекорацияСервисРаспознаванияПодключен.Заголовок = ОписаниеСостоянияСервисПодключен();
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаСервисРаспознаванияПодключен;
	ИначеЕсли Не БазаПодключенаКИТС Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаНеПодключенаИнтернетПоддержка;
	Иначе
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаНеПодключенСервисРаспознавания;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьАвторизацию()
	
	ОписаниеОшибки = "";
	Попытка
		Распознавание.ВыполнитьАвторизациюЧерезИТС();
	Исключение
		ОписаниеОшибки = ОписаниеОшибки();
	КонецПопытки;
	
	ОбновитьПараметрыАвторизации();
	ОбновитьОтображениеИнформацииОбАвторизации();
	
	Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		ВызватьИсключение ОписаниеОшибки;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьДанныеАвторизации()
	
	Распознавание.УдалитьДанныеАвторизации();
	ОбновитьПараметрыАвторизации();
	ОбновитьОтображениеИнформацииОбАвторизации();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОписаниеСостоянияСервисПодключен()
	
	Результат = Распознавание.ЛимитСтраницРаспознавания();
	Если ЗначениеЗаполнено(Результат.ОписаниеОшибки) Тогда
		ЗаписьЖурналаРегистрации(
			Распознавание.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,,,
			Результат.ОписаниеОшибки);
	КонецЕсли;
	
	Возврат СтрШаблон(
		НСтр("ru = 'Сервис распознавания документов подключен. Осталось страниц: %1'"),
		Результат.ЛимитСтраниц);
	
КонецФункции

#КонецОбласти

&НаКлиенте
Функция ВключеноАвтораспознавание()
	
	Отбор = Новый Структура("Режим",
		ПредопределенноеЗначение("Перечисление.ВариантыРежимовРаспознаванияФайлов.Автоматический"));
	
	СтрокиСАвтораспознаванием = РежимыРаспознавания.НайтиСтроки(Отбор);
	
	Возврат СтрокиСАвтораспознаванием.Количество() <> 0;
	
КонецФункции

#КонецОбласти
