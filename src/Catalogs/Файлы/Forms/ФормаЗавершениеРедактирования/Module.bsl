///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2020, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Для Каждого ФайлСсылка Из Параметры.МассивФайлов Цикл
		НовыйЭлемент = ВыбранныеФайлы.Добавить();
		НовыйЭлемент.Ссылка = ФайлСсылка;
		НовыйЭлемент.ИндексКартинки = ФайлСсылка.ИндексКартинки;
	КонецЦикла;
	
	ВозможностьСоздаватьВерсииФайлов = Параметры.ВозможностьСоздаватьВерсииФайлов;
	Редактирует = Параметры.Редактирует;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ХранитьВерсии = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыВыбранныеФайлы

&НаКлиенте
Процедура ВыбранныеФайлыПередНачаломДобавления(Элемент, Отказ, Копирование)
	Отказ = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗакончитьРедактирование()
	
	МассивФайлов = Новый Массив;
	Для Каждого ЭлементСписка Из ВыбранныеФайлы Цикл
		МассивФайлов.Добавить(ЭлементСписка.Ссылка);
	КонецЦикла;
	
	Обработчик = Новый ОписаниеОповещения("ОбновитьСписокФайлов", ЭтотОбъект,
		Новый Структура("МассивФайлов", МассивФайлов));	
	
	РаботаСФайламиКлиент.ЗакончитьРедактированиеПоСсылкам(Обработчик, МассивФайлов, УникальныйИдентификатор, Истина,Комментарий);
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписокФайлов(Результат = Неопределено, ПараметрыВыполнения = Неопределено) Экспорт
	
	КоличествоЗанятыхФайлов = РаботаСФайламиВызовСервера.ПолучитьКоличествоЗанятыхФайлов(,,Истина);
	
	СтандартныеПодсистемыКлиент.УстановитьПараметрКлиента(
		"КоличествоЗанятыхФайлов", КоличествоЗанятыхФайлов);
	
	Оповестить("ИмпортФайловЗавершен");
	
	ПоказатьОповещениеПользователя(
		НСтр("ru = 'Редактирование закончено'"),
		,
		СтрШаблон(
			НСтр("ru = '%1 файлов'"), ПараметрыВыполнения.МассивФайлов.Количество()),
		БиблиотекаКартинок.Информация32);
	
КонецПроцедуры

#КонецОбласти