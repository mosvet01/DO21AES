#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Процедура - Делает первоначальное заполнение справочника, согласно Приложению
// Приказа Минцифры России о требованиях к обмену электронными документами
// Данные загружаются из макета
// https://gosedo.ru/законодательство/
//
Процедура ПервоначальноеЗаполнение() Экспорт
	
	ТекстовыйМакет = ПолучитьМакет("КлассификаторИзПриказаМинцифры667_233_От2020_12_04");
	
	Для НомерСтроки = 1 По ТекстовыйМакет.КоличествоСтрок() Цикл
		
		ТекущаяСтрока = ТекстовыйМакет.ПолучитьСтроку(НомерСтроки);
		МассивСтрок = СтрРазделить(ТекущаяСтрока, Символы.Таб, );
		Если МассивСтрок.Количество() <> 2 Тогда
			Продолжить;
		КонецЕсли;
		
		КодМЭДО = СокрЛП(МассивСтрок[0]);
		НаименованиеПоКлассификатору = СокрЛП(МассивСтрок[1]);
		Если СтрНайти(КодМЭДО, "00") = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Предмет = СоздатьЭлемент();
		ПредметСсылка = НайтиПоРеквизиту("КодМЭДО", КодМЭДО);
		Если ЗначениеЗаполнено(ПредметСсылка) Тогда
			Предмет = ПредметСсылка.ПолучитьОбъект();
		КонецЕсли;
		
		Предмет.КодМЭДО = КодМЭДО;
		
		Если Предмет.НаименованиеПолное <> НаименованиеПоКлассификатору Тогда
			Предмет.НаименованиеПолное = НаименованиеПоКлассификатору;
			Предмет.Записать();
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

// У справочника нет вообще Наименования, только ПолноеНаименование.
// Поэтому нужно переопределить Представление
Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Поля.Добавить("НаименованиеПолное");
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Представление = Данные.НаименованиеПолное;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Найти вид документа МЭДО по коду из паспорта контейнера, или по наименованию.
// 
// Параметры:
//  КодМЭДО - Строка - Код МЭДО
//  НаименованиеМЭДО - Строка - Наименование МЭДО
// 
// Возвращаемое значение:
//  СправочникСсылка.ВидыДокументовМЭДО - Найденный вид документа, или пустая ссылка.
Функция НайтиВидДокумента(КодМЭДО, НаименованиеМЭДО) Экспорт
	
	Если Не ЗначениеЗаполнено(КодМЭДО) Тогда
		Возврат НайтиПоРеквизиту("НаименованиеПолное", НаименованиеМЭДО);
	КонецЕсли;
	
	ВидДокумента = НайтиПоРеквизиту("КодМЭДО", КодМЭДО);
	Если ВидДокумента.Пустая() Тогда
		// Не найдено по коду, может быть по наименованию:
		ВидДокумента = НайтиПоРеквизиту("НаименованиеПолное", НаименованиеМЭДО);
	КонецЕсли;
	
	Возврат ВидДокумента;
	
КонецФункции

#КонецОбласти

#КонецЕсли