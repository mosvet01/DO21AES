#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Контрагент = Параметры.Контрагент;
	ВестиУчетСканКопийОригиналовДокументов = ПолучитьФункциональнуюОпцию("ВестиУчетСканКопийОригиналовДокументов");
	ИспользоватьФайлыУВходящихДокументов = ПолучитьФункциональнуюОпцию("ИспользоватьФайлыУВходящихДокументов");
	ИспользоватьФайлыУИсходящихДокументов = ПолучитьФункциональнуюОпцию("ИспользоватьФайлыУИсходящихДокументов");
	ИспользоватьБизнесПроцессыИЗадачи = ПолучитьФункциональнуюОпцию("ИспользоватьБизнесПроцессыИЗадачи");
	ИспользоватьЭлектронныеПодписи = ЭлектроннаяПодпись.ИспользоватьЭлектронныеПодписи();
	
	ЧтениеВнутреннихДокументов = ПравоДоступа("Чтение", Метаданные.Справочники.ВнутренниеДокументы);
	ЧтениеВходящихДокументов = ПравоДоступа("Чтение", Метаданные.Справочники.ВходящиеДокументы);
	ЧтениеИсходящихДокументов = ПравоДоступа("Чтение", Метаданные.Справочники.ИсходящиеДокументы);
	
	Если ЧтениеВходящихДокументов Тогда
		Элементы.ПодписанЭПВходящие.Видимость = ИспользоватьЭлектронныеПодписи;
		Элементы.ЗадачиВходящие.Видимость = ИспользоватьБизнесПроцессыИЗадачи;
		Элементы.ФайлыВходящие.Видимость = ИспользоватьФайлыУВходящихДокументов;
		
		СписокВходяшие.Параметры.УстановитьЗначениеПараметра("Контрагент", Контрагент);
		СписокВходяшие.Параметры.УстановитьЗначениеПараметра(
			"ВестиУчетСканКопийОригиналовДокументов", ВестиУчетСканКопийОригиналовДокументов);
		СписокВходяшие.ТекстЗапроса = 
			СтрЗаменить(СписокВходяшие.ТекстЗапроса, "//", "");
	Иначе
		Элементы.СтраницаВходящиеДокументы.Видимость = Ложь;
	КонецЕсли;
	
	Если ЧтениеИсходящихДокументов Тогда
		Элементы.ПодписанЭПИсходящие.Видимость = ИспользоватьЭлектронныеПодписи;
		Элементы.ЗадачиИсходящие.Видимость = ИспользоватьБизнесПроцессыИЗадачи;
		Элементы.ФайлыИсходящие.Видимость = ИспользоватьФайлыУИсходящихДокументов;
		
		СписокИсходящие.Параметры.УстановитьЗначениеПараметра("Контрагент", Контрагент);
		СписокИсходящие.Параметры.УстановитьЗначениеПараметра(
			"ВестиУчетСканКопийОригиналовДокументов", ВестиУчетСканКопийОригиналовДокументов);
		СписокИсходящие.ТекстЗапроса = 
			СтрЗаменить(СписокИсходящие.ТекстЗапроса, "//", "");
	Иначе
		Элементы.СтраницаИсходящиеДокументы.Видимость = Ложь;
	КонецЕсли;
	
	Если ЧтениеВнутреннихДокументов Тогда
		Элементы.ПодписанЭПВнутренние.Видимость = ИспользоватьЭлектронныеПодписи;
		Элементы.ЗадачиВнутренние.Видимость = ИспользоватьБизнесПроцессыИЗадачи;
		
		СписокВнутренние.Параметры.УстановитьЗначениеПараметра("Контрагент", Контрагент);
		СписокВнутренние.Параметры.УстановитьЗначениеПараметра(
			"ВестиУчетСканКопийОригиналовДокументов", ВестиУчетСканКопийОригиналовДокументов);
	Иначе
		Элементы.СтраницаВнутренниеДокументы.Видимость = Ложь;
	КонецЕсли;
	
	ТекущаяПапка = Неопределено;
	Элементы.Папки.ТекущаяСтрока = ТекущаяПапка;
	СписокВнутренние.Параметры.УстановитьЗначениеПараметра("Папка", Элементы.Папки.ТекущаяСтрока);
	
	ВидПросмотра = Перечисления.ВидыПросмотраСпискаОбъектов.Списком;
	ПереключитьВидПросмотра();
	
	Результат = ОбновитьДоступностьШаблоновВыполнить();
	ЕстьДоступныеШаблоныВнутреннихДокументов = Результат.ЕстьДоступныеШаблоныВнутреннихДокументов;
	ЕстьДоступныеШаблоныВходящихДокументов = Результат.ЕстьДоступныеШаблоныВходящихДокументов;
	ЕстьДоступныеШаблоныИсходящихДокументов = Результат.ЕстьДоступныеШаблоныИсходящихДокументов;
	
	ПоказатьУдаленные();
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	ТекущаяПапка = Настройки["ТекущаяПапка"];
	Элементы.Папки.ТекущаяСтрока = ТекущаяПапка;
	
	Если Элементы.Папки.ТекущаяСтрока <> Неопределено Тогда
		СписокВнутренние.Параметры.УстановитьЗначениеПараметра("Папка", Элементы.Папки.ТекущаяСтрока);
	КонецЕсли;
	
	ВидПросмотра = Настройки["ВидПросмотра"];
	ПереключитьВидПросмотра();
	
	ВнутренниеПоказыватьУдаленные = Настройки["ВнутренниеПоказыватьУдаленные"];
	ВходящиеПоказыватьУдаленные = Настройки["ВходящиеПоказыватьУдаленные"];
	ИсходящиеПоказыватьУдаленные = Настройки["ИсходящиеПоказыватьУдаленные"];
	ПоказатьУдаленные();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	//Обработчик ожидания для периодического обновления количества доступных шаблонов документов через каждые 20 минут
	ПодключитьОбработчикОжидания("ОбновитьДоступностьШаблонов", 1200, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗаписьКонтроля" Тогда
		Если ЗначениеЗаполнено(Параметр.Предмет) И ДелопроизводствоКлиентСервер.ЭтоДокумент(Параметр.Предмет) Тогда 
			ОповеститьОбИзменении(Параметр.Предмет);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицФормы

&НаКлиенте
Процедура СписокВнутренниеПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	СоздатьНовыйВнутреннийДокумент(Копирование);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВходящиеПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	СоздатьНовыйВходящийДокумент(Копирование);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокИсходящиеПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	СоздатьНовыйИсходящийДокумент(Копирование);

КонецПроцедуры

&НаКлиенте
Процедура ПапкиПриАктивизацииСтроки(Элемент)
	
	Если ВидПросмотра = ПредопределенноеЗначение("Перечисление.ВидыПросмотраСпискаОбъектов.ПоПапкам")
		И ТекущаяПапка <> Элементы.Папки.ТекущаяСтрока Тогда 
		
		ТекущаяПапка = Элементы.Папки.ТекущаяСтрока;
		
		ПодключитьОбработчикОжидания("ОбработкаОжидания", 0.2, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПапкиПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПапкиПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
	Если Не ЗначениеЗаполнено(Строка) Тогда
		ЭтоПереносПапок = ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("Массив") 
			И ПараметрыПеретаскивания.Значение.Количество() > 0
			И ТипЗнч(ПараметрыПеретаскивания.Значение[0]) = Тип("СправочникСсылка.ПапкиВнутреннихДокументов");
			
		Если Не ЭтоПереносПапок Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("Массив") 
		И ПараметрыПеретаскивания.Значение.Количество() > 0 
		И ТипЗнч(ПараметрыПеретаскивания.Значение[0]) = Тип("СправочникСсылка.ВнутренниеДокументы") Тогда
		
		Если ИзменитьПапкуДокументов(ПараметрыПеретаскивания.Значение, Строка) = Истина Тогда
			Элементы.СписокВнутренние.Обновить();
			
			Если ПараметрыПеретаскивания.Значение.Количество() = 1 Тогда
				ПолноеОписание = СтрШаблон(
					НСтр("ru = 'Внутренний документ ""%1"" перенесен в папку ""%2""'"),
					ПараметрыПеретаскивания.Значение[0], Строка);
				
				ПоказатьОповещениеПользователя(
					НСтр("ru = 'Внутренний документ перенесен.'"),
					ПолучитьНавигационнуюСсылку(ПараметрыПеретаскивания.Значение[0]),
					ПолноеОписание,
					БиблиотекаКартинок.Информация32);
			Иначе
				ПолноеОписание = СтрШаблон(
					НСтр("ru = 'Внутренние документы (%1 шт.) перенесены в папку ""%2""'"),
					ПараметрыПеретаскивания.Значение.Количество(), Строка);
				
				ПоказатьОповещениеПользователя(
					НСтр("ru = 'Внутренние документы перенесены.'"),
					,
					ПолноеОписание,
					БиблиотекаКартинок.Информация32);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Строка", Строка);
	СтруктураПараметров.Вставить("ПараметрыПеретаскивания", ПараметрыПеретаскивания);
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПапкиПеретаскиваниеПродолжение",
		ЭтотОбъект,
		СтруктураПараметров);
	
	Если ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("Массив") 
		И ПараметрыПеретаскивания.Значение.Количество() >= 1 
		И ТипЗнч(ПараметрыПеретаскивания.Значение[0]) = Тип("СправочникСсылка.ПапкиВнутреннихДокументов") Тогда
		
		НайденоЗацикливание = Ложь;
		Если РаботаСФайламиВызовСервера.СменитьРодителяПапок(
			ПараметрыПеретаскивания.Значение, Строка, НайденоЗацикливание) Тогда
			
			Элементы.Папки.Обновить();
			Элементы.СписокВнутренние.Обновить();
			
			Если ПараметрыПеретаскивания.Значение.Количество() = 1 Тогда
				ПолноеОписание = СтрШаблон(
					НСтр("ru = 'Папка ""%1"" перенесена в папку ""%2""'"),
					ПараметрыПеретаскивания.Значение[0], Строка);
				
				ПоказатьОповещениеПользователя(
					НСтр("ru = 'Папка перенесена.'"),
					,
					ПолноеОписание,
					БиблиотекаКартинок.Информация32);
			Иначе
				ПолноеОписание = СтрШаблон(
					НСтр("ru = 'Папки (%1 шт.) перенесены в папку ""%2""'"),
					ПараметрыПеретаскивания.Значение.Количество(), Строка);
				
				ПоказатьОповещениеПользователя(
					НСтр("ru = 'Папки перенесены.'"),
					,
					ПолноеОписание,
					БиблиотекаКартинок.Информация32);
			КонецЕсли;
			
			ВыполнитьОбработкуОповещения(ОписаниеОповещения);
		Иначе
			Если НайденоЗацикливание = Истина Тогда
				ПоказатьПредупреждение(ОписаниеОповещения, НСтр("ru = 'Зацикливание уровней !'"));
			КонецЕсли;
		КонецЕсли;
	Иначе 
		ВыполнитьОбработкуОповещения(ОписаниеОповещения);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ПапкиПеретаскиваниеПродолжение(Результат, Параметры) Экспорт 
	
	ОбработатьПеретаскиваниеФайлов(Параметры.ПараметрыПеретаскивания, "Папка", Параметры.Строка);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВключитьПросмотрПапками(Команда)
	
	Если ВидПросмотра <> ПредопределенноеЗначение("Перечисление.ВидыПросмотраСпискаОбъектов.ПоПапкам") Тогда
		ВидПросмотра = ПредопределенноеЗначение("Перечисление.ВидыПросмотраСпискаОбъектов.ПоПапкам");
		ПереключитьВидПросмотра();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВключитьПросмотрСписком(Команда)
	
	Если ВидПросмотра <> ПредопределенноеЗначение("Перечисление.ВидыПросмотраСпискаОбъектов.Списком") Тогда
		ВидПросмотра = ПредопределенноеЗначение("Перечисление.ВидыПросмотраСпискаОбъектов.Списком");
		ПереключитьВидПросмотра();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВнутренниеПоказыватьУдаленные(Команда)
	
	ВнутренниеПоказыватьУдаленные = Не ВнутренниеПоказыватьУдаленные;
	ПоказатьУдаленные();
	
КонецПроцедуры

&НаКлиенте
Процедура ВходящиеПоказыватьУдаленные(Команда)
	
	ВходящиеПоказыватьУдаленные = Не ВходящиеПоказыватьУдаленные;
	ПоказатьУдаленные();
	
КонецПроцедуры

&НаКлиенте
Процедура ИсходящиеПоказыватьУдаленные(Команда)
	
	ИсходящиеПоказыватьУдаленные = Не ИсходящиеПоказыватьУдаленные;
	ПоказатьУдаленные();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СоздатьНовыйВнутреннийДокумент(Копирование)
	
	ТекущийДокумент = Элементы.СписокВнутренние.ТекущаяСтрока;
	
	Если Копирование Тогда 
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ЗначениеКопирования", ТекущийДокумент);
		Открытьформу("Справочник.ВнутренниеДокументы.ФормаОбъекта",
			ПараметрыФормы, Элементы.СписокВнутренние, Новый УникальныйИдентификатор);
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"СоздатьНовыйВнутреннийДокументПродолжение",
		ЭтотОбъект);
	Если ЕстьДоступныеШаблоныВнутреннихДокументов Тогда
		РаботаСШаблонамиДокументовКлиент.ПоказатьФормуСозданияДокументаПоШаблону(
			ОписаниеОповещения,
			"ШаблоныВнутреннихДокументов");
	Иначе
		Результат = "СоздатьПустойДокумент";
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, Результат);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНовыйВнутреннийДокументПродолжение(Результат, Параметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Контрагент", Контрагент);
	
	Если ВидПросмотра = ПредопределенноеЗначение("Перечисление.ВидыПросмотраСпискаОбъектов.ПоПапкам") Тогда
		Если ЗначениеЗаполнено(Элементы.Папки.ТекущаяСтрока) Тогда
			ЗначенияЗаполнения.Вставить("Папка", Элементы.Папки.ТекущаяСтрока);
		КонецЕсли;
	КонецЕсли;
	
		
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ПараметрыФормы.Вставить("ШаблонДокумента", Результат);
	Иначе
		ПараметрыФормы.Вставить("ШаблонДокумента", Результат.ШаблонДокумента);
	КонецЕсли;
	
	Открытьформу("Справочник.ВнутренниеДокументы.ФормаОбъекта",
		ПараметрыФормы, Элементы.СписокВнутренние, Новый УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНовыйВходящийДокумент(Копирование)
	
	ТекущийДокумент = Элементы.СписокВходящие.ТекущаяСтрока;
	
	Если Копирование Тогда 
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ЗначениеКопирования", ТекущийДокумент);
		Открытьформу("Справочник.ВходящиеДокументы.ФормаОбъекта",
			ПараметрыФормы, Элементы.СписокВходящие, Новый УникальныйИдентификатор);
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"СоздатьНовыйВходящийДокументПродолжение",
		ЭтотОбъект);
		
	Если ЕстьДоступныеШаблоныВходящихДокументов Тогда
		РаботаСШаблонамиДокументовКлиент.ПоказатьФормуСозданияДокументаПоШаблону(
			ОписаниеОповещения,
			"ШаблоныВходящихДокументов");
	Иначе
		Результат = "СоздатьПустойДокумент";
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, Результат);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНовыйВходящийДокументПродолжение(Результат, Параметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Отправитель", Контрагент);
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ШаблонДокумента = Результат;
	Иначе
		ШаблонДокумента = Результат.ШаблонДокумента;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	ПараметрыФормы.Вставить("ЗаполнятьРеквизитыДоСоздания", Ложь);
	ПараметрыФормы.Вставить("ШаблонДокумента", ШаблонДокумента);
	
	ИмяФормыДокумента = "Справочник.ВходящиеДокументы.ФормаОбъекта";
	
	ОткрытьФорму(ИмяФормыДокумента, ПараметрыФормы, Элементы.СписокВходящие, Новый УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНовыйИсходящийДокумент(Копирование)
	
	ТекущийДокумент = Элементы.СписокИсходящие.ТекущаяСтрока;
	
	Если Копирование Тогда 
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ЗначениеКопирования", ТекущийДокумент);
		Открытьформу("Справочник.ИсходящиеДокументы.ФормаОбъекта",
			ПараметрыФормы, Элементы.СписокИсходящие, Новый УникальныйИдентификатор);
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"СоздатьНовыйИсходящийДокументПродолжение",
		ЭтотОбъект);
		
	Если ЕстьДоступныеШаблоныИсходящихДокументов Тогда
		РаботаСШаблонамиДокументовКлиент.ПоказатьФормуСозданияДокументаПоШаблону(
			ОписаниеОповещения,
			"ШаблоныИсходящихДокументов");
	Иначе
		Результат = "СоздатьПустойДокумент";
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, Результат);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНовыйИсходящийДокументПродолжение(Результат, Параметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Контрагент", Контрагент);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ПараметрыФормы.Вставить("ШаблонДокумента", Результат);
	Иначе
		ПараметрыФормы.Вставить("ШаблонДокумента", Результат.ШаблонДокумента);
	КонецЕсли;
	
	Открытьформу("Справочник.ИсходящиеДокументы.ФормаОбъекта",
		ПараметрыФормы, Элементы.СписокИсходящие, Новый УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОжидания()
	
	СписокВнутренние.Параметры.УстановитьЗначениеПараметра("Папка", Элементы.Папки.ТекущаяСтрока);
	
КонецПроцедуры

&НаСервере
Процедура ПереключитьВидПросмотра()
	
	Параметр = СписокВнутренние.Параметры.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Папка"));
	Параметр.Использование = (ВидПросмотра = Перечисления.ВидыПросмотраСпискаОбъектов.ПоПапкам);
	
	Элементы.ВключитьПросмотрПапкамиВнутренние.Пометка = Ложь;
	Элементы.ВключитьПросмотрСпискомВнутренние.Пометка = Ложь;
	
	Элементы.Папки.Видимость = Ложь;
	
	Если ВидПросмотра = Перечисления.ВидыПросмотраСпискаОбъектов.Списком Тогда
		
		Элементы.ВключитьПросмотрСпискомВнутренние.Пометка = Истина;
		
	ИначеЕсли ВидПросмотра = Перечисления.ВидыПросмотраСпискаОбъектов.ПоПапкам Тогда
		
		Элементы.Папки.Видимость = Истина;
		
		Если Элементы.СписокВнутренние.ТекущаяСтрока <> Неопределено Тогда 
			Элементы.Папки.ТекущаяСтрока = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
				Элементы.СписокВнутренние.ТекущаяСтрока, "Папка");
		КонецЕсли;	
		Если Элементы.Папки.ТекущаяСтрока <> Неопределено Тогда 
			СписокВнутренние.Параметры.УстановитьЗначениеПараметра("Папка", Элементы.Папки.ТекущаяСтрока);
			ТекущаяПапка = Элементы.Папки.ТекущаяСтрока;
		КонецЕсли;
		
		Элементы.ВключитьПросмотрПапкамиВнутренние.Пометка = Истина;
		
	Иначе
		ВидПросмотра = Перечисления.ВидыПросмотраСпискаОбъектов.ПоПапкам;
		ПереключитьВидПросмотра();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДоступностьШаблонов()
	
	Результат = ОбновитьДоступностьШаблоновВыполнить();
	ЕстьДоступныеШаблоныВнутреннихДокументов = Результат.ЕстьДоступныеШаблоныВнутреннихДокументов;
	ЕстьДоступныеШаблоныВходящихДокументов = Результат.ЕстьДоступныеШаблоныВходящихДокументов;
	ЕстьДоступныеШаблоныИсходящихДокументов = Результат.ЕстьДоступныеШаблоныИсходящихДокументов;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОбновитьДоступностьШаблоновВыполнить()

	ЕстьДоступныеШаблоныВнутреннихДокументов = Ложь;
	ЕстьДоступныеШаблоныВходящихДокументов = Ложь;
	ЕстьДоступныеШаблоныИсходящихДокументов = Ложь;
	
	Если ПравоДоступа("Чтение", Метаданные.Справочники.ШаблоныВходящихДокументов) Тогда 
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	ИСТИНА КАК ЕстьДоступныеШаблоны
		|ИЗ
		|	Справочник.ШаблоныВходящихДокументов КАК ШаблоныВходящихДокументов";
		
		Выборка = Запрос.Выполнить();
		ЕстьДоступныеШаблоныВходящихДокументов = Не Выборка.Пустой();
	КонецЕсли;
	
	Если ПравоДоступа("Чтение", Метаданные.Справочники.ШаблоныИсходящихДокументов) Тогда 
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	ИСТИНА КАК ЕстьДоступныеШаблоны
		|ИЗ
		|	Справочник.ШаблоныИсходящихДокументов КАК ШаблоныИсходящихДокументов";
		
		Выборка = Запрос.Выполнить();
		ЕстьДоступныеШаблоныИсходящихДокументов = Не Выборка.Пустой();
	КонецЕсли;
	
	Если ПравоДоступа("Чтение", Метаданные.Справочники.ШаблоныВнутреннихДокументов) Тогда 
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	ИСТИНА КАК ЕстьДоступныеШаблоны
		|ИЗ
		|	Справочник.ШаблоныВнутреннихДокументов КАК ШаблоныВнутреннихДокументов";
	
		Выборка = Запрос.Выполнить();
		ЕстьДоступныеШаблоныВнутреннихДокументов = Не Выборка.Пустой();
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("ЕстьДоступныеШаблоныВнутреннихДокументов", ЕстьДоступныеШаблоныВнутреннихДокументов);
	Результат.Вставить("ЕстьДоступныеШаблоныИсходящихДокументов", ЕстьДоступныеШаблоныИсходящихДокументов);
	Результат.Вставить("ЕстьДоступныеШаблоныВходящихДокументов", ЕстьДоступныеШаблоныВходящихДокументов);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОбработатьПеретаскиваниеФайлов(ПараметрыПеретаскивания, ИмяРеквизита, ЗначениеРеквизита)
	
	Если ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("Файл") 
	   И ПараметрыПеретаскивания.Значение.ЭтоФайл() Тогда
		
		МассивФайлов = Новый Массив;
		МассивФайлов.Добавить(ПараметрыПеретаскивания.Значение.ПолноеИмя);
		
		ЗначенияЗаполнения = Новый Структура;
		ЗначенияЗаполнения.Вставить(ИмяРеквизита, ЗначениеРеквизита);
		
		ПараметрыОткрытияФормы = Новый Структура;
		ПараметрыОткрытияФормы.Вставить("МассивФайлов", МассивФайлов);
		ПараметрыОткрытияФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
		
		ОткрытьФорму("Справочник.ВнутренниеДокументы.ФормаОбъекта", ПараметрыОткрытияФормы);
		
	ИначеЕсли ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("Массив")
		И ПараметрыПеретаскивания.Значение.Количество() > 0 
		И ТипЗнч(ПараметрыПеретаскивания.Значение[0]) = Тип("Файл") Тогда
		
		МассивФайлов = Новый Массив;
		Для Каждого ФайлПринятый Из ПараметрыПеретаскивания.Значение Цикл
			МассивФайлов.Добавить(ФайлПринятый.ПолноеИмя);
		КонецЦикла;
		
		ЗначенияЗаполнения = Новый Структура;
		ЗначенияЗаполнения.Вставить(ИмяРеквизита, ЗначениеРеквизита);
		
		ПараметрыОткрытияФормы = Новый Структура;
		ПараметрыОткрытияФормы.Вставить("МассивФайлов", МассивФайлов);
		ПараметрыОткрытияФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
		
		ОткрытьФорму("Справочник.ВнутренниеДокументы.ФормаОбъекта", ПараметрыОткрытияФормы);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста 
Функция МожноИзменятьПапку(ДокументСсылка)
	
	Шаблон = ОбщегоНазначенияДокументооборот.ЗначениеРеквизитаОбъектаВПривилегированномРежиме(
		ДокументСсылка, "Шаблон");
	
	Если ЗначениеЗаполнено(Шаблон) И Не РольДоступна("ПолныеПрава") Тогда 
		
		ЗапретитьИзменятьРеквизитыИзШаблона 
		= ОбщегоНазначенияДокументооборот.ЗначениеРеквизитаОбъектаВПривилегированномРежиме(
			Шаблон, "ЗапретитьИзменятьРеквизитыИзШаблона");
			
		Если ЗапретитьИзменятьРеквизитыИзШаблона И ЗначениеЗаполнено(Шаблон.Папка) Тогда
			Возврат Ложь;
		КонецЕсли;
			
	КонецЕсли;
		
	Возврат Истина;
	
КонецФункции

&НаСервереБезКонтекста 
Функция ИзменитьПапкуДокументов(МассивДокументов, НоваяПапка)
	
	Если МассивДокументов.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// тот же родитель - ничего делать не надо
	Если МассивДокументов[0].Папка = НоваяПапка Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Для Каждого ДокументСсылка Из МассивДокументов Цикл
		
		Если Не ДокументооборотПраваДоступа.ПолучитьПраваПоОбъекту(ДокументСсылка).Изменение Тогда 
			ВызватьИсключение СтрШаблон(НСтр("ru = 'У вас нет права на изменение документа ""%1"".'"),
								Строка(ДокументСсылка));
		КонецЕсли;
		
		Если Не МожноИзменятьПапку(ДокументСсылка) Тогда 
			
			ВызватьИсключение СтрШаблон(НСтр("ru = 'Запрещено изменять папку у документа ""%1"".'"),
								Строка(ДокументСсылка));
		КонецЕсли;
		
	КонецЦикла;	
	
	НачатьТранзакцию();
	Попытка
		Для Каждого ДокументСсылка Из МассивДокументов Цикл
			ЗаблокироватьДанныеДляРедактирования(ДокументСсылка);
			ДокументОбъект = ДокументСсылка.ПолучитьОбъект();
			ДокументОбъект.Папка = НоваяПапка;
			ДокументОбъект.Записать();
		КонецЦикла;
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции	

&НаСервере
Процедура ПоказатьУдаленные()
	
	Если ЧтениеВнутреннихДокументов Тогда 
		Если ВнутренниеПоказыватьУдаленные Тогда
			ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(
				СписокВнутренние, "ПометкаУдаления");
			ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(
				Папки, "ПометкаУдаления");
		Иначе
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
				СписокВнутренние, "ПометкаУдаления", Ложь);
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
				Папки, "ПометкаУдаления", Ложь);
		КонецЕсли;
		
		Элементы.ВнутренниеПоказыватьУдаленные.Пометка = ВнутренниеПоказыватьУдаленные;
	КонецЕсли;
	
	Если ЧтениеВходящихДокументов Тогда 
		Если ВходящиеПоказыватьУдаленные Тогда
			ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(
				СписокВходяшие, "ПометкаУдаления");
		Иначе
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
				СписокВходяшие, "ПометкаУдаления", Ложь);
		КонецЕсли;
		
		Элементы.ВходящиеПоказыватьУдаленные.Пометка = ВходящиеПоказыватьУдаленные;
	КонецЕсли;
	
	Если ЧтениеИсходящихДокументов Тогда 
		Если ИсходящиеПоказыватьУдаленные Тогда
			ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(
				СписокИсходящие, "ПометкаУдаления");
		Иначе
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
				СписокИсходящие, "ПометкаУдаления", Ложь);
		КонецЕсли;
		
		Элементы.ИсходящиеПоказыватьУдаленные.Пометка = ИсходящиеПоказыватьУдаленные;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
