#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Проверяет, что у владельца есть банковский счет.
//
// Параметры:
//    Владелец - СправочникСсылка.Контрагенты - для которого определяем наличие банковского счета
//
// Возвращаемое значение:
//    Булево - Истина, если у владельца в базе данных есть хотя бы один банковский счет
//
Функция ИмеетБанковскийСчет(Владелец) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	БанковскиеСчета.Ссылка
		|ИЗ
		|	Справочник.БанковскиеСчета КАК БанковскиеСчета
		|ГДЕ
		|	БанковскиеСчета.Владелец = &Владелец";
	
	Запрос.УстановитьПараметр("Владелец", Владелец);
	Выборка = Запрос.Выполнить();
	
	Возврат Не Выборка.Пустой();
	
КонецФункции

// Функция определяет организацию и валюту выбранного банковского счета.
//
// Параметры:
//  БанковскийСчет - СправочникСсылка.БанковскиеСчета - Ссылка на банковский счет.
//
// Возвращаемое значение:
//	Структура - Организация и реквизиты банковского счета организации.
//
Функция ПолучитьРеквизитыБанковскогоСчетаОрганизации(БанковскийСчет) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	БанковскиеСчета.Владелец КАК Организация,
		|	БанковскиеСчета.ВалютаДенежныхСредств КАК Валюта,
		|	БанковскиеСчета.НомерСчета КАК НомерСчета,
		|	БанковскиеСчета.ВидСчета КАК ВидСчета,
		|	БанковскиеСчета.Банк КАК Банк,
		|	БанковскиеСчета.Банк.Наименование КАК НаименованиеБанка,
		|	БанковскиеСчета.Банк.Код КАК БИК,
		|	БанковскиеСчета.Банк.КоррСчет КАК КоррСчет,
		|	БанковскиеСчета.Банк.Адрес КАК АдресБанка
		|ИЗ
		|	Справочник.БанковскиеСчета КАК БанковскиеСчета
		|ГДЕ
		|	БанковскиеСчета.Ссылка = &БанковскийСчет";
	
	Запрос.УстановитьПараметр("БанковскийСчет", БанковскийСчет);
	
	РеквизитыСчета = Новый Структура;
	РеквизитыСчета.Вставить("Организация", Неопределено);
	РеквизитыСчета.Вставить("Валюта", Неопределено);
	РеквизитыСчета.Вставить("НомерСчета", "");
	РеквизитыСчета.Вставить("ВидСчета", Неопределено);
	РеквизитыСчета.Вставить("Банк", Неопределено);
	РеквизитыСчета.Вставить("НаименованиеБанка", "");
	РеквизитыСчета.Вставить("БИК", "");
	РеквизитыСчета.Вставить("КоррСчет", "");
	РеквизитыСчета.Вставить("АдресБанка", "");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(РеквизитыСчета, Выборка);
	КонецЕсли;
	
	Возврат РеквизитыСчета;
	
КонецФункции

#КонецЕсли