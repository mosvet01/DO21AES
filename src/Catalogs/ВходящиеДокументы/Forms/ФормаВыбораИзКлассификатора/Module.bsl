
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Вопрос = Параметры.Вопрос;
	Раздел = Параметры.Раздел;
	Тематика = Параметры.Тематика;
	Тема = Параметры.Тема;
	Отправитель = Параметры.Отправитель;
	Документ = Параметры.Документ;
	
	КлючСохраненияПоложенияОкна = Параметры.КлючСохраненияПоложенияОкна;
	ПолучитьКодВопроса();
	
	Если Параметры.Свойство("СокращенныйРежим") И Параметры.СокращенныйРежим Тогда 
		Элементы.ГруппаРузультат.Видимость = Ложь;
	Иначе 
		
		ОрганДляПередачи = Параметры.ОрганДляПередачи;
		СопроводительныйДокументСтрока = Параметры.СопроводительныйДокументСтрока;
		СопроводительныйДокумент = Параметры.СопроводительныйДокумент;
		
		ОтветныйДокументСтрока = Параметры.ОтветныйДокументСтрока;
		ОтветныйДокумент = Параметры.ОтветныйДокумент;
		
		ДатаОтвета = Параметры.ДатаОтвета;
		РезультатРассмотрения = Параметры.РезультатРассмотрения;
		МнениеАвтораОМерах = Параметры.МнениеАвтораОМерах;
		МнениеАвтораОРезультатах = Параметры.МнениеАвтораОРезультатах;
		
		Элементы.ГруппаОрганИСопроводительныйДокумент.Видимость = Ложь;
		Элементы.ГурппаОтветныйДокумент.Видимость = Ложь;
		Элементы.ДатаОтвета.Видимость = Ложь;
		Элементы.ГруппаМнениеАвтора.Видимость = Ложь;
		
		Если РезультатРассмотрения = ПредопределенноеЗначение("Перечисление.РезультатыРассмотренияОбращений.НаправленоВИнойОрган") Тогда 
			Элементы.ГруппаОрганИСопроводительныйДокумент.Видимость = Истина;
			Если Не ЗначениеЗаполнено(ДатаОтвета) Тогда 
				ДатаОтвета = ТекущаяДата();
			КонецЕсли;
			
		ИначеЕсли РезультатРассмотрения = ПредопределенноеЗначение("Перечисление.РезультатыРассмотренияОбращений.ВТомЧислеМерыПриняты")
			Или РезультатРассмотрения = ПредопределенноеЗначение("Перечисление.РезультатыРассмотренияОбращений.НеПоддержано")
			Или РезультатРассмотрения = ПредопределенноеЗначение("Перечисление.РезультатыРассмотренияОбращений.Поддержано")
			Или РезультатРассмотрения = ПредопределенноеЗначение("Перечисление.РезультатыРассмотренияОбращений.Разъяснено") Тогда 
			Элементы.ДатаОтвета.Видимость = Истина;
			Элементы.ГруппаМнениеАвтора.Видимость = Истина;
			
			Если Не ЗначениеЗаполнено(ДатаОтвета) Тогда 
				ДатаОтвета = ТекущаяДата();
			КонецЕсли;
			
			Если РезультатРассмотрения = ПредопределенноеЗначение("Перечисление.РезультатыРассмотренияОбращений.ВТомЧислеМерыПриняты")
				Или РезультатРассмотрения = ПредопределенноеЗначение("Перечисление.РезультатыРассмотренияОбращений.Поддержано")
				Или РезультатРассмотрения = ПредопределенноеЗначение("Перечисление.РезультатыРассмотренияОбращений.Разъяснено") Тогда 
				Элементы.ГурппаОтветныйДокумент.Видимость = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.Свойство("Заголовок") И ЗначениеЗаполнено(Параметры.Заголовок) Тогда 
		Заголовок = Параметры.Заголовок;
		АвтоЗаголовок = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Не (ЗначениеЗаполнено(Раздел) И ЗначениеЗаполнено(Тема)
		 И ЗначениеЗаполнено(Тематика) И ЗначениеЗаполнено(Вопрос)) Тогда 
		ПодключитьОбработчикОжидания("ЗапуститьВыборКода", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Работа с кодом вопроса
&НаКлиенте
Процедура КодВопросаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Раздел", Раздел);
	ПараметрыФормы.Вставить("Тематика", Тематика);
	ПараметрыФормы.Вставить("Тема", Тема);
	ПараметрыФормы.Вставить("Вопрос", Вопрос);
	ПараметрыФормы.Вставить("КодВопроса", КодВопроса);
	ПараметрыФормы.Вставить("СокращенныйРежим", Истина);
	ПараметрыФормы.Вставить("КлючСохраненияПоложенияОкна", "ВыборОдногоВопроса");
	
	Обработчик = Новый ОписаниеОповещения("КодВопросаПослеВыбора", 
		ЭтотОбъект);
		
	ОткрытьФорму("ОбщаяФорма.ПомощникВыбораВопроса", ПараметрыФормы, , , , ,
		Обработчик, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура КодВопросаПослеВыбора(Результат, Параметры) Экспорт 
	
	Если Результат <> Неопределено Тогда 
		Раздел = Результат.Раздел;
		Тематика = Результат.Тематика;
		Тема = Результат.Тема;
		Вопрос = Результат.Вопрос;
		КодВопроса = Результат.КодВопроса;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КодВопросаОчистка(Элемент, СтандартнаяОбработка)
	
	Раздел = Неопределено;
	Тематика = Неопределено;
	Тема = Неопределено;
	Вопрос = Неопределено;
	КодВопроса = "";
	
КонецПроцедуры

&НаКлиенте
Процедура РазделПриИзменении(Элемент)
	
	ПолучитьКодВопроса();
	
КонецПроцедуры

&НаКлиенте
Процедура ТемаПриИзменении(Элемент)
	
	ПолучитьКодВопроса();
	
КонецПроцедуры

&НаКлиенте
Процедура ТематикаПриИзменении(Элемент)
	
	ПолучитьКодВопроса();
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПриИзменении(Элемент)
	
	ПолучитьКодВопроса();
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатРассмотренияПриИзменении(Элемент)
	
	Элементы.ГруппаОрганИСопроводительныйДокумент.Видимость = Ложь;
	Элементы.ДатаОтвета.Видимость = Ложь;
	Элементы.ГруппаМнениеАвтора.Видимость = Ложь;
	Элементы.ГурппаОтветныйДокумент.Видимость = Ложь;
	
	Если РезультатРассмотрения = ПредопределенноеЗначение("Перечисление.РезультатыРассмотренияОбращений.НаправленоВИнойОрган") Тогда 
		Элементы.ГруппаОрганИСопроводительныйДокумент.Видимость = Истина;
		ОтветныйДокумент = Неопределено;
		
	ИначеЕсли РезультатРассмотрения = ПредопределенноеЗначение("Перечисление.РезультатыРассмотренияОбращений.ВТомЧислеМерыПриняты")
		Или РезультатРассмотрения = ПредопределенноеЗначение("Перечисление.РезультатыРассмотренияОбращений.НеПоддержано")
		Или РезультатРассмотрения = ПредопределенноеЗначение("Перечисление.РезультатыРассмотренияОбращений.Поддержано")
		Или РезультатРассмотрения = ПредопределенноеЗначение("Перечисление.РезультатыРассмотренияОбращений.Разъяснено") Тогда 
		Элементы.ДатаОтвета.Видимость = Истина;
		Элементы.ГруппаМнениеАвтора.Видимость = Истина;
		
		Если Не ЗначениеЗаполнено(ДатаОтвета) Тогда 
			ДатаОтвета = ТекущаяДата();
		КонецЕсли;
		
		Если РезультатРассмотрения = ПредопределенноеЗначение("Перечисление.РезультатыРассмотренияОбращений.ВТомЧислеМерыПриняты")
			Или РезультатРассмотрения = ПредопределенноеЗначение("Перечисление.РезультатыРассмотренияОбращений.Поддержано")
			Или РезультатРассмотрения = ПредопределенноеЗначение("Перечисление.РезультатыРассмотренияОбращений.Разъяснено") Тогда 
			Элементы.ГурппаОтветныйДокумент.Видимость = Истина;
		Иначе 
			ОтветныйДокумент = Неопределено;
		КонецЕсли;
		
		СопроводительныйДокумент = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура МнениеАвтораОРезультатахНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Комментарий", МнениеАвтораОРезультатах);
	ПараметрыФормы.Вставить("Заголовок", НСтр("ru = 'Мнение заявителя о результатах'"));
	ОписаниеОповещения = Новый ОписаниеОповещения("ВводКомментарияМнениеАвтораОРезультатахЗавершение", ЭтотОбъект);
	РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	
	ОткрытьФорму("ОбщаяФорма.ВводКомментария", ПараметрыФормы,,,,, ОписаниеОповещения, РежимОткрытияОкна);
	
КонецПроцедуры

&НаКлиенте
Процедура ВводКомментарияМнениеАвтораОРезультатахЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Строка") Тогда
		Возврат;
	КонецЕсли;
	
	МнениеАвтораОРезультатах = Результат;
	
КонецПроцедуры

&НаКлиенте
Процедура МнениеАвтораОМерахНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Комментарий", МнениеАвтораОМерах);
	ПараметрыФормы.Вставить("Заголовок", НСтр("ru = 'Мнение заявителя о мерах'"));
	ОписаниеОповещения = Новый ОписаниеОповещения("ВводКомментарияМнениеАвтораОМерахЗавершение", ЭтотОбъект);
	РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	
	ОткрытьФорму("ОбщаяФорма.ВводКомментария", ПараметрыФормы,,,,, ОписаниеОповещения, РежимОткрытияОкна);
	
КонецПроцедуры

&НаКлиенте
Процедура ВводКомментарияМнениеАвтораОМерахЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Строка") Тогда
		Возврат;
	КонецЕсли;
	
	МнениеАвтораОМерах = Результат;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганДляПередачиПриИзменении(Элемент)
	
	СопроводительныйДокумент = Неопределено;
	СопроводительныйДокументСтрока = Неопределено;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Работа с "Сопроводительный документ"

&НаКлиенте
Процедура СопроводительныйДокументСтрокаПриИзменении(Элемент)
	
	РаботаСОбращениямиКлиент.СопроводительныйДокументПриИзменении(ЭтаФорма, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СопроводительныйДокументСтрокаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РаботаСОбращениямиКлиент.СопроводительныйДокументНачалоВыбора(
		ЭтаФорма, Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СопроводительныйДокументСтрокаОчистка(Элемент, СтандартнаяОбработка)
	
	РаботаСОбращениямиКлиент.СопроводительныйДокументОчистка(
		ЭтаФорма, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СопроводительныйДокументСтрокаОткрытие(Элемент, СтандартнаяОбработка)
	
	РаботаСОбращениямиКлиент.СопроводительныйДокументОткрытие(
		ЭтаФорма, Элемент, СтандартнаяОбработка); 
	
КонецПроцедуры

&НаКлиенте
Процедура СопроводительныйДокументСтрокаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаСОбращениямиКлиент.СопроводительныйДокументОбработкаВыбора(
		ЭтаФорма, Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СопроводительныйДокументСтрокаАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	РаботаСОбращениямиКлиент.СопроводительныйДокументАвтоПодбор(
		ЭтаФорма, Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СопроводительныйДокументСтрокаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	РаботаСОбращениямиКлиент.СопроводительныйДокументОкончаниеВводаТекста(
		ЭтаФорма, Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Работа с "Ответный документ"

&НаКлиенте
Процедура ОтветныйДокументСтрокаПриИзменении(Элемент)
	
	РаботаСОбращениямиКлиент.ОтветныйДокументПриИзменении(ЭтаФорма, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтветныйДокументСтрокаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РаботаСОбращениямиКлиент.ОтветныйДокументНачалоВыбора(
		ЭтаФорма, Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтветныйДокументСтрокаОчистка(Элемент, СтандартнаяОбработка)
	
	РаботаСОбращениямиКлиент.ОтветныйДокументОчистка(
		ЭтаФорма, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтветныйДокументСтрокаОткрытие(Элемент, СтандартнаяОбработка)
	
	РаботаСОбращениямиКлиент.ОтветныйДокументОткрытие(
		ЭтаФорма, Элемент, СтандартнаяОбработка); 
	
КонецПроцедуры

&НаКлиенте
Процедура ОтветныйДокументСтрокаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаСОбращениямиКлиент.ОтветныйДокументОбработкаВыбора(
		ЭтаФорма, Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтветныйДокументСтрокаАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка)
	
	РаботаСОбращениямиКлиент.ОтветныйДокументАвтоПодбор(
		ЭтаФорма, Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтветныйДокументСтрокаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	РаботаСОбращениямиКлиент.ОтветныйДокументОкончаниеВводаТекста(
		ЭтаФорма, Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	ПараметрыЗакрытия = Новый Структура;
	ПараметрыЗакрытия.Вставить("Раздел", Раздел);
	ПараметрыЗакрытия.Вставить("Тематика", Тематика);
	ПараметрыЗакрытия.Вставить("Тема", Тема);
	ПараметрыЗакрытия.Вставить("Вопрос", Вопрос);
	ПараметрыЗакрытия.Вставить("КодВопроса", КодВопроса);
	
	ПараметрыЗакрытия.Вставить("РезультатРассмотрения", РезультатРассмотрения);
	ПараметрыЗакрытия.Вставить("ДатаОтвета", ДатаОтвета);
	ПараметрыЗакрытия.Вставить("ОрганДляПередачи", ОрганДляПередачи);
	ПараметрыЗакрытия.Вставить("СопроводительныйДокументСтрока", СопроводительныйДокументСтрока);
	ПараметрыЗакрытия.Вставить("СопроводительныйДокумент", СопроводительныйДокумент);
	ПараметрыЗакрытия.Вставить("ОтветныйДокументСтрока", ОтветныйДокументСтрока);
	ПараметрыЗакрытия.Вставить("ОтветныйДокумент", ОтветныйДокумент);
	ПараметрыЗакрытия.Вставить("МнениеАвтораОМерах", МнениеАвтораОМерах);
	ПараметрыЗакрытия.Вставить("МнениеАвтораОРезультатах", МнениеАвтораОРезультатах);
	
	Закрыть(ПараметрыЗакрытия);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗапуститьВыборКода()
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Раздел", Раздел);
	ПараметрыФормы.Вставить("Тематика", Тематика);
	ПараметрыФормы.Вставить("Тема", Тема);
	ПараметрыФормы.Вставить("Вопрос", Вопрос);
	ПараметрыФормы.Вставить("КодВопроса", КодВопроса);
	ПараметрыФормы.Вставить("СокращенныйРежим", Истина);
	ПараметрыФормы.Вставить("КлючСохраненияПоложенияОкна", "ВыборОдногоВопроса");
	
	Обработчик = Новый ОписаниеОповещения("КодВопросаПослеВыбора", 
		ЭтотОбъект);
		
	ОткрытьФорму("ОбщаяФорма.ПомощникВыбораВопроса", ПараметрыФормы, , , , ,
		Обработчик, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца)
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьКодВопроса()
	
	КодВопроса = РаботаСОбращениямиВызовСервера.СформироватьКодВопроса(Раздел, Тематика, Тема, Вопрос);
	
КонецПроцедуры

#КонецОбласти
