#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Обновление адресной книги
Перем ОбновитьДанныеОрганизацииВАдреснойКниге;
Перем ОбновитьСловаПоискаПоОрганизацииВАдреснойКниге;
Перем ОбновитьДанныеОтображенияОрганизацииВАдреснойКниге;
Перем ОбновитьДоступностьВПоискеПоОрганизации;	
	
#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ЭтоНовый() Тогда 
		ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ТекстСообщения = "";
	
	Если ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо Тогда 
	
		Если ЗначениеЗаполнено(ИНН) И Не РегламентированныеДанныеКлиентСервер.ИННСоответствуетТребованиям(ИНН, Истина, ТекстСообщения) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ИНН",, Отказ);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(КПП) И Не РегламентированныеДанныеКлиентСервер.КППСоответствуетТребованиям(КПП, ТекстСообщения) Тогда
 			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "КПП",, Отказ);
 		КонецЕсли;
	
       	Если ЗначениеЗаполнено(КодПоОКПО) И Не РегламентированныеДанныеКлиентСервер.КодПоОКПОСоответствуетТребованиям(КодПоОКПО, Истина, ТекстСообщения) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,ЭтотОбъект, "КодПоОКПО",, Отказ);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ОГРН)
			И Не РегламентированныеДанныеКлиентСервер.ОГРНСоответствуетТребованиям(ОГРН, Истина, ТекстСообщения) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ОГРН", Истина, Отказ);
		КонецЕсли;
		
	ИначеЕсли ЮрФизЛицо = Перечисления.ЮрФизЛицо.ИндивидуальныйПредприниматель Тогда 
		
		Если ЗначениеЗаполнено(ИНН) И Не РегламентированныеДанныеКлиентСервер.ИННСоответствуетТребованиям(ИНН, Ложь, ТекстСообщения) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ИНН",, Отказ);
		КонецЕсли;
		
	    Если ЗначениеЗаполнено(КодПоОКПО) И Не РегламентированныеДанныеКлиентСервер.КодПоОКПОСоответствуетТребованиям(КодПоОКПО, Ложь, ТекстСообщения) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,ЭтотОбъект, "КодПоОКПО",, Отказ);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ОГРН)
			И Не РегламентированныеДанныеКлиентСервер.ОГРНСоответствуетТребованиям(ОГРН, Ложь, ТекстСообщения) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ОГРН",, Отказ);
		КонецЕсли;
	
	КонецЕсли;	
	
	Если Не ЭтоНовый()
		И ОбособленноеПодразделение
		И ЕстьОбособленныеПодразделенияОрганизации() Тогда
		
		ТекстСообщения = НСтр("ru = 'Организация не может быть обособленным подразделением, потому что она уже выбрана в качестве головной для других организаций.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект,,, Отказ);
		
	КонецЕсли;
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	Если Не ОбособленноеПодразделение Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ГоловнаяОрганизация");
	КонецЕсли;
	
	Если Не КрупнейшийНалогоплательщик Тогда
		МассивНепроверяемыхРеквизитов.Добавить("КодНалоговогоОрганаПолучателя");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если ЮрФизЛицо = Перечисления.ЮрФизЛицо.ИндивидуальныйПредприниматель Тогда
		Если Не ПустаяСтрока(КПП) Тогда
			КПП = "";
		КонецЕсли;
	КонецЕсли;	
	
	ДополнительныеСвойства.Вставить("ПометкаУдаления", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ПометкаУдаления"));		
	
	// Обновление адресной книги 	
	ОбновитьДанныеОрганизацииВАдреснойКниге = Ложь;
	ОбновитьСловаПоискаПоОрганизацииВАдреснойКниге = Ложь;
	ОбновитьДанныеОтображенияОрганизацииВАдреснойКниге = Ложь;
	ОбновитьДоступностьВПоискеПоОрганизации = Ложь;
	
	Если ЭтоНовый() Тогда
		
		ОбновитьДанныеОрганизацииВАдреснойКниге = Истина;
		ОбновитьСловаПоискаПоОрганизацииВАдреснойКниге = Истина;
		
	Иначе
		
		РеквизитыРолиПоСсылке = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка,
			"Наименование, ПометкаУдаления");
			
		Если РеквизитыРолиПоСсылке.ПометкаУдаления <> ПометкаУдаления Тогда
			ОбновитьДанныеОрганизацииВАдреснойКниге = Истина;
			ОбновитьДанныеОтображенияОрганизацииВАдреснойКниге = Истина;
			ОбновитьДоступностьВПоискеПоОрганизации = Истина;
		КонецЕсли;
		
		Если РеквизитыРолиПоСсылке.Наименование <> Наименование Тогда
			ОбновитьДанныеОрганизацииВАдреснойКниге = Истина;
			ОбновитьДанныеОтображенияОрганизацииВАдреснойКниге = Истина;
			ОбновитьСловаПоискаПоОрганизацииВАдреснойКниге = Истина;
		КонецЕсли;
		
	КонецЕсли;	
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка
		И ЗначениеЗаполнено(ОбменДанными.Отправитель)
		И ОбменДаннымиПовтИсп.ЭтоУзелРаспределеннойИнформационнойБазы(ОбменДанными.Отправитель) Тогда
		Возврат;
	КонецЕсли;
	
	ДокументооборотПраваДоступа.ПриЗаписиРазрезаДоступа(ЭтотОбъект, Отказ);
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.ПометкаУдаления <> ПометкаУдаления
		И Справочники.Организации.КоличествоОрганизаций() = 0 Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'В справочнике должна присутствовать хотя бы одна организация.'")
				,,,,Отказ);		
	КонецЕсли;	
	
	// Обновление адресной книги
	Если ОбновитьДанныеОрганизацииВАдреснойКниге Тогда
		Справочники.АдреснаяКнига.ОбновитьДанныеОбъекта(Ссылка, Родитель, Справочники.АдреснаяКнига.Организации, Ссылка);
	КонецЕсли;

	Если ОбновитьДанныеОтображенияОрганизацииВАдреснойКниге Тогда
		Справочники.АдреснаяКнига.ОбновитьДанныеОтображенияПодчиненногоОбъекта(Ссылка);
	КонецЕсли;	
	
	Если ОбновитьСловаПоискаПоОрганизацииВАдреснойКниге Тогда
		РегистрыСведений.ПоискВАдреснойКниге.ОбновитьСловаПоискаПоОрганизации(ЭтотОбъект);
	КонецЕсли;
	
	Если ОбновитьДоступностьВПоискеПоОрганизации Тогда
		РегистрыСведений.ПоискВАдреснойКниге.ОбновитьДоступностьВПоиске(ЭтотОбъект);
	КонецЕсли;
	
	// Регламентированная отчетность
	ЭлектронныйДокументооборотСКонтролирующимиОрганами.ПриЗаписиОрганизации(ЭтотОбъект, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЕстьОбособленныеПодразделенияОрганизации()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Организации.Ссылка
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.ГоловнаяОрганизация = &Ссылка
	|	И Организации.Ссылка <> Организации.ГоловнаяОрганизация";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

#КонецОбласти

#КонецЕсли