#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКопировании(ОбъектКопирования)
	
	ДанныеКонверта = Новый ХранилищеЗначения(Неопределено);
	ДанныеСообщения = Новый ХранилищеЗначения(Неопределено);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Дата) Тогда
		Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Если Не ПометкаУдаления Тогда
		РежимЗаписи = РежимЗаписиДокумента.Проведение;
	КонецЕсли;
	
	
	Если Направление = Перечисления.НаправленияСообщенийМЭДО.Исходящее Тогда
		Если Не ЗначениеЗаполнено(ИдентификаторКвитанции) Тогда
			Если ЭтоНовый() Тогда
				СсылкаНового = ИнтеграцияСМЭДО.СсылкаНовогоОбъекта(ЭтотОбъект);
				ИдентификаторКвитанции = "" + СсылкаНового.УникальныйИдентификатор();
			Иначе
				ИдентификаторКвитанции = "" + Ссылка.УникальныйИдентификатор();
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	// для исходящего квитанции - идентификатор входящего документа, с которым он пришел:
	Если Не ЗначениеЗаполнено(ИдентификаторСообщения) Тогда
	
		Документ = ДокументПоПредметуКвитанции(Предмет);
		НаправлениеДокумента = Перечисления.НаправленияСообщенийМЭДО.НаправлениеДокумента(Документ);
		Если НаправлениеДокумента = Перечисления.НаправленияСообщенийМЭДО.Входящее Тогда
			СтруктураСостояния = РегистрыСведений.СостоянияДокументовМЭДО.ПолучитьСостояниеДокумента(
				Документ,
				Перечисления.СостоянияДокументовМЭДО.ДокументПолучен,
				"ИдентификаторСообщения",
				Дата);
			ИдентификаторСообщения = СтруктураСостояния.ИдентификаторСообщения;
		ИначеЕсли НаправлениеДокумента = Перечисления.НаправленияСообщенийМЭДО.Исходящее Тогда 
			СтруктураСостояния = РегистрыСведений.СостоянияДокументовМЭДО.ПолучитьСостояниеДокумента(
				Документ,
				Перечисления.СостоянияДокументовМЭДО.ДокументОтправлен,
				"ИдентификаторСообщения",
				Дата);
			ИдентификаторСообщения = СтруктураСостояния.ИдентификаторСообщения;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.СостоянияДокументовМЭДО.Записывать = Истина;
	
	Движение = Движения.СостоянияДокументовМЭДО.Добавить();
	Движение.Период = Дата;
	Движение.ПредметСообщения = Ссылка;
	Движение.Документ = ДокументПоПредметуКвитанции(Предмет);
	// Направление в регистре определяется по документу:
	Движение.Направление = Перечисления.НаправленияСообщенийМЭДО.НаправлениеДокумента(Движение.Документ);
	Движение.ИдентификаторСообщения = ИдентификаторСообщения;
	
	Если Направление = Перечисления.НаправленияСообщенийМЭДО.Исходящее Тогда
		
		// исходящая квитанция на входящий документ/входящее уведомление:
		Если Отправлена Тогда
			Если ЗначениеЗаполнено(КодОшибки) Или ЗначениеЗаполнено(КомментарийОшибки) Тогда
				Движение.Состояние = Перечисления.СостоянияДокументовМЭДО.ОтправленаКвитанцияОбОшибке;
			Иначе
				Движение.Состояние = Перечисления.СостоянияДокументовМЭДО.ОтправленаКвитанция;
			КонецЕсли;
		Иначе
			Движение.Состояние = Перечисления.СостоянияДокументовМЭДО.СозданаИсходящаяКвитанция;
		КонецЕсли;
		
	ИначеЕсли Направление = Перечисления.НаправленияСообщенийМЭДО.Входящее Тогда
		
		Движение.ИдентификаторДокумента = "";// не играет роли. 
		
		// Входящая квитанция на исходящий документ/исходящее уведомление:
		Если СообщениеПринято Тогда
			Движение.Состояние = Перечисления.СостоянияДокументовМЭДО.ПолученаКвитанция;
		Иначе
			Движение.Состояние = Перечисления.СостоянияДокументовМЭДО.ПолученаКвитанцияОбОшибке;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДокументПоПредметуКвитанции(Предмет)

	Если ТипЗнч(Предмет) = Тип("ДокументСсылка.УведомлениеМЭДО") Тогда
		Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Предмет, "Документ");
	Иначе
		Возврат Предмет;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецЕсли