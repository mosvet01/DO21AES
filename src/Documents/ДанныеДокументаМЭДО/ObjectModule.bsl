#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеОтвета = МЭДОСтруктурыДанных.НовыйЛегкийОтвет();
	
	// Проверка на уникальность по полю документ, должен быть только один документ "Данные документа МЭДО":
	Если ЭтоНовый() Тогда
		ДанныеПоЭтомуЖеДокументу = Документы.ДанныеДокументаМЭДО.ПолучитьДанныеДокументаМЭДО(
			Документ, "Ссылка", ДанныеОтвета);
		Если ЗначениеЗаполнено(ДанныеПоЭтомуЖеДокументу.Ссылка) Тогда
			ТекстОшибкиПодробно = СтрШаблон(
				НСтр("ru = 'Попытка второй раз создать ""Данные документа МЭДО"", уже есть другой объект: %1'"),
				ДанныеПоЭтомуЖеДокументу.Ссылка);
			МЭДО.ЗаписьВЖурналСобытий(
				Перечисления.УровниСобытийМЭДО.Ошибка,
				ДанныеПоЭтомуЖеДокументу.Ссылка,
				НСтр("ru = 'Ошибка при записи ""Данных документа МЭДО""'"),
				ТекстОшибкиПодробно,
				ДанныеОтвета);
			Отказ = Истина;
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	
	Ответственный = ИнтеграцияСМЭДО.ТекущийОтветственный();
	
	// Пометка удаления должна быть такая же, как у родительского документа:
	ПометкаУдаления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ, "ПометкаУдаления");
	
	Если Не ПометкаУдаления Тогда
		РежимЗаписи = РежимЗаписиДокумента.Проведение;
	КонецЕсли;
	
	
	// Часть реквизитов можно достать сразу из родительского документа:
	Если ИнтеграцияСМЭДО.ЭтоИсходящийДокумент(Документ) Тогда
		РеквизитыДокумента = ИнтеграцияСМЭДО.ПолучитьТребуемыеДанныеИсходящегоДокумента(
			Документ, "Организация, Контрагенты, ГрифДоступа, ДатаСоздания, ДатаРегистрации");
		Дата = РеквизитыДокумента.ДатаСоздания;
		ГрифДоступа = РеквизитыДокумента.ГрифДоступа;
		Организация = РеквизитыДокумента.Организация;
		
		// Определяем версию для отправки, достаточно хотя бы у одного контрагента, если не проставлено, 
		// то останется по умолчанию:
		Если Не ЗначениеЗаполнено(ВерсияМЭДО) Тогда
			ВерсияМЭДО = Перечисления.ВерсииФорматаМЭДО.ПоследняяВерсия();
			Для Каждого СтруктураКонтрагент Из РеквизитыДокумента.Контрагенты Цикл
				НастройкиКонтрагента = РегистрыСведений.НастройкиКонтрагентовМЭДО.ПолучитьНастройки(
					СтруктураКонтрагент.Контрагент, ДанныеОтвета);
				Если ДанныеОтвета.Успех Тогда
					МЭДО.ПрисвоитьЗаполненное(ВерсияМЭДО, НастройкиКонтрагента.ВерсияФорматаМЭДО);
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Направление = Перечисления.НаправленияСообщенийМЭДО.Исходящее;
		МЭДО.ПрисвоитьЗаполненное(ДатаРегистрации, РеквизитыДокумента.ДатаРегистрации);
	КонецЕсли;
	
	Если ИнтеграцияСМЭДО.ЭтоВходящийДокумент(Документ) Тогда
		РеквизитыДокумента = ИнтеграцияСМЭДО.ПолучитьТребуемыеДанныеВходящегоДокумента(
			Документ, "Организация, Контрагент, ГрифДоступа, ДатаСоздания, ДатаРегистрации");
		Дата = РеквизитыДокумента.ДатаСоздания;
		ГрифДоступа = РеквизитыДокумента.ГрифДоступа;
		Организация = РеквизитыДокумента.Организация;
		
		// Определяем версию для отправки по контрагенту, если не проставлено, 
		// то останется по умолчанию:
		Если Не ЗначениеЗаполнено(ВерсияМЭДО) Тогда
			ВерсияМЭДО = Перечисления.ВерсииФорматаМЭДО.ПоследняяВерсия();
			НастройкиКонтрагента = РегистрыСведений.НастройкиКонтрагентовМЭДО.ПолучитьНастройки(
				РеквизитыДокумента.Контрагент, ДанныеОтвета);
			Если ДанныеОтвета.Успех Тогда
				МЭДО.ПрисвоитьЗаполненное(ВерсияМЭДО, НастройкиКонтрагента.ВерсияФорматаМЭДО);
			КонецЕсли;
		КонецЕсли;
		
		Направление = Перечисления.НаправленияСообщенийМЭДО.Входящее;
		МЭДО.ПрисвоитьЗаполненное(ДатаРегистрации, РеквизитыДокумента.ДатаРегистрации);
	КонецЕсли;
	
	Если Отправлен И Не ЗначениеЗаполнено(ДатаРегистрации) Тогда
		МЭДО.ЗаписьВЖурналСобытий(
			Перечисления.УровниСобытийМЭДО.Ошибка,
			Ссылка,
			НСтр("ru = 'Ошибка при записи ""Данных документа МЭДО""'"),
			НСтр("ru = 'Незарегистрированный документ не может быть отправлен!'"),
			ДанныеОтвета);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	
	Если Не ЗначениеЗаполнено(ГлавныйФайл) Тогда
		ГлавныйФайл = МЭДО.ПопытатьсяОпределитьГлавныйФайл(Документ);
	КонецЕсли;
	
	
	Если Направление = Перечисления.НаправленияСообщенийМЭДО.Исходящее Тогда
		// Для исходящего документа присвоим свой идентификатор из ссылки на документ:
		Если Не ЗначениеЗаполнено(ИдентификаторДокумента) Тогда
			ИдентификаторДокумента = "" + Документ.УникальныйИдентификатор();
		КонецЕсли;
		
		// Для исходящего сообщения аналогично, но из ссылки на "Данные документа МЭДО", даже если это новый объект:
		Если Не ЗначениеЗаполнено(ИдентификаторСообщения) Тогда
			ИдентификаторСообщения = "" + ИнтеграцияСМЭДО.СсылкаНовогоОбъекта(ЭтотОбъект).УникальныйИдентификатор();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.СостоянияДокументовМЭДО.Записывать = Истина;
	
	Движение = Движения.СостоянияДокументовМЭДО.Добавить();
	ЗаполнитьЗначенияСвойств(
		Движение, ЭтотОбъект, "Направление, Документ, ИдентификаторДокумента, ИдентификаторСообщения");
	
	Движение.Период = Дата;
	Движение.ПредметСообщения = Ссылка;
	
	Состояние2 = Неопределено;
	Если Направление = Перечисления.НаправленияСообщенийМЭДО.Исходящее Тогда
		
		Движение.Состояние = Перечисления.СостоянияДокументовМЭДО.ИсходящийДокументЕщеНеОтправлен;
		
		Если ЗначениеЗаполнено(ДатаРегистрации) Тогда
			Если Отправлен Тогда
				Состояние2 = Перечисления.СостоянияДокументовМЭДО.ДокументОтправлен;
			ИначеЕсли ГотовКОтправке И ЗначениеЗаполнено(ДатаРегистрации) Тогда 
				Состояние2 = Перечисления.СостоянияДокументовМЭДО.ДокументЗарегистрированГотовКОтправке;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли Направление = Перечисления.НаправленияСообщенийМЭДО.Входящее Тогда
		
		Движение.Состояние = Перечисления.СостоянияДокументовМЭДО.ДокументПолучен;
		
		Если ЗначениеЗаполнено(ДатаРегистрации) Тогда
			Состояние2 = Перечисления.СостоянияДокументовМЭДО.ВходящийДокументЗарегистрирован;
		КонецЕсли;
	КонецЕсли;
	
	// Второе движение
	Если ЗначениеЗаполнено(Состояние2) Тогда
		Движение = Движения.СостоянияДокументовМЭДО.Добавить();
		Движение.Период = ДатаРегистрации;
		Движение.ПредметСообщения = Ссылка;
		ЗаполнитьЗначенияСвойств(
			Движение, ЭтотОбъект, "Направление, Документ, ИдентификаторДокумента, ИдентификаторСообщения");
		Движение.Состояние = Состояние2;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе

#Область Инициализация
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецОбласти

#КонецЕсли
