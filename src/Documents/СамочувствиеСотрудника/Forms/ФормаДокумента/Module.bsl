
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекПользователь = ПользователиКлиентСервер.ТекущийПользователь();
	Подразделение = РаботаСПользователями.ПолучитьПодразделение(Объект.Сотрудник);
	
	Если Не РольДоступна("ПолныеПрава")
		И Не РольДоступна("КонтрольСамочувствияСотрудников")
		И Не РаботаСПользователями.ЭтоРуководитель(ТекПользователь) Тогда 
		Элементы.Сотрудник.Видимость = Ложь;
		Элементы.Подразделение.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если Не ПараметрыЗаписи.Свойство("РежимЗаписи") Тогда 
		ПараметрыЗаписи.Вставить("РежимЗаписи", РежимЗаписиДокумента.Проведение)
	ИначеЕсли ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Запись Тогда
		ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТемператураПриИзменении(Элемент)
	
	Если Объект.Температура < 35 Или Объект.Температура > 42 Тогда 
		Объект.Температура = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТемператураРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Модифицированность = Истина;
	
	Если Направление = 1 Тогда 
		Если Объект.Температура = 0 Тогда 
			Объект.Температура = 36.6;
		ИначеЕсли Объект.Температура < 42 Тогда 
			Объект.Температура = Объект.Температура + 0.1;
		КонецЕсли;
	Иначе 
		Если Объект.Температура = 0 Тогда 
			Объект.Температура = 36.6;
		ИначеЕсли Объект.Температура > 35 Тогда 
			Объект.Температура = Объект.Температура - 0.1;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникПриИзменении(Элемент)
	
	Подразделение = РаботаСПользователями.ПолучитьПодразделение(Объект.Сотрудник);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаЗамераПриИзменении(Элемент)
	
	Объект.ТочкаЗамера = Неопределено;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НормальнаяТемпература(Команда)
	
	Объект.Температура = 36.6;
	Объект.Самочувствие = НСтр("ru = 'Самочувствие хорошее, признаков заболеваний не имею.'");
	
	ПараметрыЗаписи = Новый Структура;
	ПараметрыЗаписи.Вставить("ЗакрытьПослеЗаписи", Истина);
	ЗаписатьКлиент(ПараметрыЗаписи);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗаписатьКлиент(ПараметрыЗаписи)
	
	Если Записать(ПараметрыЗаписи) Тогда
		
		Если ПараметрыЗаписи.Свойство("ЗакрытьПослеЗаписи") И Не ПараметрыЗаписи.ЗакрытьПослеЗаписи Тогда
			Возврат;
		КонецЕсли;
		
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
