#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ПриКопировании(ОбъектКопирования)
	
	Дата = ТекущаяДатаСеанса();
	ДатаЗамера = Дата;
	ТочкаЗамера = УчетСамочувствияСотрудниковСервер.ТочкаЗамераДляСотрудникаНаДату(Сотрудник, Дата);
	Создал = ПользователиКлиентСервер.ТекущийПользователь();
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ЭтоНовый() Тогда 
		Сотрудник = ПользователиКлиентСервер.ТекущийПользователь();
		Создал = Сотрудник;
		Дата = ТекущаяДатаСеанса();
		ДатаЗамера = Дата;
		ТочкаЗамера = УчетСамочувствияСотрудниковСервер.ТочкаЗамераДляСотрудникаНаДату(
			Сотрудник, Дата);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ПовышеннаяТемпература = Ложь;
	Если ЗначениеЗаполнено(Температура) И Температура >= 37 Тогда 
		ПовышеннаяТемпература = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПовышеннаяТемпература = Ложь;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СамочувствиеСотрудника.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.СамочувствиеСотрудника КАК СамочувствиеСотрудника
		|ГДЕ
		|	СамочувствиеСотрудника.Ссылка <> &ТекущийДокумент
		|	И СамочувствиеСотрудника.Проведен
		|	И СамочувствиеСотрудника.Сотрудник = &Сотрудник
		|	И СамочувствиеСотрудника.ДатаЗамера = &ДатаЗамера
		|	И СамочувствиеСотрудника.ТочкаЗамера = &ТочкаЗамера";
	Запрос.Параметры.Вставить("ТекущийДокумент", Ссылка);
	Запрос.Параметры.Вставить("ДатаЗамера", ДатаЗамера);
	Запрос.Параметры.Вставить("Сотрудник", Сотрудник);
	Запрос.Параметры.Вставить("ТочкаЗамера", ТочкаЗамера);
	Если Не Запрос.Выполнить().Пустой() Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'На указанное время уже существует введенный замер. Укажите другое время замера'"),
			ЭтотОбъект,
			"ТочкаЗамера",, 
			Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли
