#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("БольшеНеПоказывать") Тогда
		БольшеНеПоказывать = Истина;
		КлючОбъекта = Параметры.КлючОбъекта;
		КлючНастроек = Параметры.КлючНастроек;
		// Возможно, пользователь уже нажимал кнопку "Больше не показывать".
		ПоказыватьПодсказку = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			КлючОбъекта,
			КлючНастроек, 
			Истина);
		Если Не ПоказыватьПодсказку Тогда
			Отказ = Истина;
			Возврат;
		КонецЕсли;
	Иначе
		Элементы.БольшеНеПоказывать.Видимость = Ложь;	
	КонецЕсли;	
	
	Подсказка = Параметры.Текст;
	Подсказка = СтрЗаменить(Подсказка, "<BODY>",
		"<HTML><HEAD>
		| <META content=""text/html; charset=utf-8"" http-equiv=Content-Type>
		| </HEAD>
		|<style type=""text/css"">
		|	body {
		|		overflow:    auto;
		|		margin-top:  6px; 		 
		|		margin-left: 0px; 
		|		margin-bottom: 0px;
		|		font-family: Arial, sans-serif;
		|		font-size:   10pt;}
		|	UL {
		|		padding: 10;
		|		margin: 10;
		|	}
		|</style>
		| <BODY>");
	Подсказка = СтрЗаменить(Подсказка, "</BODY>", "</BODY></HTML>");
	
	Если Параметры.Свойство("ЗаголовокКнопкиЗакрыть") Тогда
		Элементы.Закрыть.Заголовок = Параметры.ЗаголовокКнопкиЗакрыть;
		Элементы.Закрыть.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура БольшеНеПоказывать(Команда)
	
	Если БольшеНеПоказывать Тогда
		ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекСохранить(
			КлючОбъекта,
			КлючНастроек, 
			Ложь);
		ОбновитьПовторноИспользуемыеЗначения();
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти
