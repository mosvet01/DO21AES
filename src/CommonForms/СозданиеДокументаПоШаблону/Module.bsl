
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	РегламентированныйУчетОбращений = ПолучитьФункциональнуюОпцию("РегламентированныйУчетОбращений");
	Если РегламентированныйУчетОбращений Тогда 
		ПодборИзКлассификатораОбращенийГражданСервер.ИнициализироватьКэшДанныхФормы(ЭтаФорма);
	КонецЕсли;
	
	//Проверка на существование шаблонов для указанного типа документа
	НеНайденоШаблонов = Ложь;
	Запрос = Новый Запрос();
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ШаблоныДокументов.Ссылка
		|ИЗ
		|	Справочник." + Параметры.ТипШаблонаДокумента + " КАК ШаблоныДокументов
		|ГДЕ
		|	НЕ ШаблоныДокументов.ЭтоГруппа
		|	И НЕ ШаблоныДокументов.ПометкаУдаления";
	
	Если Параметры.Свойство("СозданиеОбращенияГраждан") И Параметры.СозданиеОбращенияГраждан Тогда
			
		Запрос.Текст = Запрос.Текст + "
		|	И	(ШаблоныДокументов.ВидДокумента = ЗНАЧЕНИЕ(Справочник.ВидыВходящихДокументов.ПустаяСсылка)
		|			ИЛИ ШаблоныДокументов.ВидДокумента.ЯвляетсяОбращениемОтГраждан = ИСТИНА)";
		
		СозданиеОбращенияГраждан = Параметры.СозданиеОбращенияГраждан;
	КонецЕсли;
	
	// Регламентированный учет обращений
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаСтраницаОсновные;
	Если СозданиеОбращенияГраждан И РегламентированныйУчетОбращений Тогда 
		Элементы.Далее.Видимость = Истина;
		Элементы.Далее.КнопкаПоУмолчанию = Истина;
		Элементы.Назад.Видимость = Истина;
		Элементы.Назад.Доступность = Ложь;
		Элементы.СоздатьПоШаблону.Видимость = Ложь;
		
		Элементы.ОткрытьЗагрузкуКлассификатора.Видимость = 
			Пользователи.ЭтоПолноправныйПользователь() 
			И ПодборИзКлассификатораОбращенийГражданСервер.КлассификаторОбращенийНеЗаполнен();
	Иначе 
		Элементы.Далее.Видимость = Ложь;
		Элементы.Назад.Видимость = Ложь;
	КонецЕсли;
	
	ОбщееКоличествоШаблонов = Запрос.Выполнить().Выбрать().Количество();
	Если ОбщееКоличествоШаблонов = 0 Тогда
		НеНайденоШаблонов = Истина;
		ВызватьИсключение("СоздатьПустойДокумент");
	КонецЕсли;

	ТипШаблонаДокумента = Параметры.ТипШаблонаДокумента;
	ВозможностьСозданияПустогоДокумента = Параметры.ВозможностьСозданияПустогоДокумента;
	
	Если Параметры.Свойство("НаименованиеКнопкиВыбора") Тогда 
		ЭтаФорма.Элементы.СоздатьПоШаблону.Заголовок = Параметры.НаименованиеКнопкиВыбора;
	КонецЕсли;
	
	СоздатьПоШаблону = Истина;
	
	АвтоЗаголовок = Ложь;
	Если Параметры.ВозможностьСозданияПустогоДокумента Тогда
		Если ТипШаблонаДокумента = "ШаблоныВнутреннихДокументов" Тогда
			Заголовок = НСтр("ru = 'Создание нового внутреннего документа'");
			ТипВида = "ВидыВнутреннихДокументов";
		ИначеЕсли ТипШаблонаДокумента = "ШаблоныВходящихДокументов" Тогда
			Если СозданиеОбращенияГраждан = Истина Тогда 
				Заголовок = НСтр("ru = 'Создание нового обращения'");
			Иначе 
				Заголовок = НСтр("ru = 'Создание нового входящего документа'");
			КонецЕсли;
			ТипВида = "ВидыВходящихДокументов";
		ИначеЕсли ТипШаблонаДокумента = "ШаблоныИсходящихДокументов" Тогда
			Заголовок = НСтр("ru = 'Создание нового исходящего документа'");
			ТипВида = "ВидыИсходящихДокументов";
		КонецЕсли;
	Иначе
		Если ТипШаблонаДокумента = "ШаблоныВнутреннихДокументов" Тогда
			Заголовок = НСтр("ru = 'Заполнение внутреннего документа по шаблону'");
			ТипВида = "ВидыВнутреннихДокументов";
		ИначеЕсли ТипШаблонаДокумента = "ШаблоныВходящихДокументов" Тогда
			Если СозданиеОбращенияГраждан = Истина Тогда 
				Заголовок = НСтр("ru = 'Заполнение обращения по шаблону'");
			Иначе 
				Заголовок = НСтр("ru = 'Заполнение входящего документа по шаблону'");
			КонецЕсли;
			ТипВида = "ВидыВходящихДокументов";
		ИначеЕсли ТипШаблонаДокумента = "ШаблоныИсходящихДокументов" Тогда
			Заголовок = НСтр("ru = 'Заполнение исходящего документа по шаблону'");
			ТипВида = "ВидыИсходящихДокументов";
		КонецЕсли;
	КонецЕсли;
	
	ЕстьДоступныеУчетныеЗаписиДляОтправки = 
		РаботаСПочтовымиСообщениямиВызовСервера.ЕстьДоступныеУчетныеЗаписиДляОтправки();
		
	ПостроитьДеревоШаблонов();
	СохранениеВводимыхЗначений.ЗагрузитьСписокВыбора(ЭтаФорма, "СтрокаПоиска");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ИспользованиеЛегкойПочты = ПолучитьФункциональнуюОпциюИнтерфейса("ИспользованиеЛегкойПочты");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// Регламентированный учет обращений
	Если ИмяСобытия = "Выполнена_загрузка_классификатора_обращений" Тогда
		Для Каждого ЭлементКлассификатора Из КэшДанныхФормы.ЭлементыКлассификатора Цикл
			Элементы[ЭлементКлассификатора.Значение.ТаблицаФормы].Обновить();
		КонецЦикла;
	КонецЕсли;
	// Регламентированный учет обращений
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СтрокаПоискаПриИзменении(Элемент)

	Если ЗначениеЗаполнено(СтрокаПоиска) Тогда
		
		СтрокаПоиска = СокрЛП(СтрокаПоиска);
	
		Если СтрДлина(СтрокаПоиска) < 3 И СтрокаПоиска <> "*" И СтрокаПоиска <> "**" Тогда
			ЭтаФорма.ТекущийЭлемент = Элементы.СтрокаПоиска;
			ЭтаФорма.Активизировать();
			ПоказатьПредупреждение(, НСтр("ru = 'Необходимо ввести минимум 3 символа'"));
			Возврат;
		КонецЕсли;
		
		ПустойРезультатПоиска = Ложь;
		НайтиШаблоны();
		ПустойРезультатПоиска = СписокШаблоновПоиск.ПолучитьЭлементы().Количество() = 0;
		
		Если ПустойРезультатПоиска Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'По вашему запросу ничего не найдено'"));
			ЭтаФорма.ТекущийЭлемент = Элементы.СтрокаПоиска;
		Иначе
			ЭтаФорма.ТекущийЭлемент = Элементы.СписокШаблонов;
			СохранениеВводимыхЗначенийКлиент.ОбновитьСписокВыбора(ЭтаФорма, "СтрокаПоиска", 10);
		КонецЕсли;
		
		ПоискВключен = Истина;
	Иначе
		Если ПоискВключен Тогда
			ПостроитьДеревоШаблонов();
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СтрокаПоискаОчистка(Элемент, СтандартнаяОбработка)
	
	СтрокаПоиска = Неопределено;
	ПостроитьДеревоШаблонов();
	ЭтаФорма.ТекущийЭлемент = Элементы.СписокШаблонов;
	
КонецПроцедуры

&НаКлиенте
Процедура СводкаПоШаблонуПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если Не ЗначениеЗаполнено(ДанныеСобытия.Href) Тогда 
		Возврат;
	КонецЕсли;	
	
	Если Лев(ДанныеСобытия.Href, 6) <> "v8doc:" Тогда 
		Возврат;
	КонецЕсли;	
	НавигационнаяСсылкаПоля = Сред(ДанныеСобытия.Href, 7);
	
	Если Найти(НавигационнаяСсылкаПоля, "message") > 0 Тогда 	
		
		Если Не ИспользованиеЛегкойПочты Тогда 
			ТекстСообщения = НСтр("ru = 'Для отправки письма требуется включить использование легкой почты.'");
			ПоказатьПредупреждение(, ТекстСообщения);
			Возврат;
		ИначеЕсли Не ЕстьДоступныеУчетныеЗаписиДляОтправки Тогда 
			ТекстСообщения = НСтр("ru = 'Для отправки письма требуется настройка учетной записи электронной почты.'");
			ПоказатьПредупреждение(, ТекстСообщения);
			Возврат;
		Иначе 		
			АдресПочты = СокрЛП(СтрЗаменить(НавигационнаяСсылкаПоля, "message", ""));
			РаботаСПочтовымиСообщениямиКлиент.ОткрытьФормуОтправкиПочтовогоСообщения(, АдресПочты);
		КонецЕсли;	
						
	Иначе	
		
		ПерейтиПоНавигационнойСсылке(НавигационнаяСсылкаПоля);
		
	КонецЕсли;

	
КонецПроцедуры

// Регламентированный учет обращений

&НаКлиенте
Процедура КодВопросаИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	Если СтрДлина(Текст) < 2 Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоРедактированиеКодаВопроса = Истина;
	
	ПодборИзКлассификатораОбращенийГражданКлиент.ПроанализироватьКодВопросаПолучитьСоставляющие(
		ЭтаФорма,
		Текст,
		СтандартнаяОбработка);
		
	РаботаСОбращениямиКлиент.ОбновитьСписки(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура КодВопросаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ЭтоРедактированиеКодаВопроса = Ложь;	
	
	ПодборИзКлассификатораОбращенийГражданКлиент.ПроанализироватьКодВопросаПолучитьСоставляющие(
		ЭтаФорма,
		Текст,
		СтандартнаяОбработка);
		
	РаботаСОбращениямиКлиент.ОбновитьСписки(ЭтаФорма);
	
КонецПроцедуры
// Регламентированный учет обращений

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокШаблонов

&НаКлиенте
Процедура СписокШаблоновВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если РегламентированныйУчетОбращений И СозданиеОбращенияГраждан Тогда 
		Далее(Неопределено);
	Иначе
		
		ТекущиеДанные = Элементы.СписокШаблонов.ТекущиеДанные;
		
		Если ТекущиеДанные <> Неопределено 
			И РегламентированныйУчетОбращений И ТекущиеДанные.ЭтоОбращение Тогда 
			Далее(Неопределено);
		Иначе 
			СоздатьПоШаблонуВыполнить();
		КонецЕсли;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура СписокШаблоновПриАктивизацииСтроки(Элемент)
	
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СводкаПоШаблону = ПолучитьСводкуПоШаблону(Элемент.ТекущиеДанные.КомандаСсылка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьПоШаблону(Команда)
	
	ТекущиеДанные = Элементы.СписокШаблонов.ТекущиеДанные;
		
	Если ТекущиеДанные <> Неопределено 
		И РегламентированныйУчетОбращений И ТекущиеДанные.ЭтоОбращение Тогда 
		Далее(Неопределено);
	Иначе 
		СоздатьПоШаблонуВыполнить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКарточкуШаблона(Команда)
	
	ТекущиеДанные = Элементы.СписокШаблонов.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено ИЛИ ТекущиеДанные.ЭтоГруппа 
		Или ТекущиеДанные.КомандаСсылка = "Пустой" Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные <> Неопределено И НЕ ТекущиеДанные.ЭтоГруппа Тогда
		ПоказатьЗначение(, ТекущиеДанные.КомандаСсылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьИсторию(Команда)
	
	СохранениеВводимыхЗначенийКлиент.ОчиститьСписокВыбора(ЭтаФорма, "СтрокаПоиска");
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	СоздатьПустойДокумент = Ложь;
	СоздатьПоШаблону = Ложь;
	Закрыть("ПрерватьОперацию");
	
КонецПроцедуры

// Регламентированный учет обращений
&НаКлиенте
Процедура Далее(Команда)
	
	Элементы.Назад.Доступность = Истина;
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаСтраницаОсновные Тогда
		
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаСтраницаПодборИзКлассификатора;
		Элементы.Далее.Видимость = Истина;
		Элементы.Далее.КнопкаПоУмолчанию = Истина;
		Элементы.Назад.Видимость = Истина;
		Элементы.СоздатьПоШаблону.Видимость = Ложь;
		
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаСтраницаПодборИзКлассификатора Тогда
		Если Не ПодборИзКлассификатораОбращенийГражданКлиент.КлючевыеПоляЗаполнены(ЭтаФорма) Тогда
			Возврат;
		КонецЕсли;
		
		ОписаниеВопроса = Новый Структура("Раздел, Тематика, Тема, Вопрос");
		ЗаполнитьЗначенияСвойств(ОписаниеВопроса, ЭтаФорма);
		
		Если Не ТаблицаВопросы.НайтиСтроки(ОписаниеВопроса).Количество() Тогда
			ОписаниеВопроса.Вставить("КодВопроса", КодВопроса);
			ЗаполнитьЗначенияСвойств(ТаблицаВопросы.Добавить(), ОписаниеВопроса);
		КонецЕсли;
				
		ОписаниеВопроса = Новый Структура(
			"Раздел, Тематика, Тема, Вопрос, КодВопроса",
			Неопределено, Неопределено, Неопределено, Неопределено, Неопределено);
			
		ЗаполнитьЗначенияСвойств(ЭтаФорма, ОписаниеВопроса);
		
		#Если Не ВебКлиент Тогда
			ИзменитьТекстПодсказки(Элементы.Назад.ИмяКоманды, НСтр("ru = 'Добавить еще вопрос'"));
		#КонецЕсли
		
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаСтраницаВопросы;
		Элементы.Назад.Заголовок = НСтр("ru = 'Добавить еще вопрос'");
		Элементы.Далее.Заголовок = НСтр("ru = 'Готово'");
		
		Элементы.Далее.Видимость = Истина;
		Элементы.Далее.КнопкаПоУмолчанию = Истина;
		Элементы.Назад.Видимость = Истина;
		Элементы.СоздатьПоШаблону.Видимость = Ложь;
		
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаСтраницаВопросы Тогда
		
		ОчиститьСообщения();
		Если ТаблицаВопросы.Количество() = 0 Тогда 
			ТекстОшибки = НСтр("ru = 'Не введено ни одной строки в список ""Вопросы""'");
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,
				"ТаблицаВопросы");
			Возврат;
		КонецЕсли;
		
		СоздатьПоШаблонуВыполнить(); 
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаСтраницаОсновные Тогда 
		
		Элементы.Назад.Доступность = Ложь;
		
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаСтраницаПодборИзКлассификатора Тогда 
		
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаСтраницаОсновные;
		Элементы.Назад.Доступность = Ложь;
		
		Если НЕ (РегламентированныйУчетОбращений И СозданиеОбращенияГраждан) Тогда
			ТаблицаВопросы.Очистить();
			Элементы.СоздатьПоШаблону.Видимость = Истина;
			Элементы.СоздатьПоШаблону.КнопкаПоУмолчанию = Истина;
			Элементы.Далее.Видимость = Ложь;
			Элементы.Назад.Видимость = Ложь;
		КонецЕсли;
	
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаСтраницаВопросы Тогда

		#Если Не ВебКлиент Тогда
			ИзменитьТекстПодсказки(Элементы.Назад.ИмяКоманды, НСтр("ru = 'Назад'"));
		#КонецЕсли
		
		Элементы.Назад.Заголовок = НСтр("ru = '< Назад'");
		Элементы.Далее.Заголовок = НСтр("ru = 'Далее >'");
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаСтраницаПодборИзКлассификатора;

	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЗагрузкуКлассификатора(Команда)
	
	ОткрытьФорму(
		"Обработка.ЗагрузкаКлассификатораОбращенийГраждан.Форма.ФормаВыбораИзКлассификатора",
		,
		ЭтаФорма, , , , ,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры
// Регламентированный учет обращений

#КонецОбласти

// Регламентированный учет обращений
#Область ОбработчикиСобытийТаблицыФормыРазделы

&НаКлиенте
Процедура РазделыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура РазделыПриАктивизацииСтроки(Элемент)
	
	Тема		  = Неопределено;
	Вопрос		  = Неопределено;
	ТекущиеДанные = Элементы.Тематики.ТекущиеДанные;
	
	ПодборИзКлассификатораОбращенийГражданКлиент.ОбработчикСобытияТаблицыФормыПриАктивизацииСтроки(
		ЭтаФорма, Элемент);
		
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Тематики,
		"Раздел",
		Раздел,
		ВидСравненияКомпоновкиДанных.Равно,
		,
		Истина,
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыТематики

&НаКлиенте
Процедура ТематикиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ТематикиПриАктивизацииСтроки(Элемент)
	
	Тема		  = Неопределено;
	Вопрос		  = Неопределено;
	ТекущиеДанные = Элементы.Тематики.ТекущиеДанные;
		
	ПодборИзКлассификатораОбращенийГражданКлиент.ОбработчикСобытияТаблицыФормыПриАктивизацииСтроки(ЭтаФорма, Элемент);		
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Темы,
		"Тематика",
		Тематика,
		ВидСравненияКомпоновкиДанных.Равно,
		,
		Истина,
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыТемы

&НаКлиенте
Процедура ТемыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ТемыПриАктивизацииСтроки(Элемент)
	
	Вопрос		  = Неопределено;
	ПодборИзКлассификатораОбращенийГражданКлиент.ОбработчикСобытияТаблицыФормыПриАктивизацииСтроки(ЭтаФорма, Элемент);
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Вопросы,
		"Тема",
		Тема,
		ВидСравненияКомпоновкиДанных.Равно,
		,
		Истина,
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыВопросы

&НаКлиенте
Процедура ВопросыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Далее(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросыПриАктивизацииСтроки(Элемент)
	
	ПодборИзКлассификатораОбращенийГражданКлиент.ОбработчикСобытияТаблицыФормыПриАктивизацииСтроки(ЭтаФорма, Элемент);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаВопросов

&НаКлиенте
Процедура ТаблицаВопросыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаВопросыПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаВопросыПослеУдаления(Элемент)
	
	Если Не ТаблицаВопросы.Количество() Тогда
		Назад(Неопределено);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

// Регламентированный учет обращений

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НайтиШаблоны()
	
	Дерево = РеквизитФормыВЗначение("СписокШаблоновПоиск");
	Дерево.Строки.Очистить();
	
	ПолучитьВсеШаблоныДокументов(Дерево, СтрокаПоиска);
	ЗначениеВДанныеФормы(Дерево, СписокШаблоновПоиск);
	
	Если СписокШаблоновПоиск.ПолучитьЭлементы().Количество() > 0 Тогда 
		ЗначениеВДанныеФормы(Дерево, СписокШаблонов);
		УстановитьТекущуюСтрокуВДеревеНаПервыйЗначащийЭлемент();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПоШаблонуВыполнить()
	
	ТекущиеДанные = Элементы.СписокШаблонов.ТекущиеДанные;
	ДанныеЗаполнения = Новый Структура;
	
	Если РегламентированныйУчетОбращений И ТаблицаВопросы.Количество() > 0 Тогда 
		ДанныеЗаполнения.Вставить("ТаблицаВопросы", ТаблицаВопросы);
	КонецЕсли;
	
	Если ТекущиеДанные = Неопределено ИЛИ ТекущиеДанные.ЭтоГруппа Тогда
		Возврат;
	ИначеЕсли ТекущиеДанные.КомандаСсылка = "Пустой" Тогда 
		СоздатьПустойДокумент = Истина;
		СоздатьПоШаблону = Ложь;
		
		ДанныеЗаполнения.Вставить("СоздатьПустойДокумент", Истина);
		ДанныеЗаполнения.Вставить("ШаблонДокумента", "СоздатьПустойДокумент");
		Закрыть(ДанныеЗаполнения);
		Возврат;
	КонецЕсли;
	
	СоздатьПустойДокумент = Ложь;
	СоздатьПоШаблону = Истина;
	
	ДанныеЗаполнения.Вставить("ШаблонДокумента", ТекущиеДанные.КомандаСсылка);
	
	Закрыть(ДанныеЗаполнения);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСводкуПоШаблону(ШаблонСсылка)
	
	Если НЕ ЗначениеЗаполнено(ШаблонСсылка) Тогда
		Возврат "";
	КонецЕсли;
	
	Если ТипЗнч(ШаблонСсылка) = Тип("СправочникСсылка.ШаблоныВнутреннихДокументов") Тогда
		Сводка = ОбзорДокумента.ПолучитьОбзорШаблонаВнутреннегоДокумента(ШаблонСсылка);
		Возврат Сводка;
	КонецЕсли;
	
	Если ТипЗнч(ШаблонСсылка) = Тип("СправочникСсылка.ШаблоныИсходящихДокументов") Тогда
		Сводка = ОбзорДокумента.ПолучитьОбзорШаблонаИсходящегоДокумента(ШаблонСсылка);
		Возврат Сводка;
	КонецЕсли;
	
	Если ТипЗнч(ШаблонСсылка) = Тип("СправочникСсылка.ШаблоныВходящихДокументов") Тогда
		Сводка = ОбзорДокумента.ПолучитьОбзорШаблонаВходящегоДокумента(ШаблонСсылка);
		Возврат Сводка;
	КонецЕсли;
	
	Если ТипЗнч(ШаблонСсылка) = Тип("Строка") И ШаблонСсылка = "Пустой" Тогда 
		Сводка = ОбзорДокумента.ПолучитьОбзорПустогоШаблона();
	КонецЕсли;
		
	Возврат Сводка;
	
КонецФункции

&НаСервере
Процедура ПостроитьДеревоШаблонов()
	
	Дерево = РеквизитФормыВЗначение("СписокШаблонов");
	Дерево.Строки.Очистить();
	
	Если ОбщееКоличествоШаблонов > 5 Тогда 
		ПолучитьНедавноИспользуемыеШаблоныДокументов(Дерево);
	КонецЕсли;
	
	ПолучитьВсеШаблоныДокументов(Дерево);
	
	ЗначениеВДанныеФормы(Дерево, СписокШаблонов);
	УстановитьТекущуюСтрокуВДеревеНаПервыйЗначащийЭлемент();
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьНедавноИспользуемыеШаблоныДокументов(Дерево)
	
	ТаблицаПоследних = РаботаСШаблонамиДокументовСервер.ПолучитьПоследниеИспользованныеШаблоны(
		"СправочникСсылка." + ТипШаблонаДокумента);
		
	КоличествоНедавнихШаблонов = ТаблицаПоследних.Количество();
	Если КоличествоНедавнихШаблонов = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаЗаголовка = Дерево.Строки.Добавить();
	СтрокаЗаголовка.Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Недавние (%1)'"), ТаблицаПоследних.Количество());
	СтрокаЗаголовка.КомандаСсылка = "Недавние";
	СтрокаЗаголовка.ЭтоГруппа = Истина;
	
	Для Каждого Строка Из ТаблицаПоследних Цикл
		СтрокаПредмета = СтрокаЗаголовка.Строки.Добавить();
		
		Если ЗначениеЗаполнено(Строка.Шаблон) Тогда 
			СтрокаПредмета.Представление = Строка(Строка.Шаблон);
			СтрокаПредмета.КомандаСсылка = Строка.Шаблон;
		Иначе 
			СтрокаПредмета.КомандаСсылка = "Пустой";
			СтрокаПредмета.Представление = НСтр("ru = 'Пустой'");
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ПолучитьВсеШаблоныДокументов(Дерево, СтрПоиска = "")
	
	ЭтоПоиск = ЗначениеЗаполнено(СтрПоиска);
	КоличествоШаблонов = 0;
	Запрос = Новый Запрос();
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|ВидыДокументов.Наименование,
		|ВидыДокументов.ЭтоГруппа КАК ЭтоГруппа,
		|ВидыДокументов.Ссылка,
		|ВидыДокументов.Родитель КАК Родитель
		|ИЗ
		|	Справочник." + ТипВида + " КАК ВидыДокументов
		|ГДЕ
		|	ВидыДокументов.ПометкаУдаления = ЛОЖЬ
		|	И ВидыДокументов.ЭтоГруппа
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка ИЕРАРХИЯ,
		|	Наименование";
		
	Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Если Не ЭтоПоиск И ОбщееКоличествоШаблонов > 5 И КоличествоНедавнихШаблонов > 0 Тогда 
		СтрокаВсе = Дерево.Строки.Добавить();
		СтрокаВсе.КомандаСсылка = "Все";
		СтрокаВсе.ЭтоГруппа = Истина;
	Иначе 
		СтрокаВсе = Дерево;
	КонецЕсли;
	
	Если ВозможностьСозданияПустогоДокумента И Не ЭтоПоиск Тогда 
		СтрокаПустой = СтрокаВсе.Строки.Добавить();
		СтрокаПустой.КомандаСсылка = "Пустой";
		СтрокаПустой.Представление = НСтр("ru = 'Пустой'");
		СтрокаПустой.ЭтоГруппа = Ложь;
		КоличествоШаблонов = КоличествоШаблонов + 1;
	КонецЕсли;
	
	Пока Выборка.Следующий() Цикл
		ОбъектГруппировки = Выборка.Родитель;
		
		Если ЗначениеЗаполнено(ОбъектГруппировки) Тогда
			
			НайденнаяСтрока = Дерево.Строки.Найти(ОбъектГруппировки, "ОбъектГруппировки", Истина);
			
			Если НайденнаяСтрока <> Неопределено Тогда
				НоваяСтрока = НайденнаяСтрока.Строки.Добавить();
			Иначе
				СтрокаГруппировки = СтрокаВсе.Строки.Добавить();
				СтрокаГруппировки.ОбъектГруппировки = Выборка.Ссылка;
				СтрокаГруппировки.ЭтоГруппа = Выборка.ЭтоГруппа;
				СтрокаГруппировки.Представление = Строка(Выборка.Ссылка);
				СтрокаГруппировки.КомандаСсылка = Выборка.Ссылка;
				
				Если Не Выборка.ЭтоГруппа Тогда 
					Продолжить;
				КонецЕсли;
				
				НоваяСтрока = СтрокаГруппировки.Строки.Добавить();
			КонецЕсли;
			
			НоваяСтрока.ОбъектГруппировки = Выборка.Ссылка;
			НоваяСтрока.ЭтоГруппа = Выборка.ЭтоГруппа;
			НоваяСтрока.Представление = Строка(Выборка.Ссылка);
			НоваяСтрока.КомандаСсылка = Выборка.Ссылка;
		Иначе 
			
			СтрокаГруппировки = СтрокаВсе.Строки.Добавить();
			СтрокаГруппировки.ОбъектГруппировки = Выборка.Ссылка;
			СтрокаГруппировки.ЭтоГруппа = Выборка.ЭтоГруппа;
			СтрокаГруппировки.Представление = Строка(Выборка.Ссылка);
			СтрокаГруппировки.КомандаСсылка = Выборка.Ссылка;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ШаблоныДокументов.Наименование КАК Наименование,
		|	ШаблоныДокументов.КомментарийКШаблону КАК КомментарийКШаблону,
		|	ШаблоныДокументов.ЭтоГруппа КАК ЭтоГруппа,
		|	ШаблоныДокументов.Ссылка КАК Ссылка,
		|	ШаблоныДокументов.ВидДокумента.Родитель КАК Родитель,
		|	%1
		|ИЗ
		|	Справочник." + ТипШаблонаДокумента + " КАК ШаблоныДокументов
		|	%2
		|ГДЕ
		|	ШаблоныДокументов.ПометкаУдаления = ЛОЖЬ
		|	И НЕ ШаблоныДокументов.ЭтоГруппа";
	
	Если ТипШаблонаДокумента = "ШаблоныВходящихДокументов" Тогда
		Запрос.Текст = СтрШаблон(Запрос.Текст, 
			"ЕСТЬNULL(ВидыВходящихДокументов.ЯвляетсяОбращениемОтГраждан, ЛОЖЬ) КАК ЭтоОбращение",
			"	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыВходящихДокументов КАК ВидыВходящихДокументов
			|	ПО (ШаблоныДокументов.ВидДокумента = ВидыВходящихДокументов.Ссылка)");
	Иначе 
		Запрос.Текст = СтрШаблон(Запрос.Текст,
			"ЛОЖЬ КАК ЭтоОбращение", "");
	КонецЕсли;
	
	Если СозданиеОбращенияГраждан Тогда
		Запрос.Текст = Запрос.Текст + "
			|	И (ШаблоныДокументов.ВидДокумента = ЗНАЧЕНИЕ(Справочник.ВидыВходящихДокументов.ПустаяСсылка)
			|	ИЛИ ВидыВходящихДокументов.ЯвляетсяОбращениемОтГраждан)";
	КонецЕсли;
	
	Если ЭтоПоиск Тогда 
		Запрос.Текст = Запрос.Текст + "
			|	И ШаблоныДокументов.Наименование Подобно &СтрокаПоиска";
		Запрос.УстановитьПараметр("СтрокаПоиска", "%" + СтрокаПоиска + "%");
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + "
			|УПОРЯДОЧИТЬ ПО
			|	Наименование";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ОбъектГруппировки = Выборка.Родитель;
		
		Если ЗначениеЗаполнено(ОбъектГруппировки) Тогда
			
			НайденнаяСтрока = Дерево.Строки.Найти(ОбъектГруппировки, "ОбъектГруппировки", Истина);
			
			Если НайденнаяСтрока <> Неопределено Тогда
				НоваяСтрока = НайденнаяСтрока.Строки.Добавить();
			Иначе
				СтрокаГруппировки = СтрокаВсе.Строки.Добавить();
				СтрокаГруппировки.ОбъектГруппировки = Выборка.Ссылка;
				СтрокаГруппировки.ЭтоГруппа = Выборка.ЭтоГруппа;
				СтрокаГруппировки.Представление = Строка(Выборка.Ссылка);
				СтрокаГруппировки.КомандаСсылка = Выборка.Ссылка;
				СтрокаГруппировки.ЭтоОбращение = Выборка.ЭтоОбращение;
				
				Если Не Выборка.ЭтоГруппа Тогда 
					КоличествоШаблонов = КоличествоШаблонов + 1;
					Продолжить;
				КонецЕсли;
				
				НоваяСтрока = СтрокаГруппировки.Строки.Добавить();
			КонецЕсли;
			
			НоваяСтрока.ОбъектГруппировки = Выборка.Ссылка;
			НоваяСтрока.ЭтоГруппа = Выборка.ЭтоГруппа;
			НоваяСтрока.Представление = Строка(Выборка.Ссылка);
			НоваяСтрока.КомандаСсылка = Выборка.Ссылка;
			НоваяСтрока.ЭтоОбращение = Выборка.ЭтоОбращение;
			
			Если Не Выборка.ЭтоГруппа Тогда 
				КоличествоШаблонов = КоличествоШаблонов + 1;
				Продолжить;
			КонецЕсли;
		Иначе 
			
			СтрокаГруппировки = СтрокаВсе.Строки.Добавить();
			СтрокаГруппировки.ОбъектГруппировки = Выборка.Ссылка;
			СтрокаГруппировки.ЭтоГруппа = Выборка.ЭтоГруппа;
			СтрокаГруппировки.Представление = Строка(Выборка.Ссылка);
			СтрокаГруппировки.КомандаСсылка = Выборка.Ссылка;
			СтрокаГруппировки.ЭтоОбращение = Выборка.ЭтоОбращение;
			
			Если Не Выборка.ЭтоГруппа Тогда 
				КоличествоШаблонов = КоличествоШаблонов + 1;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	УдалитьПапкиБезАктивныхШаблонов(Дерево);
	
	Если Не ЭтоПоиск И ОбщееКоличествоШаблонов > 5 
		И КоличествоНедавнихШаблонов > 0 И КоличествоШаблонов > 0 Тогда 
		СтрокаВсе.Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Все (%1)'"), КоличествоШаблонов);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьТекущуюСтрокуВДеревеНаПервыйЗначащийЭлемент()
	
	ЭлементыДерева = СписокШаблонов.ПолучитьЭлементы();
	Если ЭлементыДерева.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	ПерваяГруппа = ЭлементыДерева[0];
	Если ПерваяГруппа.ПолучитьЭлементы().Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	ПервыйЭлементПервойГруппы = ПерваяГруппа.ПолучитьЭлементы()[0];
	Индекс = ПервыйЭлементПервойГруппы.ПолучитьИдентификатор();
	Элементы.СписокШаблонов.ТекущаяСтрока = Индекс;
	
КонецПроцедуры

&НаСервере
Функция УдалитьПапкиБезАктивныхШаблонов(Дерево)
	
	ЕстьШаблоны = Ложь;
	МассивУдаляемыхГрупп = Новый Массив;
	
	Для Каждого СтрокаДерева Из Дерево.Строки Цикл
		Если СтрокаДерева.ЭтоГруппа Тогда 
			Если Не УдалитьПапкиБезАктивныхШаблонов(СтрокаДерева) Тогда 
				МассивУдаляемыхГрупп.Добавить(СтрокаДерева);
			Иначе 
				ЕстьШаблоны = Истина;
			КонецЕсли;
		Иначе 
			ЕстьШаблоны = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Группа Из МассивУдаляемыхГрупп Цикл 
		Для Каждого СтрокаДерева Из Дерево.Строки Цикл
			Если Группа = СтрокаДерева Тогда 
				Дерево.Строки.Удалить(СтрокаДерева);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат ЕстьШаблоны;
	
КонецФункции

// Регламентированный учет обращений
&НаСервере
Процедура ИзменитьТекстПодсказки(Команда, ТекстПодсказки)
	
	Команды[Элементы[Команда].ИмяКоманды].Подсказка = ТекстПодсказки;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеФормы()
	
	Элементы.КодВопроса.ОбновитьТекстРедактирования();
	Элементы.КодВопроса.УстановитьГраницыВыделения(1, 1);

КонецПроцедуры
// Регламентированный учет обращений
#КонецОбласти

#Область СлужебныеПроцедурыИФункции_ТестЦентр

&НаКлиенте
Функция ТЦСоздатьПоШаблону() Экспорт
	
	СоздатьПоШаблону(Неопределено);
	Возврат Истина;
	
КонецФункции

#КонецОбласти
