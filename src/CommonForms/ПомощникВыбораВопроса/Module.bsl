
#Область Обработчики_событий_формы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПодборИзКлассификатораОбращенийГражданСервер.ИнициализироватьКэшДанныхФормы(ЭтаФорма);
	
	Раздел	 = Параметры.Раздел;
	Тематика = Параметры.Тематика;
	Тема	 = Параметры.Тема;	
	Вопрос	 = Параметры.Вопрос;
	
	Элементы.Разделы.ТекущаяСтрока	= Раздел;
	Элементы.Тематики.ТекущаяСтрока = Тематика;
	Элементы.Темы.ТекущаяСтрока		= Тема;
	Элементы.Вопросы.ТекущаяСтрока	= Вопрос;
	
	Элементы.ОткрытьЗагрузкуКлассификатора.Видимость = 
		Пользователи.ЭтоПолноправныйПользователь() 
		И ПодборИзКлассификатораОбращенийГражданСервер.КлассификаторОбращенийНеЗаполнен();
		
	Если Параметры.Свойство("ДляПросмотра") И Параметры.ДляПросмотра Тогда 
		Элементы.Закрыть.Заголовок = НСтр("ru = 'Закрыть'");
		Элементы.Закрыть.КнопкаПоУмолчанию = Истина;
		Элементы.Готово.Видимость = Ложь;
		Элементы.Разделы.Доступность = Ложь;
		Элементы.Вопросы.Доступность = Ложь;
		Элементы.Темы.Доступность = Ложь;
		Элементы.Тематики.Доступность = Ложь;
		Элементы.КодВопроса.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Выполнена_загрузка_классификатора_обращений" Тогда
		Для Каждого ЭлементКлассификатора Из КэшДанныхФормы.ЭлементыКлассификатора Цикл
			Элементы[ЭлементКлассификатора.Значение.ТаблицаФормы].Обновить();
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Обработчики_команд_формы

&НаКлиенте
Процедура Готово(Команда)
	
	ЗавершитьВыборИзКлассификатора();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЗагрузкуКлассификатора(Команда)
	
	ОткрытьФорму(
		"Обработка.ЗагрузкаКлассификатораОбращенийГраждан.Форма.ФормаВыбораИзКлассификатора",
		,
		ЭтаФорма, , , , ,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область Обработчики_событий_элементов_формы

&НаКлиенте
Процедура КодВопросаИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	Если СтрДлина(Текст) < 2 Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоРедактированиеКодаВопроса = Истина;
	
	ПодборИзКлассификатораОбращенийГражданКлиент.ПроанализироватьКодВопросаПолучитьСоставляющие(
		ЭтаФорма,
		Текст,
		СтандартнаяОбработка);
		
	РаботаСОбращениямиКлиент.ОбновитьСписки(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура КодВопросаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)

	ЭтоРедактированиеКодаВопроса = Ложь;	
	
	ПодборИзКлассификатораОбращенийГражданКлиент.ПроанализироватьКодВопросаПолучитьСоставляющие(
		ЭтаФорма,
		Текст,
		СтандартнаяОбработка);
		
	РаботаСОбращениямиКлиент.ОбновитьСписки(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область Обработчики_событий_таблицы_формы_Разделы

&НаКлиенте
Процедура РазделыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура РазделыПриАктивизацииСтроки(Элемент)
	
	Тема = Неопределено;
	Тематика = Неопределено;
	Вопрос = Неопределено;
	ПодборИзКлассификатораОбращенийГражданКлиент.ОбработчикСобытияТаблицыФормыПриАктивизацииСтроки(ЭтаФорма, Элемент);
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Тематики,
		"Раздел",
		Раздел,
		ВидСравненияКомпоновкиДанных.Равно,
		,
		Истина,
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
		
КонецПроцедуры

#КонецОбласти

#Область Обработчики_событий_таблицы_формы_Тематики

&НаКлиенте
Процедура ТематикиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ТематикиПриАктивизацииСтроки(Элемент)
	
	Тема = Неопределено;
	Вопрос = Неопределено;
	ТекущиеДанные = Элементы.Тематики.ТекущиеДанные;
				
	ПодборИзКлассификатораОбращенийГражданКлиент.ОбработчикСобытияТаблицыФормыПриАктивизацииСтроки(ЭтаФорма, Элемент);
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Темы,
		"Тематика",
		Тематика,
		ВидСравненияКомпоновкиДанных.Равно,
		,
		Истина,
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);	
		
КонецПроцедуры

#КонецОбласти

#Область Обработчики_событий_таблицы_формы_Темы

&НаКлиенте
Процедура ТемыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ТемыПриАктивизацииСтроки(Элемент)
	
	Вопрос = Неопределено;
	ПодборИзКлассификатораОбращенийГражданКлиент.ОбработчикСобытияТаблицыФормыПриАктивизацииСтроки(ЭтаФорма, Элемент);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Вопросы,
		"Тема",
		Тема,
		ВидСравненияКомпоновкиДанных.Равно,
		,
		Истина,
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	
КонецПроцедуры

#КонецОбласти

#Область Обработчики_событий_таблицы_формы_Вопросы

&НаКлиенте
Процедура ВопросыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЗавершитьВыборИзКлассификатора();
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросыПриАктивизацииСтроки(Элемент)
	
	ПодборИзКлассификатораОбращенийГражданКлиент.ОбработчикСобытияТаблицыФормыПриАктивизацииСтроки(ЭтаФорма, Элемент);
	
КонецПроцедуры

#КонецОбласти

#Область Служебные_процедуры_и_функции

&НаКлиенте
Процедура ЗавершитьВыборИзКлассификатора()
	
	Если ПодборИзКлассификатораОбращенийГражданКлиент.КлючевыеПоляЗаполнены(ЭтаФорма) Тогда
		ПараметрыЗакрытия = Новый Структура("Раздел, Тематика, Тема, Вопрос, КодВопроса");
		ЗаполнитьЗначенияСвойств(ПараметрыЗакрытия, ЭтотОбъект);	
		Закрыть(ПараметрыЗакрытия);	
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеФормы()
	
	ВыполнитьВызовСервера();
	Элементы.КодВопроса.ОбновитьТекстРедактирования();
	Элементы.КодВопроса.УстановитьГраницыВыделения(1, 1);

КонецПроцедуры

&НаСервере
Процедура ВыполнитьВызовСервера()
	
КонецПроцедуры

#КонецОбласти
