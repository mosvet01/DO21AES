///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2019, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ОбменДанными

// Заполняет настройки, влияющие на использование плана обмена.
// 
// Параметры:
//  Настройки - Структура - настройки плана обмена по умолчанию, см. ОбменДаннымиСервер.НастройкиПланаОбменаПоУмолчанию,
//                          описание возвращаемого значения функции.
//
Процедура ПриПолученииНастроек(Настройки) Экспорт
	
	Настройки.ПредупреждатьОНесоответствииВерсийПравилОбмена = Ложь;
	Настройки.Алгоритмы.ПриПолученииОписанияВариантаНастройки = Истина;
	Настройки.Алгоритмы.ОписаниеОграниченийПередачиДанных = Истина;
	Настройки.Алгоритмы.ОписаниеЗначенийПоУмолчанию = Истина;
	Настройки.Алгоритмы.НастроитьИнтерактивнуюВыгрузку = Истина;
	Настройки.Алгоритмы.ПредставлениеОтбораИнтерактивнойВыгрузки = Истина;
	
КонецПроцедуры

// Заполняет набор параметров, определяющих вариант настройки обмена.
// 
// Параметры:
//  ОписаниеВарианта       - Структура - набор варианта настройки по умолчанию,
//                                       см. ОбменДаннымиСервер.ОписаниеВариантаНастройкиОбменаПоУмолчанию,
//                                       описание возвращаемого значения.
//  ИдентификаторНастройки - Строка    - идентификатор варианта настройки обмена.
//  ПараметрыКонтекста     - Структура - см. ОбменДаннымиСервер.ПараметрыКонтекстаПолученияОписанияВариантаНастройки,
//                                       описание возвращаемого значения функции.
//
Процедура ПриПолученииОписанияВариантаНастройки(ОписаниеВарианта, ИдентификаторНастройки, ПараметрыКонтекста) Экспорт
	
	ИдентификаторНастройки = "ОбменБПДО20";
	
	ОписаниеВарианта.ИмяКонфигурацииКорреспондента = "БухгалтерияПредприятияКОРП";
	Синоним = НСтр("ru = '1С:Бухгалтерия предприятия КОРП, ред. 3'");
	ОписаниеВарианта.ИмяФайлаНастроекДляПриемника = "Настройки_обмена_ДОКОРП_БПКОРП";
	
	ОписаниеВарианта.КраткаяИнформацияПоОбмену = 
		НСтр("ru = 'Позволяет синхронизировать данные между конфигурациями ""1С:Бухгалтерия предприятия"" и ""1С:Документооборот"".
		|Синхронизация данных выполняется в двустороннем режиме на уровне нормативно-справочной информации, договоров и заявок на оплату.'");
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		ОписаниеВарианта.ПодробнаяИнформацияПоОбмену = "";
	Иначе
		ОписаниеВарианта.ПодробнаяИнформацияПоОбмену = "ПланОбмена.ОбменБухгалтерияПредприятияДокументооборот20.Форма.ПодробнаяИнформация";
	КонецЕсли;
	
	ОписаниеВарианта.ЗаголовокКомандыДляСозданияНовогоОбменаДанными = Синоним;
	ОписаниеВарианта.НаименованиеКонфигурацииКорреспондента = Синоним;
	ОписаниеВарианта.ЗаголовокУзлаПланаОбмена = Синоним;
	
	ОписаниеВарианта.ЗаголовокКомандыДляСозданияНовогоОбменаДанными = Синоним;
	ОписаниеВарианта.ЗаголовокПомощникаСозданияОбмена =
		СтрШаблон(НСтр("ru = 'Синхронизация данных с конфигурацией %1 (настройка)'"), Синоним);
		
	ОписаниеВарианта.Отборы.Вставить("ВыгружатьДоговоры");
	ОписаниеВарианта.Отборы.Вставить("Договоры", Новый Структура("ВидДокумента"));
	
	ОписаниеВарианта.Отборы.Вставить("ВыгружатьЗаявкиНаОплату");
	ОписаниеВарианта.Отборы.Вставить("ЗаявкиНаОплату", Новый Структура("ВидДокумента"));
	
	ОписаниеВарианта.Отборы.Вставить("ОграничиватьВыгрузку");
	ОписаниеВарианта.Отборы.Вставить("Стороны", Новый Структура("Сторона"));
	
	ОписаниеВарианта.ЗначенияПоУмолчанию.Вставить("ГруппаДоступаКонтрагентов");
	
КонецПроцедуры

// Возвращает строку описания ограничений миграции данных для пользователя;
// Прикладной разработчик на основе установленных отборов на узле должен сформировать строку описания ограничений 
// удобную для восприятия пользователем.
// 
// Параметры:
//  Отборы - Структура - структура отборов на узле плана обмена.
//	ВерсияКорреспондента - Строка - Номер версии корреспондента. Используется, например, для различного
//									описания ограничений передачи данных в зависимости от версии корреспондента.
//  ИдентификаторНастройки - Строка - содержит идентификатор варианта настройки.
// 
// Возвращаемое значение:
//  Строка, Неогранич. - строка описания ограничений миграции данных для пользователя
//
Функция ОписаниеОграниченийПередачиДанных(Отборы, ВерсияКорреспондента, ИдентификаторНастройки) Экспорт
	
	// Общие сведения.
	ТекстОписания = НСтр("ru = 'Синхронизация нормативно-справочной информации:'");
	ТекстОписания = ТекстОписания + Символы.ПС + НСтр("ru = '    Корреспонденты'");
	ТекстОписания = ТекстОписания + Символы.ПС + НСтр("ru = '    Контактные лица'");
	ТекстОписания = ТекстОписания + Символы.ПС + НСтр("ru = '    Банковские счета'");
	ТекстОписания = ТекстОписания + Символы.ПС + НСтр("ru = '    Стороны'");
	ТекстОписания = ТекстОписания + Символы.ПС + НСтр("ru = '    Валюты'");
	
	// Стороны.
	Если Отборы.ОграничиватьВыгрузку Тогда
		ТекстОписания = ТекстОписания + Символы.ПС + НСтр("ru = 'Выгружаются только данные по организациям и контрагентам:'");
		ВыгружаемыеСтороны = Отборы.Стороны.Сторона;
		Для Каждого ВыгружаемаяСторона Из ВыгружаемыеСтороны Цикл
			ТекстОписания = ТекстОписания + Символы.ПС + Символы.Таб + ВыгружаемаяСторона;
		КонецЦикла;
		ТекстОписания = ТекстОписания + ".";
	Иначе
		ТекстОписания = ТекстОписания + Символы.ПС + НСтр("ru = 'Данные выгружаются без отбора по организациям и контрагентам.'");
	КонецЕсли;
	
	// Договоры.
	Если Отборы.ВыгружатьДоговоры Тогда
		ТекстОписания = ТекстОписания + Символы.ПС + НСтр("ru = 'Договоры выгружаются'");
		ВидыДокументов = Отборы.Договоры.ВидДокумента;
		Если ВидыДокументов.Количество() > 0 Тогда
			ТекстОписания = ТекстОписания + " " + НСтр("ru = 'по видам'") + ": ";
			Разделитель = "";
			Для каждого ВидДокумента из ВидыДокументов Цикл
				ТекстОписания = ТекстОписания + Разделитель + ВидДокумента;
				Разделитель = ", ";
			КонецЦикла;
		КонецЕсли;
		ТекстОписания = ТекстОписания + ".";
	Иначе
		ТекстОписания = ТекстОписания + Символы.ПС + НСтр("ru = 'Договоры не выгружаются.'");
	КонецЕсли;
	
	// Заявки на оплату.
	Если Отборы.ВыгружатьЗаявкиНаОплату Тогда
		ТекстОписания = ТекстОписания + Символы.ПС + НСтр("ru = 'Заявки на оплату выгружаются'");
		ВидыДокументов = Отборы.ЗаявкиНаОплату.ВидДокумента;
		Если ВидыДокументов.Количество() > 0 Тогда
			ТекстОписания = ТекстОписания + " " + НСтр("ru = 'по видам'") + ": ";
			Разделитель = "";
			Для каждого ВидДокумента из ВидыДокументов Цикл
				ТекстОписания = ТекстОписания + Разделитель + ВидДокумента;
				Разделитель = ", ";
			КонецЦикла;
		КонецЕсли;
		ТекстОписания = ТекстОписания + ".";
	Иначе
		ТекстОписания = ТекстОписания + Символы.ПС + НСтр("ru = 'Заявки на оплату не выгружаются.'");
	КонецЕсли;
	
	Возврат ТекстОписания;
	
КонецФункции

// Возвращает строку описания значений по умолчанию для пользователя;
// Прикладной разработчик на основе установленных значений по умолчанию на узле должен сформировать строку описания 
// удобную для восприятия пользователем.
// 
// Параметры:
//  ЗначенияПоУмолчанию - Структура - структура значений по умолчанию на узле плана обмена,
//                                       полученная при помощи функции ЗначенияПоУмолчаниюНаУзле().
//	ВерсияКорреспондента - Строка - Номер версии корреспондента. Используется, например, для различного
//									описания ограничений передачи данных в зависимости от версии корреспондента.
//  ИдентификаторНастройки - Строка - содержит идентификатор варианта настройки.
// 
// Возвращаемое значение:
//  Строка, Неогранич. - строка описания для пользователя значений по умолчанию
//
Функция ОписаниеЗначенийПоУмолчанию(ЗначенияПоУмолчанию, ВерсияКорреспондента, ИдентификаторНастройки) Экспорт
	
	ТекстОписания = "";
	
	Если ПолучитьФункциональнуюОпцию("ОграничиватьДоступНаУровнеЗаписей") Тогда
		ТекстОписания = ТекстОписания + НСтр("ru = 'Группа доступа контрагентов'") + ": ";
		Если ЗначениеЗаполнено(ЗначенияПоУмолчанию.ГруппаДоступаКонтрагентов) Тогда
			ТекстОписания = ТекстОписания + ЗначенияПоУмолчанию.ГруппаДоступаКонтрагентов;
		Иначе
			ТекстОписания = ТекстОписания + НСтр("ru = 'не назначается.'");
		КонецЕсли;
		ТекстОписания = ТекстОписания + Символы.ПС;
	КонецЕсли;
	
	Возврат ТекстОписания;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Переопределяемая настройка дополнения выгрузки

// Предназначена для настройки вариантов интерактивной настройки выгрузки по сценарию узла.
// Для настройки необходимо установить значения свойств параметров в необходимые значения.
//
// Используется для контроля режимов работы помощника интерактивного обмена данными.
//
// Параметры:
//     Получатель - ПланОбменаСсылка - Узел, для которого производится настройка
//     Параметры  - Структура        - Параметры для изменения. Содержит поля:
//
//         ВариантБезДополнения - Структура     - настройки типового варианта "Не добавлять".
//                                                Содержит поля:
//             Использование - Булево - флаг разрешения использования варианта. По умолчанию Истина.
//             Порядок       - Число  - порядок размещения варианта на форме помощника, сверху вниз. По умолчанию 1.
//             Заголовок     - Строка - позволяет переопределить название типового варианта.
//             Пояснение     - Строка - позволяет переопределить текст пояснения варианта для пользователя.
//
//         ВариантВсеДокументы - Структура      - настройки типового варианта "Добавить все документы за период".
//                                                Содержит поля:
//             Использование - Булево - флаг разрешения использования варианта. По умолчанию Истина.
//             Порядок       - Число  - порядок размещения варианта на форме помощника, сверху вниз. По умолчанию 2.
//             Заголовок     - Строка - позволяет переопределить название типового варианта.
//             Пояснение     - Строка - позволяет переопределить текст пояснения варианта для пользователя.
//
//         ВариантПроизвольныйОтбор - Структура - настройки типового варианта "Добавить данные с произвольным отбором".
//                                                Содержит поля:
//             Использование - Булево - флаг разрешения использования варианта. По умолчанию Истина.
//             Порядок       - Число  - порядок размещения варианта на форме помощника, сверху вниз. По умолчанию 3.
//             Заголовок     - Строка - позволяет переопределить название типового варианта.
//             Пояснение     - Строка - позволяет переопределить текст пояснения варианта для пользователя.
//
//         ВариантДополнительно - Структура     - настройки дополнительного варианта по сценарию узла.
//                                                Содержит поля:
//             Использование            - Булево            - флаг разрешения использования варианта. По умолчанию Ложь.
//             Порядок                  - Число             - порядок размещения варианта на форме помощника, сверху вниз. По умолчанию 4.
//             Заголовок                - Строка            - название варианта для отображения на форме.
//             ИмяФормыОтбора           - Cтрока            - Имя формы, вызываемой для редактирования настроек.
//             ЗаголовокКомандыФормы    - Cтрока            - Заголовок для отрисовки на форме команды открытия формы настроек.
//             ИспользоватьПериодОтбора - Булево            - флаг того, что необходим общий отбор по периоду. По умолчанию Ложь.
//             ПериодОтбора             - СтандартныйПериод - значение периода общего отбора, предлагаемого по умолчанию.
//
//             Отбор                    - ТаблицаЗначений   - содержит строки с описанием подробных отборов по сценарию узла.
//                                                            Содержит колонки:
//                 ПолноеИмяМетаданных - Строка                - полное имя метаданных регистрируемого объекта, отбор которого описывает строка.
//                                                               Например "Документ._ДемоПоступлениеТоваров". Можно  использовать специальные 
//                                                               значения "ВсеДокументы" и "ВсеСправочники" для отбора соответственно всех 
//                                                               документов и всех справочников, регистрирующихся на узле Получатель.
//                 ВыборПериода        - Булево                - флаг того, что данная строка описывает отбор с общим периодом.
//                 Период              - СтандартныйПериод     - значение периода общего отбора для метаданных строки, предлагаемого по умолчанию.
//                 Отбор               - ОтборКомпоновкиДанных - отбор по умолчанию. Поля отбора формируются в соответствии с общим правилами
//                                                               формирования полей компоновки. Например, для указания отбора по реквизиту
//                                                               документа "Организация", необходимо использовать поле "Ссылка.Организация"
//
//
//     Если для всех вариантов флаги разрешения использования установлены в Ложь, то страница дополнения выгрузки в помощнике
//     интерактивного обмена данными будет пропущена и дополнительная регистрация объектов производится не будет. Например, инициализация вида:
//
//          Параметры.ВариантВсеДокументы.Использование      = Ложь;
//          Параметры.ВариантБезДополнения.Использование     = Ложь;
//          Параметры.ВариантПроизвольныйОтбор.Использование = Ложь;
//          Параметры.ВариантДополнительно.Использование     = Ложь;
//
//     Приведет к тому, что шаг дополнения выгрузки будет полностью пропущен.
//
Процедура НастроитьИнтерактивнуюВыгрузку(Получатель, Параметры) Экспорт
	
	Параметры.ВариантВсеДокументы.Использование      = Ложь;
	Параметры.ВариантБезДополнения.Использование     = Ложь;
	Параметры.ВариантПроизвольныйОтбор.Использование = Ложь;
	Параметры.ВариантДополнительно.Использование     = Ложь;
	
КонецПроцедуры

// Возвращает представление отбора для варианта дополнения выгрузки по сценарию узла.
// См. описание "ВариантДополнительно" в процедуре "НастроитьИнтерактивнуюВыгрузку"
//
// Параметры:
//     Получатель - ПланОбменаСсылка - Узел, для которого определяется представление отбора
//     Параметры  - Структура        - Характеристики отбора. Содержит поля:
//         ИспользоватьПериодОтбора - Булево            - флаг того, что необходимо использовать общий отбор по периоду.
//         ПериодОтбора             - СтандартныйПериод - значение периода общего отбора.
//         Отбор                    - ТаблицаЗначений   - содержит строки с описанием подробных отборов по сценарию узла.
//                                                        Содержит колонки:
//                 ПолноеИмяМетаданных - Строка                - полное имя метаданных регистрируемого объекта, отбор которого описывает строка.
//                                                               Например "Документ._ДемоПоступлениеТоваров". Могут быть использованы специальные 
//                                                               значения "ВсеДокументы" и "ВсеСправочники" для отбора соответственно всех 
//                                                               документов и всех справочников, регистрирующихся на узле Получатель.
//                 ВыборПериода        - Булево                - флаг того, что данная строка описывает отбор с общим периодом.
//                 Период              - СтандартныйПериод     - значение периода общего отбора для метаданных строки.
//                 Отбор               - ОтборКомпоновкиДанных - поля отбора. Поля отбора формируются в соответствии с общим правилами
//                                                               формирования полей компоновки. Например, для указания отбора по реквизиту
//                                                               документа "Организация", будет использовано поле "Ссылка.Организация"
//
// Возвращаемое значение: 
//     Строка - описание отбора
//
Функция ПредставлениеОтбораИнтерактивнойВыгрузки(Получатель, Параметры) Экспорт
	
	Возврат "";
	
КонецФункции

// Конец СтандартныеПодсистемы.ОбменДанными

#КонецОбласти

#КонецОбласти

#КонецЕсли