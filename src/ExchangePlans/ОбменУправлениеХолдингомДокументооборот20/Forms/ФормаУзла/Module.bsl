///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2019, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ОбменДаннымиСервер.ФормаУзлаПриСозданииНаСервере(ЭтотОбъект, Отказ);
	
	Если Не ПолучитьФункциональнуюОпцию("ДокументооборотИспользоватьОграничениеПравДоступа") Тогда
		Элементы.ГруппаСтраницыНастроек.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	КонецЕсли;
	
	// Заполним таблицу заявок всеми поддерживаемыми типами, прочитав ранее настроенные виды документов.
	ПоддерживаемыеТипы = ПланыОбмена.ОбменУправлениеХолдингомДокументооборот20.ПоддерживаемыеТипыОпераций();
	Для Каждого КлючИЗначение из ПоддерживаемыеТипы Цикл
		
		Строка = ЗаявкиНаОперации.Добавить();
		Строка.ТипОперации = КлючИЗначение.Ключ;
		Строка.ТипОперацииПредставление = КлючИЗначение.Значение;
		
		СтруктураПоиска = Новый Структура("ТипОперации", Строка.ТипОперации);
		СтрокиУзла = Объект.ЗаявкиНаОперации.НайтиСтроки(СтруктураПоиска);
		Если СтрокиУзла.Количество() <> 0 Тогда
			Строка.ВидДокумента = СтрокиУзла[0].ВидДокумента;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// По заявкам на операции сохраним все заполненные строки.
	ТекущийОбъект.ЗаявкиНаОперации.Очистить();
	Для Каждого СтрокаТаблицы из ЗаявкиНаОперации Цикл
		
		Если ЗначениеЗаполнено(СтрокаТаблицы.ВидДокумента) Тогда
			Строка = ТекущийОбъект.ЗаявкиНаОперации.Добавить();
			Строка.ТипОперации = СтрокаТаблицы.ТипОперации;
			Строка.ВидДокумента = СтрокаТаблицы.ВидДокумента;
		КонецЕсли;
		
	КонецЦикла;
	
	ТекущийОбъект.ОграничиватьВыгрузку =
		ТекущийОбъект.Стороны.Количество() <> 0;
	ТекущийОбъект.ВыгружатьДоговоры =
		ТекущийОбъект.Договоры.Количество() <> 0;
	ТекущийОбъект.ВыгружатьЗаявкиНаОперации =
		ТекущийОбъект.ЗаявкиНаОперации.Количество() <> 0;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ОбменДаннымиСервер.ФормаУзлаПриЗаписиНаСервере(ТекущийОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_УзелПланаОбмена");
	
КонецПроцедуры

#КонецОбласти