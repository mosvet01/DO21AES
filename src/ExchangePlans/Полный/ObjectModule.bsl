#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоНовый() Тогда
		ИдентификаторУзла = Новый УникальныйИдентификатор;
	КонецЕсли;
	
	Если Ссылка <> ПланыОбмена.Полный.ЭтотУзел() Тогда
		Если ОбменДаннымиСервер.НадоВыполнитьОбработчикПослеЗагрузкиДанных(ЭтотОбъект, Ссылка) Тогда
			ПослеЗагрузкиДанных();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ЭтоНовый() И Не ЗначениеЗаполнено(ИдентификаторУзла) Тогда
		ИдентификаторУзла = Новый УникальныйИдентификатор;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриПолученииДанныхОтГлавного(ЭлементДанных, ПолучениеЭлемента, ОтправкаНазад)
	
	Если ОбменДаннымиСлужебный.РежимЗагрузкиСообщенияОбменаДаннымиПередЗапуском(
		"ЗагрузкаПараметровРаботыПрограммы") Тогда
		
		ПолучениеЭлемента = ПолучениеЭлементаДанных.Игнорировать;
		Возврат;
	КонецЕсли;
	
	РаботаСБизнесПроцессами.ОбработатьПолучениеПроцессаИзУзлаРИБ(
		Ссылка, ЭлементДанных, ПолучениеЭлемента, ОтправкаНазад);
		
	РаботаСБизнесПроцессами.ОбработатьПолучениеЗадачиИзУзлаРИБ(
		Ссылка, ЭлементДанных, ПолучениеЭлемента, ОтправкаНазад);
	
КонецПроцедуры

Процедура ПриПолученииДанныхОтПодчиненного(ЭлементДанных, ПолучениеЭлемента, ОтправкаНазад)
	
	РаботаСБизнесПроцессами.ОбработатьПолучениеПроцессаИзУзлаРИБ(
		Ссылка, ЭлементДанных, ПолучениеЭлемента, ОтправкаНазад);
		
	РаботаСБизнесПроцессами.ОбработатьПолучениеЗадачиИзУзлаРИБ(
		Ссылка, ЭлементДанных, ПолучениеЭлемента, ОтправкаНазад);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПослеЗагрузкиДанных()
	
	// СтандартныеПодсистемы.ЗащитаПерсональныхДанных
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ЗащитаПерсональныхДанных") Тогда
		МодульЗащитаПерсональныхДанных = ОбщегоНазначения.ОбщийМодуль("ЗащитаПерсональныхДанных");
		МодульЗащитаПерсональныхДанных.ПослеЗагрузкиДанных(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ЗащитаПерсональныхДанных
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
