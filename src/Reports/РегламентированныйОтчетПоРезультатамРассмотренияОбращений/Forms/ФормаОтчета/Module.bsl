
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Организация = РаботаСОрганизациями.ПолучитьОрганизациюПоУмолчанию();
	ИспользоватьУчетПоОрганизациям = ПолучитьФункциональнуюОпцию("ИспользоватьУчетПоОрганизациям");
	Если Не ИспользоватьУчетПоОрганизациям Тогда
		Элементы.Организация.Видимость = Ложь;
	Иначе	
		Элементы.Организация.Видимость = Истина;
	КонецЕсли;
	
	Период.Вариант = ВариантСтандартногоПериода.ЭтотМесяц;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьНаСервере()
	
	Результат.Очистить();
	
	Макет = РеквизитФормыВЗначение("Объект").ПолучитьМакет("МакетОтчета");
	
	ТекущаяДата = ТекущаяДатаСеанса();
	
	Область = Макет.ПолучитьОбласть("Шапка");
	Область.Параметры.ОрганизацияНаименованиеПолное = 
		РаботаСОрганизациями.ПолучитьНаименованиеОрганизации(Организация);
	Область.Параметры.ДатаНачала = Формат(Период.ДатаНачала, "ДФ=dd.MM.yyyy");
	Область.Параметры.ДатаОкончания = Формат(Период.ДатаОкончания, "ДФ=dd.MM.yyyy");
	Область.Параметры.ДатаЗаполнения = Формат(ТекущаяДата, "ДФ=dd.MM.yyyy");
	
	Результат.Вывести(Область);
	
	Область = Макет.ПолучитьОбласть("ТаблицаШапка");
	Результат.Вывести(Область);
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОбращенияВопросы.Ссылка КАК Документ
	|ПОМЕСТИТЬ ВсеОбращения
	|ИЗ
	|	Справочник.ВходящиеДокументы.ВопросыОбращения КАК ОбращенияВопросы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыВходящихДокументов КАК ВидыВходящихДокументов
	|		ПО ОбращенияВопросы.Ссылка.ВидДокумента = ВидыВходящихДокументов.Ссылка
	|ГДЕ
	|	ВидыВходящихДокументов.ЯвляетсяОбращениемОтГраждан
	|	И НЕ ОбращенияВопросы.Ссылка.ПометкаУдаления
	|	И ОбращенияВопросы.Ссылка.ДатаРегистрации > ДАТАВРЕМЯ(1, 1, 1)
	|	И (ОбращенияВопросы.Ссылка.ДатаРегистрации МЕЖДУ &ДатаНачала И &ДатаОкончания
	|			ИЛИ ОбращенияВопросы.ДатаОтвета МЕЖДУ &ДатаНачала И &ДатаОкончания)
	|	%1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА Обращения.ДатаРегистрации <> ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА Обращения.ДатаРегистрации
	|		ИНАЧЕ Обращения.ДатаСоздания
	|	КОНЕЦ КАК ДатаСортировки,
	|	Обращения.Ссылка КАК ДокументСсылка,
	|	Обращения.ИсходящийНомер КАК ДокументНомер,
	|	ВЫБОР
	|		КОГДА Обращения.ИсходящийНомер = """"
	|			ТОГДА ДАТАВРЕМЯ(1, 1, 1)
	|		ИНАЧЕ Обращения.ИсходящаяДата
	|	КОНЕЦ КАК ДокументДата,
	|	Обращения.ДатаСоздания КАК ПоступлениеДата,
	|	Обращения.ДатаРегистрации КАК ПоступлениеДатаРегистрации,
	|	ОбращенияВопросы.ДатаОтвета КАК ОтветАвторуДата,
	|	Обращения.СрокИсполненияПервоначальный КАК СрокИсполненияПервоначальный,
	|	Обращения.СрокИсполнения КАК СрокИсполнения,
	|	ВЫБОР
	|		КОГДА Обращения.СрокИсполнения > ДАТАВРЕМЯ(1, 1, 1)
	|				И Обращения.СрокИсполненияПервоначальный > ДАТАВРЕМЯ(1, 1, 1)
	|				И Обращения.СрокИсполнения > Обращения.СрокИсполненияПервоначальный
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СрокИсполненияПродлен,
	|	Обращения.Отправитель КАК Заявитель,
	|	Обращения.Отправитель.Наименование КАК ЗаявительНаименование,
	|	"""" КАК АдресЭлектроннойПочты,
	|	"""" КАК ПочтовыйАдрес,
	|	ЕСТЬNULL(ОбращенияВопросы.НомерСтроки, 1) КАК НомерСтроки,
	|	ОбращенияВопросы.Вопрос КАК Вопрос,
	|	ОбращенияВопросы.КодВопроса КАК ВопросКод,
	|	ОбращенияВопросы.РезультатРассмотрения КАК РезультатРассмотрения,
	|	ОбращенияВопросы.ОрганДляПередачи.Наименование КАК НаправленоГосУчреждение,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК НаправленоДата,
	|	"""" КАК НаправленоНомер,
	|	ОрганыИУчреждения.Ссылка КАК НаправленоОрган,
	|	ОрганыИУчреждения.Представление КАК НаправленоОрганПредставление,
	|	ОрганыИУчреждения.ЭтоОрганИсполнительнойВласти КАК ЭтоОрганИсполнительнойВласти,
	|	ОрганыИУчреждения.ИдентификаторССТУ КАК НаправленоОрганССТУ,
	|	ПОДСТРОКА(ОбращенияВопросы.МнениеАвтораОРезультатах, 1, 200) КАК МнениеАвтораОРезультате,
	|	ПОДСТРОКА(ОбращенияВопросы.МнениеАвтораОМерах, 1, 200) КАК МнениеАвтораОМерах
	|ПОМЕСТИТЬ ОбращенияИВопросы
	|ИЗ
	|	Справочник.ВходящиеДокументы КАК Обращения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВсеОбращения КАК ВсеОбращения
	|		ПО Обращения.Ссылка = ВсеОбращения.Документ
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВходящиеДокументы.ВопросыОбращения КАК ОбращенияВопросы
	|		ПО Обращения.Ссылка = ОбращенияВопросы.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК ОрганыИУчреждения
	|		ПО (ОбращенияВопросы.ОрганДляПередачи = ОрганыИУчреждения.Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СвязиДокументов.СвязанныйДокумент КАК СвязанныйДокумент,
	|	СвязиДокументов.ДополнительныйОбъектСвязи КАК ДополнительныйОбъектСвязи,
	|	ОбращенияИВопросы.ДокументСсылка КАК ДокументСсылка,
	|	СвязиДокументов.ТипСвязи КАК ТипСвязи
	|ПОМЕСТИТЬ СвязанныеДокументы
	|ИЗ
	|	ОбращенияИВопросы КАК ОбращенияИВопросы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СвязиДокументов КАК СвязиДокументов
	|		ПО ОбращенияИВопросы.ДокументСсылка = СвязиДокументов.Документ
	|			И (СвязиДокументов.ДополнительныйОбъектСвязи = ОбращенияИВопросы.Вопрос)
	|ГДЕ
	|	СвязиДокументов.ТипСвязи В(&ТипыСвязи)
	|	И ТИПЗНАЧЕНИЯ(СвязиДокументов.СвязанныйДокумент) = ТИП(Справочник.ИсходящиеДокументы)
	|	И СвязиДокументов.СвязанныйДокумент.ДатаРегистрации <> ДАТАВРЕМЯ(1, 1, 1)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОбращенияИВопросы.ДатаСортировки КАК ДатаСортировки,
	|	ОбращенияИВопросы.ДокументСсылка КАК ДокументСсылка,
	|	ОбращенияИВопросы.ДокументНомер КАК ДокументНомер,
	|	ОбращенияИВопросы.ДокументДата КАК ДокументДата,
	|	ОбращенияИВопросы.ПоступлениеДата КАК ПоступлениеДата,
	|	ОбращенияИВопросы.ПоступлениеДатаРегистрации КАК ПоступлениеДатаРегистрации,
	|	ОбращенияИВопросы.ОтветАвторуДата КАК ОтветАвторуДата,
	|	ОбращенияИВопросы.Заявитель КАК Заявитель,
	|	ОбращенияИВопросы.ЗаявительНаименование КАК ЗаявительНаименование,
	|	ОбращенияИВопросы.АдресЭлектроннойПочты КАК АдресЭлектроннойПочты,
	|	ОбращенияИВопросы.ПочтовыйАдрес КАК ПочтовыйАдрес,
	|	ОбращенияИВопросы.НомерСтроки КАК НомерСтроки,
	|	ОбращенияИВопросы.Вопрос КАК Вопрос,
	|	ОбращенияИВопросы.ВопросКод КАК ВопросКод,
	|	ОбращенияИВопросы.РезультатРассмотрения КАК РезультатРассмотрения,
	|	ОбращенияИВопросы.СрокИсполненияПродлен КАК СрокИсполненияПродлен,
	|	ОбращенияИВопросы.НаправленоГосУчреждение КАК НаправленоГосУчреждение,
	|	ЕСТЬNULL(СвязанныеДокументы.СвязанныйДокумент.ДатаРегистрации, ОбращенияИВопросы.НаправленоДата) КАК НаправленоДата,
	|	ЕСТЬNULL(СвязанныеДокументы.СвязанныйДокумент.РегистрационныйНомер, ОбращенияИВопросы.НаправленоНомер) КАК НаправленоНомер,
	|	ОбращенияИВопросы.НаправленоОрган КАК НаправленоОрган,
	|	ОбращенияИВопросы.НаправленоОрганПредставление КАК НаправленоОрганПредставление,
	|	ОбращенияИВопросы.ЭтоОрганИсполнительнойВласти КАК ЭтоОрганИсполнительнойВласти,
	|	ОбращенияИВопросы.НаправленоОрганССТУ КАК НаправленоОрганССТУ,
	|	ОбращенияИВопросы.МнениеАвтораОРезультате КАК МнениеАвтораОРезультате,
	|	ОбращенияИВопросы.МнениеАвтораОМерах КАК МнениеАвтораОМерах
	|ПОМЕСТИТЬ ОбращенияИВопросыИСвязанныеДокументы
	|ИЗ
	|	ОбращенияИВопросы КАК ОбращенияИВопросы
	|		ЛЕВОЕ СОЕДИНЕНИЕ СвязанныеДокументы КАК СвязанныеДокументы
	|		ПО ОбращенияИВопросы.ДокументСсылка = СвязанныеДокументы.ДокументСсылка
	|			И ОбращенияИВопросы.Вопрос = СвязанныеДокументы.ДополнительныйОбъектСвязи
	|			И (СвязанныеДокументы.ТипСвязи = &ПеренаправлениеПоВопросу)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОбращенияОбщие.ДокументСсылка КАК ДокументСсылка,
	|	ОбращенияОбщие.ДокументНомер КАК ДокументНомер,
	|	ОбращенияОбщие.ДокументДата КАК ДокументДата,
	|	ОбращенияОбщие.ПоступлениеДата КАК ПоступлениеДата,
	|	ОбращенияОбщие.ПоступлениеДатаРегистрации КАК ПоступлениеДатаРегистрации,
	|	0 КАК ДокументНаправленоДата,
	|	0 КАК ДокументРезультатДанОтветАвтору,
	|	0 КАК ДокументРезультатОставленоБезОтвета,
	|	ОбращенияОбщие.РезультатРассмотрения КАК РезультатРассмотрения,
	|	ОбращенияОбщие.СрокИсполненияПродлен КАК СрокИсполненияПродлен,
	|	ВЫБОР
	|		КОГДА ОбращенияОбщие.ОтветАвторуДата > ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК РезультатДанОтветАвтору,
	|	ОбращенияОбщие.ОтветАвторуДата КАК РезультатДанОтветАвторуДата,
	|	ОбращенияОбщие.Заявитель КАК Заявитель,
	|	ОбращенияОбщие.ЗаявительНаименование КАК ЗаявительНаименование,
	|	ОбращенияОбщие.АдресЭлектроннойПочты КАК АдресЭлектроннойПочты,
	|	ОбращенияОбщие.ПочтовыйАдрес КАК ПочтовыйАдрес,
	|	ОбращенияОбщие.НомерСтроки КАК НомерСтроки,
	|	ОбращенияОбщие.ВопросКод КАК ВопросКод,
	|	ОбращенияОбщие.Вопрос КАК Вопрос,
	|	ОбращенияОбщие.НаправленоДата КАК НаправленоДата,
	|	ОбращенияОбщие.НаправленоНомер КАК НаправленоНомер,
	|	ВЫБОР
	|		КОГДА ОбращенияОбщие.ЭтоОрганИсполнительнойВласти = ИСТИНА
	|			ТОГДА ОбращенияОбщие.НаправленоОрган
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК НаправленоГосУчреждение,
	|	ВЫБОР
	|		КОГДА ОбращенияОбщие.ЭтоОрганИсполнительнойВласти = ИСТИНА
	|			ТОГДА NULL
	|		ИНАЧЕ ОбращенияОбщие.НаправленоОрган
	|	КОНЕЦ КАК НаправленоИнойОгран,
	|	ОбращенияОбщие.НаправленоОрганПредставление КАК НаправленоОрганПредставление,
	|	ОбращенияОбщие.ЭтоОрганИсполнительнойВласти КАК ЭтоОрганИсполнительнойВласти,
	|	ОбращенияОбщие.НаправленоОрганССТУ КАК НаправленоОрганССТУ,
	|	ВЫБОР
	|		КОГДА ОбращенияОбщие.МнениеАвтораОРезультате = """"
	|			ТОГДА NULL
	|		ИНАЧЕ ОбращенияОбщие.МнениеАвтораОРезультате
	|	КОНЕЦ КАК МнениеАвтораОРезультате,
	|	ВЫБОР
	|		КОГДА ОбращенияОбщие.МнениеАвтораОМерах = """"
	|			ТОГДА NULL
	|		ИНАЧЕ ОбращенияОбщие.МнениеАвтораОМерах
	|	КОНЕЦ КАК МнениеАвтораОМерах
	|ИЗ
	|	ОбращенияИВопросыИСвязанныеДокументы КАК ОбращенияОбщие
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОбращенияОбщие.ДатаСортировки,
	|	ОбращенияОбщие.ДокументНомер,
	|	ОбращенияОбщие.НомерСтроки
	|ИТОГИ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫБОР
	|			КОГДА ОбращенияОбщие.ДокументДата > ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ОбращенияОбщие.ДокументСсылка
	|			ИНАЧЕ NULL
	|		КОНЕЦ) КАК ДокументДата,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫБОР
	|			КОГДА ОбращенияОбщие.ПоступлениеДата > ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ОбращенияОбщие.ДокументСсылка
	|			ИНАЧЕ NULL
	|		КОНЕЦ) КАК ПоступлениеДата,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫБОР
	|			КОГДА ОбращенияОбщие.ПоступлениеДатаРегистрации > ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ОбращенияОбщие.ДокументСсылка
	|			ИНАЧЕ NULL
	|		КОНЕЦ) КАК ПоступлениеДатаРегистрации,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫБОР
	|			КОГДА ОбращенияОбщие.НаправленоДата > ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ОбращенияОбщие.ДокументСсылка
	|			ИНАЧЕ NULL
	|		КОНЕЦ) КАК ДокументНаправленоДата,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫБОР
	|			КОГДА ОбращенияОбщие.ОтветАвторуДата > ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ОбращенияОбщие.ДокументСсылка
	|			ИНАЧЕ NULL
	|		КОНЕЦ) КАК ДокументРезультатДанОтветАвтору,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫБОР
	|			КОГДА ОбращенияОбщие.РезультатРассмотрения = ЗНАЧЕНИЕ(перечисление.РезультатыРассмотренияОбращений.ОставленоБезОтвета)
	|				ТОГДА ОбращенияОбщие.ДокументСсылка
	|			ИНАЧЕ NULL
	|		КОНЕЦ) КАК ДокументРезультатОставленоБезОтвета,
	|	КОЛИЧЕСТВО(РезультатДанОтветАвтору),
	|	КОЛИЧЕСТВО(ВопросКод),
	|	КОЛИЧЕСТВО(ВЫБОР
	|			КОГДА НаправленоДата > ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА НаправленоДата
	|			ИНАЧЕ NULL
	|		КОНЕЦ) КАК НаправленоДата,
	|	КОЛИЧЕСТВО(НаправленоГосУчреждение),
	|	КОЛИЧЕСТВО(НаправленоИнойОгран),
	|	КОЛИЧЕСТВО(МнениеАвтораОРезультате),
	|	КОЛИЧЕСТВО(МнениеАвтораОМерах)
	|ПО
	|	ОБЩИЕ,
	|	ДокументСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОбращенияОбщие.РезультатРассмотрения КАК РезультатРассмотрения,
	|	СУММА(1) КАК Количество
	|ИЗ
	|	ОбращенияИВопросыИСвязанныеДокументы КАК ОбращенияОбщие
	|ГДЕ
	|	ОбращенияОбщие.РезультатРассмотрения <> ЗНАЧЕНИЕ(Перечисление.РезультатыРассмотренияОбращений.Поддержано)
	|	И ОбращенияОбщие.РезультатРассмотрения <> ЗНАЧЕНИЕ(Перечисление.РезультатыРассмотренияОбращений.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ОбращенияОбщие.РезультатРассмотрения
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Обращения.СрокИсполненияПродлен,
	|	СУММА(1)
	|ИЗ
	|	ОбращенияИВопросы КАК Обращения
	|ГДЕ
	|	Обращения.РезультатРассмотрения = ЗНАЧЕНИЕ(Перечисление.РезультатыРассмотренияОбращений.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	Обращения.СрокИсполненияПродлен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.РезультатыРассмотренияОбращений.Поддержано),
	|	СУММА(1)
	|ИЗ
	|	ОбращенияИВопросыИСвязанныеДокументы КАК ОбращенияОбщие
	|ГДЕ
	|	ОбращенияОбщие.РезультатРассмотрения В (ЗНАЧЕНИЕ(Перечисление.РезультатыРассмотренияОбращений.Поддержано), ЗНАЧЕНИЕ(Перечисление.РезультатыРассмотренияОбращений.ВТомЧислеМерыПриняты))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Файлы.ВладелецФайла КАК ВладелецФайла,
	|	Файлы.Наименование + ""."" + Файлы.ТекущаяВерсияРасширение КАК ПолноеИмя,
	|	Файлы.Ссылка КАК Ссылка,
	|	СвязанныеДокументы.ДополнительныйОбъектСвязи КАК Вопрос,
	|	СвязанныеДокументы.ДокументСсылка КАК Обращение
	|ИЗ
	|	СвязанныеДокументы КАК СвязанныеДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Файлы КАК Файлы
	|		ПО СвязанныеДокументы.СвязанныйДокумент = Файлы.ВладелецФайла
	|ГДЕ
	|	Файлы.ПометкаУдаления = ЛОЖЬ
	|
	|УПОРЯДОЧИТЬ ПО
	|	СвязанныеДокументы.ДокументСсылка,
	|	Файлы.ВладелецФайла,
	|	Файлы.Наименование");
	
	Если ИспользоватьУчетПоОрганизациям Тогда 
		Запрос.Текст = СтрШаблон(Запрос.Текст,
			"И ОбращенияВопросы.Ссылка.Организация = &Организация");
		Запрос.УстановитьПараметр("Организация", Организация);
	Иначе 
		Запрос.Текст = СтрШаблон(Запрос.Текст, "");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ДатаНачала", Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", Период.ДатаОкончания);
	
	ТипыСвязи = Новый Массив;
	ТипыСвязи.Добавить(Справочники.ТипыСвязей.ПереадресованДокументомПоВопросу);
	ТипыСвязи.Добавить(Справочники.ТипыСвязей.ОтправленОтвет);
	Запрос.УстановитьПараметр("ТипыСвязи", ТипыСвязи);
	Запрос.УстановитьПараметр("ПеренаправлениеПоВопросу", Справочники.ТипыСвязей.ПереадресованДокументомПоВопросу);
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	//Кэш имен перечислений.
	ИменаРезультатов = Новый Соответствие;
	Для Каждого Элемент Из Метаданные.Перечисления.РезультатыРассмотренияОбращений.ЗначенияПеречисления Цикл
		Если Элемент.Имя <> "НаправленоВИнойОрган" Тогда
			ИменаРезультатов.Вставить(Перечисления.РезультатыРассмотренияОбращений[Элемент.Имя], Элемент.Имя);
		КонецЕсли;
	КонецЦикла;
	
	ИменаРезультатов.Вставить(Истина,
		"НаходитсяНаРассмотренииСПродлением");
	ИменаРезультатов.Вставить(Ложь,
		"НаходитсяНаРассмотренииБезПродления");
	
	ФайлыОтветов = РезультатыЗапроса.Получить(6).Выгрузить();
	ФайлыОтветов.Индексы.Добавить("Обращение, Вопрос");
	ФайлыОтветовОтбор = Новый Структура("Обращение, Вопрос");
	КонтактнаяИнформация = Новый Соответствие;
	
	ВыбИтог = РезультатыЗапроса.Получить(4).Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыбИтог.Следующий() Цикл
		
		ВыбДокумент = ВыбИтог.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыбДокумент.Следующий() Цикл
			
			КолВопросов = 0;
			ВыбВопрос = ВыбДокумент.Выбрать();
			Пока ВыбВопрос.Следующий() Цикл
				КолВопросов = КолВопросов + 1;
				
				Файлы = Новый Массив;
				Если ВыбВопрос.РезультатРассмотрения <> Перечисления.РезультатыРассмотренияОбращений.ОставленоБезОтвета Тогда 
					ФайлыОтветовОтбор.Обращение = ВыбВопрос.ДокументСсылка;
					ФайлыОтветовОтбор.Вопрос = ВыбВопрос.Вопрос;
					ФайлыСтроки = ФайлыОтветов.НайтиСтроки(ФайлыОтветовОтбор);
					Для Каждого Строка Из ФайлыСтроки Цикл
						Файлы.Добавить(Строка.ПолноеИмя);
					КонецЦикла;
				КонецЕсли;
				
				СтрФайлы = СтрСоединить(Файлы, ";" + Символы.ПС);
			
				Область = Макет.ПолучитьОбласть("ТаблицаСтрока");
				ЗаполнитьЗначенияСвойств(Область.Параметры, ВыбВопрос);
				
				Если ВыбВопрос.ЭтоОрганИсполнительнойВласти = Истина Тогда
					Область.Параметры.НаправленоГосУчреждениеПредставление = ВыбВопрос.НаправленоОрганПредставление;
				Иначе
					Область.Параметры.НаправленоИнойОгранПредставление = ВыбВопрос.НаправленоОрганПредставление;
				КонецЕсли;
				
				Если ПустаяСтрока(ВыбВопрос.ДокументНомер) Тогда
					Область.Параметры.ДокументНомер = "-";
				КонецЕсли;
				Если СтрФайлы <> "" Тогда
					Если ЗначениеЗаполнено(Область.Параметры.РезультатДанОтветАвторуДата) Тогда
						Область.Параметры.РезультатДанОтветАвторуДата = Формат(Область.Параметры.РезультатДанОтветАвторуДата, "ДФ=dd.MM.yyyy") +
							Символы.ПС +
							СтрФайлы;
					Иначе
						Область.Параметры.РезультатДанОтветАвторуДата = СтрФайлы;
					КонецЕсли;
					Область.Параметры.РезультатФайлОтвета = ФайлыСтроки.Получить(0).Ссылка;
				КонецЕсли;
				
				//Транспонирование результатов по колонкам.
				Если ЗначениеЗаполнено(ВыбВопрос.РезультатРассмотрения) Тогда 
					ИмяРезультата = ИменаРезультатов.Получить(ВыбВопрос.РезультатРассмотрения);
				Иначе 
					ИмяРезультата = ИменаРезультатов.Получить(ВыбВопрос.СрокИсполненияПродлен);
				КонецЕсли;
				Если ИмяРезультата <> Неопределено Тогда
					Область.Параметры["Результат" + ИмяРезультата] = Истина;
					Если ИмяРезультата = "ВТомЧислеМерыПриняты" Тогда
						//Подчиненное значение.
						Область.Параметры.РезультатПоддержано = Истина;
					КонецЕсли;
				КонецЕсли;
				
				Если КолВопросов = 1 Тогда
					//Заполнение данных заявителя. Только в первой строке.
					Заявитель = "";
					Если ЗначениеЗаполнено(ВыбВопрос.ПоступлениеДатаРегистрации) Тогда
						Область.Параметры.ПоступлениеДатаРегистрации = Формат(ВыбВопрос.ПоступлениеДатаРегистрации, "ДФ=dd.MM.yyyy");
						Заявитель = Символы.ПС;
					КонецЕсли;
					Если ЗначениеЗаполнено(ВыбВопрос.Заявитель) Тогда
						
						КИЗаявителя = КонтактнаяИнформация.Получить(ВыбВопрос.Заявитель);
						Если КИЗаявителя = Неопределено Тогда 
							ДанныеОтправителя = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВыбВопрос.Заявитель, "ЮрФизЛицо, ФизЛицо");
							Если ДанныеОтправителя.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо Тогда 
								АдресЭлектроннойПочты = УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформацииОбъекта(
									ДанныеОтправителя.ФизЛицо,
									Справочники.ВидыКонтактнойИнформации.EmailФизическогоЛица,,
									ТекущаяДатаСеанса());
								ПочтовыйАдрес = УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформацииОбъекта(
									ДанныеОтправителя.ФизЛицо,
									Справочники.ВидыКонтактнойИнформации.ДомашнийАдресФизическогоЛица,,
									ТекущаяДатаСеанса());
							Иначе 
								АдресЭлектроннойПочты = УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформацииОбъекта(
									ВыбВопрос.Заявитель,
									Справочники.ВидыКонтактнойИнформации.EmailКонтрагента,,
									ТекущаяДатаСеанса());
								ПочтовыйАдрес = УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформацииОбъекта(
									ВыбВопрос.Заявитель,
									Справочники.ВидыКонтактнойИнформации.ПочтовыйАдресКонтрагента,,
									ТекущаяДатаСеанса());
							КонецЕсли;
							
							КонтактнаяИнформация.Вставить(ВыбВопрос.Заявитель, 
								Новый Структура("АдресЭлектроннойПочты, ПочтовыйАдрес", 
								АдресЭлектроннойПочты, ПочтовыйАдрес));
						Иначе 
							АдресЭлектроннойПочты = КИЗаявителя.АдресЭлектроннойПочты;
							ПочтовыйАдрес = КИЗаявителя.ПочтовыйАдрес;
						КонецЕсли;
						
						Заявитель = Заявитель +
							"Автор: " + Строка(ВыбВопрос.ЗаявительНаименование);
						Если ЗначениеЗаполнено(АдресЭлектроннойПочты)
							Или ЗначениеЗаполнено(ПочтовыйАдрес) Тогда
							Заявитель = Заявитель + "
								|Адрес для ответа:" +
								?(ЗначениеЗаполнено(ПочтовыйАдрес), "
								|почтовый: " + ПочтовыйАдрес, "") +
								?(ЗначениеЗаполнено(АдресЭлектроннойПочты), "
								|e-mail: " + АдресЭлектроннойПочты, "");
						КонецЕсли;
						Область.Параметры.ЗаявительПредставление = Заявитель;
					КонецЕсли;
				КонецЕсли;
				
				Результат.Вывести(Область);
			КонецЦикла; //Вопрос
			
			Если КолВопросов > 1 Тогда
				//Объединение строк в колонках по документу в целом.
				КолСтрок = Результат.ВысотаТаблицы;
				НомерНач = КолСтрок - КолВопросов + 1;
				Результат.Область(НомерНач, 1, КолСтрок, 2).Объединить();
				Для НК = 3 По 5 Цикл
					Результат.Область(НомерНач, НК, КолСтрок, НК).Объединить();
				КонецЦикла;
			КонецЕсли;
			
		КонецЦикла; //Документ
		
		// Итоги таблицы.
		Область = Макет.ПолучитьОбласть("ТаблицаИтог");
		ЗаполнитьЗначенияСвойств(Область.Параметры, ВыбИтог);
		
		ВыбВопрос = РезультатыЗапроса.Получить(5).Выбрать();
		Пока ВыбВопрос.Следующий() Цикл
			// Транспонирование результатов по колонкам.
			ИмяРезультата = ИменаРезультатов.Получить(ВыбВопрос.РезультатРассмотрения);
			Если ИмяРезультата <> Неопределено Тогда
				Область.Параметры["Результат" + ИмяРезультата] = ВыбВопрос.Количество;
			КонецЕсли;
		КонецЦикла;
		
		Результат.Вывести(Область);
	
	КонецЦикла;
	
	Результат.ТолькоПросмотр = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	СформироватьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьВССТУ(Команда)
	
	ПараметрыФормы = новый Структура;
	ПараметрыФормы.Вставить("ВыгружатьСразу", Истина);
	ПараметрыФормы.Вставить("ДатаНачала", Период.ДатаНачала);
	ПараметрыФормы.Вставить("ДатаОкончания", Период.ДатаОкончания);
	ПараметрыФормы.Вставить("Организация", Организация);
	
	ОткрытьФорму("Обработка.ВыгрузкаВССТУ.Форма", ПараметрыФормы)
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьКак(Команда)
	
	Если ИспользоватьУчетПоОрганизациям И ЗначениеЗаполнено(Организация) Тогда 
		ИмяФайла = СтрШаблон(
			НСтр("ru = 'Регламентированный отчет %1 за период с %2 по %3'"), 
			СокрЛП(Организация),
			Формат(Период.ДатаНачала, "ДФ=dd.MM.yyyy"),
			Формат(Период.ДатаОкончания, "ДФ=dd.MM.yyyy"));
	Иначе 
		
		ИмяФайла = СтрШаблон(
			НСтр("ru = 'Регламентированный отчет за период с %1 по %2'"), 
			Формат(Период.ДатаНачала, "ДФ=dd.MM.yyyy"),
			Формат(Период.ДатаОкончания, "ДФ=dd.MM.yyyy"));
	КонецЕсли;
	
	ИмяФайла = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(ИмяФайла, "");
	
	ДиалогФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ДиалогФайла.Фильтр = "Файл Excel (*.xls;*.xlsx)|*.xls;*.xlsx";
	ДиалогФайла.ПолноеИмяФайла = ИмяФайла;
		
	Если ДиалогФайла.Выбрать() Тогда
		
		ТипСохранения = ТипФайлаТабличногоДокумента.XLS;
		Файл = Новый Файл(ДиалогФайла.ПолноеИмяФайла);
		Если Найти(НРег(Файл.Расширение), "xlsx") Тогда
			ТипСохранения = ТипФайлаТабличногоДокумента.XLSX;
		Иначе	
			ТипСохранения = ТипФайлаТабличногоДокумента.XLS;
		КонецЕсли;	
		
		Результат.Записать(ДиалогФайла.ПолноеИмяФайла, ТипСохранения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)
	
	Если Не ЗначениеЗаполнено(Расшифровка) Тогда 
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Расшифровка) = Тип("СправочникСсылка.Файлы") Тогда
		
		СтандартнаяОбработка = Ложь;
		ДанныеФайла = РаботаСФайламиВызовСервера.ДанныеФайлаДляОткрытия(Расшифровка,,УникальныйИдентификатор,,ПредыдущийАдресФайла);
		РаботаСФайламиКлиент.Открыть(ДанныеФайла, УникальныйИдентификатор);
		
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти