#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Данные = Неопределено;
	ПараметрыЗаполнения = Неопределено;
	Параметры.Свойство("Данные", Данные);
	Параметры.Свойство("ПараметрыЗаполнения", ПараметрыЗаполнения);
	
	Объект.ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ФормаР11001;
	УведомлениеОСпецрежимахНалогообложения.НачальныеОперацииПриСозданииНаСервере(ЭтотОбъект);
	УведомлениеОСпецрежимахНалогообложения.СформироватьСпискиВыбора(ЭтотОбъект, "СпискиВыбора2020_4");
	
	Если ТипЗнч(Данные) = Тип("Структура") Тогда
		СформироватьДеревоСтраниц();
		УведомлениеОСпецрежимахНалогообложения.СформироватьСтруктуруДанныхУведомления(ЭтотОбъект, Истина);
		УведомлениеОСпецрежимахНалогообложения.ЗагрузитьДанныеПростогоУведомления(ЭтотОбъект, Данные, ПредставлениеУведомления)
	ИначеЕсли Параметры.Свойство("Ключ") И ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.Организация = Параметры.Ключ.Организация;
		ЗагрузитьДанные(Параметры.Ключ);
	ИначеЕсли Параметры.Свойство("ЗначениеКопирования") И ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		Объект.Организация = Параметры.ЗначениеКопирования.Организация;
		ЗагрузитьДанные(Параметры.ЗначениеКопирования);
	Иначе
		Параметры.Свойство("Организация", Объект.Организация);
		СформироватьДеревоСтраниц();
		
		ВходящийКонтейнер = Новый Структура("ИмяФормы, ДеревоСтраниц", ИмяФормы, РеквизитФормыВЗначение("ДеревоСтраниц"));
		РезультатКонтейнер = Новый Структура;
		УведомлениеОСпецрежимахНалогообложения.СформироватьКонтейнерДанныхУведомления(ВходящийКонтейнер, РезультатКонтейнер, Истина);
		Для Каждого КЗ Из РезультатКонтейнер Цикл
			ЭтотОбъект[КЗ.Ключ] = КЗ.Значение;
		КонецЦикла;
		
		РезультатКонтейнер.Очистить();
		
		ИнициализироватьМногострочныеЧасти(ВходящийКонтейнер, РезультатКонтейнер);
		
		Для Каждого КЗ Из РезультатКонтейнер Цикл
			ЗначениеВРеквизитФормы(КЗ.Значение, КЗ.Ключ);
		КонецЦикла;
		
		УведомлениеОСпецрежимахНалогообложения.ДополнитьСлужебнымиСтруктурамиАдреса(ДанныеУведомления, ДанныеМногостраничныхРазделов);
		
	КонецЕсли;
	
	Если Параметры.СформироватьФормуОтчетаАвтоматически Тогда
		ЗаполнитьАвтоНаСервере(ПараметрыЗаполнения);
	КонецЕсли;
	
	Если Параметры.СформироватьПечатнуюФорму Тогда
		Модифицированность = Истина;
		СохранитьДанные();
		Отказ = Истина;
		Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
			РазблокироватьДанныеДляРедактирования(Объект.Ссылка, УникальныйИдентификатор);
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	УведомлениеОСпецрежимахНалогообложения.ЗаполнитьТаблицуФорматов(ЭтотОбъект, "Форматы2020_4");
	ИдДляСвор = УведомлениеОСпецрежимахНалогообложения.ПолучитьИдентификаторыДляСворачивания(ЭтотОбъект);
	СворачиваемыеЭлементы = ПоместитьВоВременноеХранилище(ИдДляСвор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УведомлениеОСпецрежимахНалогообложенияКлиент.ПриОткрытии(ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	РегламентированнаяОтчетностьКлиент.ПередЗакрытиемРегламентированногоОтчета(
		ЭтотОбъект, Отказ, СтандартнаяОбработка, ЗавершениеРаботы, ТекстПредупреждения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		ПриЗакрытииНаСервере();
	КонецЕсли;
	
	Оповестить("Запись_УведомлениеОСпецрежимахНалогообложения", , Объект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_УведомлениеОСпецрежимахНалогообложения", ПараметрыЗаписи, Объект.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ГиперссылкаНаименованиеЭтапаНажатие(Элемент)
	
	ПараметрыИзменения = Новый Структура;
	ПараметрыИзменения.Вставить("Форма", ЭтотОбъект);
	ПараметрыИзменения.Вставить("Организация", Объект.Организация);
	ПараметрыИзменения.Вставить("КонтролирующийОрган",
		ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ФНС"));
	ПараметрыИзменения.Вставить("ТекстВопроса", НСтр("ru='Вы уверены, что уведомление уже сдано?'"));
	
	РегламентированнаяОтчетностьКлиент.ИзменитьСтатусОтправки(ПараметрыИзменения);
	
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаПротоколНажатие(Элемент)
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьПротоколИзПанелиОтправки(ЭтотОбъект, "ФНС");
	
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыОтправкиНажатие(Элемент)
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьСостояниеОтправкиИзПанелиОтправки(ЭтотОбъект, "ФНС");
	
КонецПроцедуры

&НаКлиенте
Процедура КритическиеОшибкиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьКритическиеОшибкиИзПанелиОтправки(ЭтотОбъект, "ФНС");
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеУведомленияВыбор(Элемент, Область, СтандартнаяОбработка)
	
	ИмяОбласти = Область.Имя;
	
	Если СтрЧислоВхождений(ИмяОбласти, "ДобавитьСтроку") > 0 Тогда
		ДобавитьСтроку(Неопределено);
		СтандартнаяОбработка = Ложь;
		Модифицированность = Истина;
		Возврат;
	ИначеЕсли СтрЧислоВхождений(ИмяОбласти, "УдалитьСтроку") > 0 Тогда
		УдалитьСтроку(Неопределено);
		СтандартнаяОбработка = Ложь;
		Модифицированность = Истина;
		Возврат;
	ИначеЕсли СтрЧислоВхождений(ИмяОбласти, "ДобавитьСтраницу") > 0 Тогда
		ДобавитьСтраницу(Неопределено);
		СтандартнаяОбработка = Ложь;
		Возврат;
	ИначеЕсли СтрЧислоВхождений(ИмяОбласти, "УдалитьСтраницу") > 0 Тогда
		УведомлениеОСпецрежимахНалогообложенияКлиент.УдалитьСтраницу(ЭтотОбъект);
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
	
	Если РучнойВвод Тогда
		Возврат;
	КонецЕсли;
	
	УведомлениеОСпецрежимахНалогообложенияКлиент.ПредставлениеУведомленияВыбор(ЭтотОбъект, Область, СтандартнаяОбработка, Истина);
	
	Если СтрНайти(ИмяОбласти, "П0103") = 1 ИЛИ СтрНайти(ИмяОбласти, "П0104") = 1 Тогда
		ОбработкаАдреса(Область, СтандартнаяОбработка);
		Возврат;
	КонецЕсли;
	
	Если СтандартнаяОбработка Тогда
		Если ЭтоОбластьОКСМ(Область) Тогда
			СтандартнаяОбработка = Ложь;
			ДополнительныеПараметры = Новый Структура("Область, СтандартнаяОбработка, Элемент",
				Область, СтандартнаяОбработка, Элемент);
			ОписаниеОповещения = Новый ОписаниеОповещения("ОткрытьФормуВыбораСтраныЗавершение",
				ЭтотОбъект, ДополнительныеПараметры);
			ОткрытьФорму("Справочник.СтраныМира.ФормаВыбора",
				Новый Структура("РежимВыбора", Истина), ЭтотОбъект, , , ,
				ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеУведомленияПриИзмененииСодержимогоОбласти(Элемент, Область)
	
	УведомлениеОСпецрежимахНалогообложенияКлиент.ПриИзмененииСодержимогоОбласти(ЭтотОбъект, Область, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеУведомленияПриАктивизации(Элемент)
	
	Область = Элемент.ТекущаяОбласть;
	ИмяОбласти = Область.Имя;
	
	Элементы.ПредставлениеУведомленияКонтекстноеМенюОчиститьКодОКСМ.Доступность = ЭтоОбластьОКСМ(Область);
	НастроитьКонтекстноеМенюПредставленияУведомления(ИмяОбласти);
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоСтраниц

&НаКлиенте
Процедура ДеревоСтраницПриАктивизацииСтроки(Элемент)
	
	Если УИДТекущаяСтраница <> Элемент.ТекущиеДанные.УИД Тогда
		УИДТекущаяСтраница = Элемент.ТекущиеДанные.УИД;
		ТекущееИДНаименования = Элемент.ТекущиеДанные.ИДНаименования;
		Если Не ЗначениеЗаполнено(ТекущееИДНаименования) Тогда
			ТекущееИДНаименования = Элемент.ТекущиеДанные.ПолучитьЭлементы()[0].ИДНаименования;
			УИДТекущаяСтраница = Элемент.ТекущиеДанные.ПолучитьЭлементы()[0].УИД;
		КонецЕсли;
		
		Если Элемент.ТекущиеДанные.Многостраничность Тогда
			ИмяМакета = ПолучитьИмяВыводимогоМакета(Элемент.ТекущиеДанные);
			ПоказатьТекущуюМногостраничнуюСтраницу(ИмяМакета);
			
		Иначе
			ПоказатьТекущуюСтраницу(Элемент.ТекущиеДанные.ИмяМакета);
			
			Если Элемент.ТекущиеДанные.Многострочность Тогда
				Для Каждого СтрокаМСЧ Из Элемент.ТекущиеДанные.МногострочныеЧасти Цикл
					ИмяМСЧ = СтрокаМСЧ.Значение;
					ВывестиМногострочнуюЧасть(ИмяМСЧ);
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СформироватьXML(Команда)
	
	ВыгружаемыеДанные = СформироватьXMLНаСервере(УникальныйИдентификатор);
	Если ВыгружаемыеДанные <> Неопределено Тогда
		РегламентированнаяОтчетностьКлиент.ВыгрузитьФайлы(ВыгружаемыеДанные);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСтраницу(Команда)
	
	ДобавитьСтраницуНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	СохранитьДанные();
	Оповестить("Запись_УведомлениеОСпецрежимахНалогообложения",,Объект.Ссылка);
	Закрыть(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОтправку(Команда)
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОбновитьОтправкуИзПанелиОтправки(ЭтотОбъект, "ФНС");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьВКонтролирующийОрган(Команда)
	
	РегламентированнаяОтчетностьКлиент.ПриНажатииНаКнопкуОтправкиВКонтролирующийОрган(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьНеотправленноеИзвещение(Команда)
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОтправитьНеотправленноеИзвещениеИзПанелиОтправки(ЭтотОбъект, "ФНС");
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьКодОКСМ(Команда)
	
	ПредставлениеУведомления.ТекущаяОбласть.Значение = "";
	УведомлениеОСпецрежимахНалогообложенияКлиент.ПриИзмененииСодержимогоОбласти(ЭтотОбъект, ПредставлениеУведомления.ТекущаяОбласть, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Очистить(Команда)
	
	УведомлениеОСпецрежимахНалогообложенияКлиент.ОчиститьУведомление(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьБРО(Команда)
	
	ЕстьВыходЗаГраницы = Ложь;
	ПечатьБРОНаСервере(ЕстьВыходЗаГраницы);
	РегламентированнаяОтчетностьКлиент.ОткрытьФормуПредварительногоПросмотра(
		ЭтотОбъект, , Ложь, СтруктураРеквизитовУведомления.СписокПечатаемыхЛистов,
			Новый Структура("ЕстьВыходЗаГраницы", ЕстьВыходЗаГраницы));
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСДвухмернымШтрихкодомPDF417(Команда)
	
	Если ДоступнаПечатьPDF417(ИмяФормы) Тогда
		РегламентированнаяОтчетностьКлиент.ВывестиМашиночитаемуюФормуУведомленияОСпецрежимах(ЭтотОбъект);
	Иначе
		ТекстПредупреждения = НСтр("ru = 'Печать формы со штрихкодом PDF417 будет реализована в будущих версиях программы.'");
		ПоказатьПредупреждение( , ТекстПредупреждения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредварительныйПросмотр(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ТекстВопроса = "Перед печатью необходимо сохранить изменения. Сохранить изменения?";
		ОписаниеОповещения = Новый ОписаниеОповещения("ПредварительныйПросмотрЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 0);
		Возврат;
	ИначеЕсли Модифицированность Тогда
		СохранитьДанные();
	КонецЕсли;
	
	МассивПечати = Новый Массив;
	МассивПечати.Добавить(Объект.Ссылка);
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
		"Документ.УведомлениеОСпецрежимахНалогообложения",
		"Уведомление", МассивПечати, Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВИнтернете(Команда)
	
	РегламентированнаяОтчетностьКлиент.ПроверитьВИнтернете(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВыгрузку(Команда)
	
	ПроверитьВыгрузкуНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура РучнойВвод(Команда)
	
	РучнойВвод = Не РучнойВвод;
	Элементы.ФормаРучнойВвод.Пометка = РучнойВвод;
	
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	
	СохранитьДанные();
	Оповестить("Запись_УведомлениеОСпецрежимахНалогообложения", , Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьСтраницу() Экспорт
	
	УдалитьСтраницуНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСтроку(Команда)
	
	Область = Элементы.ПредставлениеУведомления.ТекущаяОбласть;
	ИмяОбласти = Область.Имя;
	
	Если СтрНайти(ИмяОбласти, "ДобавитьСтроку") = 1 Тогда
		ИмяОбластиДобавленияСтроки = ИмяОбласти;
		ИдМСЧ = СтрЗаменить(ИмяОбласти, "ДобавитьСтроку", "");
		ИдМСЧ = СтрЗаменить(ИдМСЧ, "Значок", "");
	Иначе
		ПолноеИмяМСЧ = ОпределитьПринадлежностьОбластиКМногострочномуРазделу(ИмяОбласти);
		ИдМСЧ = ИдМСЧ(ПолноеИмяМСЧ);
		ИмяОбластиДобавленияСтроки = "ДобавитьСтроку" + ИдМСЧ;
	КонецЕсли;
	
	ПроверкаМаксимумаСтрок = ПроверитьДостижениеМаксимальногоКоличестваСтрок(ИдМСЧ);
	
	Если ПроверкаМаксимумаСтрок.ДостигнутМаксимум Тогда
		ПоказатьПредупреждение( , ПроверкаМаксимумаСтрок.ТекстПредупреждения);
	Иначе
		ДобавитьСтрокуНаСервере(ИмяОбластиДобавленияСтроки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьСтроку(Команда)
	
	Область = Элементы.ПредставлениеУведомления.ТекущаяОбласть;
	ИмяОбласти = Область.Имя;
	
	Если СтрНайти(ИмяОбласти, "УдалитьСтроку") = 1 Тогда
		ИмяОбластиУдаленияСтроки = ИмяОбласти;
	Иначе
		ПолноеИмяМСЧ = ОпределитьПринадлежностьОбластиКМногострочномуРазделу(ИмяОбласти);
		ИдМСЧ = ИдМСЧ(ПолноеИмяМСЧ);
		Постфикс = Сред(ИмяОбласти, СтрНайти(ИмяОбласти, "_"));
		ИмяОбластиУдаленияСтроки = "УдалитьСтроку" + ИдМСЧ + Постфикс;
	КонецЕсли;
	
	Если НЕ ПредставлениеУведомления.Области[ИмяОбластиУдаленияСтроки].Гиперссылка Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура("ИмяОбластиУдаленияСтроки", ИмяОбластиУдаленияСтроки);
	ОписаниеОповещения = Новый ОписаниеОповещения("УдалитьСтрокуЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ТекстВопроса = НСтр("ru='Удалить выбранную строку?'");
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПоказатьТекущуюСтраницу(ИмяМакета)
	
	ПредставлениеУведомления.Очистить();
	Макет = Отчеты[Объект.ИмяОтчета].ПолучитьМакет(ИмяМакета);
	СкрытьСлужебныеОбласти(Макет);
	
	ПредставлениеУведомления.Вывести(Макет);
	УведомлениеОСпецрежимахНалогообложения.УстановитьФорматыВПолях(ЭтотОбъект);
	
	СтрДанных = ДанныеУведомления[ТекущееИДНаименования];
	
	Для Каждого Обл Из ПредставлениеУведомления.Области Цикл
		Если Обл.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник
		   И Обл.СодержитЗначение Тогда
			СтрДанных.Свойство(Обл.Имя, Обл.Значение);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СкрытьСлужебныеОбласти(Макет)
	
	Если ОбщегоНазначения.РежимОтладки() Тогда
		Возврат;
	КонецЕсли;
	
	СкрываемаяОбласть = Макет.Области.Найти("СкрытьПриОткрытии1");
	Если СкрываемаяОбласть <> Неопределено Тогда
		СкрываемаяОбласть.Видимость = Ложь;
	КонецЕсли;
	
	СкрываемаяОбласть = Макет.Области.Найти("СкрытьПриОткрытии2");
	Если СкрываемаяОбласть <> Неопределено Тогда
		СкрываемаяОбласть.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#Область УправлениеЭлементамиФормы

&НаКлиенте
Процедура НастроитьКонтекстноеМенюПредставленияУведомления(ИмяОбласти)
	
	ЭтоПоказательМСЧ = СтрНайти(ИмяОбласти, "П010201") = 1
	               ИЛИ СтрНайти(ИмяОбласти, "П010407") = 1
	               ИЛИ СтрНайти(ИмяОбласти, "Ж010201") = 1
	               ИЛИ СтрНайти(ИмяОбласти, "З010101") = 1;
	
	ЭтоЕдинственнаяСтрока = Истина;
	Если ЭтоПоказательМСЧ Тогда
		ИмяБезПостфикса = Лев(ИмяОбласти, СтрНайти(ИмяОбласти, "_") - 1);
		ИмяПоказателяВторойСтроки = ИмяБезПостфикса + "_2";
		ЭтоЕдинственнаяСтрока = (ПредставлениеУведомления.Области.Найти(ИмяПоказателяВторойСтроки) = Неопределено);
	КонецЕсли;
	
	Элементы.ПредставлениеУведомленияКонтекстноеМенюДобавитьСтроку.Доступность = ЭтоПоказательМСЧ;
	Элементы.ПредставлениеУведомленияКонтекстноеМенюУдалитьСтроку.Доступность =
		ЭтоПоказательМСЧ И НЕ ЭтоЕдинственнаяСтрока;
	
КонецПроцедуры

#КонецОбласти

#Область Многостраничность

&НаСервере
Процедура ПоказатьТекущуюМногостраничнуюСтраницу(ИмяМакета)
	
	ПредставлениеУведомления.Очистить();
	ПредставлениеУведомления.Вывести(Отчеты[Объект.ИмяОтчета].ПолучитьМакет(ИмяМакета));
	УведомлениеОСпецрежимахНалогообложения.УстановитьФорматыВПолях(ЭтотОбъект);
	
	СтрДанных = Неопределено;
	Для Каждого Элт Из ДанныеМногостраничныхРазделов[ТекущееИДНаименования] Цикл
		Если Элт.Значение.УИД = УИДТекущаяСтраница Тогда
			СтрДанных = Элт.Значение;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Обл Из ПредставлениеУведомления.Области Цикл
		Если Обл.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник
		   И Обл.СодержитЗначение Тогда
			СтрДанных.Свойство(Обл.Имя, Обл.Значение);
		КонецЕсли;
	КонецЦикла;
	
	НайденнаяСтрока = НайтиСтрокуВДеревеПоУИД(ДеревоСтраниц.ПолучитьЭлементы(), УИДТекущаяСтраница);
	Если НайденнаяСтрока <> Неопределено
	   И НайденнаяСтрока.ПолучитьРодителя().ПолучитьЭлементы().Количество() = 1 Тогда
		ПредставлениеУведомления.Области.УдалитьСтраницуЗначок.Текст = "";
		ПредставлениеУведомления.Области.УдалитьСтраницу.Текст = "";
		ПредставлениеУведомления.Области.УдалитьСтраницуЗначок.Гиперссылка = Ложь;
		ПредставлениеУведомления.Области.УдалитьСтраницу.Гиперссылка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьИмяВыводимогоМакета(ТекущиеДанные)
	
	Если ЗначениеЗаполнено(ТекущиеДанные.ИмяМакета) Тогда
		Возврат ТекущиеДанные.ИмяМакета;
	Иначе
		Возврат ТекущиеДанные.ПолучитьЭлементы()[0].ИмяМакета;
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция НайтиСтрокуВДеревеПоУИД(Дерево, УИД)
	
	Для Каждого Элемент Из Дерево Цикл
		Если Элемент.УИД = УИД И НЕ ПустаяСтрока(Элемент.ИДНаименования) Тогда
			Возврат Элемент;
		КонецЕсли;
		
		НайденныйИД = НайтиСтрокуВДеревеПоУИД(Элемент.ПолучитьЭлементы(), УИД);
		Если НайденныйИД <> Неопределено Тогда
			Возврат НайденныйИД;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

&НаСервере
Процедура ДобавитьСтраницуНаСервере()
	
	НовИд = УведомлениеОСпецрежимахНалогообложения.ДобавитьСтраницуУведомления(ЭтотОбъект);
	Если НовИд <> Неопределено Тогда
		Элементы.ДеревоСтраниц.ТекущаяСтрока = НовИд;
	КонецЕсли;
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьСтраницуНаСервере()
	
	НовИд = УведомлениеОСпецрежимахНалогообложения.УдалитьСтраницуНаСервере(ЭтотОбъект);
	Если НовИд <> Неопределено Тогда
		Элементы.ДеревоСтраниц.ТекущаяСтрока = НовИд;
	КонецЕсли;
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область Многострочность

&НаСервере
Процедура ДобавитьСтрокуНаСервере(ИмяОбласти)
	
	ИдМСЧ = СтрЗаменить(ИмяОбласти, "ДобавитьСтроку", "");
	ИдМСЧ = СтрЗаменить(ИдМСЧ, "Значок", "");
	ПолноеИмяМСЧ = "МногострочнаяЧасть" + ИдМСЧ;
	
	ЭтотОбъект[ПолноеИмяМСЧ].Добавить();
	ТаблицаМСЧ = РеквизитФормыВЗначение(ПолноеИмяМСЧ);
	НомерНовойСтроки = ТаблицаМСЧ.Количество();
	
	ИсходныйМакетРаздела = МакетОтчетаСодержащийМСЧ(ПолноеИмяМСЧ);
	ОбластьМСЧ = ИсходныйМакетРаздела.ПолучитьОбласть(ПолноеИмяМСЧ);
	Для Каждого КолонкаМСЧ Из ТаблицаМСЧ.Колонки Цикл
		ИмяПоказателя = КолонкаМСЧ.Имя;
		ОбластьПоказателя = ОбластьМСЧ.Область(ИмяПоказателя + "_1");
		ОбластьПоказателя.Имя = ИмяПоказателя + "_" + СтрокаЧГ0(НомерНовойСтроки);
	КонецЦикла;
	ОбластьУдаленияСтроки = ОбластьМСЧ.Область("УдалитьСтроку" + ИдМСЧ + "_1");
	ОбластьУдаленияСтроки.Имя = "УдалитьСтроку" + ИдМСЧ + "_" + СтрокаЧГ0(НомерНовойСтроки);
	
	ПозицияВставки = ПредставлениеУведомления.Область(ИмяОбласти).Верх;
	
	ПредставлениеУведомления.ВставитьОбласть(
		ОбластьМСЧ.Область(1, , ОбластьМСЧ.ВысотаТаблицы),
		ПредставлениеУведомления.Область(ПозицияВставки, , ПозицияВставки, ),
		ТипСмещенияТабличногоДокумента.ПоВертикали);
	
	УстановитьВидимостьУдаленияПервойСтроки(ИдМСЧ);
	
	ИмяОбластиДляАктивации = ТаблицаМСЧ.Колонки[0].Имя + "_" + СтрокаЧГ0(НомерНовойСтроки);
	Элементы.ПредставлениеУведомления.ТекущаяОбласть = ПредставлениеУведомления.Область(ИмяОбластиДляАктивации);
	
КонецПроцедуры

&НаСервере
Процедура УдалитьСтрокуНаСервере(ИмяОбласти)
	
	ИмяБезПостфикса = Лев(ИмяОбласти, СтрНайти(ИмяОбласти, "_") - 1);
	ИдМСЧ = СтрЗаменить(ИмяБезПостфикса, "УдалитьСтроку", "");
	ПолноеИмяМСЧ = "МногострочнаяЧасть" + ИдМСЧ;
	НомерСтроки = Число(Сред(ИмяОбласти, СтрНайти(ИмяОбласти, "_") + 1));
	
	МногострочнаяЧасть = ЭтотОбъект[ПолноеИмяМСЧ];
	
	Если МногострочнаяЧасть.Количество() <= 1 Тогда
		Возврат;
	КонецЕсли;
	
	МногострочнаяЧасть.Удалить(НомерСтроки - 1);
	
	Если ИдМСЧ = "З010101" Тогда
		ВысотаБлокаМСЧ = 3;
	Иначе
		// Обрабатываются только многострочные части с высотой 2 строки
		// и показателями, расположенными в одной строке.
		ВысотаБлокаМСЧ = 2;
	КонецЕсли;
	
	НомерСтрокиМакета = ПредставлениеУведомления.Область(ИмяОбласти).Верх;
	УдаляемаяОбласть = ПредставлениеУведомления.Область(НомерСтрокиМакета, , НомерСтрокиМакета + ВысотаБлокаМСЧ - 1, );
	ПредставлениеУведомления.УдалитьОбласть(УдаляемаяОбласть, ТипСмещенияТабличногоДокумента.ПоВертикали);
	
	ТаблицаМСЧ = РеквизитФормыВЗначение(ПолноеИмяМСЧ);
	
	Пока Истина Цикл
		ОбластьУдаленияСтроки = ПредставлениеУведомления.Области.Найти(
			"УдалитьСтроку" + ИдМСЧ + "_" + СтрокаЧГ0(НомерСтроки + 1));
		Если ОбластьУдаленияСтроки = Неопределено Тогда
			Прервать;
		КонецЕсли;
		
		Для Каждого КолонкаМСЧ Из ТаблицаМСЧ.Колонки Цикл
			ИмяПоказателя = КолонкаМСЧ.Имя;
			ОбластьПоказателя = ПредставлениеУведомления.Область(ИмяПоказателя + "_" + СтрокаЧГ0(НомерСтроки + 1));
			ОбластьПоказателя.Имя = ИмяПоказателя + "_" + СтрокаЧГ0(НомерСтроки);
		КонецЦикла;
		ОбластьУдаленияСтроки.Имя = "УдалитьСтроку" + ИдМСЧ + "_" + СтрокаЧГ0(НомерСтроки);
		
		НомерСтроки = НомерСтроки + 1;
		
	КонецЦикла;
	
	УстановитьВидимостьУдаленияПервойСтроки(ИдМСЧ);
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ВывестиМногострочнуюЧасть(ПолноеИмяМСЧ)
	
	ИдМСЧ = ИдМСЧ(ПолноеИмяМСЧ);
	ТаблицаМСЧ = РеквизитФормыВЗначение(ПолноеИмяМСЧ);
	ИсходныйМакетРаздела = МакетОтчетаСодержащийМСЧ(ПолноеИмяМСЧ);
	ПромежуточныйМакет = Новый ТабличныйДокумент;
	
	Инд = 1;
	Для Каждого Стр Из ЭтотОбъект[ПолноеИмяМСЧ] Цикл
		ОбластьМСЧ = ИсходныйМакетРаздела.ПолучитьОбласть(ПолноеИмяМСЧ);
		Для Каждого КолонкаМСЧ Из ТаблицаМСЧ.Колонки Цикл
			ИмяПоказателя = КолонкаМСЧ.Имя;
			ОбластьПоказателя = ОбластьМСЧ.Область(ИмяПоказателя + "_1");
			ОбластьПоказателя.Имя = ИмяПоказателя + "_" + СтрокаЧГ0(Инд);
			ОбластьПоказателя.Значение = Стр[ИмяПоказателя];
		КонецЦикла;
		
		ОбластьУдаленияСтроки = ОбластьМСЧ.Область("УдалитьСтроку" + ИдМСЧ + "_1");
		ОбластьУдаленияСтроки.Имя = "УдалитьСтроку" + ИдМСЧ + "_" + СтрокаЧГ0(Инд);
		
		ПромежуточныйМакет.Вывести(ОбластьМСЧ);
		Инд = Инд + 1;
		
	КонецЦикла;
	
	ИсходнаяОбластьМСЧ = ПредставлениеУведомления.Область(ПолноеИмяМСЧ);
	ПозицияВставки = ИсходнаяОбластьМСЧ.Верх;
	
	ПредставлениеУведомления.УдалитьОбласть(ИсходнаяОбластьМСЧ, ТипСмещенияТабличногоДокумента.ПоВертикали);
	
	ПредставлениеУведомления.ВставитьОбласть(
		ПромежуточныйМакет.Область(1, , ПромежуточныйМакет.ВысотаТаблицы),
		ПредставлениеУведомления.Область(ПозицияВставки, , ПозицияВставки, ),
		ТипСмещенияТабличногоДокумента.ПоВертикали);
	
	УстановитьВидимостьУдаленияПервойСтроки(ИдМСЧ);
	
КонецПроцедуры

&НаСервере
Функция МакетОтчетаСодержащийМСЧ(ПолноеИмяМСЧ)
	
	Если ПолноеИмяМСЧ = "МногострочнаяЧастьП010201" Тогда
		Возврат Отчеты[Объект.ИмяОтчета].ПолучитьМакет("Форма2020_4_Страница1");
		
	ИначеЕсли ПолноеИмяМСЧ = "МногострочнаяЧастьП010407" Тогда
		Возврат Отчеты[Объект.ИмяОтчета].ПолучитьМакет("Форма2020_4_Страница1");
		
	ИначеЕсли ПолноеИмяМСЧ = "МногострочнаяЧастьЖ010201" Тогда
		Возврат Отчеты[Объект.ИмяОтчета].ПолучитьМакет("Форма2020_4_ЛистЖ");
		
	ИначеЕсли ПолноеИмяМСЧ = "МногострочнаяЧастьЗ010101" Тогда
		Макет = Отчеты[Объект.ИмяОтчета].ПолучитьМакет("Форма2020_4_ЛистЗ");
		Строка_2000 = Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(2000));
		Макет.Области.З01010100_1.ТипЗначения = Строка_2000;
		
		Возврат Макет;
		
	Иначе
		ВызватьИсключение НСтр("ru = 'Функция МакетОтчетаСодержащийМСЧ():
			|вызов функции с непредусмотренным значением параметра'");
		
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ИдМСЧ(ПолноеИмяМСЧ)
	
	Если ПолноеИмяМСЧ = "МногострочнаяЧастьП010201" Тогда
		Возврат "П010201";
		
	ИначеЕсли ПолноеИмяМСЧ = "МногострочнаяЧастьП010407" Тогда
		Возврат "П010407";
		
	ИначеЕсли ПолноеИмяМСЧ = "МногострочнаяЧастьЖ010201" Тогда
		Возврат "Ж010201";
		
	ИначеЕсли ПолноеИмяМСЧ = "МногострочнаяЧастьЗ010101" Тогда
		Возврат "З010101";
		
	Иначе
		ВызватьИсключение НСтр("ru = 'Функция ИдМСЧ():
			|вызов функции с непредусмотренным значением параметра'");
		
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Функция ОпределитьПринадлежностьОбластиКМногострочномуРазделу(ИмяОбласти) Экспорт
	
	ИмяБезПостфикса = Лев(ИмяОбласти, СтрНайти(ИмяОбласти, "_") - 1);
	
	Если ИмяБезПостфикса = "П01020100" Тогда
		Возврат "МногострочнаяЧастьП010201";
		
	ИначеЕсли ИмяБезПостфикса = "П01040701" ИЛИ ИмяБезПостфикса = "П01040702" Тогда
		Возврат "МногострочнаяЧастьП010407";
		
	ИначеЕсли ИмяБезПостфикса = "Ж01020100" Тогда
		Возврат "МногострочнаяЧастьЖ010201";
		
	ИначеЕсли ИмяБезПостфикса = "З01010100" Тогда
		Возврат "МногострочнаяЧастьЗ010101";
		
	Иначе
		ВызватьИсключение НСтр("ru = 'Функция ОпределитьПринадлежностьОбластиКМногострочномуРазделу():
			|вызов функции с непредусмотренным значением параметра'");
		
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура УстановитьВидимостьУдаленияПервойСтроки(ИдМСЧ)
	
	ТаблицаМСЧ = ЭтотОбъект["МногострочнаяЧасть" + ИдМСЧ];
	УдалениеВозможно = (ТаблицаМСЧ.Количество() >= 2);
	
	ОбластьУдаленияСтроки = ПредставлениеУведомления.Область("УдалитьСтроку" + ИдМСЧ + "_1");
	ОбластьУдаленияСтроки.Гиперссылка = УдалениеВозможно;
	ОбластьУдаленияСтроки.Текст = ?(УдалениеВозможно, "х", "");
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьДостижениеМаксимальногоКоличестваСтрок(ИдМСЧ)
	
	Результат = Новый Структура("ДостигнутМаксимум, ТекстПредупреждения", Ложь, "");
	
	Если ИдМСЧ = "П010201" Тогда
		ИмяПредельногоПоказателя = "П01020100_18";
		Если ПредставлениеУведомления.Области.Найти(ИмяПредельногоПоказателя) <> Неопределено Тогда
			Результат.ДостигнутМаксимум = Истина;
			Результат.ТекстПредупреждения = НСтр("ru = 'Допускается не более 18 строк с указанием кодов языков народов РФ или иностранных языков'");
		КонецЕсли;
		
	ИначеЕсли ИдМСЧ = "П010407" Тогда
		ИмяПредельногоПоказателя = "П01040701_3";
		Если ПредставлениеУведомления.Области.Найти(ИмяПредельногоПоказателя) <> Неопределено Тогда
			Результат.ДостигнутМаксимум = Истина;
			Результат.ТекстПредупреждения = НСтр("ru = 'Допускается не более 3 строк с указанием здания/сооружения'");
		КонецЕсли;
		
	ИначеЕсли ИдМСЧ = "З010101" Тогда
		ИмяПредельногоПоказателя = "З01010100_3";
		Если ПредставлениеУведомления.Области.Найти(ИмяПредельногоПоказателя) <> Неопределено Тогда
			Результат.ДостигнутМаксимум = Истина;
			Результат.ТекстПредупреждения = НСтр("ru = 'Допускается не более 3 строк с указанием причин ограничения доступа к сведениям'");
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура УдалитьСтрокуЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	УдалитьСтрокуНаСервере(ДополнительныеПараметры.ИмяОбластиУдаленияСтроки);
	
КонецПроцедуры

#КонецОбласти

#Область ВводАдресаФИАС

&НаКлиенте
Процедура ОбработкаАдреса(Область, СтандартнаяОбработка) Экспорт
	
	ИмяОбласти = Область.Имя;
	
	Если СтрНайти(ИмяОбласти, "П0103") = 1 Тогда
		ОбработкаМестаНахождения(Область, СтандартнаяОбработка);
		
	ИначеЕсли СтрНайти(ИмяОбласти, "П0104") = 1 Тогда
		ОбработкаАдресаЮЛ(Область, СтандартнаяОбработка);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаАдресаЮЛ(Область, СтандартнаяОбработка)
	
	// Эмуляция механизма многострочных частей из налоговой отчетности.
	СтруктураРеквизитовФормы_СтруктураМногострочныхЧастей = Новый Структура;
	ИменаГраф = Новый Массив;
	ИменаГраф.Добавить("П01040701");
	ИменаГраф.Добавить("П01040702");
	ИменаПодчиненныхГрупп = Новый Массив;
	СтруктураРеквизитовФормы_СтруктураМногострочныхЧастей.Вставить("П010407",
		Новый Структура("ИменаГраф, ИсхКолСтрок, УровеньПодчинения, ИменаПодчиненныхГрупп",
			ИменаГраф, 1, 0, ИменаПодчиненныхГрупп));
	
	СтандартнаяОбработка = Ложь;
	
	СоответствиеПоказателейСтраницыРеквизитамАдреса = СоответствиеПоказателейСтраницыРеквизитамАдреса(ИмяФормы, "Адрес");
	
	ПараметрыОпределенияАдреса = Новый Структура;
	ПараметрыОпределенияАдреса.Вставить("СтруктураМногострочныхЧастей",
		СтруктураРеквизитовФормы_СтруктураМногострочныхЧастей);
	ПараметрыОпределенияАдреса.Вставить("СоответствиеПоказателейСтраницыРеквизитамАдреса",
		СоответствиеПоказателейСтраницыРеквизитамАдреса);
	
	ПоляАдресаВJSON = "";
	Для Каждого Элем Из СоответствиеПоказателейСтраницыРеквизитамАдреса Цикл
		Если Элем.Значение = "СтрокаАдресногоОбъекта" Тогда
			ПоляАдресаВJSON = ПредставлениеУведомления.Области[Элем.Ключ].Значение;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Заголовок", "Ввод адреса");
	ПараметрыФормы.Вставить("ЗначенияПолей", ПоляАдресаВJSON);
	ПараметрыФормы.Вставить("ВидКонтактнойИнформации",
		ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.ФактАдресОрганизации"));
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("НазначениеАдреса","Адрес");
	Оповещение = Новый ОписаниеОповещения("ОткрытьФормуКонтактнойИнформацииЗавершение",
		ЭтотОбъект, ДополнительныеПараметры);
	
	ОбщегоНазначенияКлиент.ОбщийМодуль("УправлениеКонтактнойИнформациейКлиент").ОткрытьФормуКонтактнойИнформации(
		ПараметрыФормы, , Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаМестаНахождения(Область, СтандартнаяОбработка)
	
	// Эмуляция механизма многострочных частей из налоговой отчетности.
	СтруктураРеквизитовФормы_СтруктураМногострочныхЧастей = Новый Структура;
	
	СтандартнаяОбработка = Ложь;
	
	СоответствиеПоказателейСтраницыРеквизитамАдреса = СоответствиеПоказателейСтраницыРеквизитамАдреса(ИмяФормы, "МестоНахождения");
	
	ПараметрыОпределенияАдреса = Новый Структура;
	ПараметрыОпределенияАдреса.Вставить("СтруктураМногострочныхЧастей",
		СтруктураРеквизитовФормы_СтруктураМногострочныхЧастей);
	ПараметрыОпределенияАдреса.Вставить("СоответствиеПоказателейСтраницыРеквизитамАдреса",
		СоответствиеПоказателейСтраницыРеквизитамАдреса);
	
	ПоляАдресаВJSON = "";
	Для Каждого Элем Из СоответствиеПоказателейСтраницыРеквизитамАдреса Цикл
		Если Элем.Значение = "СтрокаАдресногоОбъекта" Тогда
			ПоляАдресаВJSON = ПредставлениеУведомления.Области[Элем.Ключ].Значение;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Заголовок", "Ввод адреса");
	ПараметрыФормы.Вставить("ЗначенияПолей", ПоляАдресаВJSON);
	ПараметрыФормы.Вставить("ВидКонтактнойИнформации",
		ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.ФактАдресОрганизации"));
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("НазначениеАдреса", "МестоНахождения");
	Оповещение = Новый ОписаниеОповещения("ОткрытьФормуКонтактнойИнформацииЗавершение",
		ЭтотОбъект, ДополнительныеПараметры);
	
	ОбщегоНазначенияКлиент.ОбщийМодуль("УправлениеКонтактнойИнформациейКлиент").ОткрытьФормуКонтактнойИнформации(
		ПараметрыФормы, , Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуКонтактнойИнформацииЗавершение(Результат, Параметры) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьАдресВМестеХранения(Результат, Параметры);
	
	Элемент= Элементы.ДеревоСтраниц;
	
	ПоказатьТекущуюСтраницу(Элемент.ТекущиеДанные.ИмяМакета);
	Если Элемент.ТекущиеДанные.Многострочность Тогда
		Для Каждого СтрокаМСЧ Из Элемент.ТекущиеДанные.МногострочныеЧасти Цикл
			ИмяМСЧ = СтрокаМСЧ.Значение;
			ВывестиМногострочнуюЧасть(ИмяМСЧ);
		КонецЦикла;
	КонецЕсли;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьАдресВМестеХранения(Результат, ПараметрыАдреса)
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("Адрес", Результат.Значение);
	ПараметрыЗаполнения.Вставить("СоответствиеПоказателейСтраницыРеквизитамАдреса",
		СоответствиеПоказателейСтраницыРеквизитамАдреса(ИмяФормы, ПараметрыАдреса.НазначениеАдреса));
	ПараметрыЗаполнения.Вставить("ВерсияАдреса", 2);
	
	ВиртуальноеДеревоРазделов = Новый ДеревоЗначений;
	ВиртуальноеДеревоРазделов.Колонки.Добавить("Данные");
	ВиртуальноеДеревоРазделов.Колонки.Добавить("ДанныеМногострочныхЧастей");
	
	ВиртуальнаяВеткаРаздела = ВиртуальноеДеревоРазделов.Строки.Добавить();
	ВиртуальнаяВеткаРаздела.Данные = ДанныеУведомления.Лист001;
	
	Если ПараметрыАдреса.НазначениеАдреса = "МестоНахождения" Тогда
		ЗаполнитьАдресВФорматеФИАСНаСтраницеРеглОтчета(ВиртуальнаяВеткаРаздела, ПараметрыЗаполнения, Истина);
	Иначе
		ВиртуальнаяВеткаРаздела.ДанныеМногострочныхЧастей = Новый Структура("П010407", МногострочнаяЧастьП010407);
		ЗаполнитьАдресВФорматеФИАСНаСтраницеРеглОтчета(ВиртуальнаяВеткаРаздела, ПараметрыЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьАдресВФорматеФИАСНаСтраницеРеглОтчета(СтраницаРаздела, ПараметрыЗаполнения, ЭтоМестоНахождения = Ложь)
	
	СоответствиеПоказателейСтраницыРеквизитамАдреса = ПараметрыЗаполнения.СоответствиеПоказателейСтраницыРеквизитамАдреса;
	
	ДополнительныеПараметры = Неопределено;
	Если ПараметрыЗаполнения.Свойство("ВерсияАдреса") Тогда
		ДополнительныеПараметры = Новый Структура("ВерсияАдреса", ПараметрыЗаполнения.ВерсияАдреса);
	КонецЕсли;
	
	СтруктураАдреса = УведомлениеОСпецрежимахНалогообложения.АдресВФорматеФИАС(ПараметрыЗаполнения.Адрес, ДополнительныеПараметры);
	
	Если ЭтоМестоНахождения Тогда
		КодыГородовФедеральногоЗначения = СтрРазделить("77,78,92,99", ",", Ложь);
		Если КодыГородовФедеральногоЗначения.Найти(СтруктураАдреса.КодРегиона) <> Неопределено Тогда
			СтруктураАдреса.МуниципальныйРайонВид = "";
			СтруктураАдреса.МуниципальныйРайонСокращение = "";
			СтруктураАдреса.МуниципальныйРайон = "";
			СтруктураАдреса.ПоселениеВид = "";
			СтруктураАдреса.ПоселениеСокращение = "";
			СтруктураАдреса.Поселение = "";
			СтруктураАдреса.НаселенныйПунктВид = "";
			СтруктураАдреса.НаселенныйПунктСокращение = "";
			СтруктураАдреса.НаселенныйПункт = "";
		КонецЕсли;
	КонецЕсли;
	
	ДанныеСтраницы = СтраницаРаздела.Данные;
	
	Для Каждого Элем Из СоответствиеПоказателейСтраницыРеквизитамАдреса Цикл
		Если ДанныеСтраницы.Свойство(Элем.Ключ) Тогда
			ДанныеСтраницы.Вставить(Элем.Ключ, СтруктураАдреса[Элем.Значение]);
			
		ИначеЕсли СтраницаРаздела.ДанныеМногострочныхЧастей.Свойство(Элем.Ключ)
			И ТипЗнч(Элем.Значение) = Тип("Массив") И Элем.Значение.Количество() >= 2
			И ТипЗнч(Элем.Значение[0]) = Тип("Строка") И ЗначениеЗаполнено(Элем.Значение[0])
			И СтруктураАдреса.Свойство(Элем.Значение[0]) И ТипЗнч(СтруктураАдреса[Элем.Значение[0]]) = Тип("Массив")
			И ТипЗнч(Элем.Значение[1]) = Тип("Структура") Тогда
			
			ДанныеМСЧ = СтраницаРаздела.ДанныеМногострочныхЧастей[Элем.Ключ];
			ДанныеМСЧ.Очистить();
			
			СоответствиеМногострочнойЧастиМножественномуРеквизитуАдреса = Элем.Значение[1];
			
			МножественныйРеквизитАдреса = СтруктураАдреса[Элем.Значение[0]];
			
			Для Каждого РеквизитАдреса Из МножественныйРеквизитАдреса Цикл
				СтрокаМногострочнойЧасти = ДанныеМСЧ.Добавить();
				Для Каждого ЭлемСоответствия Из СоответствиеМногострочнойЧастиМножественномуРеквизитуАдреса Цикл
					СтрокаМногострочнойЧасти[ЭлемСоответствия.Ключ] = РеквизитАдреса[ЭлемСоответствия.Значение];
				КонецЦикла;
			КонецЦикла;
			
			Если ДанныеМСЧ.Количество() = 0 Тогда
				СтрокаМногострочнойЧасти = ДанныеМСЧ.Добавить();
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ИнтерфейсОбращенияКМодулюМенеджераИОбъекта

&НаСервере
Процедура ИнициализироватьМногострочныеЧасти(КонтейнерВходящий, КонтейнерРезультат) Экспорт
	
	Отчеты[ИДОтчета(ИмяФормы)].ИнициализироватьМногострочныеЧасти(
		ИДФормыОтчета(ИмяФормы), КонтейнерВходящий, КонтейнерРезультат);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СоответствиеПоказателейСтраницыРеквизитамАдреса(ПолноеИмяФормы, НазначениеАдреса)
	
	Возврат Отчеты[ИДОтчета(ПолноеИмяФормы)].СоответствиеПоказателейСтраницыРеквизитамАдреса(
		ИДФормыОтчета(ПолноеИмяФормы), НазначениеАдреса);
	
КонецФункции

&НаСервереБезКонтекста
Функция ДоступнаПечатьPDF417(ПолноеИмяФормы)
	
	Возврат Отчеты[ИДОтчета(ПолноеИмяФормы)].ДоступнаПечатьPDF417(
		ИДФормыОтчета(ПолноеИмяФормы));
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИДОтчета(Знач ПолноеИмяФормы)
	
	Если СтрЧислоВхождений(ПолноеИмяФормы, "ВнешнийОтчет.") > 0 Тогда
		ПолноеИмяФормы = СтрЗаменить(ПолноеИмяФормы, "ВнешнийОтчет.", "");
	ИначеЕсли СтрЧислоВхождений(ПолноеИмяФормы, "Отчет.") > 0 Тогда
		ПолноеИмяФормы = СтрЗаменить(ПолноеИмяФормы, "Отчет.", "");
	КонецЕсли;
	ПолноеИмяФормы = Лев(ПолноеИмяФормы, СтрНайти(ПолноеИмяФормы, ".Форма.") - 1);
	
	Возврат ПолноеИмяФормы;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИДФормыОтчета(ПолноеИмяФормы)
	
	ИДФормыОтчета = Сред(ПолноеИмяФормы, СтрНайти(ПолноеИмяФормы, ".Форма.") + 7);
	
	Возврат ИДФормыОтчета;
	
КонецФункции

#КонецОбласти

&НаСервере
Процедура ОчисткаОтчета() Экспорт
	
	СформироватьДеревоСтраниц();
	
	ВходящийКонтейнер = Новый Структура("ИмяФормы, ДеревоСтраниц", ИмяФормы, РеквизитФормыВЗначение("ДеревоСтраниц"));
	РезультатКонтейнер = Новый Структура;
	УведомлениеОСпецрежимахНалогообложения.СформироватьКонтейнерДанныхУведомления(ВходящийКонтейнер, РезультатКонтейнер, Истина);
	Для Каждого КЗ Из РезультатКонтейнер Цикл
		ЭтотОбъект[КЗ.Ключ] = КЗ.Значение;
	КонецЦикла;
	
	РезультатКонтейнер.Очистить();
	ИнициализироватьМногострочныеЧасти(ВходящийКонтейнер, РезультатКонтейнер);
	Для Каждого КЗ Из РезультатКонтейнер Цикл
		ЗначениеВРеквизитФормы(КЗ.Значение, КЗ.Ключ);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьАвтоНаСервере(ПараметрыЗаполнения = Неопределено)
	
	ПараметрыОтчета = Новый Структура();
	ПараметрыОтчета.Вставить("Организация", Объект.Организация);
	ПараметрыОтчета.Вставить("УникальныйИдентификаторФормы", УникальныйИдентификатор);
	ПараметрыОтчета.Вставить("ПараметрыЗаполнения", ПараметрыЗаполнения);
	
	ИДОтчета = ИДОтчета(ИмяФормы);
	ИДФормыОтчета = ИДФормыОтчета(ИмяФормы);
	Контейнер = НовыйКонтейнерДляАвтозаполнения();
	РегламентированнаяОтчетностьПереопределяемый.ЗаполнитьОтчет(ИДОтчета, ИДФормыОтчета, ПараметрыОтчета, Контейнер);
	ЗагрузитьПодготовленныеДанные(Контейнер);
	
КонецПроцедуры

&НаСервере
Функция НовыйКонтейнерДляАвтозаполнения()
	
	Контейнер = Новый Структура;
	Для Каждого КЗ Из ДанныеУведомления Цикл
		Контейнер.Вставить(КЗ.Ключ, ОбщегоНазначения.СкопироватьРекурсивно(КЗ.Значение));
	КонецЦикла;
	
	СтруктураДерева = Новый Соответствие;
	Для Каждого КЗ Из ДанныеМногостраничныхРазделов Цикл
		Для Каждого Стр Из КЗ.Значение Цикл
			СтруктураДерева[Стр.Значение.УИД] = Новый Структура("Раздел, Данные", КЗ.Ключ, ОбщегоНазначения.СкопироватьРекурсивно(Стр.Значение));
		КонецЦикла;
	КонецЦикла;
	
	Для Каждого КЗ Из СтруктураДерева Цикл
		Если КЗ.Значение.Данные.Свойство("УИДРодителя") Тогда
			Родитель = СтруктураДерева[КЗ.Значение.Данные.УИДРодителя];
			Если Не Родитель.Данные.Свойство(КЗ.Значение.Раздел) Тогда
				Родитель.Данные.Вставить(КЗ.Значение.Раздел, Новый СписокЗначений);
			КонецЕсли;
			Родитель.Данные[КЗ.Значение.Раздел].Добавить(КЗ.Значение.Данные);
		ИначеЕсли Не Контейнер.Свойство(КЗ.Значение.Раздел) Тогда
			Контейнер.Вставить(КЗ.Значение.Раздел, Новый СписокЗначений);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого КЗ Из СтруктураДерева Цикл
		КЗ.Значение.Данные.Удалить("УИД");
		КЗ.Значение.Данные.Удалить("УИДРодителя");
	КонецЦикла;
	
	Для Каждого КЗ Из СтруктураДерева Цикл
		Если Контейнер.Свойство(КЗ.Значение.Раздел) Тогда
			Контейнер[КЗ.Значение.Раздел].Добавить(КЗ.Значение.Данные);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Контейнер;
	
КонецФункции

&НаСервере
Процедура ЗагрузитьПодготовленныеДанные(Контейнер)
	
	КУдалению = Новый Массив;
	Для Каждого КЗ Из ДанныеМногостраничныхРазделов Цикл
		КЗ.Значение.Очистить();
	КонецЦикла;
	
	Для Каждого КЗ Из Контейнер Цикл
		Если ДанныеУведомления.Свойство(КЗ.Ключ) Тогда
			ЗаполнитьЗначенияСвойств(ДанныеУведомления[КЗ.Ключ], КЗ.Значение);
		ИначеЕсли ДанныеМногостраничныхРазделов.Свойство(КЗ.Ключ) Тогда
			Для Каждого Стр Из КЗ.Значение Цикл
				ВставляемыеДанные = ОбщегоНазначения.СкопироватьРекурсивно(Стр.Значение);
				ВставляемыеДанные.Вставить("УИД", Новый УникальныйИдентификатор);
				ДанныеМногостраничныхРазделов[КЗ.Ключ].Добавить(ВставляемыеДанные);
				ДобавитьДочерниеСтраницы(ВставляемыеДанные);
				КУдалению.Добавить(КЗ.Ключ);
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Элт Из КУдалению Цикл
		ВставляемыеДанные.Удалить(Элт);
	КонецЦикла;
	
	СформироватьДеревоСтраницПоДанным();
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьДочерниеСтраницы(ВставляемыеДанные)
	КУдалению = Новый Массив;
	
	Для Каждого КЗ Из ВставляемыеДанные Цикл
		Если ТипЗнч(КЗ.Значение) = Тип("СписокЗначений")
			И ДанныеМногостраничныхРазделов.Свойство(КЗ.Ключ) Тогда
			
			Для Каждого Стр Из КЗ.Значение Цикл
				ДочерниеВставляемыеДанные = ОбщегоНазначения.СкопироватьРекурсивно(Стр.Значение);
				ДочерниеВставляемыеДанные.Вставить("УИД", Новый УникальныйИдентификатор);
				ДочерниеВставляемыеДанные.Вставить("УИДРодителя", ВставляемыеДанные.УИД);
				ДанныеМногостраничныхРазделов[КЗ.Ключ].Добавить(ДочерниеВставляемыеДанные);
				ДобавитьДочерниеСтраницы(ДочерниеВставляемыеДанные);
				КУдалению.Добавить(КЗ.Ключ);
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Элт Из КУдалению Цикл
		ВставляемыеДанные.Удалить(Элт);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура СформироватьДеревоСтраницПоДанным()
	ДеревоСтраниц.ПолучитьЭлементы().Очистить();
	
	КорневойУровень = ДеревоСтраниц.ПолучитьЭлементы();
	
	Стр001 = КорневойУровень.Добавить();
	Стр001.Наименование = "Стр. 001";
	Стр001.ИндексКартинки = 1;
	Стр001.ИмяМакета = "Форма2014_1_Страница1";
	Стр001.Многостраничность = Ложь;
	Стр001.Многострочность = Ложь;
	Стр001.ИДНаименования = "Лист001";
	Стр001.УИД = Новый УникальныйИдентификатор;
	Стр001.МакетыПФ = "Печать_Форма2014_1_Страница1_1;Печать_Форма2014_1_Страница1_2";
	
#Область ЛистыАБВ
	Стр001 = КорневойУровень.Добавить();
	Стр001.Наименование = "Листы А";
	Стр001.ИндексКартинки = 1;
	Стр001.Многостраничность = Истина;
	Стр001.Многострочность = Ложь;
	
	НомерСтр = 0;
	Для Каждого Стр Из ДанныеМногостраничныхРазделов["ЛистА"] Цикл
		НомерСтр = НомерСтр + 1;
		Стр001Нов = Стр001.ПолучитьЭлементы().Добавить();
		Стр001Нов.Наименование = "Стр. " + Формат(НомерСтр, "ЧГ=");
		Стр001Нов.ИндексКартинки = 1;
		Стр001Нов.ИмяМакета = "Форма2014_1_СтраницаА";
		Стр001Нов.Многостраничность = Истина;
		Стр001Нов.Многострочность = Ложь;
		Стр001Нов.УИД = Стр.Значение.УИД;
		Стр001Нов.ИДНаименования = "ЛистА";
	КонецЦикла;
	
	Стр001 = КорневойУровень.Добавить();
	Стр001.Наименование = "Листы Б";
	Стр001.ИндексКартинки = 1;
	Стр001.Многостраничность = Истина;
	Стр001.Многострочность = Ложь;
	
	НомерСтр = 0;
	Для Каждого Стр Из ДанныеМногостраничныхРазделов["ЛистБ"] Цикл
		НомерСтр = НомерСтр + 1;
		Стр001Нов = Стр001.ПолучитьЭлементы().Добавить();
		Стр001Нов.Наименование = "Стр. " + Формат(НомерСтр, "ЧГ=");
		Стр001Нов.ИндексКартинки = 1;
		Стр001Нов.ИмяМакета = "Форма2014_1_СтраницаБ";
		Стр001Нов.Многостраничность = Истина;
		Стр001Нов.Многострочность = Ложь;
		Стр001Нов.УИД = Стр.Значение.УИД;
		Стр001Нов.ИДНаименования = "ЛистБ";
	КонецЦикла;
	
	Стр001 = КорневойУровень.Добавить();
	Стр001.Наименование = "Листы В";
	Стр001.ИндексКартинки = 1;
	Стр001.Многостраничность = Истина;
	Стр001.Многострочность = Ложь;
	
	НомерСтр = 0;
	Для Каждого Стр Из ДанныеМногостраничныхРазделов["ЛистВ"] Цикл
		НомерСтр = НомерСтр + 1;
		Стр001Нов = Стр001.ПолучитьЭлементы().Добавить();
		Стр001Нов.Наименование = "Стр. " + Формат(НомерСтр, "ЧГ=");
		Стр001Нов.ИндексКартинки = 1;
		Стр001Нов.ИмяМакета = "Форма2014_1_СтраницаВ";
		Стр001Нов.Многостраничность = Истина;
		Стр001Нов.Многострочность = Ложь;
		Стр001Нов.УИД = Стр.Значение.УИД;
		Стр001Нов.ИДНаименования = "ЛистВ";
		Стр001Нов.МакетыПФ = "Печать_Форма2014_1_СтраницаВ1;Печать_Форма2014_1_СтраницаВ2";
	КонецЦикла;
#КонецОбласти

#Область ЛистыГ
	Стр001 = КорневойУровень.Добавить();
	Стр001.Наименование = "Листы Г 1";
	Стр001.ИндексКартинки = 1;
	Стр001.Многостраничность = Истина;
	Стр001.Многострочность = Ложь;
	
	НомерСтр = 0;
	Для Каждого Стр Из ДанныеМногостраничныхРазделов["ЛистГ1"] Цикл
		НомерСтр = НомерСтр + 1;
		Стр001Нов = Стр001.ПолучитьЭлементы().Добавить();
		Стр001Нов.Наименование = "Стр. " + Формат(НомерСтр, "ЧГ=");
		Стр001Нов.ИндексКартинки = 1;
		Стр001Нов.ИмяМакета = "Форма2014_1_СтраницаГ1";
		Стр001Нов.Многостраничность = Истина;
		Стр001Нов.Многострочность = Ложь;
		Стр001Нов.УИД = Стр.Значение.УИД;
		Стр001Нов.ИДНаименования = "ЛистГ1";
		Стр001Нов.МакетыПФ = "Печать_Форма2014_1_СтраницаГ1";
		
		Стр002 = Стр001Нов.ПолучитьЭлементы().Добавить();
		Стр002.Наименование = "Листы Г 2-3";
		Стр002.ИндексКартинки = 1;
		Стр002.Многостраничность = Истина;
		Стр002.Многострочность = Ложь;
		
		НомерСтр2 = 0;
		Для Каждого СтрПодч Из ДанныеМногостраничныхРазделов["ЛистГ2_3"] Цикл
			Если СтрПодч.Значение.УИДРодителя <> Стр.Значение.УИД Тогда
				Продолжить;
			КонецЕсли;
			
			НомерСтр2 = НомерСтр2 + 1;
			Стр001Нов = Стр002.ПолучитьЭлементы().Добавить();
			Стр001Нов.Наименование = "Стр. " + Формат(НомерСтр2, "ЧГ=");
			Стр001Нов.ИндексКартинки = 1;
			Стр001Нов.ИмяМакета = "Форма2014_1_СтраницаГ2_3";
			Стр001Нов.Многостраничность = Истина;
			Стр001Нов.Многострочность = Ложь;
			Стр001Нов.УИД = СтрПодч.Значение.УИД;
			Стр001Нов.ИДНаименования = "ЛистГ2_3";
			Стр001Нов.МакетыПФ = "Печать_Форма2014_1_СтраницаГ2;Печать_Форма2014_1_СтраницаГ3";
		КонецЦикла;
	КонецЦикла;
#КонецОбласти

#Область ЛистыДЕЖЗИ
	Стр001 = КорневойУровень.Добавить();
	Стр001.Наименование = "Листы Д";
	Стр001.ИндексКартинки = 1;
	Стр001.Многостраничность = Истина;
	Стр001.Многострочность = Ложь;
	
	НомерСтр = 0;
	Для Каждого Стр Из ДанныеМногостраничныхРазделов["ЛистД"] Цикл
		НомерСтр = НомерСтр + 1;
		Стр001Нов = Стр001.ПолучитьЭлементы().Добавить();
		Стр001Нов.Наименование = "Стр. " + Формат(НомерСтр, "ЧГ=");
		Стр001Нов.ИндексКартинки = 1;
		Стр001Нов.ИмяМакета = "Форма2014_1_СтраницаД";
		Стр001Нов.Многостраничность = Истина;
		Стр001Нов.Многострочность = Ложь;
		Стр001Нов.УИД = Стр.Значение.УИД;
		Стр001Нов.ИДНаименования = "ЛистД";
	КонецЦикла;
	
	Стр001 = КорневойУровень.Добавить();
	Стр001.Наименование = "Листы Е";
	Стр001.ИндексКартинки = 1;
	Стр001.Многостраничность = Истина;
	Стр001.Многострочность = Ложь;
	
	НомерСтр = 0;
	Для Каждого Стр Из ДанныеМногостраничныхРазделов["ЛистЕ"] Цикл
		НомерСтр = НомерСтр + 1;
		Стр001Нов = Стр001.ПолучитьЭлементы().Добавить();
		Стр001Нов.Наименование = "Стр. " + Формат(НомерСтр, "ЧГ=");
		Стр001Нов.ИндексКартинки = 1;
		Стр001Нов.ИмяМакета = "Форма2014_1_СтраницаЕ";
		Стр001Нов.Многостраничность = Истина;
		Стр001Нов.Многострочность = Ложь;
		Стр001Нов.УИД = Стр.Значение.УИД;
		Стр001Нов.ИДНаименования = "ЛистЕ";
		Стр001Нов.МакетыПФ = "Печать_Форма2014_1_СтраницаЕ1;Печать_Форма2014_1_СтраницаЕ2";
	КонецЦикла;
	
	Стр001 = КорневойУровень.Добавить();
	Стр001.Наименование = "Листы Ж";
	Стр001.ИндексКартинки = 1;
	Стр001.Многостраничность = Истина;
	Стр001.Многострочность = Ложь;
	
	НомерСтр = 0;
	Для Каждого Стр Из ДанныеМногостраничныхРазделов["ЛистЖ"] Цикл
		НомерСтр = НомерСтр + 1;
		Стр001Нов = Стр001.ПолучитьЭлементы().Добавить();
		Стр001Нов.Наименование = "Стр. " + Формат(НомерСтр, "ЧГ=");
		Стр001Нов.ИндексКартинки = 1;
		Стр001Нов.ИмяМакета = "Форма2014_1_СтраницаЖ";
		Стр001Нов.Многостраничность = Истина;
		Стр001Нов.Многострочность = Ложь;
		Стр001Нов.УИД = Стр.Значение.УИД;
		Стр001Нов.ИДНаименования = "ЛистЖ";
		Стр001Нов.МакетыПФ = "Печать_Форма2014_1_СтраницаЖ1;Печать_Форма2014_1_СтраницаЖ2;Печать_Форма2014_1_СтраницаЖ3";
	КонецЦикла;
	
	Стр001 = КорневойУровень.Добавить();
	Стр001.Наименование = "Листы З";
	Стр001.ИндексКартинки = 1;
	Стр001.Многостраничность = Истина;
	Стр001.Многострочность = Ложь;
	
	НомерСтр = 0;
	Для Каждого Стр Из ДанныеМногостраничныхРазделов["ЛистЗ"] Цикл
		НомерСтр = НомерСтр + 1;
		Стр001Нов = Стр001.ПолучитьЭлементы().Добавить();
		Стр001Нов.Наименование = "Стр. " + Формат(НомерСтр, "ЧГ=");
		Стр001Нов.ИндексКартинки = 1;
		Стр001Нов.ИмяМакета = "Форма2014_1_СтраницаЗ";
		Стр001Нов.Многостраничность = Истина;
		Стр001Нов.Многострочность = Ложь;
		Стр001Нов.УИД = Стр.Значение.УИД;
		Стр001Нов.ИДНаименования = "ЛистЗ";
		Стр001Нов.МакетыПФ = "Печать_Форма2014_1_СтраницаЗ1;Печать_Форма2014_1_СтраницаЗ2";
	КонецЦикла;
	
	Стр001 = КорневойУровень.Добавить();
	Стр001.Наименование = "Лист И";
	Стр001.ИндексКартинки = 1;
	Стр001.ИмяМакета = "Форма2014_1_СтраницаИ";
	Стр001.Многостраничность = Ложь;
	Стр001.Многострочность = Истина;
	Стр001.УИД = Новый УникальныйИдентификатор;
	Стр001.ИДНаименования = "ЛистИ";
	
#КонецОбласти

#Область ЛистыКЛМН
	Стр001 = КорневойУровень.Добавить();
	Стр001.Наименование = "Лист К";
	Стр001.ИндексКартинки = 1;
	Стр001.ИмяМакета = "Форма2014_1_СтраницаК";
	Стр001.Многостраничность = Ложь;
	Стр001.Многострочность = Ложь;
	Стр001.УИД = Новый УникальныйИдентификатор;
	Стр001.ИДНаименования = "ЛистК";
	
	Стр001 = КорневойУровень.Добавить();
	Стр001.Наименование = "Листы Л";
	Стр001.ИндексКартинки = 1;
	Стр001.Многостраничность = Истина;
	Стр001.Многострочность = Ложь;
	
	НомерСтр = 0;
	Для Каждого Стр Из ДанныеМногостраничныхРазделов["ЛистЛ"] Цикл
		НомерСтр = НомерСтр + 1;
		Стр001Нов = Стр001.ПолучитьЭлементы().Добавить();
		Стр001Нов.Наименование = "Стр. " + Формат(НомерСтр, "ЧГ=");
		Стр001Нов.ИндексКартинки = 1;
		Стр001Нов.ИмяМакета = "Форма2014_1_СтраницаЛ";
		Стр001Нов.Многостраничность = Истина;
		Стр001Нов.Многострочность = Ложь;
		Стр001Нов.УИД = Стр.Значение.УИД;
		Стр001Нов.ИДНаименования = "ЛистЛ";
	КонецЦикла;
	
	Стр001 = КорневойУровень.Добавить();
	Стр001.Наименование = "Лист М";
	Стр001.ИндексКартинки = 1;
	Стр001.ИмяМакета = "Форма2014_1_СтраницаМ";
	Стр001.Многостраничность = Ложь;
	Стр001.Многострочность = Ложь;
	Стр001.УИД = Новый УникальныйИдентификатор;
	Стр001.ИДНаименования = "ЛистМ";
	
	Стр001 = КорневойУровень.Добавить();
	Стр001.Наименование = "Листы Н";
	Стр001.ИндексКартинки = 1;
	Стр001.Многостраничность = Истина;
	Стр001.Многострочность = Ложь;
	
	НомерСтр = 0;
	Для Каждого Стр Из ДанныеМногостраничныхРазделов["ЛистН"] Цикл
		НомерСтр = НомерСтр + 1;
		Стр001Нов = Стр001.ПолучитьЭлементы().Добавить();
		Стр001Нов.Наименование = "Стр. " + Формат(НомерСтр, "ЧГ=");
		Стр001Нов.ИндексКартинки = 1;
		Стр001Нов.ИмяМакета = "Форма2014_1_СтраницаН";
		Стр001Нов.Многостраничность = Истина;
		Стр001Нов.Многострочность = Ложь;
		Стр001Нов.УИД = Стр.Значение.УИД;
		Стр001Нов.ИДНаименования = "ЛистН";
		Стр001Нов.МакетыПФ = "Печать_Форма2014_1_СтраницаН1;Печать_Форма2014_1_СтраницаН2;Печать_Форма2014_1_СтраницаН3";
	КонецЦикла;
#КонецОбласти
КонецПроцедуры

&НаСервере
Процедура СформироватьДеревоСтраниц() Экспорт
	Разложение = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ИмяФормы, ".");
	ДС = Отчеты[Разложение[1]].СформироватьДеревоСтраниц(Разложение[3]);
	ЗначениеВРеквизитФормы(ДС, "ДеревоСтраниц");
КонецПроцедуры

&НаСервере
Процедура СохранитьДанные() Экспорт
	
	Если ЗначениеЗаполнено(Объект.Ссылка) И Не Модифицированность Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.Дата = ТекущаяДатаСеанса() 
	КонецЕсли;
	
	СохраняемыеСведения = Новый Структура;
	СохраняемыеСведения.Вставить("ДанныеУведомления", ДанныеУведомления);
	СохраняемыеСведения.Вставить("ДанныеМногостраничныхРазделов", ДанныеМногостраничныхРазделов);
	СохраняемыеСведения.Вставить("ДеревоСтраниц", РеквизитФормыВЗначение("ДеревоСтраниц"));
	
	СохраняемыеСведения.Вставить("МногострочныеЧасти", Новый Структура);
	СохраняемыеСведения.МногострочныеЧасти.Вставить(
		"МногострочнаяЧастьП010201", РеквизитФормыВЗначение("МногострочнаяЧастьП010201"));
	СохраняемыеСведения.МногострочныеЧасти.Вставить(
		"МногострочнаяЧастьП010407", РеквизитФормыВЗначение("МногострочнаяЧастьП010407"));
	СохраняемыеСведения.МногострочныеЧасти.Вставить(
		"МногострочнаяЧастьЖ010201", РеквизитФормыВЗначение("МногострочнаяЧастьЖ010201"));
	СохраняемыеСведения.МногострочныеЧасти.Вставить(
		"МногострочнаяЧастьЗ010101", РеквизитФормыВЗначение("МногострочнаяЧастьЗ010101"));
	
	Документ = РеквизитФормыВЗначение("Объект");
	Документ.ДанныеУведомления = Новый ХранилищеЗначения(СохраняемыеСведения);
	Документ.Записать();
	ЗначениеВДанныеФормы(Документ, Объект);
	
	РегламентированнаяОтчетность.СохранитьСтатусОтправкиУведомления(ЭтотОбъект);
	
	Модифицированность = Ложь;
	ЭтотОбъект.Заголовок = СтрЗаменить(ЭтотОбъект.Заголовок, " (создание)", "");
	
	УведомлениеОСпецрежимахНалогообложения.СохранитьНастройкиРучногоВвода(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьДанные(СсылкаНаДанные)
	
	СохраненныеСведения = СсылкаНаДанные.Ссылка.ДанныеУведомления.Получить();
	ДанныеУведомления = СохраненныеСведения.ДанныеУведомления;
	ДанныеМногостраничныхРазделов = СохраненныеСведения.ДанныеМногостраничныхРазделов;
	ЗначениеВРеквизитФормы(СохраненныеСведения.ДеревоСтраниц, "ДеревоСтраниц");
	
	Для Каждого МСЧ Из СохраненныеСведения.МногострочныеЧасти Цикл
		ПолноеИмяМСЧ = МСЧ.Ключ;
		ТаблицаМСЧ = МСЧ.Значение;
		Если ТаблицаМСЧ.Количество() = 0 Тогда
			ТаблицаМСЧ.Добавить();
		КонецЕсли;
		ЭтотОбъект[ПолноеИмяМСЧ].Загрузить(ТаблицаМСЧ);
	КонецЦикла;
	
	// В ранее сохраненных заявлениях могут отсутствовать показатели, добавившиеся в новых редакциях заявления.
	ДобавитьНедостающиеПоказатели();
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьНедостающиеПоказатели()
	
	Лист001 = ДанныеУведомления.Лист001;
	
	Если НЕ Лист001.Свойство("ПД1030100") Тогда
		Лист001.Вставить("ПД1030100", "");
	КонецЕсли;
	
	Если НЕ Лист001.Свойство("ПД1040100") Тогда
		Лист001.Вставить("ПД1040100", "");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ЭтоОбластьОКСМ(Область)
	
	ПоказателиОКСМ = "А01020400, Б01050200, Е01030800";
	
	ИмяОбласти = Область.Имя;
	
	Возврат СтрНайти(ПоказателиОКСМ, ИмяОбласти) > 0;
	
КонецФункции

&НаСервере
Процедура ПриЗакрытииНаСервере()
	СохранитьДанные();
КонецПроцедуры

&НаСервере
Функция КодЭлементаСправочника(Результат)
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Результат, "Код");
КонецФункции

&НаКлиенте
Процедура ОткрытьФормуВыбораСтраныЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат <> Неопределено Тогда
		КодЭлементаСправочника = КодЭлементаСправочника(Результат);
		Область = ДополнительныеПараметры.Область;
		Если Область.Значение <> КодЭлементаСправочника Тогда
			Область.Значение = КодЭлементаСправочника;
			Модифицированность = Истина;
		КонецЕсли;
		ПредставлениеУведомленияПриИзмененииСодержимогоОбласти(Элементы.ПредставлениеУведомления, Область);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПредварительныйПросмотрЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		СохранитьДанные();
		МассивПечати = Новый Массив;
		МассивПечати.Добавить(Объект.Ссылка);
		УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
			"Документ.УведомлениеОСпецрежимахНалогообложения",
			"Уведомление", МассивПечати, Неопределено);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция СформироватьXMLНаСервере(УникальныйИдентификатор)
	СохранитьДанные();
	Документ = РеквизитФормыВЗначение("Объект");
	Возврат Документ.ВыгрузитьДокумент(УникальныйИдентификатор);
КонецФункции

&НаСервере
Функция СформироватьВыгрузкуИПолучитьДанные() Экспорт
	Выгрузка = СформироватьXMLНаСервере(УникальныйИдентификатор);
	Если Выгрузка = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	Выгрузка = Выгрузка[0];
	Возврат Новый Структура("ТестВыгрузки,КодировкаВыгрузки,Данные,ИмяФайла", 
			Выгрузка.ТестВыгрузки, Выгрузка.КодировкаВыгрузки, 
			Отчеты[Объект.ИмяОтчета].ПолучитьМакет("TIFF_2020_4"),
			"1111501_5.03000_08.tif");
КонецФункции

&НаКлиенте
Процедура СохранитьНаКлиенте(Автосохранение = Ложь,ВыполняемоеОповещение = Неопределено) Экспорт
	
	СохранитьДанные();
	Если ВыполняемоеОповещение <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ВыполняемоеОповещение);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьВыгрузкуНаСервере()
	СохранитьДанные();
	Документ = РеквизитФормыВЗначение("Объект");
	Документ.ПроверитьДокумент(УникальныйИдентификатор);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОткрытьПрисоединенныеФайлы(Команда)
	
	РегламентированнаяОтчетностьКлиент.СохранитьУведомлениеИОткрытьФормуПрисоединенныеФайлы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПечатьБРОНаСервере(ЕстьВыходЗаГраницы)
	ЕстьВыходЗаГраницы = Ложь;
	СохранитьДанные();
	СтруктураРеквизитовУведомления = Новый Структура("СписокПечатаемыхЛистов", ОбщегоНазначения.ОбщийМодуль("Отчеты.РегламентированноеУведомлениеФормаР11001").СформироватьСписокЛистов(Объект, ЕстьВыходЗаГраницы));
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция СтрокаЧГ0(ИсходноеЧисло)
	
	Возврат Формат(ИсходноеЧисло, "ЧН=; ЧГ=");
	
КонецФункции

#КонецОбласти
