#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Выполняет поиск подходящего шаблона входящие/исходящего документа.
// Если ничего не найдено, возвращает Неопределено.
// 
// Параметры:
// 	Параметры - см. ПустыеПараметрыПодбораШаблонаДокумента.ПараметрыПодбораШаблонаДокумента
//
// Возвращаемое значение:
//  - СправочникСсылка.ШаблоныВходящихДокументов, СправочникСсылка.ШаблоныИсходящихДокументов, Неопределено
Функция ПодобратьШаблонДокумента(Параметры) Экспорт
	
	// сначала ищем по полному совпадению
	// потом поднимаемся выше
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПравилаУчетаДокументовОбменСКО.Шаблон КАК Шаблон
		|ИЗ
		|	РегистрСведений.ПравилаУчетаДокументовОбменСКО КАК ПравилаУчетаДокументовОбменСКО
		|ГДЕ
		|	ПравилаУчетаДокументовОбменСКО.Организация = &Организация
		|	И ПравилаУчетаДокументовОбменСКО.ВходящийДокумент = &ВходящийДокумент
		|	И ПравилаУчетаДокументовОбменСКО.ТипКонтролирующегоОргана = &ТипКонтролирующегоОргана
		|	И ПравилаУчетаДокументовОбменСКО.ВидЭлектронногоДокумента = &ВидЭлектронногоДокумента
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПравилаУчетаДокументовОбменСКО.Шаблон
		|ИЗ
		|	РегистрСведений.ПравилаУчетаДокументовОбменСКО КАК ПравилаУчетаДокументовОбменСКО
		|ГДЕ
		|	ПравилаУчетаДокументовОбменСКО.Организация = &Организация
		|	И ПравилаУчетаДокументовОбменСКО.ВходящийДокумент = &ВходящийДокумент
		|	И ПравилаУчетаДокументовОбменСКО.ТипКонтролирующегоОргана = &ТипКонтролирующегоОргана
		|	И ПравилаУчетаДокументовОбменСКО.ВидЭлектронногоДокумента = Неопределено
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПравилаУчетаДокументовОбменСКО.Шаблон КАК Шаблон
		|ИЗ
		|	РегистрСведений.ПравилаУчетаДокументовОбменСКО КАК ПравилаУчетаДокументовОбменСКО
		|ГДЕ
		|	ПравилаУчетаДокументовОбменСКО.Организация = &Организация
		|	И ПравилаУчетаДокументовОбменСКО.ВходящийДокумент = &ВходящийДокумент
		|	И ПравилаУчетаДокументовОбменСКО.ТипКонтролирующегоОргана = Значение(Перечисление.ТипыКонтролирующихОрганов.ПустаяСсылка)
		|	И ПравилаУчетаДокументовОбменСКО.ВидЭлектронногоДокумента = &ВидЭлектронногоДокумента
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПравилаУчетаДокументовОбменСКО.Шаблон
		|ИЗ
		|	РегистрСведений.ПравилаУчетаДокументовОбменСКО КАК ПравилаУчетаДокументовОбменСКО
		|ГДЕ
		|	ПравилаУчетаДокументовОбменСКО.Организация = &Организация
		|	И ПравилаУчетаДокументовОбменСКО.ВходящийДокумент = &ВходящийДокумент
		|	И ПравилаУчетаДокументовОбменСКО.ТипКонтролирующегоОргана = Значение(Перечисление.ТипыКонтролирующихОрганов.ПустаяСсылка)
		|	И ПравилаУчетаДокументовОбменСКО.ВидЭлектронногоДокумента = Неопределено";
	
	Запрос.УстановитьПараметр("ВходящийДокумент", Параметры.ВходящийДокумент);
	Запрос.УстановитьПараметр("ВидЭлектронногоДокумента", Параметры.ВидЭлектронногоДокумента);
	Запрос.УстановитьПараметр("Организация", Параметры.Организация);
	Запрос.УстановитьПараметр("ТипКонтролирующегоОргана", Параметры.ТипКонтролирующегоОргана);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Возврат ВыборкаДетальныеЗаписи.Шаблон;
	КонецЕсли;
	
КонецФункции

// Возвращает пустую структур параметров для поиска шаблона документа.
// 
// Возвращаемое значение:
// 	Структура - Описание:
// 	* Организация - СправочникСсылка.Организации -
// 	* ВходящийДокумент - Булево - Отметка, что документ является входящим или исходящим.
// 	* ВидЭлектронногоДокумента - ПеречислениеСсылка.ВидыДокументовПолучаемыхОтКО, ПеречислениеСсылка.ВидыДокументовОтправляемыхВКО -
// 	* ТипКонтролирующегоОргана - ПеречислениеСсылка.ТипыКонтролирующихОрганов -
Функция ПараметрыПодбораШаблонаДокумента() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	Результат.Вставить("ВходящийДокумент", Ложь);
	Результат.Вставить("ТипКонтролирующегоОргана", Перечисления.ТипыКонтролирующихОрганов.ПустаяСсылка()); 
	Результат.Вставить("ВидЭлектронногоДокумента", Перечисления.ВидыДокументовПолучаемыхОтКО.ПустаяСсылка());
	
	Возврат Результат;
КонецФункции

#КонецОбласти

#КонецЕсли
