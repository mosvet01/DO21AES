
#Область ОбработчикиСобытийФормы
 
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	УстановитьДоступностьПолей(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	УстановитьДоступностьПолей(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	УстановитьДоступностьПолей(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияОчистка(Элемент, СтандартнаяОбработка)
	УстановитьДоступностьПолей(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ВходящийДокументПриИзменении(Элемент)
	УстановитьДоступностьПолей(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ВходящийДокументОчистка(Элемент, СтандартнаяОбработка)
	УстановитьДоступностьПолей(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ТипКонтролирующегоОрганаПриИзменении(Элемент)
	УстановитьДоступностьПолей(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ТипКонтролирующегоОрганаОчистка(Элемент, СтандартнаяОбработка)
	УстановитьДоступностьПолей(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ВидЭлектронногоДокументаПриИзменении(Элемент)
	УстановитьДоступностьПолей(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ВидЭлектронногоДокументаОчистка(Элемент, СтандартнаяОбработка)
	УстановитьДоступностьПолей(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьПолей(Форма)
	
	Элементы = Форма.Элементы;
	Запись = Форма.Запись;
	
	Элементы.ВходящийДокумент.Доступность = ЗначениеЗаполнено(Запись.Организация);
	Элементы.ТипКонтролирующегоОргана.Доступность = ЗначениеЗаполнено(Запись.Организация);
	Элементы.ВидЭлектронногоДокумента.Доступность = ЗначениеЗаполнено(Запись.Организация);
	Элементы.Шаблон.Доступность = ЗначениеЗаполнено(Запись.Организация);
	
	// Проверим типы данных для выбора.
	Если Не Элементы.ВходящийДокумент.Доступность Тогда
		Запись.ВходящийДокумент = Неопределено;
	КонецЕсли;
	
	Если Элементы.ВидЭлектронногоДокумента.Доступность Тогда
		Если Запись.ВходящийДокумент = Истина
			И ТипЗнч(Запись.ВидЭлектронногоДокумента) <> Тип("ПеречислениеСсылка.ВидыДокументовПолучаемыхОтКО") Тогда
			Запись.ВидЭлектронногоДокумента =
				ПредопределенноеЗначение("Перечисление.ВидыДокументовПолучаемыхОтКО.ПустаяСсылка");
		
		ИначеЕсли Запись.ВходящийДокумент <> Истина
			И ТипЗнч(Запись.ВидЭлектронногоДокумента) <> Тип("ПеречислениеСсылка.ВидыДокументовОтправляемыхВКО") Тогда
			Запись.ВидЭлектронногоДокумента =
				ПредопределенноеЗначение("Перечисление.ВидыДокументовОтправляемыхВКО.ПустаяСсылка");
		
		КонецЕсли;
	КонецЕсли;
	
	Если Элементы.Шаблон.Доступность Тогда

		Если Запись.ВидЭлектронногоДокумента = Неопределено Тогда
			Запись.Шаблон = Неопределено;
			
		Иначе
			Если ТипЗнч(Запись.ВидЭлектронногоДокумента) = Тип("ПеречислениеСсылка.ВидыДокументовПолучаемыхОтКО")
				И ТипЗнч(Запись.Шаблон) <> Тип("СправочникСсылка.ШаблоныВходящихДокументов") Тогда
					Запись.Шаблон = ПредопределенноеЗначение("Справочник.ШаблоныВходящихДокументов.ПустаяСсылка");
					
			ИначеЕсли ТипЗнч(Запись.ВидЭлектронногоДокумента) = Тип("ПеречислениеСсылка.ВидыДокументовОтправляемыхВКО")
				И ТипЗнч(Запись.Шаблон) <> Тип("СправочникСсылка.ШаблоныИсходящихДокументов") Тогда
				Запись.Шаблон = ПредопределенноеЗначение("Справочник.ШаблоныИсходящихДокументов.ПустаяСсылка");
				
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		Запись.Шаблон = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если Не ЗначениеЗаполнено(ТекущийОбъект.ТипКонтролирующегоОргана) Тогда
		ТекущийОбъект.ТипКонтролирующегоОргана = Неопределено;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекущийОбъект.ВидЭлектронногоДокумента) Тогда
		ТекущийОбъект.ВидЭлектронногоДокумента = Неопределено;
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти


