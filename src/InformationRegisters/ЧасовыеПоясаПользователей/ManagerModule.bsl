#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает часовые пояса пользователей.
//
// Параметры:
//  МассивПользователей - Массив из СправочникСсылка.Пользователи - Пользователи.
// 
// Возвращаемое значение:
//  Соответствие - Часовые пояса пользователей.
//   * Ключ     - СправочникСсылка.Пользователи - Пользователь.
//   * Значение - Строка, Неопределено          - Часовой пояс пользователя в формате GMT{+/-}hh:mm.
//
Функция ЧасовыеПоясаПользователей(МассивПользователей) Экспорт
	
	ЧасовыеПоясаПользователей = Новый Соответствие;
	Если МассивПользователей.Количество() = 0 Тогда
		Возврат ЧасовыеПоясаПользователей;
	КонецЕсли;
	
	Для Каждого Пользователь Из МассивПользователей Цикл
		ЧасовыеПоясаПользователей[Пользователь] = Неопределено;
	КонецЦикла;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЧасовыеПоясаПользователей.Пользователь КАК Пользователь,
		|	ЧасовыеПоясаПользователей.ЧасовойПояс КАК ЧасовойПояс
		|ИЗ
		|	РегистрСведений.ЧасовыеПоясаПользователей КАК ЧасовыеПоясаПользователей
		|ГДЕ
		|	ЧасовыеПоясаПользователей.Пользователь В(&МассивПользователей)");
	
	Запрос.УстановитьПараметр("МассивПользователей", МассивПользователей);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЧасовыеПоясаПользователей[Выборка.Пользователь] = Выборка.ЧасовойПояс;
	КонецЦикла;
	
	Возврат ЧасовыеПоясаПользователей;
	
КонецФункции

// Устанавливает часовой пояс пользователя.
//
// Параметры:
//  Пользователь - СправочникСсылка.Пользователи - Пользователь.
//  ЧасовойПояс  - Строка                        - Часовой пояс пользователя в формате GMT{+/-}hh:mm.
//
Процедура УстановитьЧасовойПоясПользователя(Пользователь, ЧасовойПояс) Экспорт
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Пользователь.Установить(Пользователь);
	
	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.Пользователь = Пользователь;
	НоваяЗапись.ЧасовойПояс = ЧасовойПояс;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли