#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьСведенияОПрочтении(Объект, Прочтен, Знач Пользователь = Неопределено) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Пользователь = Неопределено Тогда
		Пользователь = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
	Запись = РегистрыСведений.СведенияОПрочтенииДокументовЭДО.СоздатьМенеджерЗаписи();
	Запись.Пользователь = Пользователь;
	Запись.Объект = Объект;
	Запись.Прочитать();
	
	Запись.Пользователь = Пользователь;
	Запись.Объект = Объект;
	Запись.Прочтен = Прочтен;
	Запись.Записать(Истина);
	
КонецПроцедуры

Процедура УбратьСведенияОПрочтении(Объект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = РегистрыСведений.СведенияОПрочтенииДокументовЭДО.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Объект.Установить(Объект);
	НаборЗаписей.Записать();
	
КонецПроцедуры

Процедура УбратьСведенияОПрочтенииВсемКромеАвтора(Объект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = РегистрыСведений.СведенияОПрочтенииДокументовЭДО.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Объект.Установить(Объект);
	НаборЗаписей.Записать();
	
	РаботаСПрочтениямиВызовСервера.УстановитьСвойствоПрочтен(Объект, Истина);
	
КонецПроцедуры

Функция ОбъектБылПрочтен(Объект, Знач Пользователь = Неопределено) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Пользователь = Неопределено Тогда
		Пользователь = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
	Прочтен = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СведенияОПрочтенииДокументовЭДО.Прочтен
		|ИЗ
		|	РегистрСведений.СведенияОПрочтенииДокументовЭДО КАК СведенияОПрочтенииДокументовЭДО
		|ГДЕ
		|	СведенияОПрочтенииДокументовЭДО.Пользователь = &Пользователь
		|	И СведенияОПрочтенииДокументовЭДО.Объект = &Объект";
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	Запрос.УстановитьПараметр("Объект", Объект);
	
	Попытка 
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Прочтен = Выборка.Прочтен;
		Иначе
			Возврат Неопределено;
		КонецЕсли;
		
		Возврат Прочтен;
		
	Исключение
		СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Произошла ошибка при получении сведений о прочтении:
			|%1'", ОбщегоНазначения.КодОсновногоЯзыка()),
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Прочтение электронного документа. Получение сведений о прочтении'",
				ОбщегоНазначения.КодОсновногоЯзыка()), 
			УровеньЖурналаРегистрации.Ошибка, , Объект, СообщениеОбОшибке);
		ВызватьИсключение;
	КонецПопытки;
	
КонецФункции

#КонецОбласти

#КонецЕсли