#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ВнешнийРесурс = Параметры.ВнешнийРесурс;
	Комментарий = Параметры.Комментарий;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВнешнийРесурсОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Не ЗначениеЗаполнено(ВнешнийРесурс) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Введите адрес внешней ссылки.'"));
	Иначе
		ПерейтиПоНавигационнойСсылке(ВнешнийРесурс);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВнешнийРесурсПриИзменении(Элемент)
	
	Если СтрНачинаетсяС(ВнешнийРесурс, "e1cib") Тогда
		ТекстПредупреждения = 
			НСтр("ru = 'Для объектов в других информационных базах следует использовать внешние ссылки.'");
	Иначе
		ТекстПредупреждения = "";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	Если Не ЗначениеЗаполнено(ВнешнийРесурс) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Введите адрес внешней ссылки.'"));
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("ВнешнийРесурс", ВнешнийРесурс);
	Результат.Вставить("Комментарий", Комментарий);
	
	Закрыть(Результат);
	
КонецПроцедуры

#КонецОбласти