#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Добавляет сотрудника в регистр.
//
// Параметры:
//  Сотрудник - СправочникСсылка.Пользователи
//
Процедура Добавить(Сотрудник) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Сотрудник.Установить(Сотрудник);
	
	Запись = НаборЗаписей.Добавить();
	Запись.Сотрудник = Сотрудник;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Удаляет запись из регистра.
//
// Параметры:
//  Сотрудник - СправочникСсылка.Пользователи
//
Процедура УдалитьЗапись(Сотрудник) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Сотрудник.Установить(Сотрудник);
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Удаляет запись из регистра.
//
// Параметры:
//  Сотрудник - СправочникСсылка.Пользователи
//
Функция СотрудникНаКонтроле(Сотрудник) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИСТИНА
		|ИЗ
		|	РегистрСведений.СотрудникиДляКонтроляСамочувствия КАК СотрудникиДляКонтроляСамочувствия
		|ГДЕ
		|	СотрудникиДляКонтроляСамочувствия.Сотрудник = &Сотрудник";
	Запрос.Параметры.Вставить("Сотрудник", Сотрудник);
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

Процедура ЗаполнитьВсехСотрудников(Параметры) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Параметры.ПрогрессВыполнения.ВсегоОбъектов = 0 Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Пользователи.Ссылка) КАК КоличествоОбъектов
			|ИЗ
			|	Справочник.Пользователи КАК Пользователи
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СотрудникиДляКонтроляСамочувствия КАК СотрудникиДляКонтроляСамочувствия
			|		ПО Пользователи.Ссылка = СотрудникиДляКонтроляСамочувствия.Сотрудник
			|ГДЕ
			|	НЕ Пользователи.ПометкаУдаления
			|	И НЕ Пользователи.Недействителен
			|	И НЕ Пользователи.Служебный
			|	И СотрудникиДляКонтроляСамочувствия.Сотрудник ЕСТЬ NULL
			|
			|УПОРЯДОЧИТЬ ПО
			|	КоличествоОбъектов УБЫВ";
		
		ВсегоОбъектов = 0;
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ВсегоОбъектов = ВсегоОбъектов + Выборка.КоличествоОбъектов;
		КонецЦикла;
		
		Параметры.ПрогрессВыполнения.ВсегоОбъектов = ВсегоОбъектов;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	Пользователи.Ссылка КАК Сотрудник
		|ИЗ
		|	Справочник.Пользователи КАК Пользователи
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СотрудникиДляКонтроляСамочувствия КАК СотрудникиДляКонтроляСамочувствия
		|		ПО Пользователи.Ссылка = СотрудникиДляКонтроляСамочувствия.Сотрудник
		|ГДЕ
		|	НЕ Пользователи.ПометкаУдаления
		|	И НЕ Пользователи.Недействителен
		|	И НЕ Пользователи.Служебный
		|	И СотрудникиДляКонтроляСамочувствия.Сотрудник ЕСТЬ NULL";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Попытка
			РегистрыСведений.СотрудникиДляКонтроляСамочувствия.Добавить(Выборка.Сотрудник);
		Исключение
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,,,
				СтрШаблон(НСтр("ru = 'Не удалось поставить пользователя %1 в контроль самочувствия:
						|%2'"),
					Выборка.Сотрудник,
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())));
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ПрогрессВыполнения.ОбработаноОбъектов = 
		Параметры.ПрогрессВыполнения.ОбработаноОбъектов + Выборка.Количество();
	
	Параметры.ОбработкаЗавершена = Выборка.Количество() = 0;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли