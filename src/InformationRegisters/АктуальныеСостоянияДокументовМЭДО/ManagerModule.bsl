// @strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Обновить актуальное состояние.
// 
// Параметры:
//  Документ - ОпределяемыйТип.ПредметМЭДО - Документ
//  Состояние - ПеречислениеСсылка.СостоянияДокументовМЭДО - Состояние, какое нужно записать.
// 
// Возвращаемое значение:
//  Булево - Произведена запись или нет.
Функция ОбновитьСостояние(Документ) Экспорт
	
	Если Не ЗначениеЗаполнено(Документ) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТекущееЗаписанноеСостояние = ПолучитьСостояние(Документ);
	
	СрезСостояния = РегистрыСведений.СостоянияДокументовМЭДО.ПолучитьСрезСостоянияДокумента(
		Документ, Неопределено, "Состояние", ТекущаяДатаСеанса(), Неопределено);
	Если Не ЗначениеЗаполнено(СрезСостояния.Состояние) Тогда
		СрезСостояния.Состояние = Перечисления.СостоянияДокументовМЭДО.ПустаяСсылка();
	КонецЕсли;
	Если ТекущееЗаписанноеСостояние = СрезСостояния.Состояние Тогда
		Возврат Ложь; // уже есть.
	КонецЕсли;
	
	Набор = СоздатьНаборЗаписей();
	Набор.Отбор.Документ.Установить(Документ);
	Запись = Набор.Добавить();
	Запись.Документ = Документ;
	Запись.Состояние = СрезСостояния.Состояние;
	Набор.Записать();
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Получить актуальное состояние документа.
// 
// Параметры:
//  Документ - ОпределяемыйТип.ПредметМЭДО - Документ
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.СостоянияДокументовМЭДО, Произвольный - Получить состояние
Функция ПолучитьСостояние(Документ)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Состояния.Состояние
		|ИЗ
		|	РегистрСведений.АктуальныеСостоянияДокументовМЭДО КАК Состояния
		|ГДЕ
		|	Состояния.Документ = &Документ");
	Запрос.УстановитьПараметр("Документ", Документ);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Состояние;
	КонецЕсли;
	
	Возврат Перечисления.СостоянияДокументовМЭДО.ПустаяСсылка();
	
КонецФункции

#КонецОбласти

#КонецЕсли
