#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ИзменитьЗапись(Пользователь, Период, НомерДобавления, Удалена = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НЗ = СоздатьНаборЗаписей();
	НЗ.Отбор.Пользователь.Установить(Пользователь);
	НЗ.Отбор.НомерДобавления.Установить(НомерДобавления);
	НЗ.Отбор.День.Установить(НачалоДня(Период));
	
	МЗ = НЗ.Добавить();
	МЗ.День = НачалоДня(Период);
	МЗ.Пользователь = Пользователь;
	МЗ.НомерДобавления = НомерДобавления;
	МЗ.Граница = ТекущаяУниверсальнаяДатаВМиллисекундах();
	МЗ.Удалена = Удалена;
	НЗ.Записать();
	
КонецПроцедуры

Функция  ЕстьИзмененияПозжеГраницыОтправленныхДанных(МобильноеПриложение, Пользователь, Период, НомерДобавления) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СведенияОМобильныхКлиентах.ГраницаСборкиДанных КАК Граница
		|ПОМЕСТИТЬ ГраницаСборкиДанных
		|ИЗ
		|	РегистрСведений.СведенияОМобильныхКлиентах КАК СведенияОМобильныхКлиентах
		|ГДЕ
		|	СведенияОМобильныхКлиентах.МобильныйКлиент = &МобильноеПриложение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	МП_ИзмененныеТрудозатраты.Граница КАК Граница
		|ИЗ
		|	РегистрСведений.МП_ИзмененныеТрудозатраты КАК МП_ИзмененныеТрудозатраты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ГраницаСборкиДанных КАК ПоследняяОтметкаСборкиДанных
		|		ПО МП_ИзмененныеТрудозатраты.Граница > ПоследняяОтметкаСборкиДанных.Граница
		|ГДЕ
		|	МП_ИзмененныеТрудозатраты.День = КОНЕЦПЕРИОДА(&Период, ДЕНЬ)
		|	И МП_ИзмененныеТрудозатраты.Пользователь = &Пользователь
		|	И МП_ИзмененныеТрудозатраты.НомерДобавления = &НомерДобавления";
	
	Запрос.УстановитьПараметр("МобильноеПриложение", МобильноеПриложение);
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	Запрос.УстановитьПараметр("НомерДобавления", НомерДобавления);
	Запрос.УстановитьПараметр("Период", Период);
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

Процедура УдалитьЗаписьОТрудозатрате(Пользователь, День, НомерДобавления) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	МП_ИзмененныеТрудозатраты.День КАК День
		|ИЗ
		|	РегистрСведений.МП_ИзмененныеТрудозатраты КАК МП_ИзмененныеТрудозатраты
		|ГДЕ
		|	МП_ИзмененныеТрудозатраты.Пользователь = &Пользователь
		|	И МП_ИзмененныеТрудозатраты.НомерДобавления = &НомерДобавления
		|	И МП_ИзмененныеТрудозатраты.День = НачалоПериода(&День, День)";
	
	Запрос.УстановитьПараметр("День", День);
	Запрос.УстановитьПараметр("НомерДобавления", НомерДобавления);
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ИзменитьЗапись(Пользователь, День, НомерДобавления, Истина);
	
КонецПроцедуры

#КонецЕсли
