// @strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Функция - Получает настройки контрагента для МЭДО.
// 
// Параметры:
//  Контрагент - ОпределяемыйТип.КонтрагентМЭДО - Контрагент, для которого надо получить настройки.
//  ДанныеОтвета - см. МЭДОСтруктурыДанных.НовыйОтвет.
// 
// Возвращаемое значение:
//  Структура,Неопределено - Поля структуры соответствуют ресурсам регистра:
//   * Контрагент - ОпределяемыйТип.КонтрагентМЭДО
//   * АдресМЭДО - Строка
//   * ВерсияФорматаМЭДО - ПеречислениеСсылка.ВерсииФорматаМЭДО
Функция ПолучитьНастройки(Контрагент, ДанныеОтвета) Экспорт
	
	Если Не ЗначениеЗаполнено(Контрагент) Тогда
		Текст = НСтр("ru = 'Не задан контрагент'");
		МЭДО.ЗаписьВЖурналСобытий(
			Перечисления.УровниСобытийМЭДО.Ошибка, Текст, Текст,
			НСтр("ru = 'При получении настроек контрагента для МЭДО не задан контрагент'"), ДанныеОтвета);
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ *
		|ИЗ
		|	РегистрСведений.НастройкиКонтрагентовМЭДО КАК Настройки
		|ГДЕ
		|	Настройки.Контрагент = &Контрагент");
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		МЭДО.ЗаписьВЖурналСобытий(
			Перечисления.УровниСобытийМЭДО.Ошибка,
			Контрагент,
			НСтр("ru = 'Ошибка в настройках контрагента'"), 
			СтрШаблон(НСтр("ru = 'Не заданы настройки контрагента %1 для МЭДО (Кнопка в карточке контрагента)'"), Контрагент),
			ДанныеОтвета);
	КонецЕсли;
	
	Возврат МЭДОСтруктурыДанных.РезультатЗапросаВСтруктуру(Результат);
	
КонецФункции

// Обновить настройки контрагента. Если они не изменились, но ничего не делать.
// 
// Параметры:
//  Контрагент - ОпределяемыйТип.КонтрагентМЭДО - Контрагент
//  Настройки - Структура - Поля структуры соответствуют ресурсам регистра, можно передавать не все поля:
//   * Контрагент - ОпределяемыйТип.КонтрагентМЭДО
//   * АдресМЭДО - Строка
//   * ВерсияФорматаМЭДО - ПеречислениеСсылка.ВерсииФорматаМЭДО
// 
// Возвращаемое значение:
//  Булево - Обновление произошло или нет.
Функция ОбновитьНастройки(Контрагент, Настройки) Экспорт
	
	Если Не ЗначениеЗаполнено(Контрагент) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ЕстьАдрес = Настройки.Свойство("АдресМЭДО") И ЗначениеЗаполнено(Настройки.АдресМЭДО);
	ЕстьВерсия = Настройки.Свойство("ВерсияФорматаМЭДО") И ЗначениеЗаполнено(Настройки.ВерсияФорматаМЭДО);
	
	Если ЕстьАдрес Или ЕстьВерсия Тогда
		Набор = СоздатьНаборЗаписей();
		Набор.Отбор.Контрагент.Установить(Контрагент);
		Набор.Прочитать();
		Записать = Ложь;
		Запись = Неопределено;
		Если Набор.Количество() = 0 Тогда
			Запись = Набор.Добавить();
		Иначе
			Запись = Набор[0];
		КонецЕсли;
		Запись.Контрагент = Контрагент;
		Если ЕстьАдрес И МЭДО.ПрисвоитьОтличающееся(Запись.АдресМЭДО, Настройки.АдресМЭДО) Тогда
			Записать = Истина;
		КонецЕсли;
		Если ЕстьВерсия И МЭДО.ПрисвоитьОтличающееся(Запись.ВерсияФорматаМЭДО, Настройки.ВерсияФорматаМЭДО) Тогда
			Записать = Истина;
		КонецЕсли;
		Если Записать Тогда
			Набор.Записать();
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#КонецЕсли