
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Очередь.Параметры.УстановитьЗначениеПараметра("СмещениеВремени", ТекущаяДатаСеанса() - ТекущаяУниверсальнаяДата());
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьНаСервере()
	
	ОтметкиВремени.РазобратьОчередь(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура Обработать(Команда)
	ОбработатьНаСервере();
	Элементы.Очередь.Обновить();
КонецПроцедуры


&НаСервереБезКонтекста
Процедура ОчередьПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	Ключи = Новый Массив;
	ЗапросыДанных = Новый Массив;
	ЗапросыОчереди = Новый Массив();
	Наборы = Новый Соответствие;
	Запрос = Новый Запрос();
	Данные = Новый Массив;
	
	Для Каждого Строка Из Строки Цикл
		ТекущиеДанные = Строка.Значение.Данные;
		Если ТекущиеДанные.ТипКлюча = 3 И ЗначениеЗаполнено(ТекущиеДанные.Ключ) Тогда
			Данные.Добавить(ТекущиеДанные);
			
			Если ТекущиеДанные.Удаление Тогда
				Если Наборы[ТекущиеДанные.Окно] = Неопределено Тогда
					ЗапросыОчереди.Добавить(СтрШаблон(
					"ВЫБРАТЬ
					|	Т.ИдентификаторКлюча КАК _Ключ,
					|	Т.Объект КАК _Объект,
					|	ИСТИНА КАК _Удаление,
					|	Т.Отметка КАК Отметка,
					|	Т.ЗначенияКлюча КАК ЗначенияКлюча
					|ИЗ
					|	РегистрСведений.ОтметкиВремениОчередь%1 КАК Т
					|ГДЕ
					|	Т.ТипКлюча = 3
					|	И Т.Удаление = ИСТИНА
					|	И Т.ИдентификаторКлюча В(&Ключи)",
					ТекущиеДанные.Окно));
					Наборы.Вставить(ТекущиеДанные.Окно, ЗапросыОчереди.ВГраница());
				КонецЕсли;
			Иначе
				Если Наборы[ТекущиеДанные.Объект] = Неопределено Тогда
					Метаданное = ОбщегоНазначения.ОбъектМетаданныхПоИдентификатору(ТекущиеДанные.Объект, Ложь);
					Если Метаданное = Неопределено Тогда
						Продолжить;
					КонецЕсли;
					
					Н = Формат(ЗапросыДанных.ВГраница()+1, "ЧГ=0");
					Поля = Новый Массив;
					Набор = РегистрыСведений[Метаданное.Имя].СоздатьНаборЗаписей();
					Для Каждого Поле Из Набор.Отбор Цикл
						Поля.Добавить(Поле.Имя);
						Поле.Использование = Истина;
					КонецЦикла;
					
					ЗапросыДанных.Добавить(СтрШаблон(
					"ВЫБРАТЬ
					|	Т.ОтметкиВремениИдентификаторНабораЗаписей КАК _Ключ,
					|	&Объект%3 КАК _Объект,
					|	ЛОЖЬ КАК _Удаление,
					|	%2
					|ИЗ
					|	%1 КАК Т
					|ГДЕ
					|	Т.ОтметкиВремениИдентификаторНабораЗаписей В(&Ключи)",
					Метаданное.ПолноеИмя(),
					СтрСоединить(Поля, ", "),
					Н));
					Запрос.УстановитьПараметр("Объект" + Н, ТекущиеДанные.Объект);
					Наборы.Вставить(ТекущиеДанные.Объект, Набор);
					Поля.Очистить();
				КонецЕсли;
			КонецЕсли;
			
			Ключи.Добавить(ТекущиеДанные.Ключ);
		КонецЕсли;
		
	КонецЦикла;
	
	Если Ключи.Количество() Тогда
		Если ЗапросыОчереди.Количество() Тогда
			ЗапросыДанных.Добавить(СтрСоединить(ЗапросыОчереди, "
				|ОБЪЕДИНИТЬ ВСЕ
				|"));
		КонецЕсли;
		ЗапросыОчереди.Очистить();
		Запрос.Текст = СтрСоединить(ЗапросыДанных, "
			|;
			|"); 
		ЗапросыДанных.Очистить();
		Запрос.УстановитьПараметр("Ключи", Ключи);
		
		Пакет = Запрос.ВыполнитьПакет();
		Для Каждого Результат Из Пакет Цикл
			Выборка = Результат.Выбрать();
			Пока Выборка.Следующий() Цикл
				Для Каждого ТекущиеДанные Из Данные Цикл
					Если ТекущиеДанные.Ключ = Выборка._Ключ
						И ТекущиеДанные.Объект = Выборка._Объект
						И ТекущиеДанные.Удаление = Выборка._Удаление Тогда
						
						Если ТекущиеДанные.Удаление Тогда
							
							Если ТекущиеДанные.Отметка = Выборка.Отметка Тогда
								Попытка
									ТекущиеДанные.ЗначенияКлюча = Строка(Выборка.ЗначенияКлюча.Получить());
								Исключение
									ТекущиеДанные.ЗначенияКлюча = ОписаниеОшибки();
								КонецПопытки;
								Прервать;
							КонецЕсли;
												
						Иначе
							
							Набор = Наборы[ТекущиеДанные.Объект];
							Для Каждого Поле Из Набор.Отбор Цикл
								Поле.Значение = Выборка[Поле.Имя];
							КонецЦикла;
							ТекущиеДанные.ЗначенияКлюча = Набор.Отбор;
							
						КонецЕсли;
											
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
		
		Данные.Очистить();
		Пакет.Очистить();
		Ключи.Очистить();
		
	КонецЕсли;
			
КонецПроцедуры


&НаСервереБезКонтекста
Функция ИмяОбъекта(Объект)
	
	Возврат Объект.ПолноеИмя;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолеОбъекта(Объект, ТипКлюча)
	
	Возврат ОтметкиВремени.КлючевоеПолеИсточника(Объект, ТипКлюча);
	
КонецФункции


&НаКлиенте
Процедура ОчередьВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные.ТипКлюча = 0 Тогда
		//Ссылка
		ПоказатьЗначение(,ТекущиеДанные.Ключ);
	ИначеЕсли ТекущиеДанные.ТипКлюча = 9 Тогда
		//Константа
		ОткрытьФорму(ИмяОбъекта(ТекущиеДанные.Объект) + ".ФормаКонстант");
	Иначе
		//Наборы записей
		ОткрытьФорму(ИмяОбъекта(ТекущиеДанные.Объект) + ".ФормаСписка",
			Новый Структура("Отбор", Новый Структура(ПолеОбъекта(ТекущиеДанные.Объект, ТекущиеДанные.ТипКлюча), ТекущиеДанные.Ключ)),,
			ТекущиеДанные.Ключ);
	КонецЕсли;
	
КонецПроцедуры

