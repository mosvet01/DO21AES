#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Добавляет запись в протокол.
//
// Параметры:
//  Группа - Строка - группа записей в протоколе.
//  Запрос - ПеречислениеСсылка.ЗапросыКСервисуРаспознавания
//  Ответ - Строка - ответ на запрос, который вернул сервис.
//
Процедура ДобавитьЗаписьВПротокол(Группа, Запрос, Ответ) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	УниверсальнаяДатаВМиллисекундах = ТекущаяУниверсальнаяДатаВМиллисекундах();
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.УниверсальнаяДатаВМиллисекундах.Установить(УниверсальнаяДатаВМиллисекундах);
	НаборЗаписей.Отбор.Группа.Установить(Группа);
	НаборЗаписей.Отбор.Запрос.Установить(Запрос);
	
	Запись = НаборЗаписей.Добавить();
	Запись.УниверсальнаяДатаВМиллисекундах = УниверсальнаяДатаВМиллисекундах;
	Запись.Группа = Группа;
	Запись.Запрос = Запрос;
	Запись.Ответ = Ответ;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Удаляет устаревшие записи из протокола с учетом настройки
// СрокХраненияПротоколаРаботыССервисомРаспознавания.
//
Процедура УдалитьУстаревшиеЗаписи() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	СрокХраненияПротокола = 
		РаботаСФайламиВызовСервера.СрокХраненияПротоколаРаботыССервисомРаспознавания();
	Если СрокХраненияПротокола = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПротоколРаботыССервисомРаспознавания.УниверсальнаяДатаВМиллисекундах КАК УниверсальнаяДатаВМиллисекундах,
		|	ПротоколРаботыССервисомРаспознавания.Группа КАК Группа,
		|	ПротоколРаботыССервисомРаспознавания.Запрос КАК Запрос
		|ИЗ
		|	РегистрСведений.ПротоколРаботыССервисомРаспознавания КАК ПротоколРаботыССервисомРаспознавания
		|ГДЕ
		|	ПротоколРаботыССервисомРаспознавания.УниверсальнаяДатаВМиллисекундах < &УниверсальнаяДатаВМиллисекундах";
	Запрос.УстановитьПараметр("УниверсальнаяДатаВМиллисекундах", 
		ТекущаяУниверсальнаяДатаВМиллисекундах() - СрокХраненияПротокола * 86400 * 1000);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.УниверсальнаяДатаВМиллисекундах.Установить(Выборка.УниверсальнаяДатаВМиллисекундах);
		НаборЗаписей.Отбор.Группа.Установить(Выборка.Группа);
		НаборЗаписей.Отбор.Запрос.Установить(Выборка.Запрос);
		НаборЗаписей.Записать();
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
