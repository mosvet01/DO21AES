
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТолькоПросмотр = Истина;
	
	ДатаСеанса = ТекущаяДатаСеанса();
	ДатаСеансаУниверсальная = УниверсальноеВремя(ДатаСеанса, ЧасовойПоясСеанса());
	ПоправкаКУниверсальномуВремени = ДатаСеансаУниверсальная - ДатаСеанса;
	
	Дата = Дата(1,1,1) + Запись.УниверсальнаяДатаВМиллисекундах / 1000 - ПоправкаКУниверсальномуВремени;
	
	Элементы.ФормаНавигационнаяСсылкаФайла.Видимость = ЗначениеЗаполнено(Запись.Группа);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НавигационнаяСсылкаФайла(Команда)
	
	ПараметрыФормы = Новый Структура;
	
	ПараметрыФормы.Вставить("Заголовок", 
		НСтр("ru = 'Навигационная ссылка на файл'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()));
		
	ПараметрыФормы.Вставить(
		"ТекстСообщения",
		НавигационнаяСсылкаФайлаПоИдентификатору(Запись.Группа));
	
	ОткрытьФорму("ОбщаяФорма.Сообщение",
		ПараметрыФормы, ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция НавигационнаяСсылкаФайлаПоИдентификатору(ИДФайла) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат ПолучитьНавигационнуюСсылку(
		Справочники.ВерсииФайлов.ПолучитьСсылку(
		Новый УникальныйИдентификатор(ИДФайла)));
	
КонецФункции

#КонецОбласти
