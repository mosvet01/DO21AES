
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МассивКолонок = Новый Массив;
	МассивКолонок.Добавить("Дата");
	Список.УстановитьОграниченияИспользованияВПорядке(МассивКолонок);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	ГруппыЗаписей = ГруппыЗаписейДляОтбораПоФайлу(ОтборПоФайлу);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Список, "Группы", ГруппыЗаписей, ЗначениеЗаполнено(ОтборПоФайлу));
	
	ОбщегоНазначенияДокументооборотКлиентСервер.ПоказатьСкрытьКнопкуОчисткиОтбора(
		Элементы.ОтборПоФайлу, ОтборПоФайлу);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборПоФайлуПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(ОтборПоФайлу) Тогда
		ГруппыЗаписей = ГруппыЗаписейДляОтбораПоФайлу(ОтборПоФайлу);
	Иначе
		ГруппыЗаписей = Новый Массив;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
			Список, "Группы", ГруппыЗаписей, ЗначениеЗаполнено(ОтборПоФайлу));
	
	ОбщегоНазначенияДокументооборотКлиентСервер.ПоказатьСкрытьКнопкуОчисткиОтбора(
		Элементы.ОтборПоФайлу, ОтборПоФайлу);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПоФайлуНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	КодОсновногоЯзыка = ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка();
	
	ВариантыОтбораПоФайлу = Новый СписокЗначений;
	ВариантыОтбораПоФайлу.Добавить("Файл", НСтр("ru = 'Файл'", КодОсновногоЯзыка));
	ВариантыОтбораПоФайлу.Добавить("ВерсияФайла", НСтр("ru = 'Версия файла'", КодОсновногоЯзыка));
	ВариантыОтбораПоФайлу.Добавить("НавигационнаяСсылка", НСтр("ru = 'Навигационная ссылка'", КодОсновногоЯзыка));
	
	ВариантыОтбораПоФайлу.ПоказатьВыборЭлемента(
		Новый ОписаниеОповещения("ПродолжениеОтборПоФайлуНачалоВыбора", ЭтотОбъект),
		НСтр("ru = 'Выбор типа данных'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()));
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжениеОтборПоФайлуНачалоВыбора(ВыбранныйЭлемент, ДопПараметры) Экспорт
	
	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ВыбранныйЭлемент.Значение = "НавигационнаяСсылка" Тогда
		
		КодОсновногоЯзыка = ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка();
		
		ПоказатьВводСтроки(
			Новый ОписаниеОповещения("ПродолжениеОтборПоФайлуНачалоВыбора1", ЭтотОбъект),,
			НСтр("ru = 'Навигационная ссылка файла или версии файла'", КодОсновногоЯзыка));
		
		Возврат;
	КонецЕсли;
	
	ФормаДляВыбора = "";
	Если ВыбранныйЭлемент.Значение = "Файл" Тогда
		ФормаДляВыбора = "Справочник.Файлы.ФормаВыбора";
	ИначеЕсли ВыбранныйЭлемент.Значение = "ВерсияФайла" Тогда
		ФормаДляВыбора = "Справочник.ВерсииФайлов.ФормаВыбора";
	КонецЕсли;
	
	ОткрытьФорму(
		ФормаДляВыбора,,
		Элементы.ОтборПоФайлу,,,,,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжениеОтборПоФайлуНачалоВыбора1(НавигационнаяСсылка, ДопПараметры) Экспорт
	
	Если НавигационнаяСсылка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОтборПоФайлу = НавигационнаяСсылка;
	
	ОтборПоФайлуПриИзменении(Элементы.ОтборПоФайлу);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы_Список

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	ДатаСеанса = ТекущаяДатаСеанса();
	ДатаСеансаУниверсальная = УниверсальноеВремя(ДатаСеанса, ЧасовойПоясСеанса());
	ПоправкаКУниверсальномуВремени = ДатаСеансаУниверсальная - ДатаСеанса;
	
	Для Каждого СтрокаСписка Из Строки Цикл
		СтрокаСписка.Значение.Данные.Дата = СтрокаСписка.Значение.Данные.Дата
			+ СтрокаСписка.Значение.Данные.УниверсальнаяДатаВМиллисекундах / 1000
			- ПоправкаКУниверсальномуВремени;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НавигационнаяСсылкаФайла(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Или Не ЗначениеЗаполнено(ТекущиеДанные.Группа) Тогда
		ПоказатьПредупреждение(,
			НСтр("ru = 'Для получения ссылки необходимо выделить в списке запись с заполненной Группой.'",
			ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()));
		Возврат;
	КонецЕсли;
	
	ИДФайла = ТекущиеДанные.Группа;
	
	ПараметрыФормы = Новый Структура;
	
	ПараметрыФормы.Вставить("Заголовок", 
		НСтр("ru = 'Навигационная ссылка на файл'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()));
		
	ПараметрыФормы.Вставить(
		"ТекстСообщения",
		НавигационнаяСсылкаФайлаПоИдентификатору(ТекущиеДанные.Группа));
	
	ОткрытьФорму("ОбщаяФорма.Сообщение",
		ПараметрыФормы, ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ГруппыЗаписейДляОтбораПоФайлу(Файл)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ГруппыЗаписей = Новый Массив;
	
	Если ТипЗнч(Файл) = Тип("Строка") Тогда
		ЗначениеФайл = РаботаС_HTMLВызовСервера.СсылкаПоНавигационной(Файл);
	Иначе
		ЗначениеФайл = Файл;
	КонецЕсли;
	
	Если ТипЗнч(ЗначениеФайл) = Тип("СправочникСсылка.ВерсииФайлов") Тогда
		ГруппыЗаписей.Добавить(Строка(ЗначениеФайл.УникальныйИдентификатор()));
	ИначеЕсли ТипЗнч(ЗначениеФайл) = Тип("СправочникСсылка.Файлы") Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ВерсииФайлов.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.ВерсииФайлов КАК ВерсииФайлов
			|ГДЕ
			|	ВерсииФайлов.Владелец = &Владелец";
		Запрос.УстановитьПараметр("Владелец", ЗначениеФайл);
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			ГруппыЗаписей.Добавить(Строка(Выборка.Ссылка.УникальныйИдентификатор()));
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ГруппыЗаписей;
	
КонецФункции

&НаСервереБезКонтекста
Функция НавигационнаяСсылкаФайлаПоИдентификатору(ИДФайла) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат ПолучитьНавигационнуюСсылку(
		Справочники.ВерсииФайлов.ПолучитьСсылку(
		Новый УникальныйИдентификатор(ИДФайла)));
	
КонецФункции

#КонецОбласти
