

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если Не ОбменДанными.Загрузка Тогда
		Для Каждого Запись Из ЭтотОбъект Цикл
			Запись.Модифицированность = Истина;			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если Не ОбменДанными.Загрузка Тогда
		Если ЭтотОбъект.Количество() = 0 Тогда
			Блокировка = Новый БлокировкаДанных;
			Блокировка.Добавить("РегистрСведений.МиграцияДанныхИзВнешнихСистемОбъекты");
			Блокировка.Заблокировать();
			
			Запрос = Новый Запрос(
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	ПереходОбъекты.Модифицированность
			|ИЗ
			|	РегистрСведений.МиграцияДанныхИзВнешнихСистемОбъекты КАК ПереходОбъекты
			|ГДЕ
			|	ПереходОбъекты.Модифицированность = ИСТИНА");
			
			Если Запрос.Выполнить().Пустой() Тогда
				Запрос = Новый Запрос(
				"ВЫБРАТЬ ПЕРВЫЕ 1
				|	*
				|ИЗ
				|	РегистрСведений.МиграцияДанныхИзВнешнихСистемОбъекты КАК ПереходОбъекты");
				Набор = РегистрыСведений.МиграцияДанныхИзВнешнихСистемОбъекты.СоздатьНаборЗаписей();
				Набор.Загрузить(Запрос.Выполнить().Выгрузить());
				Если Набор.Количество() Тогда
					Запись = Набор[0];
					Запись.Модифицированность = Истина;
					Для Каждого Поле Из Набор.Отбор Цикл
						Поле.Установить(Запись[Поле.Имя]);
					КонецЦикла;
					Набор.ОбменДанными.Загрузка = Истина;
					Набор.Записать();
				КонецЕсли;
			КонецЕсли; 
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

