#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПриСозданииНаСервереРедакцииКонфигурации();
	
	Если Параметры.Свойство("Организация") И ЗначениеЗаполнено(Параметры.Организация) Тогда
		
		ЗаписьВРегистре = РегистрыСведений.НастройкиОрганизацийМЭДО.СоздатьМенеджерЗаписи();
		ЗаписьВРегистре.Организация = Параметры.Организация;
		ЗаписьВРегистре.Прочитать();
		
		Если ЗначениеЗаполнено(ЗаписьВРегистре.Организация) Тогда
			ЗначениеВДанныеФормы(ЗаписьВРегистре, Запись);
		Иначе // Нет в БД, это новая запись:
			Запись.Организация = Параметры.Организация;
			Запись.НаименованиеМЭДО = ИнтеграцияСМЭДО.НаименованиеЮрлицаНаДату(Параметры.Организация, ТекущаяДатаСеанса());
			Запись.ШтампСлева = 10; // по умолчанию.
			Запись.ШтампСверху = 10; // по умолчанию.
			Запись.СпособОбмена = Перечисления.СпособыОбменаМЭДО.ДискСеть;
			Запись.РазмерПорции = 50; // по умолчанию.
		КонецЕсли;
		
	КонецЕсли;
	
	ДоступностьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ЗаписьНастроекМЭДО", Запись.Организация, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СпособОбменаПриИзменении(Элемент)
	
	ДоступностьВидимость();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ДоступностьВидимость()
	
	Элементы.ГруппаДискСеть.Видимость	= (Запись.СпособОбмена = Перечисления.СпособыОбменаМЭДО.ДискСеть);
	Элементы.ГруппаFTP.Видимость	= (Запись.СпособОбмена = Перечисления.СпособыОбменаМЭДО.FTP);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервереРедакцииКонфигурации()
	
	Элементы.НаименованиеМЭДО.Заголовок = 
		СтрШаблон(НСтр("ru = 'Наименование %1 для МЭДО'"), ИнтеграцияСМЭДО.ОрганизацииРодительный());
	
КонецПроцедуры

#КонецОбласти