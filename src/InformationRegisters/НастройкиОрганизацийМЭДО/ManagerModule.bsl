#Если Сервер Или ТолстыйКлиентОбычноеПриложение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Функция - Получает настройки организации для МЭДО.
//
// Параметры:
//  Организация - ОпределяемыйТип.Организация - организация, для которой надо получить настройки.
//  ДанныеОтвета - см. МЭДОСтруктурыДанных.НовыйОтвет.
//  СписокНастроек - Строка,Неопределено - Список настроек через запятую, если не указан, то получить все настройки
// 
// Возвращаемое значение:
//  Структура,Неопределено - Поля структуры соответствуют ресурсам регистра.
//
Функция ПолучитьНастройки(Знач Организация, ДанныеОтвета, СписокНастроек = Неопределено) Экспорт
	
	Данные = Неопределено;
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Если ПолучитьФункциональнуюОпцию("ВестиУчетПоОрганизациям") Тогда
			Текст = НСтр("ru = 'Ошибка в настройках'");
			ТекстОшибкиПодробно = СтрШаблон(НСтр("ru = 'Не передано значение %1 при получении настроек МЭДО'"),
				РедакцииКонфигурацииКлиентСервер.ОрганизацииРодительный());
			МЭДО.ЗаписьВЖурналСобытий(
				Перечисления.УровниСобытийМЭДО.Ошибка, Текст, Текст, ТекстОшибкиПодробно, ДанныеОтвета);
			Возврат Данные;
		Иначе
			Организация = ИнтеграцияСМЭДО.ОрганизацияПоУмолчанию();
		КонецЕсли;
	КонецЕсли;
	
	Данные = МЭДОПовтИсп.ПолучитьНастройкиОрганизации(Организация, СписокНастроек);
	
	ПроверкаКритичноВажныхПолей(Организация, Данные, ДанныеОтвета);
	
	Возврат Данные;
	
КонецФункции

// Функция - Получает настройки организации для МЭДО.
//
// Параметры:
//  Организация - ОпределяемыйТип.Организация - организация, для которой надо получить настройки.
//  Данные - Структура - Поля структуры соответствуют ресурсам регистра.
//  ДанныеОтвета - см. МЭДОСтруктурыДанных.НовыйОтвет.
Процедура ПроверкаКритичноВажныхПолей(Организация, Данные, ДанныеОтвета) Экспорт
	
	Если Данные.Свойство("Организация") И Не ЗначениеЗаполнено(Данные.Организация) Тогда
		ТекстОшибкиПодробно = СтрШаблон(
			НСтр("ru = 'Не заданы настройки %1 %2 для МЭДО (Кнопка в карточке %1)'"),
			ИнтеграцияСМЭДО.ОрганизацииРодительный(), Организация);
		МЭДО.ЗаписьВЖурналСобытий(
			Перечисления.УровниСобытийМЭДО.Ошибка, 
			Организация,
			Текст_ОшибкаВНастройках(),
			ТекстОшибкиПодробно,
			ДанныеОтвета);
		Возврат;
	КонецЕсли;
	
	// Некоторые настройки особенно важны:
	Если Данные.Свойство("ИдентификаторМЭДО") И Не ЗначениеЗаполнено(Данные.ИдентификаторМЭДО) Тогда
		ТекстОшибкиПодробно = СтрШаблон(
			НСтр("ru = 'В настройках МЭДО не указан идентификатор %1 %2'"),
			ИнтеграцияСМЭДО.ОрганизацииРодительный(), Организация);
		МЭДО.ЗаписьВЖурналСобытий(
			Перечисления.УровниСобытийМЭДО.Ошибка, Организация,
			Текст_ОшибкаВНастройках(), ТекстОшибкиПодробно, ДанныеОтвета);
		Возврат;
	КонецЕсли;
	
	Если Данные.Свойство("НаименованиеМЭДО") И Не ЗначениеЗаполнено(Данные.НаименованиеМЭДО) Тогда
		ТекстОшибкиПодробно = СтрШаблон(
			НСтр("ru = 'В настройках МЭДО не указано наименование %1 %2'"),
			ИнтеграцияСМЭДО.ОрганизацииРодительный(), Организация);
		МЭДО.ЗаписьВЖурналСобытий(
			Перечисления.УровниСобытийМЭДО.Ошибка, Организация,
			Текст_ОшибкаВНастройках(), ТекстОшибкиПодробно, ДанныеОтвета);
		Возврат;
	КонецЕсли;
	
	Если Данные.Свойство("КаталогОтправки") И Не ЗначениеЗаполнено(Данные.КаталогОтправки) Тогда
		ТекстОшибкиПодробно = СтрШаблон(
			НСтр("ru = 'В настройках МЭДО не указан каталог отправки %1 %2'"),
			ИнтеграцияСМЭДО.ОрганизацииРодительный(), Организация);
		МЭДО.ЗаписьВЖурналСобытий(
			Перечисления.УровниСобытийМЭДО.Ошибка, Организация,
			Текст_ОшибкаВНастройках(), ТекстОшибкиПодробно, ДанныеОтвета);
		Возврат;
	КонецЕсли;
	
	Если Данные.Свойство("КаталогПолучения") И Не ЗначениеЗаполнено(Данные.КаталогПолучения) Тогда
		ТекстОшибкиПодробно = СтрШаблон(
			НСтр("ru = 'В настройках МЭДО не указан каталог получения %1 %2'"),
			ИнтеграцияСМЭДО.ОрганизацииРодительный(), Организация);
		МЭДО.ЗаписьВЖурналСобытий(
			Перечисления.УровниСобытийМЭДО.Ошибка, Организация,
			Текст_ОшибкаВНастройках(), ТекстОшибкиПодробно, ДанныеОтвета);
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция Текст_ОшибкаВНастройках()
	Возврат НСтр("ru = 'Ошибка в настройках'");
КонецФункции

#КонецОбласти

#КонецЕсли