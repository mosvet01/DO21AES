////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Получает проектя для синхронизации пользователя.
//
// Параметры:
//  Пользователь - СправочникСсылка.Пользователи - Обрабатываемый пользователь.
// Возвращаемое значение:
//  Таблица - Таблица с колонками "Проект", "Выбран".
Функция ТаблицаПроектыДляСинхронизации(Пользователь, СоставПроектовИзменился) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	МП_СинхронизацияПроектов.Проект КАК Проект,
		|	НЕ МП_СинхронизацияПроектов.Удален КАК Выбран
		|ИЗ
		|	РегистрСведений.МП_СинхронизацияПроектов КАК МП_СинхронизацияПроектов
		|ГДЕ
		|	МП_СинхронизацияПроектов.Пользователь = &Пользователь
		|
		|УПОРЯДОЧИТЬ ПО
		|	Проект
		|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Возврат РезультатЗапроса.Выгрузить();
	Иначе
		СоставПроектовИзменился = Истина;
		Возврат ТаблицаПроектыДляСинхронизацииПоУмолчанию(Пользователь);
	КонецЕсли;
	
КонецФункции

// Получает проекты для синхронизации по умолчанию
//
// Параметры:
//  Пользователь - Необходимый пользователь 
// Возвращаемое значение:
//  Таблица - Таблица с колонками "Проект", Выбран.
Функция ТаблицаПроектыДляСинхронизацииПоУмолчанию(Пользователь)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ФактическиеТрудозатраты.Проект КАК Проект,
		|	ЛОЖЬ КАК Выбран
		|ИЗ
		|	РегистрСведений.ФактическиеТрудозатраты КАК ФактическиеТрудозатраты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Проекты КАК Проекты
		|		ПО ФактическиеТрудозатраты.Проект = Проекты.Ссылка
		|ГДЕ
		|	ФактическиеТрудозатраты.Пользователь = &Пользователь
		|	И ФактическиеТрудозатраты.ДатаДобавления >= &ДатаУстаревания
		|	И НЕ ФактическиеТрудозатраты.Удалена
		|
		|УПОРЯДОЧИТЬ ПО
		|	Проект
		|АВТОУПОРЯДОЧИВАНИЕ";
	
	ДатаУстаревания = РегистрыСведений.ОбменСМобильнымиНастройкиПользователей.ДатаУстаревания(Пользователь);
	Запрос.УстановитьПараметр("ДатаУстаревания", ДатаУстаревания);
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	РезультатЗапроса = Запрос.Выполнить();

	Возврат РезультатЗапроса.Выгрузить();
		
КонецФункции

Процедура Добавить(Пользователь, Проект, Удален = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	МЗ = СоздатьМенеджерЗаписи();
	МЗ.Проект = Проект;
	МЗ.Пользователь = Пользователь;
	МЗ.Удален = Удален;
	МЗ.Граница = ТекущаяУниверсальнаяДатаВМиллисекундах();
	МЗ.Записать();
	
КонецПроцедуры


//Удаляет все проекты из синхронизации.
// Параметры:
//  Пользователь - СправочникСсылка.Пользователи - Пользователь. 
//
Процедура УдалитьВсеПроектыИзСинхронизацииПриНеобходимости(Пользователь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	МП_СинхронизацияПроектов.Пользователь,
		|	МП_СинхронизацияПроектов.Проект
		|ИЗ
		|	РегистрСведений.МП_СинхронизацияПроектов КАК МП_СинхронизацияПроектов
		|ГДЕ
		|	НЕ МП_СинхронизацияПроектов.Удален
		|	И МП_СинхронизацияПроектов.Пользователь = &Пользователь";
	
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Добавить(Пользователь, ВыборкаДетальныеЗаписи.Проект, Истина);
	КонецЦикла;

КонецПроцедуры

//Определяем учавствует ли проект в синхронизации и если нет - добавляет в синхронизацию
//Параметры
// Пользователь - Справочник.Пользователи - пользователь, по которому происходит проверка
// Проект - Справочник.Проект - проверяемый проект
Процедура СинхронизироватьПроектПриНеобходимости(Пользователь, Проект, ПроектПоУмолчанию = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	//Новый выбранный проект пользователем в отчетах нужно добавить в синхронизацию,
	//для чтобы на мобильном не было битых ссылок
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	МП_СинхронизацияПроектов.Пользователь КАК Пользователь
		|ИЗ
		|	РегистрСведений.МП_СинхронизацияПроектов КАК МП_СинхронизацияПроектов
		|ГДЕ
		|	МП_СинхронизацияПроектов.Пользователь = &Пользователь
		|	И МП_СинхронизацияПроектов.Проект = &Проект";
	
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	Запрос.УстановитьПараметр("Проект", Проект);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		
		Добавить(Пользователь, Проект);
		
	КонецЕсли;
	
	Если ПроектПоУмолчанию Тогда
		
		//Добавляем изменение умолчательного проекта для выгрузки на мобильный
		РегистрыСведений.ИзмененныеНастройкиСинхронизацииСМобильнымКлиентом.ДобавитьЗапись(Пользователь,
			Перечисления.ОбменСМобильнымиТипыНастроекПользователей.ПроектПоУмолчанию);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
