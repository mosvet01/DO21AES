// Шифрует вложения, указанные в структуре ПараметрыОтправки.
//
Функция ЗашифроватьВложения(ПараметрыОтправки, Сообщение, УникальныйИдентификатор, ОписаниеОповещения) Экспорт
	
	Если Не ПараметрыОтправки.Шифровать Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ПараметрыОтправки.ОтпечаткиСертификатовШифрования.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Операция = НСтр("ru = 'Шифрование'");
	ПараметрыВыполнения = Новый Структура("ПараметрыОтправки, Сообщение, УникальныйИдентификатор, ОписаниеОповещения",
		ПараметрыОтправки, Сообщение, УникальныйИдентификатор, ОписаниеОповещения);
	ОбработчикПродолжения = Новый ОписаниеОповещения("ПослеСозданияМенеджера", ЭтотОбъект, ПараметрыВыполнения);
	ЭлектроннаяПодписьСлужебныйКлиент.СоздатьМенеджерКриптографии(ОбработчикПродолжения, Операция);
	
КонецФункции

// Завершение процедуры ПодписатьОбъект.
Процедура ПослеСозданияМенеджера(ОписаниеДанных, ПараметрыВыполнения) Экспорт
	
	Если ОписаниеДанных = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерКриптографии = ОписаниеДанных;
	
	ПараметрыОтправки = ПараметрыВыполнения.ПараметрыОтправки;
	Сообщение = ПараметрыВыполнения.Сообщение;
	УникальныйИдентификатор = ПараметрыВыполнения.УникальныйИдентификатор;
	ОписаниеОповещения = ПараметрыВыполнения.ОписаниеОповещения;
	
	МассивСертификатовДляШифрования = Новый Массив;
	Для Каждого Данные Из ПараметрыОтправки.ОтпечаткиСертификатовШифрования Цикл
		СертификатКриптографии = Новый СертификатКриптографии(Данные);
		МассивСертификатовДляШифрования.Добавить(СертификатКриптографии);
	КонецЦикла;	
		
	Для каждого Вложение Из Сообщение.Вложения Цикл
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(Вложение.Адрес);
		ИмяФайла = Вложение.ИмяФайла;
		Если НРег(ИмяФайла) = НРег("1C-53898-2010.xml") Тогда
			Продолжить; // Файлы обмена сообщениями не шифруются.
		КонецЕсли;
		ИмяФайлаИнфо = РаботаСоСтроками.РазложитьИмяФайла(ИмяФайла);
		Если НРег(ИмяФайлаИнфо.Расширение) = НРег(ПараметрыОтправки.РасширениеДляФайловПодписи) Тогда
			Продолжить; // Файлы подписи не шифруются.
		КонецЕсли;
		Если Вложение.Зашифрован Тогда
			ДвоичныеДанные = МенеджерКриптографии.Расшифровать(ДвоичныеДанные);
		Иначе
			ДобавитьЗначениеКСтрокеЧерезРазделитель(
				ИмяФайла,
				".",
				ПараметрыОтправки.РасширениеДляЗашифрованныхФайлов);
		КонецЕсли;
		ДвоичныеДанные = МенеджерКриптографии.Зашифровать(ДвоичныеДанные, МассивСертификатовДляШифрования);
		УдалитьИзВременногоХранилища(Вложение.Адрес);
		Вложение.Адрес = ПоместитьВоВременноеХранилище(ДвоичныеДанные, УникальныйИдентификатор);
		Вложение.ИмяФайла = ИмяФайла;
		Вложение.Зашифрован = Истина;
	КонецЦикла;
	
	ЛегкаяПочтаКлиент.ОтправитьПослеШифрования(ОписаниеОповещения, ПараметрыОтправки, Сообщение);
	
КонецПроцедуры
