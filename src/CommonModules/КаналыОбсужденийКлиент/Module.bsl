////////////////////////////////////////////////////////////////////////////////
// Модуль для работы с каналами обсуждений.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

Процедура ПриНачалеРаботыСистемы() Экспорт
	
	Если Не СистемаВзаимодействия.ИнформационнаяБазаЗарегистрирована() Тогда
		Возврат;
	КонецЕсли;
	
	// Подключение обработчика отправки сообщений.
	ОбработчикПослеОтправки = Новый ОписаниеОповещения("ОбработкаОтправкиСообщения", ЭтотОбъект);
	СистемаВзаимодействия.ПодключитьОбработчикПослеОтправкиСообщения(ОбработчикПослеОтправки);
	
	// Параметры приложения.
	ПараметрыРаботыКлиентаПриЗапуске =
		СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиентаПриЗапуске();
	ИДКаналовСтроками = ПараметрыРаботыКлиентаПриЗапуске.ИдентификаторыСВКаналовПользователяСтроками;
	ИДКаналов = Новый Соответствие;
	Для Каждого ИДКаналаСтрокой Из ИДКаналовСтроками Цикл
		ИДКаналов[Новый ИдентификаторПользователяСистемыВзаимодействия(ИДКаналаСтрокой)] = Истина;
	КонецЦикла;
	ИмяПараметра = "Обсуждения.ИдентификаторыСВКаналовПользователя";
	Если ПараметрыПриложения[ИмяПараметра] = Неопределено Тогда
		ПараметрыПриложения.Вставить(ИмяПараметра, ИДКаналов);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаСлужебногоОповещения(Событие, Параметры) Экспорт
	
	Если Событие = "ДобавлениеВКанал" Тогда
		ИдентификаторСВКанала = Параметры.ИдентификаторСВКанала;
		ИмяПараметра = "Обсуждения.ИдентификаторыСВКаналовПользователя";
		Если ПараметрыПриложения[ИмяПараметра] <> Неопределено Тогда
			// Добавление идентфикатора нового канала в общий список.
			ПараметрыПриложения[ИмяПараметра][ИдентификаторСВКанала] = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//Обрабатывает сообщение отправленное пользователем.
//
//Параметры: 
//	Сообщение - СообщениеСистемыВзаимодействия - Сообщение отправленное пользователем
//	Обсуждение - ОбсуждениеСистемыВзаимодейтвия - Обсуждение в котором было написано сообщение
//	Дополнительные параметры - значение, которое было указано при создании объекта ОписаниеОповещения
//		см.СоздатьЧатБота 
//
Процедура ОбработкаОтправкиСообщения(Сообщение, Обсуждение, ДополнительныеПараметры) Экспорт
	
	Если Обсуждение.Групповое Тогда
		Возврат;
	КонецЕсли;
	
	ИДТекущегоПользователяСВ = СистемаВзаимодействия.ИдентификаторТекущегоПользователя();
	Если Сообщение.Автор <> ИДТекущегоПользователяСВ Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторыСВКаналов = ПараметрыПриложения["Обсуждения.ИдентификаторыСВКаналовПользователя"];
	Для Каждого Участник Из Обсуждение.Участники Цикл
		Если Участник <> ИДТекущегоПользователяСВ Тогда
			Если ИдентификаторыСВКаналов[Участник] = Неопределено Тогда
				// Это не обсуждение с каналом.
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	КаналыОбсужденийВызовСервера.ОбработатьСообщениеПользователя(
		Сообщение.Идентификатор, Обсуждение.Идентификатор);
	
КонецПроцедуры

#КонецОбласти
