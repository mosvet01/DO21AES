
#Область Программый_интерфейс

// Возвращает элемент классификатора по переданному коду.
//
// Параметры:
//    ТипЭлемента - Строка - название метаданных.
//    КодЭлемента - Строка - код элемента в нужном справочнике.
//
// Возвращаемое значение:
//    Ссылка на РазделОбращения, ТемуОбращения, ТематикуОбращения или ВопросОбращения, в зависимости от ТипаЭлемента.
//
Функция ПолучитьЭлементКлассификатора(ТипЭлемента, КодЭлемента) Экспорт
	
	Возврат ПодборИзКлассификатораОбращенийГражданСервер.ПолучитьЭлементКлассификатора(
		ТипЭлемента, КодЭлемента);
	
КонецФункции

// Проверяет корректность вопроса.
//
// Параметры:
//    Раздел - СправочникСсылка.РазделыОбращений - ссылка на Раздел классификатора.
//    Тематика - СправочникСсылка.ТематикиОбращений - ссылка на Тематику классификатора.
//    Тема - СправочникСсылка.ТемыОбращений - ссылка на Тему классификатора.
//    Вопрос - СправочникСсылка.ВопросыОбращений - ссылка на Вопрос классификатора.
//    ТекстОшибки - Строка - текст возвращаемой ошибки.
//
// Возвращаемое значение:
//    Истина - Булево - если вопрос корректен
//    Ложь - в противном случае.
//
Функция ВопросКорректен(Раздел, Тематика, Тема, Вопрос, ТекстОшибки) Экспорт
	
	РазделТематики = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Тематика, "Раздел");
	Если ЗначениеЗаполнено(РазделТематики) И РазделТематики <> Раздел Тогда 
		ТекстОшибки = Нстр("ru = 'Некорректный код вопроса: Тематика вопроса не соответствует разделу.'");
		Возврат Ложь;
	КонецЕсли;
	
	ТематикаТемы = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Тема, "Тематика");
	Если ЗначениеЗаполнено(ТематикаТемы) И ТематикаТемы <> Тематика Тогда 
		ТекстОшибки = Нстр("ru = 'Некорректный код вопроса: Тема вопроса не соответствует тематике.'");
		Возврат Ложь;
	КонецЕсли;
	
	ТемаВопроса = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Вопрос, "Тема");
	Если ЗначениеЗаполнено(ТемаВопроса) И ТемаВопроса <> Тема Тогда 
		ТекстОшибки = Нстр("ru = 'Некорректный код вопроса: Вопрос не соответствует теме.'");
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#Область Служебные_процедуры_и_функции

#КонецОбласти
