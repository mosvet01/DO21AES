
////////////////////////////////////////////////////////////////////////////////
// Обзор процессов клиент: содержит клиентские процедуры и функции по формированию
//                                HTML-обзора процессов и их шаблонов.
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Обработчик нажатия поля ПредставлениеHTML
//
Процедура ПредставлениеHTMLПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка, Форма) Экспорт
	
	Если ОбзорПроцессовКлиентПереопределяемый.ПредставлениеHTMLПриНажатии(
		Элемент, ДанныеСобытия, СтандартнаяОбработка, Форма) Тогда
		
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	Если Не ЗначениеЗаполнено(ДанныеСобытия.Href) Тогда 
		Возврат;
	КонецЕсли;	
	
	Если СтрНачинаетсяС(ДанныеСобытия.Href, "v8doc:") Тогда
		
		НавигационнаяСсылкаПоля = СтрЗаменить(ДанныеСобытия.Href, "v8doc:", "");
		
		Если СтрокаНачинаетсяСПодстроки(НавигационнаяСсылкаПоля, "processnum") Тогда
			
			ПараметрыФормы = Новый Структура("ЗначениеОтбора", Форма.ТекущийШаблон);
			ОткрытьФорму("КритерийОтбора.БизнесПроцессыПоШаблону.ФормаСписка", ПараметрыФормы);
			
		Иначе
			
			ПерейтиПоНавигационнойСсылке(НавигационнаяСсылкаПоля);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Проверяет, содержит ли ссылка схему
Функция СтрокаНачинаетсяСПодстроки(Строка, Подстрока)
	
	Если Найти(НРег(СокрЛ(Строка)), НРег(Подстрока)) = 1 Тогда
		
		Возврат Истина;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// Обработчик нажатия поля ПредставлениеHTML
//
Процедура ПредставлениеПроцессаHTMLПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка, Форма) Экспорт
	
	Если ОбзорПроцессовКлиентПереопределяемый.ПредставлениеПроцессаHTMLПриНажатии(
		Элемент, ДанныеСобытия, СтандартнаяОбработка, Форма) Тогда
		
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	Если Не ЗначениеЗаполнено(ДанныеСобытия.Href) Тогда 
		Возврат;
	КонецЕсли;
	
	СтрСсылка = СтрЗаменить(ДанныеСобытия.Href, "v8doc:", "");
	
	Если СтрНачинаетсяС(СтрСсылка, "Подзадачи") Тогда
		
		СтрСсылка = СтрЗаменить(СтрСсылка, "Подзадачи_", "");
		УникальныйИдентификаторПроцесса = Лев(СтрСсылка, 36);
		НаименованиеПроцесса = СтрЗаменить(СтрСсылка, УникальныйИдентификаторПроцесса + "_", "");
		
		СсылкаНаПроцесс = ОбзорПроцессовВызовСервера.СсылкаНаПроцессПоУникальномуИдентификатору(
			Новый УникальныйИдентификатор(УникальныйИдентификаторПроцесса),
			НаименованиеПроцесса);
		
		ОткрытьФорму("ОбщаяФорма.ПроцессыИЗадачи",
			Новый Структура("Предмет", СсылкаНаПроцесс));
			
	ИначеЕсли СтрНачинаетсяС(СтрСсылка, "Подзадача") Тогда
			
		СтрСсылка = СтрЗаменить(СтрСсылка, "Подзадача_", "");
		УникальныйИдентификаторЗадача = Лев(СтрСсылка, 36);
		
		ПараметрыЗадачи = ОбзорПроцессовВызовСервера.ПараметрыЗадачиПоУникальномуИдентификатору(
			Новый УникальныйИдентификатор(УникальныйИдентификаторЗадача));
			
		СписокЗначений = Новый СписокЗначений;
		
		СписокЗначений.Добавить("НаписатьПисьмо", НСтр("ru='Написать письмо'"),, 
			БиблиотекаКартинок.ЭлектронноеПисьмо);
		СписокЗначений.Добавить("ЗадатьВопрос", НСтр("ru='Задать вопрос'"),, 
			БиблиотекаКартинок.ВопросАвтору);
		СписокЗначений.Добавить("ОткрытьЗадачу", НСтр("ru='Открыть задачу'"),, 
			БиблиотекаКартинок.Задача);
		СписокЗначений.Добавить("ПосмотретьПользователя", НСтр("ru='Посмотреть пользователя'"),, 
			БиблиотекаКартинок.Пользователь);
			
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"КомандаПодзадачаВыбораЗначенияПродолжение",
			ЭтотОбъект,
			ПараметрыЗадачи);
		
		Форма.ПоказатьВыборИзМеню(ОписаниеОповещения, СписокЗначений, Элемент);
		
	ИначеЕсли Найти(ДанныеСобытия.Href, "ПоказатьПерепискуПоДокументу") Тогда
		
		ОбзорЗадачКлиент.ОткрытьПереписку(ДанныеСобытия.Href);
		
	ИначеЕсли СтрНачинаетсяС(ДанныеСобытия.Href, "v8doc:") Тогда 
		
		ПерейтиПоНавигационнойСсылке(СтрСсылка);
		
	ИначеЕсли Найти(ДанныеСобытия.Href, "ОткрытьКарточкуКонтроля") Тогда
		
		КонтрольКлиент.ОбработкаКомандыКонтроль(Форма.ТекущийПроцесс, Форма);
		
	КонецЕсли;
	
КонецПроцедуры

// Обработчик команды с подменю - продолжение
Процедура КомандаПодзадачаВыбораЗначенияПродолжение(Результат, Параметры) Экспорт
	
	Если Результат = Неопределено Или ТипЗнч(Результат) <> Тип("ЭлементСпискаЗначений") Тогда
		Возврат;
	КонецЕсли;	
	
	Если Результат.Значение = "НаписатьПисьмо" Тогда
		
		АдресПочты = Параметры.Адрес;
		РаботаСПочтовымиСообщениямиКлиент.ОткрытьФормуОтправкиПочтовогоСообщения(, АдресПочты);
		
		Возврат;
		
	ИначеЕсли Результат.Значение = "ЗадатьВопрос" Тогда
		
		ЗначенияЗаполнения = Новый Структура;
		ЗначенияЗаполнения.Вставить("Задача", Параметры.Задача);
		ЗначенияЗаполнения.Вставить("Кому", Параметры.Исполнитель);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
		
		ОткрытьФорму("БизнесПроцесс.РешениеВопросовВыполненияЗадач.ФормаОбъекта", ПараметрыФормы);
		
		Возврат;
	ИначеЕсли Результат.Значение = "ОткрытьЗадачу" Тогда
		ПоказатьЗначение(, Параметры.Задача);
		Возврат;
	ИначеЕсли Результат.Значение = "ПосмотретьПользователя" Тогда
		ПоказатьЗначение(, Параметры.Исполнитель);
		Возврат;
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти
