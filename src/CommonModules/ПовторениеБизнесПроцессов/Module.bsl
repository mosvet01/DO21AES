
////////////////////////////////////////////////////////////////////////////////
// Повторение бизнес процессов: содержит серверные процедуры и функции механизма
//                              повторение процессов
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Выполняет формирование бизнес-процессов из регламентного задания 
Процедура ПовторениеБизнесПроцессовПоРасписанию() Экспорт 
	
	Отказ = Ложь;
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.ПовторениеБизнесПроцессов, Отказ);
	
	Если Отказ = Истина Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьПовторениеБизнесПроцессов") Тогда 
		Возврат;
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкаПовторенияБизнесПроцессов.БизнесПроцесс,
		|	НастройкаПовторенияБизнесПроцессов.Расписание,
		|	НастройкаПовторенияБизнесПроцессов.ДатаПоследнегоПовторения,
		|	НастройкаПовторенияБизнесПроцессов.ГрафикРаботы
		|ИЗ
		|	РегистрСведений.НастройкаПовторенияБизнесПроцессов КАК НастройкаПовторенияБизнесПроцессов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеБизнесПроцессов КАК ДанныеБизнесПроцессов
		|		ПО НастройкаПовторенияБизнесПроцессов.БизнесПроцесс = ДанныеБизнесПроцессов.БизнесПроцесс
		|ГДЕ
		|	НЕ НастройкаПовторенияБизнесПроцессов.ПовторениеЗавершено
		|	И ДанныеБизнесПроцессов.УзелОбмена = &УзелОбмена";
		
	ЭтотУзелОбмена = РаботаСБизнесПроцессами.ЭтотУзелОбменаДляОбработкиПроцессов();
	Запрос.УстановитьПараметр("УзелОбмена", ЭтотУзелОбмена);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ТекущаяДата = ТекущаяДатаСеанса();
		
		Расписание = Выборка.Расписание.Получить();
		
		ВыполнитьПовторение = НеобходимоПовторениеПроцесса(
			ТекущаяДата, Расписание, Выборка.ГрафикРаботы, Выборка.ДатаПоследнегоПовторения);
		
		Если ВыполнитьПовторение Тогда
			
			Результат = ПовторитьБизнесПроцесс(ТекущаяДата, Выборка.БизнесПроцесс);
			
			Если Результат Тогда 
				МенеджерЗаписи = РегистрыСведений.НастройкаПовторенияБизнесПроцессов.СоздатьМенеджерЗаписи();
				МенеджерЗаписи.БизнесПроцесс = Выборка.БизнесПроцесс;
				МенеджерЗаписи.Прочитать();
				МенеджерЗаписи.ДатаПоследнегоПовторения = ТекущаяДата;
				МенеджерЗаписи.Записать();
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Расписание.ДатаКонца) И ТекущаяДата > Расписание.ДатаКонца Тогда 
			МенеджерЗаписи = РегистрыСведений.НастройкаПовторенияБизнесПроцессов.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.БизнесПроцесс = Выборка.БизнесПроцесс;
			МенеджерЗаписи.Прочитать();
			МенеджерЗаписи.ПовторениеЗавершено = Истина;
			МенеджерЗаписи.Записать();
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Возвращает представление расписания в удобном для чтения виде
//
// Параметры:
//   Расписание - РасписаниеРегламентногоЗадания - расписание повторения процесса
//   ГрафикРаботы - СправочникСсылка.ГрафикиРаботы - график работы по которому происходит
//                  повторение процесса
//
// Возвращаемое значение:
//   Строка
//
Функция ПолучитьПредставлениеРасписания(Знач Расписание, ГрафикРаботы) Экспорт
	
	Если Расписание = Неопределено Тогда 
		Возврат НСтр("ru = 'Расписание не задано'");
	КонецЕсли;	
	
	ПредставлениеРасписания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Выполнять с %1'"),
		Формат(Расписание.ДатаНачала, "ДФ=dd.MM.yyyy"));
	
	Если ЗначениеЗаполнено(Расписание.ДатаКонца) Тогда 
		ПредставлениеРасписания = ПредставлениеРасписания + 
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = ' по %1'"),
			Формат(Расписание.ДатаКонца, "ДФ=dd.MM.yyyy"));
	КонецЕсли;
	
	ПредставлениеРасписания = ПредставлениеРасписания + 
	СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = ' в %1'"), 
		Формат(Расписание.ВремяНачала, "ДФ=ЧЧ:мм; ДП=00:00"));
	
	Если Расписание.ДниНедели.Количество() > 0 Тогда  
		
		Если Расписание.ПериодНедель = 1 Тогда
			ПредставлениеРасписания = ПредставлениеРасписания + НСтр("ru = ' еженедельно в '");
		Иначе
			ПредставлениеНедель = ОбщегоНазначенияДокументооборотКлиентСервер.ПредметИсчисленияПрописью(
				Расписание.ПериодНедель, 
				НСтр("ru = 'неделю'") + "," + НСтр("ru = 'недели'") + "," + НСтр("ru = 'недель'"));
			
			ПредставлениеКаждый = ОбщегоНазначенияДокументооборотКлиентСервер.ПредметИсчисленияПрописью(
				Расписание.ПериодНедель,
				НСтр("ru = 'каждую'") + "," + НСтр("ru = 'каждые'") + "," + НСтр("ru = 'каждые'"));
				
			ПредставлениеРасписания = ПредставлениеРасписания
				+ " " + ПредставлениеКаждый
				+ " " + Расписание.ПериодНедель
				+ " " + ПредставлениеНедель
				+ НСтр("ru = ' в '");
			
		КонецЕсли;
		
		Для Каждого ДеньНедели Из Расписание.ДниНедели Цикл
			Если ДеньНедели = 1 Тогда 
				ПредставлениеРасписания = ПредставлениеРасписания + НСтр("ru = 'понедельник, '");
			КонецЕсли;
			Если ДеньНедели = 2 Тогда
				ПредставлениеРасписания = ПредставлениеРасписания + НСтр("ru = 'вторник, '");
			КонецЕсли;
			Если ДеньНедели = 3 Тогда
				ПредставлениеРасписания = ПредставлениеРасписания + НСтр("ru = 'среда, '");
			КонецЕсли;
			Если ДеньНедели = 4 Тогда
				ПредставлениеРасписания = ПредставлениеРасписания + НСтр("ru = 'четверг, '");
			КонецЕсли;
			Если ДеньНедели = 5 Тогда
				ПредставлениеРасписания = ПредставлениеРасписания + НСтр("ru = 'пятница, '");
			КонецЕсли;
			Если ДеньНедели = 6 Тогда
				ПредставлениеРасписания = ПредставлениеРасписания + НСтр("ru = 'суббота, '");
			КонецЕсли;
			Если ДеньНедели = 7 Тогда
				ПредставлениеРасписания = ПредставлениеРасписания + НСтр("ru = 'воскресенье, '");
			КонецЕсли;
		КонецЦикла;	
		ПредставлениеРасписания = Лев(ПредставлениеРасписания, СтрДлина(ПредставлениеРасписания)-2);
		
	ИначеЕсли Расписание.ДеньВМесяце <> 0 Тогда 
		
		Если Расписание.Месяцы.Количество() > 0 Тогда
			
			ПредставлениеРасписания = ПредставлениеРасписания + НСтр("ru = ' в '");
			
			Для Каждого Месяц Из Расписание.Месяцы Цикл
				Если Месяц = 1 Тогда
					ПредставлениеРасписания = ПредставлениеРасписания + НСтр("ru = 'янв, '");
				КонецЕсли;
				Если Месяц = 2 Тогда
					ПредставлениеРасписания = ПредставлениеРасписания + НСтр("ru = 'фев, '");
				КонецЕсли;
				Если Месяц = 3 Тогда
					ПредставлениеРасписания = ПредставлениеРасписания + НСтр("ru = 'мар, '")  КонецЕсли;
				Если Месяц = 4 Тогда ПредставлениеРасписания = ПредставлениеРасписания + НСтр("ru = 'апр, '");
				КонецЕсли;
				Если Месяц = 5 Тогда
					ПредставлениеРасписания = ПредставлениеРасписания + НСтр("ru = 'май, '");
				КонецЕсли;
				Если Месяц = 6 Тогда
					ПредставлениеРасписания = ПредставлениеРасписания + НСтр("ru = 'июн, '");
				КонецЕсли;
				Если Месяц = 7 Тогда
					ПредставлениеРасписания = ПредставлениеРасписания + НСтр("ru = 'июл, '");
				КонецЕсли;
				Если Месяц = 8 Тогда
					ПредставлениеРасписания = ПредставлениеРасписания + НСтр("ru = 'авг, '");
				КонецЕсли;
				Если Месяц = 9 Тогда
					ПредставлениеРасписания = ПредставлениеРасписания + НСтр("ru = 'сен, '");
				КонецЕсли;
				Если Месяц = 10 Тогда
					ПредставлениеРасписания = ПредставлениеРасписания + НСтр("ru = 'окт, '");
				КонецЕсли;
				Если Месяц = 11 Тогда
					ПредставлениеРасписания = ПредставлениеРасписания + НСтр("ru = 'ноя, '");
				КонецЕсли;
				Если Месяц = 12 Тогда
					ПредставлениеРасписания = ПредставлениеРасписания + НСтр("ru = 'дек, '");
				КонецЕсли;
			КонецЦикла;
			
		Иначе
			ПредставлениеРасписания = ПредставлениеРасписания + НСтр("ru = ' ежемесячно '");
		КонецЕсли;
		
		Если Расписание.ДеньВМесяце > 0 Тогда 
			ПредставлениеРасписания = ПредставлениеРасписания
				+ Строка(Расписание.ДеньВМесяце)
				+ НСтр("ru = '-го числа месяца'");
		Иначе
			ПредставлениеРасписания = ПредставлениеРасписания
				+ Строка(-Расписание.ДеньВМесяце)
				+ НСтр("ru = '-го дня месяца с конца'");
		КонецЕсли;
		
	Иначе
		
		Если Расписание.ПериодПовтораДней = 1 Тогда
			ПредставлениеРасписания = ПредставлениеРасписания + НСтр("ru = ' ежедневно'");
		Иначе
		
			ПредставлениеДней = ОбщегоНазначенияДокументооборотКлиентСервер.ПредметИсчисленияПрописью(
				Расписание.ПериодПовтораДней, 
				НСтр("ru = 'день'") + "," + НСтр("ru = 'дня'") + "," + НСтр("ru = 'дней'"));
			
			ПредставлениеКаждый = ОбщегоНазначенияДокументооборотКлиентСервер.ПредметИсчисленияПрописью(
				Расписание.ПериодПовтораДней,
				НСтр("ru = 'каждый'") + "," + НСтр("ru = 'каждые'") + "," + НСтр("ru = 'каждые'"));
				
			ПредставлениеРасписания = ПредставлениеРасписания
				+ " " + ПредставлениеКаждый
				+ " " + Расписание.ПериодПовтораДней
				+ " " + ПредставлениеДней;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьГрафикиРаботы")
		И ЗначениеЗаполнено(ГрафикРаботы) Тогда
		
		ОписаниеГрафикаРабот = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = ' по графику работ: ""%1""'"),
			ГрафикРаботы);
		ПредставлениеРасписания = ПредставлениеРасписания + ОписаниеГрафикаРабот;
	КонецЕсли;
	
	Возврат ПредставлениеРасписания;
	
КонецФункции	

// Возвращает необходимость повторения процесса в указанную дату по
// рассписанию и графику работы.
//
// Параметры:
//  ДатаПовторения - Дата - Дата, для которой выполняется проверка.
//  Расписание - РасписаниеРегламентногоЗадания - расписание.
//  ГрафикРаботы - СправочникСсылка.ГрафикиРаботы - ссылка на график работы.
//  ДатаПоследнегоПовторения - Дата - дата последнего повторения по расписанию.
//
// Возвращаемое значение:
//  Булево - возвращает Истину, если требуется повторение.
//
Функция НеобходимоПовторениеПроцесса(ДатаПовторения, Расписание,
	ГрафикРаботы, ДатаПоследнегоПовторения) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДатыДляПроверкиРасписания = Новый Массив;
	ДатыДляПроверкиРасписания.Добавить(ДатаПовторения);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьГрафикиРаботы")
		И ЗначениеЗаполнено(ГрафикРаботы) Тогда
		
		Если Не ГрафикиРаботы.ЭтоРабочаяДатаВремя(ГрафикРаботы, ДатаПовторения) Тогда
			Возврат Ложь;
		КонецЕсли;
		
		// Если текущий день является первым рабочим после выходных,
		// то добавляем для проверки все предыдущие выходные дни.
		// Это нужно, чтобы стартовать периодические процессы,
		// старт которых выпал на выходной день.
		
		НерабочийДень = ДатаПовторения - 86400;
		Пока Не ГрафикиРаботы.ЭтоРабочаяДатаВремя(ГрафикРаботы, НерабочийДень) Цикл
			
			Если Не ЗначениеЗаполнено(ДатаПоследнегоПовторения)
				Или НерабочийДень > ДатаПоследнегоПовторения Тогда
			
				ДатыДляПроверкиРасписания.Добавить(НерабочийДень);
			КонецЕсли;
			
			
			НерабочийДень = НерабочийДень - 86400;
		КонецЦикла;
		
	КонецЕсли;
	
	Для Каждого ПроверяемаяДата Из ДатыДляПроверкиРасписания Цикл
		Если Расписание.ТребуетсяВыполнение(ПроверяемаяДата, ДатаПоследнегоПовторения) Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

// Возвращает дату следующего повторения.
//
// Параметры:
//  Расписание - РасписаниеРегламентногоЗадания - расписание.
//  ГрафикРаботы - СправочникСсылка.ГрафикиРаботы - ссылка на график работы.
//  ДатаПоследнегоПовторения - Дата - дата последнего повторения по расписанию.
//
// Возвращаемое значение:
//  Дата
//
Функция ДатаСледующегоПовторенияПроцесса(Расписание, ГрафикРаботы, ДатаПоследнегоПовторения) Экспорт
	
	ТекущаяДата = ТекущаяДатаСеанса();
	ТекущийГод = Год(ТекущаяДата);
	
	ДатаПовторения = НачалоДня(ТекущаяДата)
		+ 3600 * Час(Расписание.ВремяНачала)
		+ 60 * Минута(Расписание.ВремяНачала)
		+ Секунда(Расписание.ВремяНачала);
	ГодПовторения = ТекущийГод;
	
	ДатаСледующегоПовторения = Дата(1,1,1);
	
	// Дату повторения ищем в текущем и следующем году, т.к.
	// самый длинный период - год и если повторение настроено,
	// однозначно найдется в текущем или следующем году.
	// Перебираем все даты, т.к. проверка (Расписание.ТребуетсяВыполнение)
	// выполняется быстро.
	
	Пока ГодПовторения <= ТекущийГод + 1 Цикл
		
		Если ДатаПовторения > ДатаПоследнегоПовторения
			И Расписание.ТребуетсяВыполнение(ДатаПовторения, ДатаПоследнегоПовторения) Тогда
			
			ДатаСледующегоПовторения = ДатаПовторения;
			Прервать;
		КонецЕсли;
		
		ДатаПовторения = ДатаПовторения + 86400;
		ГодПовторения = Год(ДатаПовторения);
		
	КонецЦикла;
	
	// При использовании графика работы корректируем найденную дату следующего повторения:
	// если дата след. повторения попадает на выходной, то ищем и возвращаем первое рабочее время.
	Если ЗначениеЗаполнено(ГрафикРаботы) Тогда
		ДатаСледующегоПовторения = ГрафикиРаботы.ПолучитьПервоеРабочееВремяДня(
			ГрафикРаботы, ДатаСледующегоПовторения);
	КонецЕсли;
	
	Возврат ДатаСледующегоПовторения;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Создает новый процесс по основанию, от заданной даты.
//
// Параметры:
//   ТекущаяДата - Дата - дата и время создания нового процесса.
//   БизнесПроцессОснование - БизнесПроцессСсылка - ссылка на процесс Основание
//
// Возвращаемое значение:
//   Булево - если процесс создан успешно возвращается Истина, иначе формируется
//            уведомление пользователю, пишется запись в журнал регистрации и
//            возвращается Ложь.
//
Функция ПовторитьБизнесПроцесс(ТекущаяДата, БизнесПроцессОснование)
	
	Результат = Истина;
	ТекстСообщения = "";
	
	НачатьТранзакцию();
	Попытка
		
		НовыйБизнесПроцесс = БизнесПроцессОснование.Скопировать();
		НовыйБизнесПроцесс.Дата = ТекущаяДата;
		НовыйБизнесПроцесс.Автор = БизнесПроцессОснование.Автор;
		
		// Расчет сроков исполнения
		ОбновитьВариантыУстановкиСроков(НовыйБизнесПроцесс);
		СрокиИсполненияПроцессов.РассчитатьСрокИсполненияДляНовогоПроцесса(НовыйБизнесПроцесс);
		
		НовыйБизнесПроцесс.Записать();
		СтартПроцессовСервер.СтартоватьПроцесс(НовыйБизнесПроцесс);
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'При автоматическом повторении процесса произошла ошибка: %1.'",
				ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		УровеньВажностиСобытия = УровеньЖурналаРегистрации.Ошибка;
		
		Результат = Ложь;
		
		РаботаСУведомлениями.ОбработатьУведомлениеПрограммы(
			ТекстСообщения,
			БизнесПроцессОснование.Автор,
			БизнесПроцессОснование);
		
	КонецПопытки;
	
	Если ПустаяСтрока(ТекстСообщения) Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Выполнено повторение процесса. Сформирован новый процесс %1.'",
				ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			Строка(НовыйБизнесПроцесс));
		УровеньВажностиСобытия = УровеньЖурналаРегистрации.Информация;
	КонецЕсли;	
	
	ЗаписьЖурналаРегистрации(
		НСтр("ru = 'Повторение процессов.'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
		УровеньВажностиСобытия, 
		БизнесПроцессОснование.Метаданные(),
		БизнесПроцессОснование, 
		ТекстСообщения);
		
	Возврат Результат;
	
КонецФункции

// Устанавливает всем срокам процесса вариант установки ОтносительныйСрок
//
// Параметры:
//  Процесс - БизнесПроцессОбъект
//
Процедура ОбновитьВариантыУстановкиСроков(Процесс)
	
	МетаданныеПроцесса = Процесс.Ссылка.Метаданные();
	
	ВариантОтносительныйСрок = 
		Перечисления.ВариантыУстановкиСрокаИсполнения.ОтносительныйСрок;
	
	Если МетаданныеПроцесса.Реквизиты.Найти("ВариантУстановкиСрокаИсполнения") <> Неопределено Тогда
		Процесс.ВариантУстановкиСрокаИсполнения = ВариантОтносительныйСрок;
	КонецЕсли;
	
	Если МетаданныеПроцесса.Реквизиты.Найти("ВариантУстановкиСрокаОбработкиРезультатов") <> Неопределено Тогда
		Процесс.ВариантУстановкиСрокаОбработкиРезультатов = ВариантОтносительныйСрок;
	КонецЕсли;
	
	Если МетаданныеПроцесса.ТабличныеЧасти.Найти("Исполнители") <> Неопределено Тогда
		Для Каждого СтрИсполнитель Из Процесс.Исполнители Цикл
			СтрИсполнитель.ВариантУстановкиСрокаИсполнения = ВариантОтносительныйСрок;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти