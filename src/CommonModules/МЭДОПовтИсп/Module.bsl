// @strict-types

#Область СлужебныйПрограммныйИнтерфейс

// Функция - Получает настройки организации для МЭДО.
//
// Параметры:
//  Организация		- ОпределяемыйТип.Организация - Организация, для которой надо получить настройки.
//  СписокНастроек	- Строка,Неопределено - Список настроек через запятую, если не указан, то получить все настройки
// 
// Возвращаемое значение:
//  Структура,Неопределено - Поля структуры соответствуют ресурсам регистра.
//
Функция ПолучитьНастройкиОрганизации(Знач Организация, СписокНастроек = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Если ПолучитьФункциональнуюОпцию("ВестиУчетПоОрганизациям") Тогда
			Возврат Неопределено;
		Иначе
			Организация = ИнтеграцияСМЭДО.ОрганизацияПоУмолчанию();
		КонецЕсли;
	КонецЕсли;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ *
		|ИЗ
		|	РегистрСведений.НастройкиОрганизацийМЭДО КАК Настройки
		|ГДЕ
		|	Настройки.Организация = &Организация");
	Если ЗначениеЗаполнено(СписокНастроек) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "*", СписокНастроек);
	КонецЕсли;
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Возврат МЭДОСтруктурыДанных.РезультатЗапросаВСтруктуру(Запрос.Выполнить());
	
КонецФункции

#КонецОбласти
