#Область ПрограммныйИнтерфейс

// Проверяет, что это не новый элемент справочника
// 
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - форма обработчика:
// 		* Объект - СправочникОбъект -
// 	ИмяФормы - Строка - имя подчиненной формы
// 	ОповещениеПослеЗаписи - ОписаниеОповещения - Описание
// 	ТекстВопроса - Строка - текст вопроса
//
Процедура ПроверитьЧтоЭтоНеНовыйЭлемент(Форма, ИмяФормы, ОповещениеПослеЗаписи, ТекстВопроса = Неопределено) Экспорт
	
	// Проверим, что это не новый элемент справочника.
	Если Не ЗначениеЗаполнено(Форма.Объект.Ссылка) Тогда
		
		Если ТекстВопроса = Неопределено Тогда
			ТекстВопроса = Нстр("ru = 'Данные еще не записаны.
			|Переход к ""%ИмяФормы%"" возможен только после записи данных.
			|Данные будут записаны.';
			|en = 'Data is not saved yet.
			|You can go to ""%ИмяФормы%"" only after saving the data.
			|Data will be saved.'");
			ТекстВопроса = СтрЗаменить(ТекстВопроса, "%ИмяФормы%", ИмяФормы);
		КонецЕсли;
		ПараметрыОповещения = Новый Структура("Оповещение, Форма", ОповещениеПослеЗаписи, Форма);
		ПоказатьВопрос(Новый ОписаниеОповещения("ПроверитьЧтоЭтоНеНовыйЭлементОбработкаОтвета", ЭтотОбъект, ПараметрыОповещения), ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
		Возврат;
		
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ОповещениеПослеЗаписи);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьЧтоЭтоНеНовыйЭлементОбработкаОтвета(КодОтвета, ДополнительныеПараметры) Экспорт
	
	Если КодОтвета = КодВозвратаДиалога.ОК Тогда 
		Форма = ДополнительныеПараметры.Форма; // РасширениеУправляемойФормыДляДокумента - 
		
		ПараметрыЗаписи = Новый Структура;
		ПараметрыЗаписи.Вставить("НеЗакрыватьФорму", Истина);
		
		ЭлементЗаписан = Форма.Записать(ПараметрыЗаписи);
		
		Если Не ЭлементЗаписан Тогда
			Возврат;
		КонецЕсли;
		
		Оповещение = ДополнительныеПараметры.Оповещение;
		
		ВыполнитьОбработкуОповещения(Оповещение);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
