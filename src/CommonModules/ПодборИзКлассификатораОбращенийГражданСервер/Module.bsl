
#Область Программый_интерфейс

// Инициализирует Кэш, использующийся при подборе из классификатора обращений граждан
//
// Параметры:
//    Форма - ФормаКлиентскогоПриложения - Форма вызова.
//
Функция ИнициализироватьКэшДанныхФормы(Форма) Экспорт
	
	// Кэш, использующийся при подборе из классификатора
	Форма.КэшДанныхФормы = Новый Структура(
		"ЭлементыКлассификатора, СтрокиДереваТематикИТем, СхемаКодаВопроса",
		Новый Соответствие,
		Новый Соответствие,
		Новый Соответствие);
		
	Форма.КэшДанныхФормы.ЭлементыКлассификатора.Вставить(0, Новый Структура("Тип, ПолеФормы, ТаблицаФормы", "Справочник.РазделыОбращений", "Раздел", "Разделы"));
	Форма.КэшДанныхФормы.ЭлементыКлассификатора.Вставить(1, Новый Структура("Тип, ПолеФормы, ТаблицаФормы", "Справочник.ТематикиОбращений", "Тематика", "Тематики"));
	Форма.КэшДанныхФормы.ЭлементыКлассификатора.Вставить(2, Новый Структура("Тип, ПолеФормы, ТаблицаФормы", "Справочник.ТемыОбращений", "Тема", "Темы"));
	Форма.КэшДанныхФормы.ЭлементыКлассификатора.Вставить(3, Новый Структура("Тип, ПолеФормы, ТаблицаФормы", "Справочник.ВопросыОбращений", "Вопрос", "Вопросы"));	
	
	Форма.КэшДанныхФормы.СхемаКодаВопроса.Вставить(Тип("СправочникСсылка.РазделыОбращений"), 0);
	Форма.КэшДанныхФормы.СхемаКодаВопроса.Вставить(Тип("СправочникСсылка.ТематикиОбращений"), 1);
	Форма.КэшДанныхФормы.СхемаКодаВопроса.Вставить(Тип("СправочникСсылка.ТемыОбращений"), 2);
	Форма.КэшДанныхФормы.СхемаКодаВопроса.Вставить(Тип("СправочникСсылка.ВопросыОбращений"), 3);	
	
КонецФункции

// Возвращает элемент классификатора по переданному коду.
//
// Параметры:
//    ТипЭлемента - Строка - название метаданных.
//    КодЭлемента - Строка - код элемента в нужном справочнике.
//
// Возвращаемое значение:
//    Ссылка на РазделОбращения, ТемуОбращения, ТематикуОбращения или ВопросОбращения, в зависимости от ТипаЭлемента.
//
Функция ПолучитьЭлементКлассификатора(ТипЭлемента, КодЭлемента) Экспорт
	
	Результат = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ТипЭлемента).ПустаяСсылка();
	
	Запрос = Новый Запрос(СтрШаблон("ВЫБРАТЬ Ссылка ИЗ %1 
		|ГДЕ Код = &КодЭлемента И Не ПометкаУдаления И РедакцияКлассификатора = &РедакцияКлассификатора", ТипЭлемента));
	Запрос.УстановитьПараметр("КодЭлемента", КодЭлемента);
	Запрос.УстановитьПараметр("РедакцияКлассификатора", ДелопроизводствоСерверПовтИсп.РедакцияКлассификатора());
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Проверяет, заполнен ли Классификатор обращений граждан.
//
// Возвращаемое значение:
//    Истина - Булево - если создан хотя бы один элемент классификатора
//    Ложь - в противном случае.
//
Функция КлассификаторОбращенийНеЗаполнен() Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	РазделыОбращений.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.РазделыОбращений КАК РазделыОбращений
		|ГДЕ
		|	НЕ РазделыОбращений.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ТематикиОбращений.Ссылка
		|ИЗ
		|	Справочник.ТематикиОбращений КАК ТематикиОбращений
		|ГДЕ
		|	НЕ ТематикиОбращений.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ТемыОбращений.Ссылка
		|ИЗ
		|	Справочник.ТемыОбращений КАК ТемыОбращений
		|ГДЕ
		|	НЕ ТемыОбращений.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ВопросыОбращений.Ссылка
		|ИЗ
		|	Справочник.ВопросыОбращений КАК ВопросыОбращений
		|ГДЕ
		|	НЕ ВопросыОбращений.ПометкаУдаления");
	
	Возврат Запрос.Выполнить().Пустой();
	
КонецФункции

#КонецОбласти

#Область Служебные_процедуры_и_функции

#КонецОбласти
