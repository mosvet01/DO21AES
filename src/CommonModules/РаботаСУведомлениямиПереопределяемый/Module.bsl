////////////////////////////////////////////////////////////////////////////////
// Модуль для работы с уведомлениями.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Фомирует список бизнес-событий, по которым необходим группировать уведомления.
//
// Параметры:
//  ВидыСобытий - Массив - Сформированные типовые виды событий для группировки.
//  ПолучательУведомления - СправочникСсылка.Пользователь - Получатель уведомления.
//  СпособУведомления - ПеречислениеСсылка.СпособыУведомления - Способ уведомления.
//
// Возвращаемое значение:
//  Массив - Виды бизнес-событий для группировки.
//
Процедура ВидыСобытийДляГруппировки(ВидыСобытий, ПолучательУведомления, СпособУведомления) Экспорт
	
	Если СпособУведомления = Перечисления.СпособыУведомления.ПоПочте Тогда
		ВидыСобытий.Добавить(Справочники.ВидыБизнесСобытий.ВозобновлениеБизнесПроцесса);
		ВидыСобытий.Добавить(Справочники.ВидыБизнесСобытий.ОстановкаБизнесПроцесса);
		ВидыСобытий.Добавить(Справочники.ВидыБизнесСобытий.ПрерываниеБизнесПроцесса);
		ВидыСобытий.Добавить(Справочники.ВидыБизнесСобытий.СозданиеСообщения);
		ВидыСобытий.Добавить(Перечисления.СобытияУведомлений.ПодошелСрокКонтроля);
		ВидыСобытий.Добавить(Перечисления.СобытияУведомлений.ПросроченКонтроль);
		ВидыСобытий.Добавить(Перечисления.СобытияУведомлений.СозданиеЗаписиКалендаря);
		ВидыСобытий.Добавить(Перечисления.СобытияУведомлений.ПросроченаКонтрольнаяТочка);
		ВидыСобытий.Добавить(Перечисления.СобытияУведомлений.ПодошелСрокКонтрольнойТочки);
		ВидыСобытий.Добавить(Перечисления.СобытияУведомлений.ПросроченаОценкаКонтрольнойТочки);
		ВидыСобытий.Добавить(Перечисления.СобытияУведомлений.ОбменПоЭДОВыполненУспешно);
		ВидыСобытий.Добавить(Перечисления.СобытияУведомлений.ОтклонениеДокументаКонтрагентомПоЭДО);
		ВидыСобытий.Добавить(Перечисления.СобытияУведомлений.ТребуетсяАннулированиеДокумента);
		ВидыСобытий.Добавить(Перечисления.СобытияУведомлений.ДокументАннулирован);
	ИначеЕсли СпособУведомления = Перечисления.СпособыУведомления.ПоSMS Тогда
		ВидыСобытий.Добавить(Справочники.ВидыБизнесСобытий.СозданиеСообщения);
		ВидыСобытий.Добавить(Перечисления.СобытияУведомлений.ПодошелСрокКонтроля);
		ВидыСобытий.Добавить(Перечисления.СобытияУведомлений.ПросроченКонтроль);
		ВидыСобытий.Добавить(Перечисления.СобытияУведомлений.СозданиеЗаписиКалендаря);
		ВидыСобытий.Добавить(Перечисления.СобытияУведомлений.ПросроченаКонтрольнаяТочка);
		ВидыСобытий.Добавить(Перечисления.СобытияУведомлений.ПодошелСрокКонтрольнойТочки);
		ВидыСобытий.Добавить(Перечисления.СобытияУведомлений.ПросроченаОценкаКонтрольнойТочки);
		ВидыСобытий.Добавить(Перечисления.СобытияУведомлений.ОбменПоЭДОВыполненУспешно);
		ВидыСобытий.Добавить(Перечисления.СобытияУведомлений.ОтклонениеДокументаКонтрагентомПоЭДО);
		ВидыСобытий.Добавить(Перечисления.СобытияУведомлений.ТребуетсяАннулированиеДокумента);
		ВидыСобытий.Добавить(Перечисления.СобытияУведомлений.ДокументАннулирован);
	ИначеЕсли СпособУведомления = Перечисления.СпособыУведомления.Окном Тогда
		ВидыСобытий.Добавить(Перечисления.СобытияУведомлений.ПодошелСрокКонтроля);
		ВидыСобытий.Добавить(Перечисления.СобытияУведомлений.ПросроченКонтроль);
		ВидыСобытий.Добавить(Перечисления.СобытияУведомлений.ПросроченаКонтрольнаяТочка);
		ВидыСобытий.Добавить(Перечисления.СобытияУведомлений.ПодошелСрокКонтрольнойТочки);
		ВидыСобытий.Добавить(Перечисления.СобытияУведомлений.ПросроченаОценкаКонтрольнойТочки);
	КонецЕсли;
	
	Если СпособУведомления = Перечисления.СпособыУведомления.ПоПочте Тогда
		
		// Учитываем глобальную настройку.
		ИспользоватьВыполнениеЗадачПоПочте = ПолучитьФункциональнуюОпцию("ИспользоватьВыполнениеЗадачПоПочте");
		
		Если ИспользоватьВыполнениеЗадачПоПочте Тогда
		
			ВключеноВыполнениеЗадачПоПочте = РегистрыСведений.НастройкиУведомлений.ПолучитьДополнительнуюНастройку(
				ПолучательУведомления,
				Перечисления.НастройкиУведомлений.ВыполнениеЗадачПоПочте);
			Если ВключеноВыполнениеЗадачПоПочте Тогда
				Индекс = ВидыСобытий.Найти(Справочники.ВидыБизнесСобытий.СозданиеЗадачи);
				Если Индекс <> Неопределено Тогда
					ВидыСобытий.Удалить(Индекс);
				КонецЕсли;
				Индекс = ВидыСобытий.Найти(Справочники.ВидыБизнесСобытий.ПеренаправлениеЗадачи);
				Если Индекс <> Неопределено Тогда
					ВидыСобытий.Удалить(Индекс);
				КонецЕсли;
			КонецЕсли;
		
		КонецЕсли;
		
	КонецЕсли;
	
	// Вызов переопределяемого метода для возможной корректировки состава вида событий для группировки.
	ВидыСобытийДляГруппировкиПереопределяемый(ВидыСобытий, ПолучательУведомления, СпособУведомления);
	
КонецПроцедуры

// Заполняет таблицу доступных уведомлений с учетом функциональных опций.
//
// Параметры:
//  ДоступныеУведомления - ТаблицаЗначений - Доступные уведомления.
//
Функция ДоступныеУведомления(ДоступныеУведомления) Экспорт
	
	СтруктураДополнительнойНастройки = Новый ФиксированнаяСтруктура("Настройка, Представление");
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьБизнесПроцессыИЗадачи") Тогда
		
		// Выполнение моей задачи
		СтрокаУведомления = ДоступныеУведомления.Добавить();
		СтрокаУведомления.ВидСобытия = Перечисления.СобытияУведомлений.ВыполнениеМоейЗадачи;
		СтрокаУведомления.ОсновноеСобытие = Справочники.ВидыБизнесСобытий.ВыполнениеЗадачи;
		СтрокаУведомления.Представление = НСтр("ru = 'Выполнение моей задачи'");
		СтрокаУведомления.ДоступнаПодписка = Истина;
		СтрокаУведомления.ДоступнаЧастота = Ложь;
		СтрокаУведомления.ДоступенСрок = Ложь;
		
		// Перенаправление моей задачи
		СтрокаУведомления = ДоступныеУведомления.Добавить();
		СтрокаУведомления.ВидСобытия = Перечисления.СобытияУведомлений.ПеренаправлениеМоейЗадачи;
		СтрокаУведомления.ОсновноеСобытие = Справочники.ВидыБизнесСобытий.ПеренаправлениеЗадачи;
		СтрокаУведомления.Представление = НСтр("ru = 'Перенаправление моей задачи'");
		СтрокаУведомления.ДоступнаПодписка = Истина;
		СтрокаУведомления.ДоступнаЧастота = Ложь;
		СтрокаУведомления.ДоступенСрок = Ложь;
		
		// Остановка и прерывание процессов.
		СтрокаУведомления = ДоступныеУведомления.Добавить();
		СтрокаУведомления.ВидСобытия = Справочники.ВидыБизнесСобытий.ПрерываниеБизнесПроцесса;
		СтрокаУведомления.Представление = НСтр("ru = 'Остановка и прерывание процессов'");
		СтрокаУведомления.ДоступнаПодписка = Истина;
		СтрокаУведомления.ДоступнаЧастота = Ложь;
		СтрокаУведомления.ДоступенСрок = Ложь;
		
		// Выполнение задач по почте.
		СтрокаУведомления = ДоступныеУведомления.Найти(Справочники.ВидыБизнесСобытий.СозданиеЗадачи, "ВидСобытия");
		Если СтрокаУведомления <> Неопределено
			И ПолучитьФункциональнуюОпцию("ИспользоватьВыполнениеЗадачПоПочте") Тогда
			
			ДополнительнаяНастройка = Новый Структура(СтруктураДополнительнойНастройки);
			ДополнительнаяНастройка.Настройка = Перечисления.НастройкиУведомлений.ВыполнениеЗадачПоПочте;
			ДополнительнаяНастройка.Представление = НСтр("ru = 'Использовать выполнение задач по почте'");
			СтрокаУведомления.ДополнительныеНастройки.Добавить(ДополнительнаяНастройка);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьОбсуждения") Тогда
		
		// Появление ответа в форуме.
		СтрокаУведомления = ДоступныеУведомления.Добавить();
		СтрокаУведомления.ВидСобытия = Справочники.ВидыБизнесСобытий.СозданиеСообщения;
		СтрокаУведомления.Представление = НСтр("ru = 'Появление ответа в форуме'");
		СтрокаУведомления.ДоступнаПодписка = Истина;
		СтрокаУведомления.ДоступнаЧастота = Ложь;
		СтрокаУведомления.ДоступенСрок = Ложь;
		
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьОтсутствия") Тогда
		
		// Создание отсутствия.
		СтрокаУведомления = ДоступныеУведомления.Добавить();
		СтрокаУведомления.ВидСобытия = Справочники.ВидыБизнесСобытий.СозданиеОтсутствия;
		СтрокаУведомления.Представление = НСтр("ru = 'Создание отсутствия'");
		СтрокаУведомления.ДоступнаПодписка = Истина;
		СтрокаУведомления.ДоступнаЧастота = Ложь;
		СтрокаУведомления.ДоступенСрок = Ложь;
		
		// Изменение отсутствия.
		СтрокаУведомления = ДоступныеУведомления.Добавить();
		СтрокаУведомления.ВидСобытия = Справочники.ВидыБизнесСобытий.ИзменениеОтсутствия;
		СтрокаУведомления.Представление = НСтр("ru = 'Изменение отсутствия'");
		СтрокаУведомления.ДоступнаПодписка = Истина;
		СтрокаУведомления.ДоступнаЧастота = Ложь;
		СтрокаУведомления.ДоступенСрок = Ложь;
		
	КонецЕсли;
	
	// Уведомления программы.
	СтрокаУведомления = ДоступныеУведомления.Добавить();
	СтрокаУведомления.ВидСобытия = Перечисления.СобытияУведомлений.УведомлениеПрограммы;
	СтрокаУведомления.Представление = НСтр("ru = 'Уведомления программы'");
	СтрокаУведомления.ДоступнаПодписка = Истина;
	СтрокаУведомления.ДоступнаЧастота = Ложь;
	СтрокаУведомления.ДоступенСрок = Ложь;
	
	Если ПолучитьФункциональнуюОпцию("ВестиУчетПоПроектам") Тогда
		
		// Уведомление об отсутствии исполнителя проектной задачи.
		СтрокаУведомления = ДоступныеУведомления.Добавить();
		СтрокаУведомления.ВидСобытия = Перечисления.СобытияУведомлений.НеУказанИсполнительПроектнойЗадачи;
		СтрокаУведомления.Представление = НСтр("ru = 'Не указан исполнитель проектной задачи'");
		СтрокаУведомления.ДоступнаПодписка = Истина;
		СтрокаУведомления.ДоступнаЧастота = Истина;
		СтрокаУведомления.ДоступенСрок = Ложь;
		
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьКонтрольОбъектов") Тогда
		
		// Уведомление о просроченном контроле.
		СтрокаУведомления = ДоступныеУведомления.Добавить();
		СтрокаУведомления.ВидСобытия = Перечисления.СобытияУведомлений.ПросроченКонтроль;
		СтрокаУведомления.Представление = НСтр("ru = 'Просрочен контрольный срок'");
		СтрокаУведомления.ДоступнаПодписка = Истина;
		СтрокаУведомления.ДоступнаЧастота = Истина;
		СтрокаУведомления.ДоступенСрок = Ложь;
		
		// Приближение контрольного срока.
		СтрокаУведомления = ДоступныеУведомления.Добавить();
		СтрокаУведомления.ВидСобытия = Перечисления.СобытияУведомлений.ПодошелСрокКонтроля;
		СтрокаУведомления.Представление = НСтр("ru = 'Приближение контрольного срока'");
		СтрокаУведомления.ДоступнаПодписка = Истина;
		СтрокаУведомления.ДоступнаЧастота = Истина;
		СтрокаУведомления.ДоступенСрок = Истина;
		
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьМониторингПроцессов") Тогда
		
		Если ПравоДоступа("Просмотр", Метаданные.Справочники.ПоказателиПроцессов) Тогда
			
			// Уведомление об изменении занчения показателя процессов.
			СтрокаУведомления = ДоступныеУведомления.Добавить();
			СтрокаУведомления.ВидСобытия = Справочники.ВидыБизнесСобытий.ИзменениеЗначенияПоказателяПроцесса;
			СтрокаУведомления.Представление = НСтр("ru = 'Изменение значения показателя процесса'");
			СтрокаУведомления.ДоступнаПодписка = Истина;
			СтрокаУведомления.ДоступнаЧастота = Ложь;
			СтрокаУведомления.ДоступенСрок = Ложь;
			
			// Процентное изменение показателя.
			ДополнительнаяНастройка = Новый Структура(СтруктураДополнительнойНастройки);
			ДополнительнаяНастройка.Настройка = Перечисления.НастройкиУведомлений.ПроцентноеИзменение;
			ДополнительнаяНастройка.Представление = НСтр("ru = 'Процентное изменение показателя'");
			СтрокаУведомления.ДополнительныеНастройки.Добавить(ДополнительнаяНастройка);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьРабочийКалендарь") Тогда
		
		// Создание записи календаря.
		СтрокаУведомления = ДоступныеУведомления.Добавить();
		СтрокаУведомления.ВидСобытия = Перечисления.СобытияУведомлений.СозданиеЗаписиКалендаря;
		СтрокаУведомления.Представление = НСтр("ru = 'Новая запись календаря'");
		СтрокаУведомления.ДоступнаПодписка = Истина;
		СтрокаУведомления.ДоступнаЧастота = Ложь;
		СтрокаУведомления.ДоступенСрок = Ложь;
		
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьБронированиеПомещений") Тогда
		
		// Создание брони.
		СтрокаУведомления = ДоступныеУведомления.Добавить();
		СтрокаУведомления.ВидСобытия = Справочники.ВидыБизнесСобытий.СозданиеБрони;
		СтрокаУведомления.Представление = НСтр("ru = 'Создание брони'");
		СтрокаУведомления.ДоступнаПодписка = Истина;
		СтрокаУведомления.ДоступнаЧастота = Ложь;
		СтрокаУведомления.ДоступенСрок = Ложь;
		
		// Изменение брони.
		СтрокаУведомления = ДоступныеУведомления.Добавить();
		СтрокаУведомления.ВидСобытия = Справочники.ВидыБизнесСобытий.ИзменениеБрони;
		СтрокаУведомления.Представление = НСтр("ru = 'Изменение брони'");
		СтрокаУведомления.ДоступнаПодписка = Истина;
		СтрокаУведомления.ДоступнаЧастота = Ложь;
		СтрокаУведомления.ДоступенСрок = Ложь;
		
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьКонтрольныеТочки") Тогда
		
		// Уведомление о просроченном контроле.
		СтрокаУведомления = ДоступныеУведомления.Добавить();
		СтрокаУведомления.ВидСобытия = Перечисления.СобытияУведомлений.ПросроченаКонтрольнаяТочка;
		СтрокаУведомления.Представление = НСтр("ru = 'Просрочена контрольная точка'");
		СтрокаУведомления.ДоступнаПодписка = Истина;
		СтрокаУведомления.ДоступнаЧастота = Истина;
		СтрокаУведомления.ДоступенСрок = Ложь;
		
		// Приближение контрольного срока.
		СтрокаУведомления = ДоступныеУведомления.Добавить();
		СтрокаУведомления.ВидСобытия = Перечисления.СобытияУведомлений.ПодошелСрокКонтрольнойТочки;
		СтрокаУведомления.Представление = НСтр("ru = 'Приближение контрольной точки'");
		СтрокаУведомления.ДоступнаПодписка = Истина;
		СтрокаУведомления.ДоступнаЧастота = Истина;
		СтрокаУведомления.ДоступенСрок = Истина;
		
		// Приближение контрольного срока.
		СтрокаУведомления = ДоступныеУведомления.Добавить();
		СтрокаУведомления.ВидСобытия = Перечисления.СобытияУведомлений.ПросроченаОценкаКонтрольнойТочки;
		СтрокаУведомления.Представление = НСтр("ru = 'Просрочена оценка контрольной точки'");
		СтрокаУведомления.ДоступнаПодписка = Истина;
		СтрокаУведомления.ДоступнаЧастота = Истина;
		СтрокаУведомления.ДоступенСрок = Ложь;
		
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьОбменЭД") Тогда
		
		// Обмен по ЭДО выполнен успешно.
		СтрокаУведомления = ДоступныеУведомления.Добавить();
		СтрокаУведомления.ВидСобытия = Перечисления.СобытияУведомлений.ОбменПоЭДОВыполненУспешно;
		СтрокаУведомления.ОсновноеСобытие = Справочники.ВидыБизнесСобытий.ИзменениеСостоянияДокументаПоЭДО;
		СтрокаУведомления.Представление = НСтр("ru = 'Успешно выполнен обмен по ЭДО'");
		СтрокаУведомления.ДоступнаПодписка = Истина;
		СтрокаУведомления.ДоступнаЧастота = Ложь;
		СтрокаУведомления.ДоступенСрок = Ложь;
		
		// Отклонение документа контрагентом по ЭДО.
		СтрокаУведомления = ДоступныеУведомления.Добавить();
		СтрокаУведомления.ВидСобытия = Перечисления.СобытияУведомлений.ОтклонениеДокументаКонтрагентомПоЭДО;
		СтрокаУведомления.ОсновноеСобытие = Справочники.ВидыБизнесСобытий.ИзменениеСостоянияДокументаПоЭДО;
		СтрокаУведомления.Представление = НСтр("ru = 'Отклонение документа контрагентом по ЭДО'");
		СтрокаУведомления.ДоступнаПодписка = Истина;
		СтрокаУведомления.ДоступнаЧастота = Ложь;
		СтрокаУведомления.ДоступенСрок = Ложь;
		
		// Требуется аннулирование документа
		СтрокаУведомления = ДоступныеУведомления.Добавить();
		СтрокаУведомления.ВидСобытия = Перечисления.СобытияУведомлений.ТребуетсяАннулированиеДокумента;
		СтрокаУведомления.ОсновноеСобытие = Справочники.ВидыБизнесСобытий.ИзменениеСостоянияДокументаПоЭДО;
		СтрокаУведомления.Представление = НСтр("ru = 'Требуется аннулирование документа'");
		СтрокаУведомления.ДоступнаПодписка = Истина;
		СтрокаУведомления.ДоступнаЧастота = Ложь;
		СтрокаУведомления.ДоступенСрок = Ложь;
		
		// Документ аннулирован
		СтрокаУведомления = ДоступныеУведомления.Добавить();
		СтрокаУведомления.ВидСобытия = Перечисления.СобытияУведомлений.ДокументАннулирован;
		СтрокаУведомления.ОсновноеСобытие = Справочники.ВидыБизнесСобытий.ИзменениеСостоянияДокументаПоЭДО;
		СтрокаУведомления.Представление = НСтр("ru = 'Документ аннулирован'");
		СтрокаУведомления.ДоступнаПодписка = Истина;
		СтрокаУведомления.ДоступнаЧастота = Ложь;
		СтрокаУведомления.ДоступенСрок = Ложь;
		
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьОтчетностьВКонтролирующиеОрганы") Тогда
		
		СтрокаУведомления = ДоступныеУведомления.Добавить();
		СтрокаУведомления.ВидСобытия = Справочники.ВидыБизнесСобытий.ИзменениеСостоянияДокументаОбменСКО;
		СтрокаУведомления.Представление = НСтр("ru = 'Изменение состояния документа (обмен с контролирующими органами)'");
		СтрокаУведомления.ДоступнаПодписка = Истина;
		СтрокаУведомления.ДоступнаЧастота = Ложь;
		СтрокаУведомления.ДоступенСрок = Ложь;
		
	КонецЕсли;
	
КонецФункции

// Формирует текстовое представление задачи
Функция СформироватьПредставлениеЗадачи(
	Задача,
	ВидСобытия,
	ДополнительноеОписание,
	КодЯзыка) Экспорт
	
	ИспользоватьДатуИВремяВСрокахЗадач = ПолучитьФункциональнуюОпцию("ИспользоватьДатуИВремяВСрокахЗадач");
	НадписьНеУказанСрок = НСтр("ru = 'не указан'", КодЯзыка);
	Если ИспользоватьДатуИВремяВСрокахЗадач Тогда
		ФорматДаты = СтрШаблон("ДЛФ=DDT; ДП='%1'", НадписьНеУказанСрок);
	Иначе
		ФорматДаты = СтрШаблон("ДЛФ=DD; ДП='%1'", НадписьНеУказанСрок);
	КонецЕсли;
	
	ПредставлениеЗадачи = "";
	
	РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
		ПредставлениеЗадачи,
		НСтр("ru = 'Задача'", КодЯзыка),
		Задача.Ссылка);
	
	Если ЗначениеЗаполнено(Задача.БизнесПроцесс)
		И РаботаСУведомлениями.ЭтоПоддерживаемыйБизнесПроцесс(Задача.БизнесПроцесс) Тогда
		
		БизнесПроцессОбъект = Задача.БизнесПроцесс.ПолучитьОбъект();
		Если БизнесПроцессОбъект <> Неопределено Тогда
			
			ОписаниеУведомленияЗадачиПоБизнесПроцессу =
				БизнесПроцессОбъект.ПолучитьОписаниеУведомленияЗадачи(Задача, КодЯзыка);
			
			Если ЗначениеЗаполнено(ОписаниеУведомленияЗадачиПоБизнесПроцессу) Тогда
				ДобавитьЗначениеКСтрокеЧерезРазделитель(
					ПредставлениеЗадачи,
					Символы.ПС,
					ОписаниеУведомленияЗадачиПоБизнесПроцессу);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ВидСобытия = Перечисления.СобытияУведомлений.ПодошелСрокЗадачи
		Или ВидСобытия = Перечисления.СобытияУведомлений.ПросроченаЗадача
		Или ВидСобытия = Перечисления.СобытияУведомлений.ПросроченаЗадачаАвтора Тогда
		
		ПредставлениеСрока = РаботаСУведомлениями.ПолучитьПредставлениеСрока(
			Задача.Исполнитель,
			Задача.СрокИсполнения,
			ВидСобытия,
			КодЯзыка);
		
		ДобавитьЗначениеКСтрокеЧерезРазделитель(
			ПредставлениеЗадачи,
			Символы.ПС,
			ПредставлениеСрока);
		
	КонецЕсли;
	
	Если ВидСобытия = Справочники.ВидыБизнесСобытий.ПеренаправлениеЗадачи
		Или ВидСобытия = Перечисления.СобытияУведомлений.ПеренаправлениеМоейЗадачи Тогда
		
		Если ЗначениеЗаполнено(ДополнительноеОписание) Тогда
			ДобавитьЗначениеКСтрокеЧерезРазделитель(
				ПредставлениеЗадачи,
				Символы.ПС,
				ДополнительноеОписание);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ВидСобытия <> Справочники.ВидыБизнесСобытий.ВыполнениеЗадачи Тогда
		РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
			ПредставлениеЗадачи,
			НСтр("ru = 'Описание'", КодЯзыка),
			Задача.Описание);
	КонецЕсли;
	
	РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
		ПредставлениеЗадачи,
		НСтр("ru = 'Крайний срок'", КодЯзыка),
		Формат(Задача.СрокИсполнения, ФорматДаты));
	РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
		ПредставлениеЗадачи,
		НСтр("ru = 'Исполнитель'", КодЯзыка),
		Задача.Исполнитель);
	РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
		ПредставлениеЗадачи,
		НСтр("ru = 'Роль'", КодЯзыка),
		Задача.РольИсполнителя);
	
	Если ЗначениеЗаполнено(Задача.ДатаИсполнения) И Задача.Выполнена Тогда
		РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
			ПредставлениеЗадачи,
			НСтр("ru = 'Выполнено'", КодЯзыка),
			Формат(Задача.ДатаИсполнения, ФорматДаты));
	КонецЕсли;
	
	РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
		ПредставлениеЗадачи,
		НСтр("ru = 'Комментарий'", КодЯзыка),
		Задача.РезультатВыполнения);
	РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
		ПредставлениеЗадачи,
		НСтр("ru = 'Автор'", КодЯзыка),
		Задача.Автор);
	
	Предметы = МультипредметностьКлиентСервер.ПолучитьМассивСтруктурПредметовОбъекта(Задача);
	Если Предметы.Количество() = 1 Тогда
		РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
			ПредставлениеЗадачи,
			НСтр("ru = 'Предмет'", КодЯзыка),
			Строка(Предметы[0].Предмет));
	ИначеЕсли Предметы.Количество() > 1 Тогда
		РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
			ПредставлениеЗадачи,
			НСтр("ru = 'Предметы'", КодЯзыка),
			МультипредметностьКлиентСервер.ПредметыСтрокой(Предметы));
	КонецЕсли;
	
	РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
		ПредставлениеЗадачи,
		НСтр("ru = 'Ссылка'", КодЯзыка),
		РаботаСУведомлениями.ПолучитьНавигационнуюСсылкуУведомления(Задача));
	
	Возврат ПредставлениеЗадачи;
	
КонецФункции

// Формирует текстовое представление объекта.
//
// Параметры:
//  Объект - СправочникСсылка, ДокументСсылка - Объект, по которому формируется уведомление.
//  ВидСобытия - СправочникСсылка.ВидыБизнесСобытий, ПеречислениеСсылка.СобытияУведомлений - Событие, произошедшее с объектом.
//  ПолучательУведомления - СправочникСсылка.Пользователи - Получатель уведомления.
//  ДополнительноеОписание - Строка - Дополнительное описание.
// 
// Возвращаемое значение:
//  Строка - Текстовое представление объекта.
//
Функция СформироватьПредставлениеОбъекта(Объект, ВидСобытия, ПолучательУведомления, ДополнительноеОписание) Экспорт
	
	ПредставлениеОбъекта = СформироватьПредставлениеОбъектаПереопределяемый(
		Объект,
		ВидСобытия,
		ПолучательУведомления,
		ДополнительноеОписание);
	Если ЗначениеЗаполнено(ПредставлениеОбъекта) Тогда
		Возврат ПредставлениеОбъекта;
	КонецЕсли;
	
	КодЯзыка = РаботаСУведомлениями.КодЯзыка(ПолучательУведомления);
	Если ТипЗнч(Объект) = Тип("СправочникСсылка.СообщенияОбсуждений") Тогда
		
		ПредставлениеОбъекта = СформироватьПредставлениеСообщения(
			Объект,
			ВидСобытия,
			ДополнительноеОписание,
			КодЯзыка);
		
	ИначеЕсли ТипЗнч(Объект) = Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда
		
		ПредставлениеОбъекта = СформироватьПредставлениеЗадачи(
			Объект,
			ВидСобытия,
			ДополнительноеОписание,
			КодЯзыка);
		
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.Контроль") Тогда
		
		ПредставлениеОбъекта = СформироватьПредставлениеКонтрольнойКарточки(
			Объект,
			ВидСобытия,
			ДополнительноеОписание,
			КодЯзыка);
		
	ИначеЕсли ТипЗнч(Объект) = Тип("ДокументСсылка.Отсутствие") Тогда
		
		ПредставлениеОбъекта = СформироватьПредставлениеОтсутствия(
			Объект,
			ВидСобытия,
			ДополнительноеОписание,
			КодЯзыка);
		
	ИначеЕсли ТипЗнч(Объект) = Тип("ДокументСсылка.Бронь") Тогда
		
		ПредставлениеОбъекта = СформироватьПредставлениеБрони(
			Объект,
			ВидСобытия,
			ДополнительноеОписание,
			КодЯзыка);
		
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.ПоказателиПроцессов") Тогда
		
		ПредставлениеОбъекта = СформироватьПредставлениеПоказателяПроцесса(
			Объект,
			ВидСобытия,
			ДополнительноеОписание,
			КодЯзыка);
		
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.ЗаписиРабочегоКалендаря") Тогда
		
		ПредставлениеОбъекта = СформироватьПредставлениеЗаписиКалендаря(
			Объект,
			ВидСобытия,
			ДополнительноеОписание,
			КодЯзыка);
		
	ИначеЕсли РаботаСУведомлениями.ЭтоПоддерживаемыйБизнесПроцесс(Объект) Тогда
		
		ПредставлениеОбъекта = СформироватьПредставлениеПроцесса(
			Объект,
			ВидСобытия,
			ДополнительноеОписание,
			КодЯзыка);
		
	ИначеЕсли ВидСобытия = Перечисления.СобытияУведомлений.УведомлениеПрограммы Тогда
		
		ПредставлениеОбъекта = СформироватьПредставлениеУведомленияПрограммы(Объект, ДополнительноеОписание, КодЯзыка);
		
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.КонтрольныеТочки") Тогда
		
		ПредставлениеОбъекта = СформироватьПредставлениеКонтрольнойТочки(
			Объект,
			ВидСобытия,
			ДополнительноеОписание,
			КодЯзыка);
		
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.ВнутренниеДокументы") Тогда
		
		ПредставлениеОбъекта = СформироватьПредставлениеВнутреннегоДокумента(
			Объект,
			ВидСобытия,
			ДополнительноеОписание,
			КодЯзыка);
		
	КонецЕсли;
	
	Возврат ПредставлениеОбъекта
	
КонецФункции

// Формирует текст уведомления по событию с группировкой.
//
// Параметры:
//  ОбъектПодписки - СправочникСсылка, ДокументСсылка - Объект подписки формируемого уведомления.
//  ВидСобытия - СправочникСсылка.ВидыБизнесСобытий, ПеречислениеСсылка.СобытияУведомлений - Вид события формируемого уведомления.
//  ОбъектыУведомления - Массив - Объекты формируемого уведомления.
//  ПолучательУведомления - СправочникСсылка.Пользователи - Получатель формируемого уведомления.
//  СпособУведомления - ПеречислениеСсылка.СпособыУведомления - Способ формируемого уведомления.
//  ДополнительныеОписания - Соответствие - Дополнительные описания.
// 
// Возвращаемое значение:
//  Строка - Текст уведомления.
//
Функция СформироватьТекстУведомленияПоОбъектамУведомления(
	ОбъектПодписки,
	ВидСобытия,
	ОбъектыУведомления,
	ПолучательУведомления,
	СпособУведомления,
	ДополнительныеОписания) Экспорт
	
	ТекстУведомления = СформироватьТекстУведомленияПоОбъектамУведомленияПереопределяемый(
		ОбъектПодписки,
		ВидСобытия,
		ОбъектыУведомления,
		ПолучательУведомления,
		СпособУведомления,
		ДополнительныеОписания);
	Если ЗначениеЗаполнено(ТекстУведомления) Тогда
		Возврат ТекстУведомления;
	КонецЕсли;
	
	Если ВидСобытия = Перечисления.СобытияУведомлений.ПодошелСрокКонтроля Тогда
		
		Для Каждого ОбъектУведомления Из ОбъектыУведомления Цикл
			
			ДополнительноеОписание = ОбъектыУведомления.Количество();
			
			ПредставлениеОбъекта = РаботаСУведомлениями.СформироватьПредставлениеОбъекта(
				ОбъектУведомления,
				ВидСобытия,
				ПолучательУведомления,
				ДополнительноеОписание);
			
			ДобавитьЗначениеКСтрокеЧерезРазделитель(
				ТекстУведомления,
				Символы.ПС + Символы.ПС,
				ПредставлениеОбъекта);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ТекстУведомления;
	
КонецФункции

// Формирует текст уведомления по событию без группировки.
//
// Параметры:
//  ОбъектПодписки - СправочникСсылка, ДокументСсылка - Объект подписки формируемого уведомления.
//  ВидСобытия - СправочникСсылка.ВидыБизнесСобытий, ПеречислениеСсылка.СобытияУведомлений - Вид события формируемого уведомления.
//  ОбъектУведомления - СправочникСсылка, ДокументСсылка - Объект формируемого уведомления.
//  ПолучательУведомления - СправочникСсылка.Пользователи - Получатель формируемого уведомления.
//  СпособУведомления - ПеречислениеСсылка.СпособыУведомления - Способ формируемого уведомления.
//  ДополнительноеОписание - Строка - Дополнительное описание.
// 
// Возвращаемое значение:
//  Строка - Текст уведомления.
//
Функция СформироватьТекстУведомленияПоСобытию(
	ОбъектПодписки,
	ВидСобытия,
	ОбъектУведомления,
	ПолучательУведомления,
	СпособУведомления,
	ДополнительноеОписание) Экспорт
	
	ТекстУведомления = СформироватьТекстУведомленияПоСобытиюПереопределяемый(
		ОбъектПодписки,
		ВидСобытия,
		ОбъектУведомления,
		ПолучательУведомления,
		СпособУведомления,
		ДополнительноеОписание);
	Если ЗначениеЗаполнено(ТекстУведомления) Тогда
		Возврат ТекстУведомления;
	КонецЕсли;
	
	Если СпособУведомления = Перечисления.СпособыУведомления.ПоПочте
		И (ВидСобытия = Справочники.ВидыБизнесСобытий.СозданиеЗадачи
		Или ВидСобытия = Справочники.ВидыБизнесСобытий.ПеренаправлениеЗадачи) Тогда
		
		// Учитываем глобальную настройку.
		ИспользоватьВыполнениеЗадачПоПочте = ПолучитьФункциональнуюОпцию("ИспользоватьВыполнениеЗадачПоПочте");
		
		Если ИспользоватьВыполнениеЗадачПоПочте Тогда
			
			ВключеноВыполнениеЗадачПоПочте = РегистрыСведений.НастройкиУведомлений.ПолучитьДополнительнуюНастройку(
				ПолучательУведомления,
				Перечисления.НастройкиУведомлений.ВыполнениеЗадачПоПочте);
			Если ВключеноВыполнениеЗадачПоПочте
				И ВыполнениеЗадачПоПочтеСервер.ВозможноВыполнениеЗадачиПоПочте(ОбъектУведомления) Тогда
				
				ТекстУведомления = 
					ВыполнениеЗадачПоПочтеСервер.СформироватьТекстУведомленияПоЗадачеСВозможностьюВыполненияПоПочте(
						ОбъектУведомления, ПолучательУведомления);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если СпособУведомления = Перечисления.СпособыУведомления.ПоPush Тогда
		ПредставлениеОбъекта = РаботаСУведомлениями.СформироватьПредставлениеОбъекта(
			ОбъектУведомления,
			ВидСобытия,
			ПолучательУведомления,
			ДополнительноеОписание);
		ТекстУведомления = 
			СтрШаблон(
				НСтр("ru = 'Событие: %1'", РаботаСУведомлениями.КодЯзыка(ПолучательУведомления)),
				ВидСобытия)
			+ Символы.ПС
			+ ПредставлениеОбъекта;
		
	ИначеЕсли ВидСобытия = Перечисления.СобытияУведомлений.УведомлениеПрограммы Тогда
		
		КодЯзыка = РаботаСУведомлениями.КодЯзыка(ПолучательУведомления);
		Если ЗначениеЗаполнено(ДополнительноеОписание) Тогда
			ТекстУведомления = ДополнительноеОписание;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ОбъектУведомления) И СпособУведомления <> Перечисления.СпособыУведомления.Окном Тогда
			РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
				ТекстУведомления,
				НСтр("ru = 'Ссылка'", КодЯзыка),
				РаботаСУведомлениями.ПолучитьНавигационнуюСсылкуУведомления(ОбъектУведомления));
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ТекстУведомления) Тогда
			ТекстУведомления = НСтр("ru = 'Уведомление 1С Документооборот'", КодЯзыка);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ТекстУведомления;
	
КонецФункции

// Формирует тему уведомления по событию с группировкой.
//
// Параметры:
//  ОбъектПодписки - СправочникСсылка - Объект подписки формируемого уведомления.
//  ВидСобытия - СправочникСсылка.ВидыБизнесСобытий, ПеречислениеСсылка.СобытияУведомлений - Вид события формируемого уведомления.
//  ОбъектыУведомления - Массив - Объекты формируемого уведомления.
//  ПолучательУведомления - СправочникСсылка.Пользователи - Получатель формируемого уведомления.
//  СпособУведомления - ПеречислениеСсылка.СпособыУведомления - Способ формируемого уведомления.
//  ДополнительныеОписания - Соответствие - Дополнительные описания.
// 
// Возвращаемое значение:
//  Строка - Тема уведомления.
//
Функция СформироватьТемуУведомленияПоОбъектамУведомления(
	ОбъектПодписки,
	ВидСобытия,
	ОбъектыУведомления,
	ПолучательУведомления,
	СпособУведомления,
	ДополнительныеОписания) Экспорт
	
	ТемаУведомления = СформироватьТемуУведомленияПоОбъектамУведомленияПереопределяемый(
		ОбъектПодписки,
		ВидСобытия,
		ОбъектыУведомления,
		ПолучательУведомления,
		СпособУведомления,
		ДополнительныеОписания);
	Если ЗначениеЗаполнено(ТемаУведомления) Тогда
		Возврат ТемаУведомления;
	КонецЕсли;
	
	КодЯзыка = РаботаСУведомлениями.КодЯзыка(ПолучательУведомления);
	Если ВидСобытия = Справочники.ВидыБизнесСобытий.СозданиеФайла Тогда
		
		ПредставлениеОбъектаПодписки = "";
		Если ТипЗнч(ОбъектПодписки) = Тип("СправочникСсылка.Мероприятия") Тогда
			ПредставлениеОбъектаПодписки = СтрШаблон(
				НСтр("ru = 'в мероприятии ""%1""'", КодЯзыка),
				ОбъектПодписки.Ссылка);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ПредставлениеОбъектаПодписки) Тогда
			Если ОбъектыУведомления.Количество() = 1 Тогда
				ОбъектУведомления = ОбъектыУведомления[0];
				ТемаУведомления = СтрШаблон(
					НСтр("ru = 'Новый файл ""%1"" %2'", КодЯзыка),
					ОбъектУведомления.Ссылка,
					ПредставлениеОбъектаПодписки);
			Иначе
				КоличествоОбъектовУведомления = ОбъектыУведомления.Количество();
				ТемаУведомления = СтрШаблон(
					НСтр("ru = 'Новые файлы (%1) %2'", КодЯзыка),
					КоличествоОбъектовУведомления,
					ПредставлениеОбъектаПодписки);
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ВидСобытия = Справочники.ВидыБизнесСобытий.СозданиеСообщения
		Или ВидСобытия = Перечисления.СобытияУведомлений.СозданиеТемыФорума Тогда
		
		Если ОбъектыУведомления[0].ПервоеСообщениеТемы Тогда
			
			ТемаСообщения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбъектыУведомления[0], "ВладелецСообщения");
			РазделФорума = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТемаСообщения, "Папка");
			ТемаУведомления = СтрШаблон(
				НСтр("ru = 'Новая тема ""%1"" в разделе ""%2""'", КодЯзыка),
				ТемаСообщения,
				РазделФорума);
			
		Иначе
			
			Если ТипЗнч(ОбъектПодписки) = Тип("СправочникСсылка.СообщенияОбсуждений") Тогда
				ТемаСообщения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбъектПодписки.Ссылка, "ВладелецСообщения");
				ПредставлениеОбъектаПодписки = СтрШаблон(
					НСтр("ru = 'в теме ""%1""'", КодЯзыка),
					ТемаСообщения);
			ИначеЕсли ТипЗнч(ОбъектПодписки) = Тип("СправочникСсылка.ТемыОбсуждений") Тогда
				ПредставлениеОбъектаПодписки = СтрШаблон(
					НСтр("ru = 'в теме ""%1""'", КодЯзыка),
					ОбъектПодписки.Ссылка);
			ИначеЕсли ТипЗнч(ОбъектПодписки) = Тип("СправочникСсылка.ПапкиФорума") Тогда
				ТемаСообщения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбъектыУведомления[0], "ВладелецСообщения");
				ПредставлениеОбъектаПодписки = СтрШаблон(
					НСтр("ru = 'в теме ""%1""'", КодЯзыка),
					ТемаСообщения);
			Иначе
				ПредставлениеОбъектаПодписки = СтрШаблон(
					НСтр("ru = 'в обсуждении ""%1""'", КодЯзыка),
					ОбъектПодписки.Ссылка);
			КонецЕсли;
			ТемаУведомления = СтрШаблон(
				НСтр("ru = 'Новое сообщение %1'", КодЯзыка),
				ПредставлениеОбъектаПодписки);
			
		КонецЕсли;
		
	ИначеЕсли ВидСобытия = Перечисления.СобытияУведомлений.ПодошелСрокКонтроля Тогда
		
		Если ОбъектыУведомления.Количество() = 1 Тогда
			ОбъектУведомления = ОбъектыУведомления[0];
			ПредставлениеСрока = РаботаСУведомлениями.ПолучитьПредставлениеСрока(
				ОбъектУведомления.Контролер, 
				ОбъектУведомления.СрокИсполнения, 
				ВидСобытия,
				КодЯзыка,
				Истина);
			ТемаУведомления = СтрШаблон(
				"%1: ""%2""",
				ПредставлениеСрока,
				ОбъектУведомления.Наименование);
		Иначе
			КоличествоОбъектовУведомления = ОбъектыУведомления.Количество();
			ТемаУведомления = СтрШаблон(
				НСтр("ru = 'Подошел срок контроля (%1)'"),
				КоличествоОбъектовУведомления);
		КонецЕсли;
		
	ИначеЕсли ВидСобытия = Перечисления.СобытияУведомлений.ПросроченКонтроль Тогда
		
		Если ОбъектыУведомления.Количество() = 1 Тогда
			ОбъектУведомления = ОбъектыУведомления[0];
			ПредставлениеСрока = РаботаСУведомлениями.ПолучитьПредставлениеСрока(
				ОбъектУведомления.Контролер, 
				ОбъектУведомления.СрокИсполнения, 
				ВидСобытия,
				КодЯзыка,
				Истина);
			ТемаУведомления = СтрШаблон(
				"%1: ""%2""",
				ПредставлениеСрока,
				ОбъектУведомления.Наименование);
		Иначе
			КоличествоОбъектовУведомления = ОбъектыУведомления.Количество();
			ТемаУведомления = СтрШаблон(
				НСтр("ru = 'Истекли сроки контроля (%1)'"),
				КоличествоОбъектовУведомления);
		КонецЕсли;
		
	ИначеЕсли ВидСобытия = Справочники.ВидыБизнесСобытий.ПрерываниеБизнесПроцесса Тогда
		
		ОбъектУведомления = ОбъектыУведомления[0];
		Если ОбъектыУведомления.Количество() = 1 Тогда
			Если ТипЗнч(ОбъектУведомления) = Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда
				ТемаУведомления = НСтр("ru = 'Задача прервана ""%1""'", КодЯзыка);
			Иначе
				ТемаУведомления = НСтр("ru = 'Процесс прерван ""%1""'", КодЯзыка);
			КонецЕсли;
			ТемаУведомления = СтрШаблон(
				ТемаУведомления,
				ОбъектУведомления.Ссылка);
		Иначе
			Если ТипЗнч(ОбъектУведомления) = Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда
				ТемаУведомления = НСтр("ru = 'Задачи прерваны (%1)'", КодЯзыка);
			Иначе
				ТемаУведомления = НСтр("ru = 'Процессы прерваны (%1)'", КодЯзыка);
			КонецЕсли;
			КоличествоОбъектовУведомления = ОбъектыУведомления.Количество();
			ТемаУведомления = СтрШаблон(
				ТемаУведомления,
				КоличествоОбъектовУведомления);
		КонецЕсли;
		
	ИначеЕсли ВидСобытия = Справочники.ВидыБизнесСобытий.ОстановкаБизнесПроцесса Тогда
		
		ОбъектУведомления = ОбъектыУведомления[0];
		Если ОбъектыУведомления.Количество() = 1 Тогда
			Если ТипЗнч(ОбъектУведомления) = Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда
				ТемаУведомления = НСтр("ru = 'Задача остановлена ""%1""'", КодЯзыка);
			Иначе
				ТемаУведомления = НСтр("ru = 'Процесс остановлен ""%1""'", КодЯзыка);
			КонецЕсли;
			ТемаУведомления = СтрШаблон(
				ТемаУведомления,
				ОбъектУведомления.Ссылка);
		Иначе
			Если ТипЗнч(ОбъектУведомления) = Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда
				ТемаУведомления = НСтр("ru = 'Задачи остановлены (%1)'", КодЯзыка);
			Иначе
				ТемаУведомления = НСтр("ru = 'Процессы остановлены (%1)'", КодЯзыка);
			КонецЕсли;
			КоличествоОбъектовУведомления = ОбъектыУведомления.Количество();
			ТемаУведомления = СтрШаблон(
				ТемаУведомления,
				КоличествоОбъектовУведомления);
		КонецЕсли;
		
	ИначеЕсли ВидСобытия = Справочники.ВидыБизнесСобытий.ВозобновлениеБизнесПроцесса Тогда
		
		ОбъектУведомления = ОбъектыУведомления[0];
		Если ОбъектыУведомления.Количество() = 1 Тогда
			Если ТипЗнч(ОбъектУведомления) = Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда
				ТемаУведомления = НСтр("ru = 'Задача возобновлена ""%1""'", КодЯзыка);
			Иначе
				ТемаУведомления = НСтр("ru = 'Процесс возобновлен ""%1""'", КодЯзыка);
			КонецЕсли;
			ТемаУведомления = СтрШаблон(
				ТемаУведомления,
				ОбъектУведомления.Ссылка);
		Иначе
			Если ТипЗнч(ОбъектУведомления) = Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда
				ТемаУведомления = НСтр("ru = 'Задачи возобновлены (%1)'", КодЯзыка);
			Иначе
				ТемаУведомления = НСтр("ru = 'Процессы возобновлены (%1)'", КодЯзыка);
			КонецЕсли;
			КоличествоОбъектовУведомления = ОбъектыУведомления.Количество();
			ТемаУведомления = СтрШаблон(
				ТемаУведомления,
				КоличествоОбъектовУведомления);
		КонецЕсли;
		
	ИначеЕсли ВидСобытия = Перечисления.СобытияУведомлений.УведомлениеПрограммы Тогда
		
		ТемаУведомления = НСтр("ru = 'Уведомление 1С Документооборот'", КодЯзыка);
		
	ИначеЕсли ВидСобытия = Перечисления.СобытияУведомлений.СозданиеЗаписиКалендаря Тогда
		
		Если ТипЗнч(ОбъектПодписки) = Тип("СправочникСсылка.Пользователи") Тогда
			Если ОбъектыУведомления.Количество() = 1 Тогда
				ОбъектУведомления = ОбъектыУведомления[0];
				ТемаУведомления = СтрШаблон(
					НСтр("ru = 'Новая запись в календаре пользователя ""%1""'", КодЯзыка),
					ОбъектПодписки);
			Иначе
				КоличествоОбъектовУведомления = ОбъектыУведомления.Количество();
				ТемаУведомления = СтрШаблон(
					НСтр("ru = 'Новые записи (%1) в календаре пользователя ""%2""'", КодЯзыка),
					КоличествоОбъектовУведомления,
					ОбъектПодписки);
			КонецЕсли;
		Иначе
			Если ОбъектыУведомления.Количество() = 1 Тогда
				ОбъектУведомления = ОбъектыУведомления[0];
				ТемаУведомления = СтрШаблон(
					НСтр("ru = 'Новая запись календаря ""%1""'", КодЯзыка),
					ОбъектУведомления);
			Иначе
				КоличествоОбъектовУведомления = ОбъектыУведомления.Количество();
				ТемаУведомления = СтрШаблон(
					НСтр("ru = 'Новые записи календаря (%1)'", КодЯзыка),
					КоличествоОбъектовУведомления);
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ВидСобытия = Перечисления.СобытияУведомлений.ПросроченаКонтрольнаяТочка Тогда
		
		Если ОбъектыУведомления.Количество() = 1 Тогда
			ОбъектУведомления = ОбъектыУведомления[0];
			ПредставлениеСрока = РаботаСУведомлениями.ПолучитьПредставлениеСрока(
				ОбъектУведомления.Ответственный,
				ОбъектУведомления.ПлановыйСрок,
				ВидСобытия,
				КодЯзыка);
			ТемаУведомления = СтрШаблон(
				"%1: ""%2""",
				ПредставлениеСрока,
				ОбъектУведомления.Наименование);
		Иначе
			КоличествоОбъектовУведомления = ОбъектыУведомления.Количество();
			ТемаУведомления = СтрШаблон(
				НСтр("ru = 'Просрочены контрольные точки (%1)'", КодЯзыка),
				КоличествоОбъектовУведомления);
		КонецЕсли;
		
	ИначеЕсли ВидСобытия = Перечисления.СобытияУведомлений.ПодошелСрокКонтрольнойТочки Тогда
		
		Если ОбъектыУведомления.Количество() = 1 Тогда
			ОбъектУведомления = ОбъектыУведомления[0];
			ПредставлениеСрока = РаботаСУведомлениями.ПолучитьПредставлениеСрока(
				ОбъектУведомления.Ответственный,
				ОбъектУведомления.ПлановыйСрок,
				ВидСобытия,
				КодЯзыка);
			ТемаУведомления = СтрШаблон(
				"%1: ""%2""",
				ПредставлениеСрока,
				ОбъектУведомления.Наименование);
		Иначе
			КоличествоОбъектовУведомления = ОбъектыУведомления.Количество();
			ТемаУведомления = СтрШаблон(
				НСтр("ru = 'Подошел срок контрольных точек (%1)'", КодЯзыка),
				КоличествоОбъектовУведомления);
		КонецЕсли;
		
	ИначеЕсли ВидСобытия = Перечисления.СобытияУведомлений.ПросроченаОценкаКонтрольнойТочки Тогда
		
		Если ОбъектыУведомления.Количество() = 1 Тогда
			ОбъектУведомления = ОбъектыУведомления[0];
			ТемаУведомления = СтрШаблон(
				НСтр("ru = 'Обновите недельную оценку контрольной точки: %1'", КодЯзыка),
				ОбъектУведомления.Наименование);
		Иначе
			КоличествоОбъектовУведомления = ОбъектыУведомления.Количество();
			ТемаУведомления = СтрШаблон(
				НСтр("ru = 'Обновите недельные оценки контрольных точек (%1)'", КодЯзыка),
				КоличествоОбъектовУведомления);
		КонецЕсли;
		
	ИначеЕсли (ВидСобытия = Справочники.ВидыБизнесСобытий.СозданиеВнутреннегоДокумента
			Или ВидСобытия = Справочники.ВидыБизнесСобытий.СозданиеВходящегоДокумента
			Или ВидСобытия = Справочники.ВидыБизнесСобытий.СозданиеИсходящегоДокумента)
		И ОбъектПодписки = Справочники.ИсточникиДанных.ЭДО Тогда
		
		Если ОбъектыУведомления.Количество() = 1 Тогда
			
			ОбъектУведомления = ОбъектыУведомления[0];
			ДанныеСостоянияЭДО = ОбменСКонтрагентамиДОВызовСервера.ПолучитьСостояниеДокумента(ОбъектУведомления);
			
			ТемаУведомления = СтрШаблон(
				НСтр("ru = '%1: Новый документ ""%2"" от %3. Требуется проверка.'", КодЯзыка),
				Справочники.ИсточникиДанных.ЭДО,
				ОбъектУведомления,
				ДанныеСостоянияЭДО.Контрагент);
			
		Иначе
			
			СтрокаСЧисломНовыхДокументов = СтрокаСЧислом(
				НСтр("ru=';%1 новый документ;;%1 новых документа;%1 новых документов;%1 новых документа'", КодЯзыка),
				ОбъектыУведомления.Количество(),
				ВидЧисловогоЗначения.Количественное);
			
			ТемаУведомления = СтрШаблон("%1: %2. %3.",
				Справочники.ИсточникиДанных.ЭДО,
				СтрокаСЧисломНовыхДокументов,
				НСтр("ru = 'Требуется проверка'", КодЯзыка));
			
		КонецЕсли;
		
	ИначеЕсли ВидСобытия = Перечисления.СобытияУведомлений.ОтклонениеДокументаКонтрагентомПоЭДО Тогда
		
		Если ОбъектыУведомления.Количество() = 1 Тогда
			
			ОбъектУведомления = ОбъектыУведомления[0];
			ДанныеСостоянияЭДО = ОбменСКонтрагентамиДОВызовСервера.ПолучитьСостояниеДокумента(ОбъектУведомления);
			
			ТемаУведомления = СтрШаблон(
				НСтр("ru = '%1: %2 отклонен %3. Требуется корректировка.'", КодЯзыка),
				Справочники.ИсточникиДанных.ЭДО,
				ОбъектУведомления,
				ДанныеСостоянияЭДО.Контрагент);
			
		Иначе
			
			СтрокаСЧисломОтклоненныхДокументов = СтрокаСЧислом(
				НСтр("ru=';%1 отклоненный документ;;%1 отклоненных документа;%1 отклоненных документов;%1 отклоненных документа'", КодЯзыка),
				ОбъектыУведомления.Количество(),
				ВидЧисловогоЗначения.Количественное);
			
			ТемаУведомления = СтрШаблон("%1: %2. %3.",
				Справочники.ИсточникиДанных.ЭДО,
				СтрокаСЧисломОтклоненныхДокументов,
				НСтр("ru = 'Требуется корректировка'", КодЯзыка));
			
		КонецЕсли;
		
	ИначеЕсли ВидСобытия = Перечисления.СобытияУведомлений.ОбменПоЭДОВыполненУспешно Тогда
		
		Если ОбъектыУведомления.Количество() = 1 Тогда
			
			ОбъектУведомления = ОбъектыУведомления[0];
			ДанныеСостоянияЭДО = ОбменСКонтрагентамиДОВызовСервера.ПолучитьСостояниеДокумента(ОбъектУведомления);
			
			ТемаУведомления = СтрШаблон(
				НСтр("ru = '%1: %2 успешно выполнен обмен с %3'", КодЯзыка),
				Справочники.ИсточникиДанных.ЭДО,
				ОбъектУведомления,
				ДанныеСостоянияЭДО.Контрагент);
			
		Иначе
			
			СтрокаСЧисломУспешныхОбменов = СтрокаСЧислом(
				НСтр("ru=';Успешно выполнен %1 обмен;;Успешно выполнено %1 обмена;Успешно выполнено %1 обменов;Успешно выполнено %1 обмена'", КодЯзыка),
				ОбъектыУведомления.Количество(),
				ВидЧисловогоЗначения.Количественное);
			
			ТемаУведомления = СтрШаблон("%1: %2",
				Справочники.ИсточникиДанных.ЭДО,
				СтрокаСЧисломУспешныхОбменов);
			
		КонецЕсли;
		
	ИначеЕсли ВидСобытия = Перечисления.СобытияУведомлений.ТребуетсяАннулированиеДокумента Тогда
		
		Если ОбъектыУведомления.Количество() = 1 Тогда
			
			ОбъектУведомления = ОбъектыУведомления[0];
			
			ТемаУведомления = СтрШаблон(
				НСтр("ru = 'Требуется аннулирование ""%1""'", КодЯзыка),
				ОбъектУведомления);
			
		Иначе
			
			СтрокаСЧисломТребуемыхАннулирований = СтрокаСЧислом(
				НСтр("ru=';Требуется аннулирование %1 документа;;Требуется аннулирование %1 документов;
					|Требуется аннулирование %1 документов;Требуется аннулирование %1 документа'", КодЯзыка),
				ОбъектыУведомления.Количество(),
				ВидЧисловогоЗначения.Количественное);
			
			ТемаУведомления = СтрокаСЧисломТребуемыхАннулирований;
			
		КонецЕсли;
		
	ИначеЕсли ВидСобытия = Перечисления.СобытияУведомлений.ДокументАннулирован Тогда
		
		Если ОбъектыУведомления.Количество() = 1 Тогда
			
			ОбъектУведомления = ОбъектыУведомления[0];
			
			ТемаУведомления = СтрШаблон(
				НСтр("ru = 'Аннулирован документ ""%1""'", КодЯзыка),
				ОбъектУведомления);
			
		Иначе
			
			СтрокаСЧисломАннулированныхДокументов = СтрокаСЧислом(
				НСтр("ru=';Аннулирован %1 документ;;Аннулированы %1 документа;
					|Аннулированы %1 документов;Аннулировано %1 документ'", КодЯзыка),
				ОбъектыУведомления.Количество(),
				ВидЧисловогоЗначения.Количественное);
			
			ТемаУведомления = СтрокаСЧисломАннулированныхДокументов;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ТемаУведомления;
	
КонецФункции

// Формирует тему уведомления по событию без группировки.
//
// Параметры:
//  ОбъектПодписки - СправочникСсылка, ДокументСсылка - Объект подписки формируемого уведомления.
//  ВидСобытия - СправочникСсылка.ВидыБизнесСобытий, ПеречислениеСсылка.СобытияУведомлений - Вид события формируемого уведомления.
//  ОбъектУведомления - СправочникСсылка, ДокументСсылка - Объект формируемого уведомления.
//  ПолучательУведомления - СправочникСсылка.Пользователи - Получатель формируемого уведомления.
//  СпособУведомления - ПеречислениеСсылка.СпособыУведомления - Способ формируемого уведомления.
//  ДополнительноеОписание - Строка - Дополнительное описание.
// 
// Возвращаемое значение:
//  Строка - Тема уведомления.
//
Функция СформироватьТемуУведомленияПоСобытию(
	ОбъектПодписки,
	ВидСобытия,
	ОбъектУведомления,
	ПолучательУведомления,
	СпособУведомления,
	ДополнительноеОписание) Экспорт
	
	ТемаУведомления = СформироватьТемуУведомленияПоСобытиюПереопределяемый(
		ОбъектПодписки,
		ВидСобытия,
		ОбъектУведомления,
		ПолучательУведомления,
		СпособУведомления,
		ДополнительноеОписание);
	Если ЗначениеЗаполнено(ТемаУведомления) Тогда
		Возврат ТемаУведомления;
	КонецЕсли;
	
	Если СпособУведомления = Перечисления.СпособыУведомления.ПоПочте
		И (ВидСобытия = Справочники.ВидыБизнесСобытий.СозданиеЗадачи
		Или ВидСобытия = Справочники.ВидыБизнесСобытий.ПеренаправлениеЗадачи) Тогда
		
		// Учитываем глобальную настройку.
		ИспользоватьВыполнениеЗадачПоПочте = ПолучитьФункциональнуюОпцию("ИспользоватьВыполнениеЗадачПоПочте");
		
		Если ИспользоватьВыполнениеЗадачПоПочте Тогда
			
			ВключеноВыполнениеЗадачПоПочте = РегистрыСведений.НастройкиУведомлений.ПолучитьДополнительнуюНастройку(
				ПолучательУведомления,
				Перечисления.НастройкиУведомлений.ВыполнениеЗадачПоПочте);
			Если ВключеноВыполнениеЗадачПоПочте
				И ВыполнениеЗадачПоПочтеСервер.ВозможноВыполнениеЗадачиПоПочте(ОбъектУведомления) Тогда
				
				ТемаУведомления =
					ВыполнениеЗадачПоПочтеСервер.СформироватьТемуУведомленияПоЗадачеСВозможностьюВыполненияПоПочте(
						ОбъектУведомления, ПолучательУведомления);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ВидСобытия = Перечисления.СобытияУведомлений.УведомлениеПрограммы Тогда
		
		Если ЗначениеЗаполнено(ДополнительноеОписание) Тогда
			ТемаУведомления = СтрПолучитьСтроку(ДополнительноеОписание, 1);
		Иначе
			КодЯзыка = РаботаСУведомлениями.КодЯзыка(ПолучательУведомления);
			ТемаУведомления = НСтр("ru = 'Уведомление 1С Документооборот'", КодЯзыка);
		КонецЕсли;
		
	ИначеЕсли ВидСобытия = Справочники.ВидыБизнесСобытий.СозданиеСообщения
		Или ВидСобытия = Перечисления.СобытияУведомлений.СозданиеТемыФорума Тогда
		
		Если ОбъектУведомления.ПервоеСообщениеТемы Тогда
			
			ТемаСообщения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбъектУведомления, "ВладелецСообщения");
			РазделФорума = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТемаСообщения, "Папка");
			ТемаУведомления = СтрШаблон(
				НСтр("ru = 'Новая тема ""%1"" в разделе ""%2""'", КодЯзыка),
				ТемаСообщения,
				РазделФорума);
			
		Иначе
			
			Если ТипЗнч(ОбъектПодписки) = Тип("СправочникСсылка.СообщенияОбсуждений") Тогда
				ТемаСообщения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбъектПодписки.Ссылка, "ВладелецСообщения");
				ПредставлениеОбъектаПодписки = СтрШаблон(
					НСтр("ru = 'в теме ""%1""'", КодЯзыка),
					ТемаСообщения);
			ИначеЕсли ТипЗнч(ОбъектПодписки) = Тип("СправочникСсылка.ТемыОбсуждений") Тогда
				ПредставлениеОбъектаПодписки = СтрШаблон(
					НСтр("ru = 'в теме ""%1""'", КодЯзыка),
					ОбъектПодписки.Ссылка);
			ИначеЕсли ТипЗнч(ОбъектПодписки) = Тип("СправочникСсылка.ПапкиФорума") Тогда
				ТемаСообщения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбъектУведомления, "ВладелецСообщения");
				ПредставлениеОбъектаПодписки = СтрШаблон(
					НСтр("ru = 'в теме ""%1""'", КодЯзыка),
					ТемаСообщения);
			Иначе
				ПредставлениеОбъектаПодписки = СтрШаблон(
					НСтр("ru = 'в обсуждении ""%1""'", КодЯзыка),
					ОбъектПодписки.Ссылка);
			КонецЕсли;
			ТемаУведомления = СтрШаблон(
				НСтр("ru = 'Новое сообщение %1'", КодЯзыка),
				ПредставлениеОбъектаПодписки);
			
		КонецЕсли;
		
	ИначеЕсли ВидСобытия = Перечисления.СобытияУведомлений.ПеренаправлениеМоейЗадачи Тогда
		КодЯзыка = РаботаСУведомлениями.КодЯзыка(ПолучательУведомления);
		Если СтрНачинаетсяС(ДополнительноеОписание, НСтр("ru = 'Автоперенаправление'", КодЯзыка)) Тогда
			ТемаУведомления = СтрШаблон(
				"%1 ""%2""",
				НСтр("ru = 'Автоперенаправление моей задачи'", КодЯзыка),
				ОбъектУведомления);
		КонецЕсли;
		
	ИначеЕсли (ВидСобытия = Справочники.ВидыБизнесСобытий.СозданиеВнутреннегоДокумента
			Или ВидСобытия = Справочники.ВидыБизнесСобытий.СозданиеВходящегоДокумента
			Или ВидСобытия = Справочники.ВидыБизнесСобытий.СозданиеИсходящегоДокумента)
		И ОбъектУведомления.Источник = Справочники.ИсточникиДанных.ЭДО Тогда
		
		КодЯзыка = РаботаСУведомлениями.КодЯзыка(ПолучательУведомления);
		
		ДанныеСостоянияЭДО = ОбменСКонтрагентамиДОВызовСервера.ПолучитьСостояниеДокумента(ОбъектУведомления);
		
		ТемаУведомления = СтрШаблон(
			НСтр("ru = '%1: Новый документ ""%2"" от %3. Требуется проверка.'", КодЯзыка),
			Справочники.ИсточникиДанных.ЭДО,
			ОбъектУведомления,
			ДанныеСостоянияЭДО.Контрагент);
		
	ИначеЕсли ВидСобытия = Перечисления.СобытияУведомлений.ОтклонениеДокументаКонтрагентомПоЭДО Тогда
		
		ДанныеСостоянияЭДО = ОбменСКонтрагентамиДОВызовСервера.ПолучитьСостояниеДокумента(ОбъектУведомления);
		
		ТемаУведомления = СтрШаблон(
			НСтр("ru = '%1: %2 отклонен %3. Требуется корректировка.'", КодЯзыка),
			Справочники.ИсточникиДанных.ЭДО,
			ОбъектУведомления,
			ДанныеСостоянияЭДО.Контрагент);
		
	ИначеЕсли ВидСобытия = Перечисления.СобытияУведомлений.ОбменПоЭДОВыполненУспешно Тогда
		
		ДанныеСостоянияЭДО = ОбменСКонтрагентамиДОВызовСервера.ПолучитьСостояниеДокумента(ОбъектУведомления);
		
		ТемаУведомления = СтрШаблон(
			НСтр("ru = '%1: %2 успешно выполнен обмен с %3'", КодЯзыка),
			Справочники.ИсточникиДанных.ЭДО,
			ОбъектУведомления,
			ДанныеСостоянияЭДО.Контрагент);
		
	ИначеЕсли ВидСобытия = Перечисления.СобытияУведомлений.ТребуетсяАннулированиеДокумента Тогда
		
		ТемаУведомления = СтрШаблон(
			НСтр("ru = 'Требуется аннулирование ""%1""'", КодЯзыка),
			ОбъектУведомления);
		
	ИначеЕсли ВидСобытия = Перечисления.СобытияУведомлений.ДокументАннулирован Тогда
		
		ТемаУведомления = СтрШаблон(
			НСтр("ru = 'Аннулирован документ ""%1""'", КодЯзыка),
			ОбъектУведомления);
		
	КонецЕсли;
	
	Возврат ТемаУведомления;
	
КонецФункции

// Формирует уведомления по событию с группировкой.
// Сформированные уведомления добавляются в параметр СформированныеУведомления.
//
// Параметры:
//  ПолучательУведомления - СправочникСсылка.Пользователи - Получатель формируемого уведомления.
//  СпособУведомления - ПеречислениеСсылка.СпособыУведомления - Способ формируемого уведомления.
//  ВидСобытия - СправочникСсылка.ВидыБизнесСобытий, ПеречислениеСсылка.СобытияУведомлений - Вид события формируемого уведомления.
//  Уведомления - ТаблицаЗначений - Таблица уведомлений о событиях для пользователей.
//   * Объект - СправочникСсылка, ДокументСсылка - Объект с которым произошло событие.
//   * Пользователь - СправочникСсылка.Пользователи - Пользователь, для которого предназанчено увдеомление.
//   * СпособУведомления - ПеречислениеСсылка.СпособыУведомления - Способ доставки уведомления.
//   * ВидСобытия - СправочникСсылка.ВидыБизнесСобытий, ПеречислениеСсылка.СобытияУведомлений - Произошедшее событие.
//   * ОбъектПодписки - СправочникСсылка, ДокументСсылка - Объект, в связи с подпиской на который рассылается уведомление.
//   * ТекстУведомления - Строка - Дополнительный текст уведомления, сформированный на этапе обработки событий.
//  СформированныеУведомления - ТаблицаЗначений - Сформированные для отправки уведомления.
//   * Пользователь - СправочникСсылка.Пользователи - Получатель уведомления.
//   * СпособУведомления - ПеречислениеСсылка.СпособыУведомления - Способ доставки уведомления.
//   * ТемаУведомления - Строка - Тема уведомления.
//   * ТекстУведомления - Строка - Текст уведомления.
//   * Уведомления - Массив - Уведомления, которые будут удалены из очереди, после отправки данного сформированного уведомления.
//   * Файлы - Массив - Файлы уведомления.
//
// Возвращаемое значение:
//  Булево - Поведение переопределено.
//
Функция СформироватьУведомленияПоВидуБизнесСобытия(
	ПолучательУведомления,
	СпособУведомления,
	ВидСобытия,
	Уведомления,
	СформированныеУведомления) Экспорт
	
	Если СформироватьУведомленияПоВидуБизнесСобытияПереопределяемый(
		ПолучательУведомления,
		СпособУведомления,
		ВидСобытия,
		Уведомления,
		СформированныеУведомления) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Переопределено = Ложь;
	
	Отбор = Новый Структура();
	Отбор.Вставить("Пользователь", ПолучательУведомления);
	Отбор.Вставить("СпособУведомления", СпособУведомления);
	Отбор.Вставить("ВидСобытия", ВидСобытия);
	УведомленияПользователя = Уведомления.НайтиСтроки(Отбор);
	
	УведомленияПодписки = Новый Массив;
	ДополнительныеОписания = Новый Соответствие;
	Если ВидСобытия = Справочники.ВидыБизнесСобытий.СозданиеСообщения Тогда
		
		Переопределено = Истина;
		
		// Группировка уведомлений по предмету / теме / сообщению.
		ОбъектыПодписки = Новый Массив;
		
		// Подписка на предметы.
		Для Каждого Уведомление Из УведомленияПользователя Цикл
			
			Если ТипЗнч(Уведомление.ОбъектПодписки) = Тип("СправочникСсылка.СообщенияОбсуждений")
				Или ТипЗнч(Уведомление.ОбъектПодписки) = Тип("СправочникСсылка.ТемыОбсуждений") Тогда
				
				Продолжить;
				
			КонецЕсли;
			
			РаботаСУведомлениями.ДобавитьУведомлениеВМассив(УведомленияПодписки, Уведомление);
			
			Если ОбъектыПодписки.Найти(Уведомление.ОбъектПодписки) = Неопределено Тогда
				
				ОбъектыПодписки.Добавить(Уведомление.ОбъектПодписки);
				
			КонецЕсли;
			
		КонецЦикла;
		
		// Подписка на темы.
		Для Каждого Уведомление Из УведомленияПользователя Цикл
			
			Если ТипЗнч(Уведомление.ОбъектПодписки) <> Тип("СправочникСсылка.ТемыОбсуждений") Тогда
				Продолжить;
			КонецЕсли;
			
			РаботаСУведомлениями.ДобавитьУведомлениеВМассив(УведомленияПодписки, Уведомление);
			
			ПредметОбсуждения = Уведомление.ОбъектПодписки.Документ; 
			Если ОбъектыПодписки.Найти(ПредметОбсуждения) = Неопределено
				И ОбъектыПодписки.Найти(Уведомление.ОбъектПодписки) = Неопределено  Тогда
				
				ОбъектыПодписки.Добавить(Уведомление.ОбъектПодписки);
				
			КонецЕсли;
			
		КонецЦикла;
		
		// Подписка на сообщения.
		Для Каждого Уведомление Из УведомленияПользователя Цикл
			
			Если ТипЗнч(Уведомление.ОбъектПодписки) <> Тип("СправочникСсылка.СообщенияОбсуждений") Тогда
				Продолжить;
			КонецЕсли;
			
			РаботаСУведомлениями.ДобавитьУведомлениеВМассив(УведомленияПодписки, Уведомление);
			
			ТемаОбсуждения = Уведомление.ОбъектПодписки.ВладелецСообщения;
			Уведомление.ОбъектПодписки = ТемаОбсуждения;
			ПредметОбсуждения = ТемаОбсуждения.Документ;
			Если ОбъектыПодписки.Найти(ТемаОбсуждения) = Неопределено
				И ОбъектыПодписки.Найти(ПредметОбсуждения) = Неопределено
				И ОбъектыПодписки.Найти(Уведомление.ОбъектПодписки) = Неопределено Тогда
				
				ОбъектыПодписки.Добавить(ТемаОбсуждения);
				
			КонецЕсли;
			
		КонецЦикла;
		
		// Группировка по объектам подписки.
		Для Каждого ОбъектПодписки Из ОбъектыПодписки Цикл
			
			Отбор = Новый Структура();
			Отбор.Вставить("Пользователь", ПолучательУведомления);
			Отбор.Вставить("СпособУведомления", СпособУведомления);
			Отбор.Вставить("ВидСобытия", ВидСобытия);
			Отбор.Вставить("ОбъектПодписки", ОбъектПодписки);
			УведомленияПользователяПоВидуИПоОбъектуПодписки = Уведомления.НайтиСтроки(Отбор);
			
			ОбъектыУведомления = Новый Массив;
			Для Каждого Уведомление Из УведомленияПользователяПоВидуИПоОбъектуПодписки Цикл
				Если ОбъектыУведомления.Найти(Уведомление.Объект) = Неопределено Тогда
					ОбъектыУведомления.Добавить(Уведомление.Объект);
				КонецЕсли;
			КонецЦикла;
			
			Если ОбъектыУведомления.Количество() > 0 Тогда
				
				ДанныеУведомления = РаботаСУведомлениями.ДанныеУведомленияПоОбъектам(
					ОбъектПодписки,
					ВидСобытия,
					ОбъектыУведомления,
					ПолучательУведомления,
					СпособУведомления,
					ДополнительныеОписания);
				
				СформированноеУведомление = СформированныеУведомления.Добавить();
				ЗаполнитьЗначенияСвойств(СформированноеУведомление, ДанныеУведомления);
				СформированноеУведомление.Пользователь = ПолучательУведомления;
				СформированноеУведомление.СпособУведомления = СпособУведомления;
				СформированноеУведомление.Уведомления = УведомленияПодписки;
				
			КонецЕсли;
			
		КонецЦикла;
		
	ИначеЕсли ВидСобытия = Справочники.ВидыБизнесСобытий.ПрерываниеБизнесПроцесса 
		Или ВидСобытия = Справочники.ВидыБизнесСобытий.ВозобновлениеБизнесПроцесса
		Или ВидСобытия = Справочники.ВидыБизнесСобытий.ОстановкаБизнесПроцесса Тогда
		
		Переопределено = Истина;
		
		// Группировка по процессу.
		ОбъектыПодписки = Новый Массив;
		
		// Подписка на процессы.
		Для Каждого Уведомление Из УведомленияПользователя Цикл
			
			Если Не РаботаСУведомлениями.ЭтоПоддерживаемыйБизнесПроцесс(Уведомление.ОбъектПодписки) Тогда
				Продолжить;
			КонецЕсли;
			
			РаботаСУведомлениями.ДобавитьУведомлениеВМассив(УведомленияПодписки, Уведомление);
			Если ОбъектыПодписки.Найти(Уведомление.ОбъектПодписки) = Неопределено Тогда
				ОбъектыПодписки.Добавить(Уведомление.ОбъектПодписки);
			КонецЕсли;
			
		КонецЦикла;
		
		// Подписка на задачи.
		Для Каждого Уведомление Из УведомленияПользователя Цикл
			
			Если ТипЗнч(Уведомление.ОбъектПодписки) <> Тип("ЗадачаСсылка.ЗадачаИсполнителя") Тогда
				Продолжить;
			КонецЕсли;
			
			РаботаСУведомлениями.ДобавитьУведомлениеВМассив(УведомленияПодписки, Уведомление);
			Процесс = Уведомление.ОбъектПодписки.БизнесПроцесс;
			Если ОбъектыПодписки.Найти(Процесс) = Неопределено
				И ОбъектыПодписки.Найти(Уведомление.ОбъектПодписки) = Неопределено Тогда
				ОбъектыПодписки.Добавить(Уведомление.ОбъектПодписки);
			КонецЕсли;
			
		КонецЦикла;
		
		// Группировка по объектам подписки.
		Для Каждого ОбъектПодписки Из ОбъектыПодписки Цикл
			
			Отбор = Новый Структура();
			Отбор.Вставить("Пользователь", ПолучательУведомления);
			Отбор.Вставить("СпособУведомления", СпособУведомления);
			Отбор.Вставить("ВидСобытия", ВидСобытия);
			Отбор.Вставить("ОбъектПодписки", ОбъектПодписки);
			УведомленияПользователяПоВидуИПоОбъектуПодписки = Уведомления.НайтиСтроки(Отбор);
			
			ОбъектыУведомления = Новый Массив;
			Для Каждого Уведомление Из УведомленияПользователяПоВидуИПоОбъектуПодписки Цикл
				Если ОбъектыУведомления.Найти(Уведомление.Объект) = Неопределено Тогда
					ОбъектыУведомления.Добавить(Уведомление.Объект);
				КонецЕсли;
			КонецЦикла;
			
			Если ОбъектыУведомления.Количество() > 0 Тогда
				
				ДанныеУведомления = РаботаСУведомлениями.ДанныеУведомленияПоОбъектам(
					ОбъектПодписки,
					ВидСобытия,
					ОбъектыУведомления,
					ПолучательУведомления,
					СпособУведомления,
					ДополнительныеОписания);
				
				СформированноеУведомление = СформированныеУведомления.Добавить();
				ЗаполнитьЗначенияСвойств(СформированноеУведомление, ДанныеУведомления);
				СформированноеУведомление.Пользователь = ПолучательУведомления;
				СформированноеУведомление.СпособУведомления = СпособУведомления;
				СформированноеУведомление.Уведомления = УведомленияПодписки;
				
			КонецЕсли;
			
		КонецЦикла;
		
	ИначеЕсли ВидСобытия = Перечисления.СобытияУведомлений.ПодошелСрокКонтроля
		Или ВидСобытия = Перечисления.СобытияУведомлений.ПросроченКонтроль
		Или ВидСобытия = Перечисления.СобытияУведомлений.УведомлениеПрограммы
		Или ВидСобытия = Перечисления.СобытияУведомлений.ПросроченаКонтрольнаяТочка
		Или ВидСобытия = Перечисления.СобытияУведомлений.ПодошелСрокКонтрольнойТочки
		Или ВидСобытия = Перечисления.СобытияУведомлений.ПросроченаОценкаКонтрольнойТочки
		Или ВидСобытия = Перечисления.СобытияУведомлений.ОбменПоЭДОВыполненУспешно
		Или ВидСобытия = Перечисления.СобытияУведомлений.ОтклонениеДокументаКонтрагентомПоЭДО
		Или ВидСобытия = Перечисления.СобытияУведомлений.ТребуетсяАннулированиеДокумента
		Или ВидСобытия = Перечисления.СобытияУведомлений.ДокументАннулирован Тогда
		
		Переопределено = Истина;
		
		// Группировка по событию.
		РаботаСУведомлениями.СформироватьУведомленияГруппировкаПоСобытию(
			ПолучательУведомления,
			СпособУведомления,
			ВидСобытия,
			Уведомления,
			СформированныеУведомления);
		
	ИначеЕсли ВидСобытия = Перечисления.СобытияУведомлений.СозданиеЗаписиКалендаря Тогда
		
		// Группировка по событию и объекту подписки.
		РаботаСУведомлениями.СформироватьУведомленияГруппировкаПоОбъектуПодписки(
			ПолучательУведомления,
			СпособУведомления,
			ВидСобытия,
			Уведомления,
			СформированныеУведомления);
		
	КонецЕсли;
	
	Возврат Переопределено;
	
КонецФункции

// Формирует уведомления по событиям без группировки.
// Сформированные уведомления добавляются в параметр СформированныеУведомления.
//
// Параметры:
//  ПолучательУведомления - СправочникСсылка.Пользователи - Получатель формируемого уведомления.
//  СпособУведомления - ПеречислениеСсылка.СпособыУведомления - Способ формируемого уведомления.
//  ОбработанныеВидыБизнесСобытий - Массив - Виды события, по которым уже сгруппированы уведомления.
//  Уведомления - ТаблицаЗначений - Таблица уведомлений о событиях для пользователей.
//   * Объект - СправочникСсылка, ДокументСсылка - Объект с которым произошло событие.
//   * Пользователь - СправочникСсылка.Пользователи - Пользователь, для которого предназанчено увдеомление.
//   * СпособУведомления - ПеречислениеСсылка.СпособыУведомления - Способ доставки уведомления.
//   * ВидСобытия - СправочникСсылка.ВидыБизнесСобытий, ПеречислениеСсылка.СобытияУведомлений - Произошедшее событие.
//   * ОбъектПодписки - СправочникСсылка, ДокументСсылка - Объект, в связи с подпиской на который рассылается уведомление.
//   * ТекстУведомления - Строка - Дополнительный текст уведомления, сформированный на этапе обработки событий.
//  СформированныеУведомления - ТаблицаЗначений - Сформированные для отправки уведомления.
//   * Пользователь - СправочникСсылка.Пользователи - Получатель уведомления.
//   * СпособУведомления - ПеречислениеСсылка.СпособыУведомления - Способ доставки уведомления.
//   * ТемаУведомления - Строка - Тема уведомления.
//   * ТекстУведомления - Строка - Текст уведомления.
//   * Уведомления - Массив - Уведомления, которые будут удалены из очереди, после отправки данного сформированного уведомления.
//   * Файлы - Массив - Файлы уведомления.
//
// Возвращаемое значение:
//  Булево - Поведение переопределено.
//
Функция СформироватьУведомленияПоСобытиям(
	ПолучательУведомления,
	СпособУведомления,
	ОбработанныеВидыБизнесСобытий,
	Уведомления,
	СформированныеУведомления) Экспорт
	
	Если СформироватьУведомленияПоСобытиямПереопределяемый(
		ПолучательУведомления,
		СпособУведомления,
		ОбработанныеВидыБизнесСобытий,
		Уведомления,
		СформированныеУведомления) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Переопределено = Ложь;
	
	Возврат Переопределено;
	
КонецФункции

// Формирует файлы уведомления по событию с группировки.
//
// Параметры:
//  ОбъектПодписки - СправочникСсылка, ДокументСсылка - Объект подписки формируемого уведомления.
//  ВидСобытия - СправочникСсылка.ВидыБизнесСобытий, ПеречислениеСсылка.СобытияУведомлений - Вид события формируемого уведомления.
//  ОбъектУведомления - СправочникСсылка, ДокументСсылка - Объект формируемого уведомления.
//  ПолучательУведомления - СправочникСсылка.Пользователи - Получатель формируемого уведомления.
//  СпособУведомления - ПеречислениеСсылка.СпособыУведомления - Способ формируемого уведомления.
//  ДополнительноеОписание - Строка - Дополнительное описание.
// 
// Возвращаемое значение:
//  Массив - Массив структур файлов уведомлений.
//   * Ссылка - СправочникСсылка.Файлы - Ссылка на файл.
//   * ИмяФайла - Строка - Имя файла.
//
Функция СформироватьФайлыУведомленияПоОбъектамУведомления(
	ОбъектПодписки,
	ВидСобытия,
	ОбъектУведомления,
	ПолучательУведомления,
	СпособУведомления,
	ДополнительноеОписание) Экспорт
	
	Файлы = СформироватьФайлыУведомленияПоОбъектамУведомленияПереопределяемый(
		ОбъектПодписки,
		ВидСобытия,
		ОбъектУведомления,
		ПолучательУведомления,
		СпособУведомления,
		ДополнительноеОписание);
	Если Файлы <> Неопределено Тогда
		Возврат Файлы;
	КонецЕсли;
	
	Возврат Файлы;
	
КонецФункции

// Формирует файлы уведомления по событию без группировки.
//
// Параметры:
//  ОбъектПодписки - СправочникСсылка, ДокументСсылка - Объект подписки формируемого уведомления.
//  ВидСобытия - СправочникСсылка.ВидыБизнесСобытий, ПеречислениеСсылка.СобытияУведомлений - Вид события формируемого уведомления.
//  ОбъектУведомления - СправочникСсылка, ДокументСсылка - Объект формируемого уведомления.
//  ПолучательУведомления - СправочникСсылка.Пользователи - Получатель формируемого уведомления.
//  СпособУведомления - ПеречислениеСсылка.СпособыУведомления - Способ формируемого уведомления.
//  ДополнительноеОписание - Строка - Дополнительное описание.
// 
// Возвращаемое значение:
//  Массив - Массив структур файлов уведомлений.
//   * Ссылка - СправочникСсылка.Файлы - Ссылка на файл.
//   * ИмяФайла - Строка - Имя файла.
//
Функция СформироватьФайлыУведомленияПоСобытию(
	ОбъектПодписки,
	ВидСобытия,
	ОбъектУведомления,
	ПолучательУведомления,
	СпособУведомления,
	ДополнительноеОписание) Экспорт
	
	Файлы = СформироватьФайлыУведомленияПоСобытиюПереопределяемый(
		ОбъектПодписки,
		ВидСобытия,
		ОбъектУведомления,
		ПолучательУведомления,
		СпособУведомления,
		ДополнительноеОписание);
	Если Файлы <> Неопределено Тогда
		Возврат Файлы;
	КонецЕсли;
	
	Если СпособУведомления = Перечисления.СпособыУведомления.ПоПочте
		И (ВидСобытия = Справочники.ВидыБизнесСобытий.СозданиеЗадачи
		Или ВидСобытия = Справочники.ВидыБизнесСобытий.ПеренаправлениеЗадачи) Тогда
		
		// Учитываем глобальную настройку.
		ИспользоватьВыполнениеЗадачПоПочте = ПолучитьФункциональнуюОпцию("ИспользоватьВыполнениеЗадачПоПочте");
		
		Если ИспользоватьВыполнениеЗадачПоПочте Тогда
			
			ВключеноВыполнениеЗадачПоПочте = РегистрыСведений.НастройкиУведомлений.ПолучитьДополнительнуюНастройку(
				ПолучательУведомления,
				Перечисления.НастройкиУведомлений.ВыполнениеЗадачПоПочте);
			Если ВключеноВыполнениеЗадачПоПочте
				И ВыполнениеЗадачПоПочтеСервер.ВозможноВыполнениеЗадачиПоПочте(ОбъектУведомления) Тогда
				
				Файлы = ВыполнениеЗадачПоПочтеСервер.СформироватьФайлыУведомленияПоЗадачеСВозможностьюВыполненияПоПочте(
					ОбъектУведомления, ПолучательУведомления);
				
			КонецЕсли;
		
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Файлы;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Позволяет скорректировать виды событий, по которым следует группировать уведомления.
// В массиве ВидыСобытий поступают уже полностью сформированные типовые виды событий для группировки.
//
// Параметры:
//  ВидыСобытий - Массив - Сформированные типовые виды событий для группировки.
//  ПолучательУведомления - СправочникСсылка.Пользователь - Получатель уведомления.
//  СпособУведомления - ПеречислениеСсылка.СпособыУведомления - Способ уведомления.
//
Процедура ВидыСобытийДляГруппировкиПереопределяемый(ВидыСобытий, ПолучательУведомления, СпособУведомления)
	
	// Пример. Не группировать уведомления о новой задаче. Для Великановой не группировать уведомления по почте.
	//Индекс = ВидыСобытий.Найти(Справочники.ВидыБизнесСобытий.СозданиеЗадачи);
	//Если Индекс <> Неопределено Тогда
	//	ВидыСобытий.Удалить(Индекс);
	//КонецЕсли;
	//
	//Если СпособУведомления = Перечисления.СпособыУведомления.ПоПочте Тогда
	//	Если СтрНайти(ПолучательУведомления, "Великанова") Тогда
	//		ВидыСобытий.Очистить();
	//	КонецЕсли;
	//КонецЕсли;
	
КонецПроцедуры

// Формирует текстовое представление брони.
//
// Параметры:
//  Бронь - ДокументСсылка.Бронь - Бронь, по которой формируется уведомление.
//  ВидСобытия - СправочникСсылка.ВидыБизнесСобытий, ПеречислениеСсылка.СобытияУведомлений - Событие, произошедшее с объектом.
//  ДополнительноеОписание - Строка - Дополнительное описание.
// 
// Возвращаемое значение:
//  Строка - Текстовое представление брони.
//
Функция СформироватьПредставлениеБрони(
	Бронь,
	ВидСобытия,
	ДополнительноеОписание,
	КодЯзыка)
	
	ПредставлениеОбъекта = "";
	
	РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
		ПредставлениеОбъекта,
		НСтр("ru = 'Бронь'", КодЯзыка),
		Бронь.Ссылка);
	Если Бронь.ТипЗаписи = Перечисления.ТипЗаписиКалендаря.ПовторяющеесяСобытие Тогда
		БроньОбъект = Бронь.Ссылка.ПолучитьОбъект();
		ПредставлениеПовторения = БроньОбъект.ПолучитьПредставлениеПовторения();
		РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
			ПредставлениеОбъекта,
			НСтр("ru = 'Повторять'", КодЯзыка),
			ПредставлениеПовторения);
	КонецЕсли;
	Если Бронь.ПометкаУдаления Тогда
		РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
			ПредставлениеОбъекта,
			"",
			НСтр("ru = 'Бронь помечена на удаление.'", КодЯзыка));
	КонецЕсли;
	РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
		ПредставлениеОбъекта,
		НСтр("ru = 'Автор'", КодЯзыка),
		Бронь.Автор);
	РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
		ПредставлениеОбъекта,
		НСтр("ru = 'Количество человек'", КодЯзыка),
		Бронь.КоличествоЧеловек);
	РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
		ПредставлениеОбъекта,
		НСтр("ru = 'Техническое обеспечение'", КодЯзыка),
		Бронь.ТехническоеОбеспечение);
	РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
		ПредставлениеОбъекта,
		НСтр("ru = 'Хозяйственное обеспечение'", КодЯзыка),
		Бронь.ХозяйственноеОбеспечение);
	РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
		ПредставлениеОбъекта,
		НСтр("ru = 'Комментарий'", КодЯзыка),
		Бронь.Комментарий);
	
	РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
		ПредставлениеОбъекта,
		НСтр("ru = 'Ссылка'", КодЯзыка),
		РаботаСУведомлениями.ПолучитьНавигационнуюСсылкуУведомления(Бронь));
	
	Возврат ПредставлениеОбъекта;
	
КонецФункции

// Формирует текстовое представление записи рабочего календаря.
//
// Параметры:
//  ЗаписьКалендаря - СправочникСсылка.ЗаписиРабочегоКалендаря - Запись рабочего календаря.
//  ВидСобытия - СправочникСсылка.ВидыБизнесСобытий,
//               ПеречислениеСсылка.СобытияУведомлений - Событие, произошедшее с объектом.
//  ДополнительноеОписание - Строка - Дополнительное описание.
// 
// Возвращаемое значение:
//  Строка - Текстовое представление записи рабочего календаря.
//
Функция СформироватьПредставлениеЗаписиКалендаря(
	ЗаписьКалендаря,
	ВидСобытия,
	ДополнительноеОписание,
	КодЯзыка)
	
	Представление = "";
	
	РеквизитыЗаписиКалендаря = Справочники.ЗаписиРабочегоКалендаря.РеквизитыЗаписиКалендаря(ЗаписьКалендаря);
	
	// Заголовок и пользователь.
	РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
		Представление,
		,
		РеквизитыЗаписиКалендаря.Наименование,
		РеквизитыЗаписиКалендаря.Автор);
	
	// Дата
	ПредставлениеДаты = РаботаСРабочимКалендаремКлиентСервер.ПолучитьПредставлениеДаты(
		РеквизитыЗаписиКалендаря.ДатаНачала,
		РеквизитыЗаписиКалендаря.ДатаОкончания, 
		РеквизитыЗаписиКалендаря.ВесьДень);
	РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
		Представление,
		,
		ПредставлениеДаты);
	
	// Повторение
	РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
		Представление,
		НСтр("ru = 'Повторять'", КодЯзыка), 
		РеквизитыЗаписиКалендаря.Повторять);
	
	// Описание
	РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
		Представление,
		,
		РеквизитыЗаписиКалендаря.Описание);
	
	// Вид мероприятия и место проведения.
	Если ТипЗнч(РеквизитыЗаписиКалендаря.Предмет) = Тип("СправочникСсылка.Мероприятия") Тогда
		
		РеквизитыМероприятия = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			РеквизитыЗаписиКалендаря.Предмет, "ВидМероприятия, МестоПроведения");
		РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
			Представление,
			НСтр("ru = 'Вид мероприятия'", КодЯзыка), 
			РеквизитыМероприятия.ВидМероприятия);
		РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
			Представление,
			НСтр("ru = 'Место проведения'", КодЯзыка), 
			РеквизитыМероприятия.МестоПроведения);
		
	КонецЕсли;
	
	НавигационнаяСсылка = РаботаСУведомлениями.ПолучитьНавигационнуюСсылкуУведомления(ЗаписьКалендаря);
	РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
		Представление,
		НСтр("ru = 'Ссылка'", КодЯзыка), 
		НавигационнаяСсылка);
	
	Возврат Представление;
	
КонецФункции

// Формирует текстовое представление карточки контроля.
//
// Параметры:
//  КарточкаКонтроля - СправочникСсылка.Контроль - Контрольная карточка, по которой формируется уведомление.
//  ВидСобытия - СправочникСсылка.ВидыБизнесСобытий, ПеречислениеСсылка.СобытияУведомлений - Событие, произошедшее с объектом.
//  ДополнительноеОписание - Строка - Дополнительное описание.
// 
// Возвращаемое значение:
//  Строка - Текстовое представление контрольной карточки.
//
Функция СформироватьПредставлениеКонтрольнойКарточки(
	КарточкаКонтроля,
	ВидСобытия,
	ДополнительноеОписание,
	КодЯзыка)
	
	НадписьНеУказанСрок = НСтр("ru = 'не указан'", КодЯзыка);
	ФорматДаты = СтрШаблон("ДЛФ=DD; ДП='%1'", НадписьНеУказанСрок);
	
	ПредставлениеКонтроля = "";
	
	РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
		ПредставлениеКонтроля,
		НСтр("ru = 'Контрольная карточка'", КодЯзыка),
		КарточкаКонтроля.Ссылка);
	Если ЗначениеЗаполнено(ДополнительноеОписание) И ДополнительноеОписание > 1 Тогда 
		ПредставлениеСрока = РаботаСУведомлениями.ПолучитьПредставлениеСрока(
			КарточкаКонтроля.Контролер,
			КарточкаКонтроля.СрокИсполнения,
			ВидСобытия,
			КодЯзыка,
			Истина);
		ДобавитьЗначениеКСтрокеЧерезРазделитель(
			ПредставлениеКонтроля,
			Символы.ПС,
			ПредставлениеСрока);
	КонецЕсли;
	РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
		ПредставлениеКонтроля,
		НСтр("ru = 'Описание'", КодЯзыка),
		КарточкаКонтроля.Описание);
	
	Если ЗначениеЗаполнено(КарточкаКонтроля.Предмет) Тогда
		РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
			ПредставлениеКонтроля,
			НСтр("ru = 'Предмет'", КодЯзыка),
			Контроль.СформироватьПредставлениеПредмета(КарточкаКонтроля.Предмет));
	КонецЕсли;
	
	РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
		ПредставлениеКонтроля,
		НСтр("ru = 'Срок контроля'", КодЯзыка),
		Формат(КарточкаКонтроля.СрокИсполнения, ФорматДаты));
	РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
		ПредставлениеКонтроля,
		НСтр("ru = 'Кого контролировать'", КодЯзыка),
		КарточкаКонтроля.ИсполнителиСтрокой);
	РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
		ПредставлениеКонтроля,
		НСтр("ru = 'Контролер'", КодЯзыка),
		КарточкаКонтроля.Контролер);
	РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
		ПредставлениеКонтроля,
		НСтр("ru = 'Ссылка'", КодЯзыка),
		РаботаСУведомлениями.ПолучитьНавигационнуюСсылкуУведомления(КарточкаКонтроля));
	
	Возврат ПредставлениеКонтроля;
	
КонецФункции

// Формирует текстовое представление контрольной точки.
//
// Параметры:
//  КонтрольнаяТочка - СправочникСсылка.КонтрольныеТочки - Контрольная точки, по которой формируется уведомление.
//  ВидСобытия - СправочникСсылка.ВидыБизнесСобытий, ПеречислениеСсылка.СобытияУведомлений - Событие, произошедшее с объектом.
//  ДополнительноеОписание - Строка - Дополнительное описание.
// 
// Возвращаемое значение:
//  Строка - Текстовое представление контрольной карточки.
//
Функция СформироватьПредставлениеКонтрольнойТочки(
	КонтрольнаяТочка,
	ВидСобытия,
	ДополнительноеОписание,
	КодЯзыка)
	
	НадписьНеУказанСрок = НСтр("ru = 'не указан'", КодЯзыка);
	ФорматДаты = СтрШаблон("ДЛФ=DD; ДП='%1'", НадписьНеУказанСрок);
	
	ПредставлениеКонтрольнойТочки = "";
	
	Если ВидСобытия = Перечисления.СобытияУведомлений.ПодошелСрокКонтрольнойТочки
		Или ВидСобытия = Перечисления.СобытияУведомлений.ПросроченаКонтрольнаяТочка Тогда
		
		ПредставлениеСрока = РаботаСУведомлениями.ПолучитьПредставлениеСрока(
			КонтрольнаяТочка.Ответственный,
			КонтрольнаяТочка.ПлановыйСрок,
			ВидСобытия,
			КодЯзыка);
		
		ДобавитьЗначениеКСтрокеЧерезРазделитель(
			ПредставлениеКонтрольнойТочки,
			Символы.ПС,
			ПредставлениеСрока);
		
	КонецЕсли;
	РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
		ПредставлениеКонтрольнойТочки,
		НСтр("ru = 'Контрольная карточка'", КодЯзыка),
		Символы.ПС + КонтрольнаяТочка.Описание);
	
	РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
		ПредставлениеКонтрольнойТочки,
		НСтр("ru = 'Ссылка'", КодЯзыка),
		РаботаСУведомлениями.ПолучитьНавигационнуюСсылкуУведомления(КонтрольнаяТочка));
	
	Возврат ПредставлениеКонтрольнойТочки;
	
КонецФункции

// Позволяет скорректировать формирование текстового представления объекта для уведомления.
//
// Параметры:
//  Объект - СправочникСсылка, ДокументСсылка - Объект, по которому формируется уведомление.
//  ВидСобытия - СправочникСсылка.ВидыБизнесСобытий, ПеречислениеСсылка.СобытияУведомлений - Событие, произошедшее с объектом.
//  ПолучательУведомления - СправочникСсылка.Пользователи - Получатель уведомления.
//  ДополнительноеОписание - Строка - Дополнительное описание.
// 
// Возвращаемое значение:
//  Строка - Текстовое представление объекта.
//
Функция СформироватьПредставлениеОбъектаПереопределяемый(Объект, ВидСобытия, ПолучательУведомления, ДополнительноеОписание)
	
	ПредставлениеОбъекта = "";
	
	// Пример. Новое представление текст описания файла во всех уведомлениях по файлу.
	//КодЯзыка = РаботаСУведомлениями.КодЯзыка(ПолучательУведомления);
	//Если ТипЗнч(Объект) = Тип("СправочникСсылка.Файлы") Тогда
	//	ПредставлениеОбъекта = СтрШаблон(
	//		НСтр("ru = 'Файл <a href=""%4""><b>%1</b> (%2, %3)</a>'", КодЯзыка),
	//		Объект,
	//		Объект.ТекущаяВерсияАвтор,
	//		Объект.ТекущаяВерсияДатаСоздания,
	//		РаботаСУведомлениями.ПолучитьНавигационнуюСсылкуУведомления(Объект));
	//КонецЕсли;
	
	Возврат ПредставлениеОбъекта
	
КонецФункции

// Формирует текстовое представление отсутствия.
//
// Параметры:
//  Отсутствие - ДокументСсылка.Отсутствие - Отсутствие, по которому формируется уведомление.
//  ВидСобытия - СправочникСсылка.ВидыБизнесСобытий, ПеречислениеСсылка.СобытияУведомлений - Событие, произошедшее с объектом.
//  ДополнительноеОписание - Строка - Дополнительное описание.
// 
// Возвращаемое значение:
//  Строка - Текстовое представление отсутствия.
//
Функция СформироватьПредставлениеОтсутствия(
	Отсутствие,
	ВидСобытия,
	ДополнительноеОписание,
	КодЯзыка)
	
	ПредставлениеОтсутствия = Отсутствия.ПолучитьПредставлениеОтсутствияДляТекстаПисьма(Отсутствие);
	РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
		ПредставлениеОтсутствия,
		НСтр("ru = 'Ссылка'", КодЯзыка), 
		РаботаСУведомлениями.ПолучитьНавигационнуюСсылкуУведомления(Отсутствие));
	
	Возврат ПредставлениеОтсутствия;
	
КонецФункции

// Формирует текстовое представление показателя процесса.
//
// Параметры:
//  ПоказательПроцесса - СправочникСсылка.ПоказателиПроцессов - Показатель процесса, по которому формируется уведомление.
//  ВидСобытия - СправочникСсылка.ВидыБизнесСобытий, ПеречислениеСсылка.СобытияУведомлений - Событие, произошедшее с объектом.
//  ДополнительноеОписание - Строка - Дополнительное описание.
// 
// Возвращаемое значение:
//  Строка - Текстовое представление показателя процесса.
//
Функция СформироватьПредставлениеПоказателяПроцесса(
	ПоказательПроцесса,
	ВидСобытия,
	ДополнительноеОписание,
	КодЯзыка) Экспорт
	
	ПредставлениеОбъекта = "";
	
	Если ВидСобытия = Справочники.ВидыБизнесСобытий.ИзменениеЗначенияПоказателяПроцесса Тогда
		ТекущиеДанные = РегистрыСведений.ЗначенияПоказателейПроцессов.ТекущиеДанные(ПоказательПроцесса);
		Если ТекущиеДанные <> Неопределено И ТекущиеДанные.ДинамикаПоказателя <> 0 Тогда
			ПредставлениеДинамики = 
				?(ТекущиеДанные.ДинамикаПоказателя = 1,
					НСтр("ru = 'выросло'", КодЯзыка),
					НСтр("ru = 'снизилось'", КодЯзыка));
			ПредставлениеОбъекта = СтрШаблон(
				НСтр("ru = 'Значение показателя ""%1"" <b>%2</b> на <b>%3%%</b> и составляет <b>%4</b>.'", КодЯзыка),
				Строка(ПоказательПроцесса),
				ПредставлениеДинамики,
				ТекущиеДанные.ИзменениеПроцент,
				ТекущиеДанные.ПредставлениеЗначения);
		КонецЕсли;
	КонецЕсли;
	
	Если ПустаяСтрока(ПредставлениеОбъекта) Тогда
		РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
			ПредставлениеОбъекта,
			НСтр("ru = 'Показатель процесса'", КодЯзыка),
			ПоказательПроцесса.Ссылка);
	КонецЕсли;
	
	РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
		ПредставлениеОбъекта,
		НСтр("ru = 'Ссылка'", КодЯзыка),
		РаботаСУведомлениями.ПолучитьНавигационнуюСсылкуУведомления(ПоказательПроцесса));
	
	Возврат ПредставлениеОбъекта;
	
КонецФункции

// Формирует текстовое представление бизнес-процесса.
//
// Параметры:
//  Процесс - БизнесПроцессСсылка - Бизнес-процесс, по которому формируется уведомление.
//  ВидСобытия - СправочникСсылка.ВидыБизнесСобытий, ПеречислениеСсылка.СобытияУведомлений - Событие, произошедшее с объектом.
//  ДополнительноеОписание - Строка - Дополнительное описание.
// 
// Возвращаемое значение:
//  Строка - Текстовое представление бизнес-процесса.
//
Функция СформироватьПредставлениеПроцесса(
	Процесс,
	ВидСобытия,
	ДополнительноеОписание,
	КодЯзыка)
	
	ПредставлениеПроцесса = "";
	
	РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
		ПредставлениеПроцесса,
		ТипЗнч(Процесс.Ссылка),
		Процесс.Ссылка);
	Если ВидСобытия = Справочники.ВидыБизнесСобытий.ПрерываниеБизнесПроцесса Тогда
		РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
			ПредставлениеПроцесса,
			НСтр("ru = 'Причина прерывания'", КодЯзыка),
			Процесс.ПричинаПрерывания);
	КонецЕсли;
	РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
		ПредставлениеПроцесса,
		НСтр("ru = 'Ссылка'", КодЯзыка),
		РаботаСУведомлениями.ПолучитьНавигационнуюСсылкуУведомления(Процесс));
	
	Возврат ПредставлениеПроцесса;
	
КонецФункции

// Формирует текстовое представление сообщения.
//
// Параметры:
//  Сообщение - СправочникСсылка.СообщенияОбсуждений - Сообщение, по которому формируется уведомление.
//  ВидСобытия - СправочникСсылка.ВидыБизнесСобытий, ПеречислениеСсылка.СобытияУведомлений - Событие, произошедшее с объектом.
//  ДополнительноеОписание - Строка - Дополнительное описание.
// 
// Возвращаемое значение:
//  Строка - Текстовое представление сообщения
//
Функция СформироватьПредставлениеСообщения(
	Сообщение,
	ВидСобытия,
	ДополнительноеОписание,
	КодЯзыка)
	
	ПредставлениеСообщения = "";
	
	РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
		ПредставлениеСообщения,
		НСтр("ru = 'Сообщение'", КодЯзыка),
		Сообщение.ТекстСообщения);
	РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
		ПредставлениеСообщения,
		НСтр("ru = 'Тема'", КодЯзыка),
		Сообщение.ВладелецСообщения);
	Если ЗначениеЗаполнено(Сообщение.ВладелецСообщения) Тогда
		ТемаСообщения = Сообщение.ВладелецСообщения;
		РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
			ПредставлениеСообщения,
			НСтр("ru = 'Предмет обсуждения'", КодЯзыка),
			ТемаСообщения.Документ, ТипЗнч(ТемаСообщения.Документ));
	КонецЕсли;
	РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
		ПредставлениеСообщения,
		НСтр("ru = 'Автор'", КодЯзыка),
		Сообщение.Автор);
	РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
		ПредставлениеСообщения,
		НСтр("ru = 'Дата'", КодЯзыка),
		Сообщение.ДатаСоздания);
	РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
		ПредставлениеСообщения,
		НСтр("ru = 'Ссылка'", КодЯзыка), 
		РаботаСУведомлениями.ПолучитьНавигационнуюСсылкуУведомления(Сообщение));
	
	Возврат ПредставлениеСообщения;
	
КонецФункции

// Формирует текстовое представление уведомления программы.
// 
// Параметры:
//  Объект - ЛюбаяСсылка - Объект уведомления программы.
//  Описание - Строка - Описание уедомления.
//
// Возвращаемое значение:
//  Строка - Текстовое представление уведомления программы.
//
Функция СформироватьПредставлениеУведомленияПрограммы(
	Объект,
	Описание,
	КодЯзыка)
	
	ПредставлениеУведомления = "";
	
	НавигационнаяСсылкаНаОбъектУведомления = "";
	Если Объект <> Неопределено Тогда
		НавигационнаяСсылкаНаОбъектУведомления =
			РаботаСУведомлениями.ПолучитьНавигационнуюСсылкуУведомления(Объект);
	КонецЕсли;
	
	РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
		ПредставлениеУведомления,
		,
		Описание);
	РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
		ПредставлениеУведомления,
		НСтр("ru = 'Ссылка на объект уведомления'", КодЯзыка),
		НавигационнаяСсылкаНаОбъектУведомления);
	
	Возврат ПредставлениеУведомления;
	
КонецФункции

// Формирует текстовое представление внутреннего документа для поступления и отклонения по ЭДО.
//
Функция СформироватьПредставлениеВнутреннегоДокумента(
	ВнутреннийДокумент,
	ВидСобытия,
	ДополнительноеОписание,
	КодЯзыка)
	
	Если ВидСобытия = Справочники.ВидыБизнесСобытий.СозданиеВнутреннегоДокумента
		И ВнутреннийДокумент.Источник = Справочники.ИсточникиДанных.ЭДО Тогда
		
		ДанныеСостоянияЭДО = ОбменСКонтрагентамиДОВызовСервера.ПолучитьСостояниеДокумента(ВнутреннийДокумент);
		
		ПредставлениеВнутреннегоДокумента = СтрШаблон(
			НСтр("ru = 'От контрагента %1 по ЭДО поступил новый документ: %2. Требуется проверка.'", КодЯзыка),
			ДанныеСостоянияЭДО.Контрагент,
			Строка(ВнутреннийДокумент));
		
		РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
			ПредставлениеВнутреннегоДокумента,
			НСтр("ru = 'Комментарий'", КодЯзыка),
			ДанныеСостоянияЭДО.Комментарий);
		
		РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
			ПредставлениеВнутреннегоДокумента,,
			РаботаСУведомлениями.ПолучитьНавигационнуюСсылкуУведомления(ВнутреннийДокумент));
		
	ИначеЕсли ВидСобытия = Перечисления.СобытияУведомлений.ОтклонениеДокументаКонтрагентомПоЭДО Тогда
		
		ДанныеСостоянияЭДО = ОбменСКонтрагентамиДОВызовСервера.ПолучитьСостояниеДокумента(ВнутреннийДокумент);
		
		ПредставлениеВнутреннегоДокумента = СтрШаблон(
			НСтр("ru = 'Контрагент %1 по ЭДО отклонил документ: %2. Требуется корректировка.'", КодЯзыка),
			ДанныеСостоянияЭДО.Контрагент,
			Строка(ВнутреннийДокумент));
		
		РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
			ПредставлениеВнутреннегоДокумента,
			НСтр("ru = 'Комментарий'", КодЯзыка),
			ДанныеСостоянияЭДО.Комментарий);
		
		РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
			ПредставлениеВнутреннегоДокумента,,
			РаботаСУведомлениями.ПолучитьНавигационнуюСсылкуУведомления(ВнутреннийДокумент));
		
	ИначеЕсли ВидСобытия = Перечисления.СобытияУведомлений.ОбменПоЭДОВыполненУспешно Тогда
		
		ДанныеСостоянияЭДО = ОбменСКонтрагентамиДОВызовСервера.ПолучитьСостояниеДокумента(ВнутреннийДокумент);
		
		ПредставлениеВнутреннегоДокумента = СтрШаблон(
			НСтр("ru = 'Обмен с контрагентом %1 по ЭДО выполнен успешно: %2.'", КодЯзыка),
			ДанныеСостоянияЭДО.Контрагент,
			Строка(ВнутреннийДокумент));
		
		РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
			ПредставлениеВнутреннегоДокумента,
			НСтр("ru = 'Комментарий'", КодЯзыка),
			ДанныеСостоянияЭДО.Комментарий);
		
		РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
			ПредставлениеВнутреннегоДокумента,,
			РаботаСУведомлениями.ПолучитьНавигационнуюСсылкуУведомления(ВнутреннийДокумент));
		
	ИначеЕсли ВидСобытия = Перечисления.СобытияУведомлений.ТребуетсяАннулированиеДокумента Тогда
		
		ДанныеАннулирования =
			ОбменСКонтрагентамиДОВызовСервера.ЗапущенныеПроцессыАннулированияДокумента(ВнутреннийДокумент);
		
		ПредставлениеВнутреннегоДокумента = СтрШаблон(
			НСтр("ru = 'Получено предложение об аннулировании документа %1'", КодЯзыка),
			Строка(ВнутреннийДокумент));
		
		РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
			ПредставлениеВнутреннегоДокумента,,
			РаботаСУведомлениями.ПолучитьНавигационнуюСсылкуУведомления(ВнутреннийДокумент));
		
		Если ЗначениеЗаполнено(ДанныеАннулирования.ДокументАннулирования) Тогда
			РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
				ПредставлениеВнутреннегоДокумента,,
				СтрШаблон(НСтр("ru = 'Документ аннулирования: %1'"),
					РаботаСУведомлениями.ПолучитьНавигационнуюСсылкуУведомления(ВнутреннийДокумент)));
		КонецЕсли;
		
	ИначеЕсли ВидСобытия = Перечисления.СобытияУведомлений.ДокументАннулирован Тогда
		
		ДанныеАннулирования =
			ОбменСКонтрагентамиДОВызовСервера.ЗапущенныеПроцессыАннулированияДокумента(ВнутреннийДокумент);
		
		ПредставлениеВнутреннегоДокумента = СтрШаблон(
			НСтр("ru = 'Аннулирован документ %1'", КодЯзыка),
			Строка(ВнутреннийДокумент));
		
		РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
			ПредставлениеВнутреннегоДокумента,,
			РаботаСУведомлениями.ПолучитьНавигационнуюСсылкуУведомления(ВнутреннийДокумент));
		
		Если ЗначениеЗаполнено(ДанныеАннулирования.ДокументАннулирования) Тогда
			РаботаСУведомлениями.ДобавитьРеквизитКСтроке(
				ПредставлениеВнутреннегоДокумента,,
				СтрШаблон(НСтр("ru = 'Документ аннулирования: %1'"),
					РаботаСУведомлениями.ПолучитьНавигационнуюСсылкуУведомления(ВнутреннийДокумент)));
		КонецЕсли;
		
	Иначе
		
		ПредставлениеВнутреннегоДокумента = "";
		
	КонецЕсли;
	
	Возврат ПредставлениеВнутреннегоДокумента;
	
КонецФункции

// Позволяет скорректировать текст уведомления по событию с группировкой.
//
// Параметры:
//  ОбъектПодписки - СправочникСсылка - Объект подписки формируемого уведомления.
//  ВидСобытия - СправочникСсылка.ВидыБизнесСобытий, ПеречислениеСсылка.СобытияУведомлений - Вид события формируемого уведомления.
//  ОбъектыУведомления - Массив - Объекты формируемого уведомления.
//  ПолучательУведомления - СправочникСсылка.Пользователи - Получатель формируемого уведомления.
//  СпособУведомления - ПеречислениеСсылка.СпособыУведомления - Способ формируемого уведомления.
//  ДополнительныеОписания - Соответствие - Дополнительные описания.
// 
// Возвращаемое значение:
//  Строка - Текст уведомления.
//
Функция СформироватьТекстУведомленияПоОбъектамУведомленияПереопределяемый(
	ОбъектПодписки,
	ВидСобытия,
	ОбъектыУведомления,
	ПолучательУведомления,
	СпособУведомления,
	ДополнительныеОписания)
	
	ТекстУведомления = "";
	
	// Пример. Новый разделитель между текстами уведомлений при группировке.
	//Для Каждого ОбъектУведомления Из ОбъектыУведомления Цикл
	//	
	//	Если ВидСобытия = Перечисления.СобытияУведомлений.ПодошелСрокКонтроля Тогда
	//		ДополнительноеОписание = ОбъектыУведомления.Количество();
	//	ИначеЕсли ВидСобытия = Справочники.ВидыБизнесСобытий.ПеренаправлениеЗадачи Тогда
	//		ДополнительноеОписание = ДополнительныеОписания.Получить(ОбъектУведомления);
	//	Иначе
	//		ДополнительноеОписание = Неопределено;
	//	КонецЕсли;
	//	
	//	ПредставлениеОбъекта = РаботаСУведомлениями.СформироватьПредставлениеОбъекта(
	//		ОбъектУведомления,
	//		ВидСобытия,
	//		ПолучательУведомления,
	//		ДополнительноеОписание);
	//	
	//	ДобавитьЗначениеКСтрокеЧерезРазделитель(
	//		ТекстУведомления,
	//		"<hr>",
	//		ПредставлениеОбъекта);
	//	
	//КонецЦикла;
	
	Возврат ТекстУведомления;
	
КонецФункции

// Позволяет скорректировать текст уведомления по событию без группировки.
//
// Параметры:
//  ОбъектПодписки - СправочникСсылка, ДокументСсылка - Объект подписки формируемого уведомления.
//  ВидСобытия - СправочникСсылка.ВидыБизнесСобытий, ПеречислениеСсылка.СобытияУведомлений - Вид события формируемого уведомления.
//  ОбъектУведомления - СправочникСсылка, ДокументСсылка - Объект формируемого уведомления.
//  ПолучательУведомления - СправочникСсылка.Пользователи - Получатель формируемого уведомления.
//  СпособУведомления - ПеречислениеСсылка.СпособыУведомления - Способ формируемого уведомления.
//  ДополнительноеОписание - Строка - Дополнительное описание.
// 
// Возвращаемое значение:
//  Строка - Текст уведомления.
//
Функция СформироватьТекстУведомленияПоСобытиюПереопределяемый(
	ОбъектПодписки,
	ВидСобытия,
	ОбъектУведомления,
	ПолучательУведомления,
	СпособУведомления,
	ДополнительноеОписание)
	
	ТекстУведомления = "";
	
	// Пример. Новый текст уведомления о новой задаче (без группировки).
	// Требуется использовать вместе с примером из ВидыСобытийДляГруппировкиПереопределяемый,
	// чтобы отключить группирвоку уведомлений.
	//Если ВидСобытия = Справочники.ВидыБизнесСобытий.СозданиеЗадачи Тогда
	//	
	//	РеквизитыЗадачи = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ОбъектУведомления, "СрокИсполнения, Наименование");
	//	НеУказанПредставление = НСтр("ru = 'Не указан'", КодЯзыка);
	//	ФорматнаяСтрока = СтрШаблон("ДФ='dd.MM.yyyy HH:mm'; ДП='%1'", НеУказанПредставление);
	//	ПредставлениеСрока = Формат(РеквизитыЗадачи.СрокИсполнения, ФорматнаяСтрока);
	//	
	//	ТекстСсылокВыполненияЗадачи = "";
	//	ВключеноВыполнениеЗадачПоПочте = РегистрыСведений.НастройкиУведомлений.ПолучитьДополнительнуюНастройку(
	//		ПолучательУведомления,
	//		Перечисления.НастройкиУведомлений.ВыполнениеЗадачПоПочте);
	//	Если ВключеноВыполнениеЗадачПоПочте
	//		И ВыполнениеЗадачПоПочтеСервер.ВозможноВыполнениеЗадачиПоПочте(ОбъектУведомления) Тогда
	//		СсылкиОтветов = ВыполнениеЗадачПоПочтеСервер.ПолучитьОтветыВВидеСсылокMailto(ОбъектУведомления);
	//		ТекстСсылокВыполненияЗадачи = СсылкиОтветов.Текст;
	//	КонецЕсли;
	//	
	//	ТекстУведомления = СтрШаблон(
	//		НСтр("ru = 'Уважаемый (ая) %1!
	//			|
	//			|Вам поступила задача ""%2"".
	//			|Крайний срок исполнения задачи: %3.
	//			|Вы можетее перейти к задаче, нажав на ссылку: <a href=""%4"">Перейти к выполнению задачу</a>
	//			|%5
	//			|
	//			|С уважением,
	//			|Сервис уведомлений 1С:Документооборот'", КодЯзыка),
	//		ПолучательУведомления,
	//		РеквизитыЗадачи.Наименование,
	//		ПредставлениеСрока,
	//		РаботаСУведомлениями.ПолучитьНавигационнуюСсылкуУведомления(ОбъектУведомления),
	//		ТекстСсылокВыполненияЗадачи);
	//	
	//КонецЕсли;
	
	Если ВидСобытия = Перечисления.СобытияУведомлений.НеУказанИсполнительПроектнойЗадачи Тогда
		ТекстУведомления = ДополнительноеОписание;
	КонецЕсли;
	
	Возврат ТекстУведомления;
	
КонецФункции

// Позволяет скорректировать тему уведомления по событию с группировкой.
//
// Параметры:
//  ОбъектПодписки - СправочникСсылка - Объект подписки формируемого уведомления.
//  ВидСобытия - СправочникСсылка.ВидыБизнесСобытий, ПеречислениеСсылка.СобытияУведомлений - Вид события формируемого уведомления.
//  ОбъектыУведомления - Массив - Объекты формируемого уведомления.
//  ПолучательУведомления - СправочникСсылка.Пользователи - Получатель формируемого уведомления.
//  СпособУведомления - ПеречислениеСсылка.СпособыУведомления - Способ формируемого уведомления.
//  ДополнительныеОписания - Соответствие - Дополнительные описания.
// 
// Возвращаемое значение:
//  Строка - Тема уведомления.
//
Функция СформироватьТемуУведомленияПоОбъектамУведомленияПереопределяемый(
	ОбъектПодписки,
	ВидСобытия,
	ОбъектыУведомления,
	ПолучательУведомления,
	СпособУведомления,
	ДополнительныеОписания)
	
	ТемаУведомления = "";
	
	// Пример. Новая тема уведомления о новых файлах (с группировкой).
	//КодЯзыка = РаботаСУведомлениями.КодЯзыка(ПолучательУведомления);
	//Если ВидСобытия = Справочники.ВидыБизнесСобытий.СозданиеФайла Тогда
	//	
	//	ТемаУведомления = "";
	//	Если ОбъектыУведомления.Количество() = 1 Тогда
	//		ОбъектУведомления = ОбъектыУведомления[0];
	//		ТемаУведомления = СтрШаблон(
	//			НСтр("ru = 'Создан новый файл ""%1""'", КодЯзыка),
	//			ОбъектУведомления.Ссылка);
	//	Иначе
	//		КоличествоОбъектовУведомления = ОбъектыУведомления.Количество();
	//		ТемаУведомления = СтрШаблон(
	//			НСтр("ru = 'Созданы новые файлы (%1)'", КодЯзыка),
	//			КоличествоОбъектовУведомления);
	//	КонецЕсли;
	//	
	//КонецЕсли;
	
	Возврат ТемаУведомления;
	
КонецФункции

// Позволяет скорректировать тему уведомления по событию без группировки.
//
// Параметры:
//  ОбъектПодписки - СправочникСсылка, ДокументСсылка - Объект подписки формируемого уведомления.
//  ВидСобытия - СправочникСсылка.ВидыБизнесСобытий, ПеречислениеСсылка.СобытияУведомлений - Вид события формируемого уведомления.
//  ОбъектУведомления - СправочникСсылка, ДокументСсылка - Объект формируемого уведомления.
//  ПолучательУведомления - СправочникСсылка.Пользователи - Получатель формируемого уведомления.
//  СпособУведомления - ПеречислениеСсылка.СпособыУведомления - Способ формируемого уведомления.
//  ДополнительноеОписание - Строка - Дополнительное описание.
// 
// Возвращаемое значение:
//  Строка - Тема уведомления.
//
Функция СформироватьТемуУведомленияПоСобытиюПереопределяемый(
	ОбъектПодписки,
	ВидСобытия,
	ОбъектУведомления,
	ПолучательУведомления,
	СпособУведомления,
	ДополнительноеОписание)
	
	ТемаУведомления = "";
	
	// Пример. Новая тема уведомления о новой задаче (без группировки).
	// Требуется использовать вместе с примером из ВидыСобытийДляГруппировкиПереопределяемый,
	// чтобы отключить группирвоку уведомлений.
	//КодЯзыка = РаботаСУведомлениями.КодЯзыка(ПолучательУведомления);
	//Если ВидСобытия = Справочники.ВидыБизнесСобытий.СозданиеЗадачи Тогда
	//	НаименованиеЗадачи = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбъектУведомления, "Наименование");
	//	ТемаУведомления = СтрШаблон(
	//		НСтр("ru = 'Вам поступила задача ""%1""'", КодЯзыка),
	//		НаименованиеЗадачи);
	//КонецЕсли;
	
	Возврат ТемаУведомления;
	
КонецФункции

// Позволяет скорректировать формирование уведомления по событию с группировкой.
// Сформированные уведомления добавляются в параметр СформированныеУведомления.
//
// Параметры:
//  ПолучательУведомления - СправочникСсылка.Пользователи - Получатель формируемого уведомления.
//  СпособУведомления - ПеречислениеСсылка.СпособыУведомления - Способ формируемого уведомления.
//  ВидСобытия - СправочникСсылка.ВидыБизнесСобытий, ПеречислениеСсылка.СобытияУведомлений - Вид события формируемого уведомления.
//  Уведомления - ТаблицаЗначений - Таблица уведомлений о событиях для пользователей.
//   * Объект - СправочникСсылка, ДокументСсылка - Объект с которым произошло событие.
//   * Пользователь - СправочникСсылка.Пользователи - Пользователь, для которого предназанчено увдеомление.
//   * СпособУведомления - ПеречислениеСсылка.СпособыУведомления - Способ доставки уведомления.
//   * ВидСобытия - СправочникСсылка.ВидыБизнесСобытий, ПеречислениеСсылка.СобытияУведомлений - Произошедшее событие.
//   * ОбъектПодписки - СправочникСсылка, ДокументСсылка - Объект, в связи с подпиской на который рассылается уведомление.
//   * ТекстУведомления - Строка - Дополнительный текст уведомления, сформированный на этапе обработки событий.
//  СформированныеУведомления - ТаблицаЗначений - Сформированные для отправки уведомления.
//   * Пользователь - СправочникСсылка.Пользователи - Получатель уведомления.
//   * СпособУведомления - ПеречислениеСсылка.СпособыУведомления - Способ доставки уведомления.
//   * ТемаУведомления - Строка - Тема уведомления.
//   * ТекстУведомления - Строка - Текст уведомления.
//   * Уведомления - Массив - Уведомления, которые будут удалены из очереди, после отправки данного сформированного уведомления.
//   * Файлы - Массив - Файлы уведомления.
//
// Возвращаемое значение:
//  Булево - Поведение переопределено.
//
Функция СформироватьУведомленияПоВидуБизнесСобытияПереопределяемый(
	ПолучательУведомления,
	СпособУведомления,
	ВидСобытия,
	Уведомления,
	СформированныеУведомления)
	
	Переопределено = Ложь;
	
	// Пример. Уведомление о создании файла группировать не по объекту подписки, а только по событию.
	//Если ВидСобытия = Справочники.ВидыБизнесСобытий.СозданиеФайла Тогда
	//	
	//	Переопределено = Истина;
	//	
	//	// Группировка по событию и объекту подписки.
	//	РаботаСУведомлениями.СформироватьУведомленияГруппировкаПоСобытию(
	//		ПолучательУведомления,
	//		СпособУведомления,
	//		ВидСобытия,
	//		Уведомления,
	//		СформированныеУведомления);
	//	
	//КонецЕсли;
	
	Возврат Переопределено;
	
КонецФункции

// Позволяет скорректировать формирование уведомления по событию без группировки.
// Сформированные уведомления добавляются в параметр СформированныеУведомления.
//
// Параметры:
//  ПолучательУведомления - СправочникСсылка.Пользователи - Получатель формируемого уведомления.
//  СпособУведомления - ПеречислениеСсылка.СпособыУведомления - Способ формируемого уведомления.
//  ОбработанныеВидыБизнесСобытий - Массив - Виды события, по которым уже сгруппированы уведомления.
//  Уведомления - ТаблицаЗначений - Таблица уведомлений о событиях для пользователей.
//   * Объект - СправочникСсылка, ДокументСсылка - Объект с которым произошло событие.
//   * Пользователь - СправочникСсылка.Пользователи - Пользователь, для которого предназанчено увдеомление.
//   * СпособУведомления - ПеречислениеСсылка.СпособыУведомления - Способ доставки уведомления.
//   * ВидСобытия - СправочникСсылка.ВидыБизнесСобытий, ПеречислениеСсылка.СобытияУведомлений - Произошедшее событие.
//   * ОбъектПодписки - СправочникСсылка, ДокументСсылка - Объект, в связи с подпиской на который рассылается уведомление.
//   * ТекстУведомления - Строка - Дополнительный текст уведомления, сформированный на этапе обработки событий.
//  СформированныеУведомления - ТаблицаЗначений - Сформированные для отправки уведомления.
//   * Пользователь - СправочникСсылка.Пользователи - Получатель уведомления.
//   * СпособУведомления - ПеречислениеСсылка.СпособыУведомления - Способ доставки уведомления.
//   * ТемаУведомления - Строка - Тема уведомления.
//   * ТекстУведомления - Строка - Текст уведомления.
//   * Уведомления - Массив - Уведомления, которые будут удалены из очереди, после отправки данного сформированного уведомления.
//   * Файлы - Массив - Файлы уведомления.
//
// Возвращаемое значение:
//  Булево - Поведение переопределено.
//
Функция СформироватьУведомленияПоСобытиямПереопределяемый(
	ПолучательУведомления,
	СпособУведомления,
	ОбработанныеВидыБизнесСобытий,
	Уведомления,
	СформированныеУведомления) Экспорт
	
	Переопределено = Ложь;
	
	// Пример. Для пользователя Великанова все не сгруппированные уведомления объединять в одно письмо.
	//КодЯзыка = РаботаСУведомлениями.КодЯзыка(ПолучательУведомления);
	//Если СтрНайти(ПолучательУведомления, "Великанова") Тогда
	//	
	//	Переопределено = Истина;
	//	
	//	Отбор = Новый Структура();
	//	Отбор.Вставить("Пользователь", ПолучательУведомления);
	//	Отбор.Вставить("СпособУведомления", СпособУведомления);
	//	УведомленияПользователя = Уведомления.НайтиСтроки(Отбор);
	//	
	//	ТекстУведомления = Новый Массив;
	//	УведомленияПодписки = Новый Массив;
	//	
	//	Для Каждого Уведомление Из УведомленияПользователя Цикл
	//		
	//		Если ОбработанныеВидыБизнесСобытий.Найти(Уведомление.ВидСобытия) <> Неопределено Тогда
	//			Продолжить;
	//		КонецЕсли;
	//		
	//		ДанныеУведомления = РаботаСУведомлениями.ДанныеУведомленияПоСобытию(
	//			Уведомление.ОбъектПодписки, 
	//			Уведомление.ВидСобытия, 
	//			Уведомление.Объект,
	//			ПолучательУведомления,
	//			Уведомление.ТекстУведомления);
	//		
	//		ТекстУведомления.Добавить(
	//			СтрШаблон(
	//				"<b>%1</b>
	//				|%2",
	//				ДанныеУведомления.ТемаУведомления,
	//				ДанныеУведомления.ТекстУведомления));
	//		РаботаСУведомлениями.ДобавитьУведомлениеВМассив(УведомленияПодписки, Уведомление);
	//		
	//	КонецЦикла;
	//	
	//	ТекстУведомления = СтрСоединить(ТекстУведомления, "<hr>");
	//	
	//	СформированноеУведомление = СформированныеУведомления.Добавить();
	//	СформированноеУведомление.ТемаУведомления = НСтр("ru = 'Новые события в Документообороте'", КодЯзыка);
	//	СформированноеУведомление.ТекстУведомления = ТекстУведомления;
	//	СформированноеУведомление.Файлы = Новый Массив;
	//	СформированноеУведомление.Пользователь = ПолучательУведомления;
	//	СформированноеУведомление.СпособУведомления = СпособУведомления;
	//	СформированноеУведомление.Уведомления = УведомленияПодписки;
	//	
	//КонецЕсли;
	
	Возврат Переопределено;
	
КонецФункции

// Позволяет скорректировать файлы уведомления по событию с группировкой.
//
// Параметры:
//  ОбъектПодписки - СправочникСсылка - Объект подписки формируемого уведомления.
//  ВидСобытия - СправочникСсылка.ВидыБизнесСобытий, ПеречислениеСсылка.СобытияУведомлений - Вид события формируемого уведомления.
//  ОбъектыУведомления - Массив - Объекты формируемого уведомления.
//  ПолучательУведомления - СправочникСсылка.Пользователи - Получатель формируемого уведомления.
//  СпособУведомления - ПеречислениеСсылка.СпособыУведомления - Способ формируемого уведомления.
//  ДополнительныеОписания - Соответствие - Дополнительные описания.
// 
// Возвращаемое значение:
//  Массив - Массив структур файлов уведомлений.
//   * Ссылка - СправочникСсылка.Файлы - Ссылка на файл.
//   * ИмяФайла - Строка - Имя файла.
//
Функция СформироватьФайлыУведомленияПоОбъектамУведомленияПереопределяемый(
	ОбъектПодписки,
	ВидСобытия,
	ОбъектыУведомления,
	ПолучательУведомления,
	СпособУведомления,
	ДополнительныеОписания)
	
	Файлы = Неопределено;
	
	// Пример. В уведомление о создании файла прикладывать файлы (с группировкой).
	//Если ВидСобытия = Справочники.ВидыБизнесСобытий.СозданиеФайла Тогда
	//	
	//	Файлы = Новый Массив;
	//	Для Каждого Файл Из ОбъектыУведомления Цикл
	//		
	//		ИмяСРасширением = Файл.ПолноеНаименование;
	//		Если ЗначениеЗаполнено(Файл.ТекущаяВерсияРасширение) Тогда
	//			ИмяСРасширением = ИмяСРасширением + "." + Файл.ТекущаяВерсияРасширение;
	//		КонецЕсли;
	//		
	//		МаксимальныйРазмерВложений = 20 * 1024 * 1024; // 20 МБ
	//		
	//		ТекущийРазмерВложений = 0;
	//		ТекущийРазмерВложений = ТекущийРазмерВложений + Файл.ТекущаяВерсияРазмер;
	//		Если ТекущийРазмерВложений <= МаксимальныйРазмерВложений Тогда
	//			СтруктураФайла = Новый Структура("Ссылка, ИмяФайла");
	//			СтруктураФайла.Ссылка = Файл;
	//			СтруктураФайла.ИмяФайла = ИмяСРасширением;
	//			Файлы.Добавить(СтруктураФайла);
	//		Иначе
	//			Прервать;
	//		КонецЕсли;
	//		
	//	КонецЦикла;
	//	
	//КонецЕсли;
	
	Возврат Файлы;
	
КонецФункции

// Позволяет скорректировать файлы уведомления по событию без группировки.
//
// Параметры:
//  ОбъектПодписки - СправочникСсылка, ДокументСсылка - Объект подписки формируемого уведомления.
//  ВидСобытия - СправочникСсылка.ВидыБизнесСобытий, ПеречислениеСсылка.СобытияУведомлений - Вид события формируемого уведомления.
//  ОбъектУведомления - СправочникСсылка, ДокументСсылка - Объект формируемого уведомления.
//  ПолучательУведомления - СправочникСсылка.Пользователи - Получатель формируемого уведомления.
//  СпособУведомления - ПеречислениеСсылка.СпособыУведомления - Способ формируемого уведомления.
//  ДополнительноеОписание - Строка - Дополнительное описание.
// 
// Возвращаемое значение:
//  Массив - Массив структур файлов уведомлений.
//   * Ссылка - СправочникСсылка.Файлы - Ссылка на файл.
//   * ИмяФайла - Строка - Имя файла.
//
Функция СформироватьФайлыУведомленияПоСобытиюПереопределяемый(
	ОбъектПодписки,
	ВидСобытия,
	ОбъектУведомления,
	ПолучательУведомления,
	СпособУведомления,
	ДополнительноеОписание)
	
	Файлы = Неопределено;
	
	// Пример. В уведомление об изменении файла прикладывать файл (без группировки).
	//Если ВидСобытия = Справочники.ВидыБизнесСобытий.ИзменениеФайла Тогда
	//	
	//	Файлы = Новый Массив;
	//	
	//	Файл = ОбъектУведомления;
	//	
	//	ИмяСРасширением = Файл.ПолноеНаименование; 
	//	Если ЗначениеЗаполнено(Файл.ТекущаяВерсияРасширение) Тогда
	//		ИмяСРасширением = ИмяСРасширением + "." + Файл.ТекущаяВерсияРасширение;
	//	КонецЕсли;
	//	
	//	МаксимальныйРазмерВложений = 20 * 1024 * 1024; // 20 МБ
	//	
	//	ТекущийРазмерВложений = 0;
	//	ТекущийРазмерВложений = ТекущийРазмерВложений + Файл.ТекущаяВерсияРазмер;
	//	Если ТекущийРазмерВложений <= МаксимальныйРазмерВложений Тогда
	//		СтруктураФайла = Новый Структура("Ссылка, ИмяФайла");
	//		СтруктураФайла.Ссылка = Файл;
	//		СтруктураФайла.ИмяФайла = ИмяСРасширением;
	//		Файлы.Добавить(СтруктураФайла);
	//	КонецЕсли;
	//	
	//КонецЕсли;
	
	Возврат Файлы;
	
КонецФункции

#КонецОбласти