////////////////////////////////////////////////////////////////////////////////
// Модуль содержит процедуры и функции для работы с электронными подписями.
// 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Заполняет список подписей файла.
//
// Параметры:
//  ТекущийОбъект - СправочникСсылка.ВерсииФайлов
//
Процедура ЗаполнитьСписокПодписейФайла(ТекущийОбъект, ЭлементыДерева, КоличествоПодписей,
	УникальныйИдентификатор, ПолучитьДляПомеченногоНаУдалениеОбъекта = Ложь) Экспорт
	
	КоличествоПодписей = 0;
	
	Если Не ТекущийОбъект.ПодписанЭП Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеПодписей = ПодписиОбъектаСУчетомДоверенностей(ТекущийОбъект, УникальныйИдентификатор);
	ЗаполнитьСтатусыПодписейИДоверенностей(ДанныеПодписей);
	
	Для Каждого ДанныеПодписи Из ДанныеПодписей Цикл
		НоваяСтрока = ЭлементыДерева.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеПодписи);
		
		Если НоваяСтрока.Свойство("АвторПодписиИСтатус") Тогда
			НоваяСтрока.АвторПодписиИСтатус = 
				НоваяСтрока.АвторПодписи
				+ Символы.ПС
				+ НоваяСтрока.Статус;
		КонецЕсли;
		Если НоваяСтрока.Свойство("ДатаПодписиИКомментарий") Тогда
			НоваяСтрока.ДатаПодписиИКомментарий = 
				Формат(НоваяСтрока.ДатаПодписи, "ДФ='dd.MM.yyyy HH:mm'")
				+ Символы.ПС
				+ НоваяСтрока.Комментарий;
		КонецЕсли;
		
		КоличествоПодписей = КоличествоПодписей + 1;
	КонецЦикла;
	
КонецПроцедуры

// Заполняет список подписей Входящего Исходящего Внутреннего документа в дереве.
//
Процедура ЗаполнитьСписокПодписейДокумента(ТекущийОбъект, ЭлементыДерева, КоличествоПодписей,
	УникальныйИдентификатор) Экспорт
	
	КоличествоПодписей = 0;
	
	Если Не ТекущийОбъект.ПодписанЭП Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеПодписей = ПодписиОбъектаСУчетомДоверенностей(ТекущийОбъект, УникальныйИдентификатор);
	ЗаполнитьСтатусыПодписейИДоверенностей(ДанныеПодписей);
	
	Для Каждого ДанныеПодписи Из ДанныеПодписей Цикл
		НоваяСтрока = ЭлементыДерева.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеПодписи);
		
		Если НоваяСтрока.Свойство("АвторПодписиИСтатус") Тогда
			НоваяСтрока.АвторПодписиИСтатус = 
				НоваяСтрока.АвторПодписи
				+ Символы.ПС
				+ НоваяСтрока.Статус;
		КонецЕсли;
		Если НоваяСтрока.Свойство("ДатаПодписиИКомментарий") Тогда
			НоваяСтрока.ДатаПодписиИКомментарий = 
				Формат(НоваяСтрока.ДатаПодписи, "ДФ='dd.MM.yyyy HH:mm'")
				+ Символы.ПС
				+ НоваяСтрока.Комментарий;
		КонецЕсли;
		
		КоличествоПодписей = КоличествоПодписей + 1;
	КонецЦикла;
	
КонецПроцедуры

// Заполняет список подписей Входящего Исходящего Внутреннего документа и его подчиненных файлов.
//
Процедура ЗаполнитьСписокПодписей(ОбъектСсылка, ТаблицаПодписей, УникальныйИдентификатор, ГруппаЭП) Экспорт
	
	ВсегоПодписей = 0;
	ТаблицаПодписей.ПолучитьЭлементы().Очистить();
	
	ЭлементыДерева = ТаблицаПодписей.ПолучитьЭлементы();
	
	НоваяСтрока = ЭлементыДерева.Добавить();
	// для ветки дерева используем КомуВыданСертификат как Представление
	НоваяСтрока.Объект = ОбъектСсылка;
	НоваяСтрока.КомуВыданСертификат = Строка(ТипЗнч(ОбъектСсылка)) + " """ + Строка(ОбъектСсылка) + """"; 
	Если НоваяСтрока.Свойство("АвторПодписиИСтатус") Тогда
		НоваяСтрока.АвторПодписиИСтатус = НоваяСтрока.КомуВыданСертификат; 
	КонецЕсли;
	НоваяСтрока.ИндексКартинки = 0;  // иконка справочника
	КоличествоПодписей = 0;
	
	ЗаполнитьСписокПодписейДокумента(ОбъектСсылка, 
		НоваяСтрока.ПолучитьЭлементы(), КоличествоПодписей, УникальныйИдентификатор);
	ВсегоПодписей = ВсегоПодписей + КоличествоПодписей;
	
	Если КоличествоПодписей = 0 Тогда
		ЭлементыДерева.Удалить(НоваяСтрока);
	КонецЕсли;
	
	МассивФайлов = РаботаСФайламиВызовСервера.ПолучитьВсеПодчиненныеФайлы(ОбъектСсылка);
	
	Для Каждого ФайлСсылка Из МассивФайлов Цикл
		
		НоваяСтрока = ЭлементыДерева.Добавить();
		// для ветки дерева используем КомуВыданСертификат как Представление
		НоваяСтрока.Объект = ФайлСсылка;
		НоваяСтрока.КомуВыданСертификат = Строка(ТипЗнч(ФайлСсылка)) + " """ + Строка(ФайлСсылка) + """";
		Если НоваяСтрока.Свойство("АвторПодписиИСтатус") Тогда
			НоваяСтрока.АвторПодписиИСтатус = НоваяСтрока.КомуВыданСертификат;
		КонецЕсли;
		НоваяСтрока.ИндексКартинки = ФайлСсылка.ИндексКартинки;
		КоличествоПодписей = 0;
		
		ЗаполнитьСписокПодписейФайла(ФайлСсылка.ТекущаяВерсия, НоваяСтрока.ПолучитьЭлементы(), 
			КоличествоПодписей, УникальныйИдентификатор);
		ВсегоПодписей = ВсегоПодписей + КоличествоПодписей;
		
		Если КоличествоПодписей = 0 Тогда
			ЭлементыДерева.Удалить(НоваяСтрока);
		КонецЕсли;
		
	КонецЦикла;
	
	Если ГруппаЭП <> Неопределено Тогда
		ТекстЗаголовка = НСтр("ru = 'ЭП'");
		Если ВсегоПодписей <> 0 Тогда
			ТекстЗаголовка = ТекстЗаголовка + " (" + Строка(ВсегоПодписей) + ")";
		КонецЕсли;
		ГруппаЭП.Заголовок = ТекстЗаголовка;
		
		Если ВсегоПодписей = 0 Тогда 
			ГруппаЭП.Видимость = Ложь;
		Иначе	
			ГруппаЭП.Видимость = Истина;
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

Функция ЗаполнитьДанныеПодписиОбъекта(ОбъектСсылка, ИдентификаторФормы) Экспорт
	
	ДанныеПодписей = ПодписиОбъектаСУчетомДоверенностей(ОбъектСсылка, ИдентификаторФормы);
	ЗаполнитьСтатусыПодписейИДоверенностей(ДанныеПодписей);
	
	Если ДанныеПодписей.Количество() = 0 Тогда
		Возврат НовыеДанныеПодписиСУчетомДоверенности();
	Иначе
		Возврат ДанныеПодписей[0];
	КонецЕсли;
	
КонецФункции

// Удаляет выделенные ЭП.
//
Процедура УдалитьПодписиИОбновитьСписок(РеквизитПодписанИзменен, ВыделенныеСтроки,
	ОбъектСсылка, ТаблицаПодписей, УникальныйИдентификатор, ГруппаЭП) Экспорт
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ЭлектроннаяПодпись") Тогда
		Возврат;
	КонецЕсли;
	МодульЭлектроннаяПодпись = ОбщегоНазначения.ОбщийМодуль("ЭлектроннаяПодпись");
	
	ВсеОбъекты = Новый Соответствие; // соответствие объекта и таблицы номеров строк табл части
	
	МассивВыделенныеСтроки = Новый Массив;
	Для Каждого Элемент Из ВыделенныеСтроки Цикл
		
		ДанныеСтроки = ТаблицаПодписей.НайтиПоИдентификатору(Элемент);
		
		Если ДанныеСтроки.Объект <> Неопределено И (НЕ ДанныеСтроки.Объект.Пустая()) Тогда
			
			Если Не ЗначениеЗаполнено(ДанныеСтроки.УникальныйИдентификатор) Тогда
				Продолжить;
			КонецЕсли;
			
			МассивПодписей = ВсеОбъекты.Получить(ДанныеСтроки.Объект);
			
			Если МассивПодписей = Неопределено Тогда
				МассивПодписей = Новый Массив;
			КонецЕсли;
			
			МассивПодписей.Добавить(ДанныеСтроки.УникальныйИдентификатор);
			
			ВсеОбъекты.Вставить(ДанныеСтроки.Объект, МассивПодписей);
			
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ПараКлючЗначение Из ВсеОбъекты Цикл
		
		ТекущийОбъектСсылка = ПараКлючЗначение.Ключ;
		ТаблицаВыделенныеСтроки = ПараКлючЗначение.Значение;
		
		Если ТипЗнч(ТекущийОбъектСсылка) = Тип("СправочникСсылка.ВерсииФайлов") Тогда
			
			РаботаСФайламиСлужебныйВызовСервера.УдалитьПодписиВерсииФайла(ТекущийОбъектСсылка, ТаблицаВыделенныеСтроки, 
				РеквизитПодписанИзменен, УникальныйИдентификатор);
			
		Иначе
				
			МодульЭлектроннаяПодпись.УдалитьПодпись(ТекущийОбъектСсылка, ТаблицаВыделенныеСтроки, 
				УникальныйИдентификатор);
				
			ОбъектПодписан = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущийОбъектСсылка, "ПодписанЭП");
			
			КоличествоПодписей = КоличествоПодписей(ТекущийОбъектСсылка);
			Если ОбъектПодписан = Истина И КоличествоПодписей = 0 Тогда
				РеквизитПодписанИзменен = Истина;
				Объект = ТекущийОбъектСсылка.ПолучитьОбъект(); 
				ЗаблокироватьДанныеДляРедактирования(ТекущийОбъектСсылка, , УникальныйИдентификатор);
				Объект.ПодписанЭП = Ложь;
				Объект.Записать();
				РазблокироватьДанныеДляРедактирования(ТекущийОбъектСсылка, УникальныйИдентификатор);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ВсеОбъекты.Количество() > 0 Тогда
		ЗаполнитьСписокПодписей(ОбъектСсылка, ТаблицаПодписей, 
			УникальныйИдентификатор, ГруппаЭП);
	КонецЕсли;
	
КонецПроцедуры

// Удаляем все подписи объекта
//
Процедура УдалитьПодписиОбъекта(ПодписанныйОбъект) Экспорт
	
	НаборЗаписей = РегистрыСведений.ЭлектронныеПодписи.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Объект.Установить(ПодписанныйОбъект);
	НаборЗаписей.Очистить();
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Снимает признак "Подписан ЭП" у указанного объекта, при необходимости снимая и у владельцев.
//
Процедура СнятьПризнакПодписанЭП(Ссылка) Экспорт
	
	СсылкиКСнятию = Новый Массив;
	Если Ссылка.Метаданные().Реквизиты.Найти("ПодписанЭП") <> Неопределено
		И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ПодписанЭП") = Истина Тогда
		СсылкиКСнятию.Добавить(Ссылка);
	КонецЕсли;
	
	Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.ВерсииФайлов") Тогда
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка,
			"Владелец, Владелец.ТекущаяВерсия, Владелец.ПодписанЭП");
		Если ЗначенияРеквизитов.ВладелецТекущаяВерсия = Ссылка
			И ЗначенияРеквизитов.ВладелецПодписанЭП = Истина Тогда
			СсылкиКСнятию.Добавить(ЗначенияРеквизитов.Владелец);
		КонецЕсли;
	КонецЕсли;
	
	Для Каждого СсылкаКСнятию Из СсылкиКСнятию Цикл
		Если ТипЗнч(СсылкаКСнятию) = Тип("СправочникСсылка.Файлы") Тогда
			ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаКСнятию,
				"ВладелецФайла, ВладелецФайла.ПодписанЭП");
			Если ЗначенияРеквизитов.ВладелецФайлаПодписанЭП = Истина
				И КоличествоПодписей(ЗначенияРеквизитов.ВладелецФайла) = 0 Тогда
				СсылкиКСнятию.Добавить(ЗначенияРеквизитов.ВладелецФайла);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если СсылкиКСнятию.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	
	Блокировка = Новый БлокировкаДанных;
	Для Каждого СсылкаКСнятию Из СсылкиКСнятию Цикл
		ЭлементБлокировки = Блокировка.Добавить(СсылкаКСнятию.Метаданные().ПолноеИмя());
		ЭлементБлокировки.УстановитьЗначение("Ссылка", СсылкаКСнятию);
	КонецЦикла;
	
	Попытка
		
		Блокировка.Заблокировать();
		
		Для Каждого СсылкаКСнятию Из СсылкиКСнятию Цикл
			ОбъектКСнятию = СсылкаКСнятию.ПолучитьОбъект();
			ОбъектКСнятию.ПодписанЭП = Ложь;
			ОбъектКСнятию.Записать();
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

// Для Вх Исх Внутр документов формирует массив значений Полей (включая имя поля) - только важные реквизиты.
//
// Параметры:
//  ОбъектСсылка - СправочникСсылка
//  Версия - Число - Версия набора подписываемых ключевых параметров объекта.
//  ДополнительныеПараметры - Структура -
//  
Функция СформироватьНаборВажныхПолей(Знач ОбъектСсылка, Версия = Неопределено,
	ДополнительныеПараметры = Неопределено) Экспорт
	
	МассивИмен = Новый Массив;
	
	Если ТипЗнч(ОбъектСсылка) = Тип("СправочникСсылка.ВнутренниеДокументы") Тогда
		МассивИмен = Справочники.ВнутренниеДокументы.ПолучитьИменаКлючевыхРеквизитов(Версия);
	ИначеЕсли ТипЗнч(ОбъектСсылка) = Тип("СправочникСсылка.ВходящиеДокументы") Тогда
		МассивИмен = Справочники.ВходящиеДокументы.ПолучитьИменаКлючевыхРеквизитов(Версия);
	ИначеЕсли ТипЗнч(ОбъектСсылка) = Тип("СправочникСсылка.ИсходящиеДокументы") Тогда
		МассивИмен = Справочники.ИсходящиеДокументы.ПолучитьИменаКлючевыхРеквизитов(Версия);
	ИначеЕсли ТипЗнч(ОбъектСсылка) = Тип("СправочникСсылка.ВизыСогласования") Тогда
		МассивИмен = Справочники.ВизыСогласования.ПолучитьИменаКлючевыхРеквизитов(Версия);
	ИначеЕсли ТипЗнч(ОбъектСсылка) = Тип("СправочникСсылка.Резолюции") Тогда
		МассивИмен = Справочники.Резолюции.ПолучитьИменаКлючевыхРеквизитов(Версия);
	ИначеЕсли ТипЗнч(ОбъектСсылка) = Тип("Структура") Тогда
		Для Каждого Элемент Из ОбъектСсылка Цикл
			МассивИмен.Добавить(Элемент.Ключ);
		КонецЦикла;
	КонецЕсли;
	
	ПараметрыФормирования = ПараметрыФормированияНабораВажныхПолей(Версия, ДополнительныеПараметры);
	
	МассивПолей = Новый Массив;
	Для Каждого ИмяПоля Из МассивИмен Цикл
		
		Если ПараметрыФормирования.ИсключаемыеРеквизиты[ИмяПоля] = Истина Тогда
			Продолжить;
		КонецЕсли;
		
		ПозицияТочки = Найти(ИмяПоля, ".");
		Если ПозицияТочки = 0 Тогда
			
			ДобавитьЗаписьПоляОбъектаВНаборВажныхПолей(ОбъектСсылка, ИмяПоля, МассивПолей, ПараметрыФормирования);
			
		Иначе  // табличные части отдельно обработать
			ИмяТабличнойЧасти = Лев(ИмяПоля, ПозицияТочки - 1);
			ИмяКолонки = Сред(ИмяПоля, ПозицияТочки + 1);
			
			РезультатЗапросаКТаблице = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбъектСсылка, ИмяТабличнойЧасти);
			ВыборкаСтрок = РезультатЗапросаКТаблице.Выбрать();
			НомерСтроки = 0;
			Пока ВыборкаСтрок.Следующий() Цикл
				
				ЗначениеПоля = ЗначениеПоляТаблицыДляНабораВажныхПолей(
					ВыборкаСтрок, ИмяТабличнойЧасти, ИмяКолонки, НомерСтроки, ПараметрыФормирования);
				
				ИмяЯчейки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					 "%1_%2_%3", ИмяТабличнойЧасти, НомерСтроки, ИмяКолонки);
				СтрокаЗначения = ПолучитьДанныеОбъектаСтрокой(ЗначениеПоля, ИмяЯчейки);
				МассивПолей.Добавить(СтрокаЗначения);
				
				НомерСтроки = НомерСтроки + 1;
				
			КонецЦикла;
			
		КонецЕсли;
	КонецЦикла;
	
	Возврат МассивПолей;
	
КонецФункции

// Формирует файл с именами и значениями полей.
//
Процедура СформироватьФайлКлючевыхПолей(МассивПолей, ИмяФайлаСПутем) Экспорт
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	
	Для Каждого ЗначениеПоля Из МассивПолей Цикл
		ТекстовыйДокумент.ДобавитьСтроку(ЗначениеПоля);
	КонецЦикла;
	
	ТекстовыйДокумент.Записать(ИмяФайлаСПутем);
	
КонецПроцедуры	

// Получить объекты для подписи - массив структур АдресФайла или ДвоичныеДанные, ОбъектСсылкаДляПодписи
//  где АдресФайла - адрес файла во временном хранилище
//
Процедура ПолучитьОбъектыДляПодписи(ОбъектСсылка, УникальныйИдентификаторФормы, МассивОбъектовДляПодписи) Экспорт
	
	Если ДелопроизводствоКлиентСервер.ЭтоДокумент(ОбъектСсылка) Тогда
		
		// тут формируем файл из важных полей карточки
		ДвоичныеДанные = ПолучитьДвоичныеДанныеОбъекта(ОбъектСсылка);
		
		ОбъектДляПодписи = Новый Структура("ДвоичныеДанные, ОбъектСсылкаДляПодписи", 
			ДвоичныеДанные, ОбъектСсылка);
		МассивОбъектовДляПодписи.Добавить(ОбъектДляПодписи);
		
		// тут собираем все подчиненные файлы
		МассивВозврата = РаботаСФайламиВызовСервера.ПолучитьДанныеФайлаИНавигационнуюСсылкуВерсииВсехПодчиненныхФайлов(ОбъектСсылка, УникальныйИдентификаторФормы);
		ПервыйФайл = Истина;
		
		Для Каждого СтруктураВозврата Из МассивВозврата Цикл
			
			ДанныеФайла = СтруктураВозврата.ДанныеФайла;
			АдресФайла = СтруктураВозврата.НавигационнаяСсылкаВерсии;
			ОбъектСсылкаДляПодписи = ДанныеФайла.Ссылка;
			
			СтруктураДляПодписи = Неопределено;
			
			// только первый файл кладем как ДвоичныеДанные
			Если ПервыйФайл Тогда
				
				ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресФайла);
				УдалитьИзВременногоХранилища(АдресФайла);
				
				СтруктураДляПодписи = Новый Структура("ДвоичныеДанные, ОбъектСсылкаДляПодписи", 
					ДвоичныеДанные, ОбъектСсылкаДляПодписи);
					
				ПервыйФайл = Ложь;
			Иначе
				СтруктураДляПодписи = Новый Структура("АдресФайла, ОбъектСсылкаДляПодписи", 
					АдресФайла, ОбъектСсылкаДляПодписи);
			КонецЕсли;
			
			МассивОбъектовДляПодписи.Добавить(СтруктураДляПодписи);
			
		КонецЦикла;
		
	ИначеЕсли ТипЗнч(ОбъектСсылка) = Тип("СправочникСсылка.ВизыСогласования") 
		ИЛИ ТипЗнч(ОбъектСсылка) = Тип("СправочникСсылка.Резолюции") ИЛИ ТипЗнч(ОбъектСсылка) = Тип("Структура") Тогда 
		
		ДвоичныеДанные = ПолучитьДвоичныеДанныеОбъекта(ОбъектСсылка);
		
		ОбъектДляПодписи = Новый Структура("ДвоичныеДанные, ОбъектСсылкаДляПодписи", 
			ДвоичныеДанные, ОбъектСсылка);
		МассивОбъектовДляПодписи.Добавить(ОбъектДляПодписи);
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает двоичные данные объекта (Входящий Исходящий Внутренний).
//
// Параметры:
//  ОбъектСсылка
//  Версия - Число - Версия набора подписываемых ключевых параметров объекта.
//  ДополнительныеПараметры - Структура -
//
Функция ПолучитьДвоичныеДанныеОбъекта(ОбъектСсылка, Версия = Неопределено,
	ДополнительныеПараметры = Неопределено) Экспорт
	
	ИмяФайлаСПутем = ПолучитьИмяВременногоФайла();
	МассивПолей = СформироватьНаборВажныхПолей(ОбъектСсылка, Версия, ДополнительныеПараметры);
	СформироватьФайлКлючевыхПолей(МассивПолей, ИмяФайлаСПутем);
	
	ДвоичныеДанные = Новый ДвоичныеДанные(ИмяФайлаСПутем);
	
	УдалитьФайлы(ИмяФайлаСПутем);
	
	Возврат ДвоичныеДанные;
	
КонецФункции

// Возвращает двоичные данные объекта (Входящий Исходящий Внутренний) и подписи.
//
// Параметры:
//  ОбъектСсылка
//  АдресПодписи
//  Версия - Число - Версия набора подписываемых ключевых параметров объекта.
//  ДополнительныеПараметры - Структура -
//
// Возвращаемое значение:
//  Структура -
//    ДвоичныеДанные
//    ДвоичныеДанныеПодписи
//
Функция ПолучитьДвоичныеДанныеОбъектаИПодписи(ОбъектСсылка,
	АдресПодписи, Версия = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	ДвоичныеДанные = ПолучитьДвоичныеДанныеОбъекта(ОбъектСсылка, Версия, ДополнительныеПараметры);
	
	Попытка
		ДвоичныеДанныеПодписи = ПолучитьИзВременногоХранилища(АдресПодписи);
	Исключение
		ДвоичныеДанныеПодписи = Неопределено;
	КонецПопытки;
	
	СтруктураВозврата = Новый Структура("ДвоичныеДанные, ДвоичныеДанныеПодписи", 
		ДвоичныеДанные, ДвоичныеДанныеПодписи);
	
	Возврат СтруктураВозврата;
	
КонецФункции

// Заносит информацию о массиве ЭП, замещая прежние подписи.
//
Процедура ЗаменитьИнформациюОПодписяхОбъекта(МассивДанныхДляЗанесенияВРегистр, ОбъектСсылка,
	УникальныйИдентификатор = Неопределено) Экспорт
	
	Если ТипЗнч(ОбъектСсылка) = Тип("СправочникСсылка.Файлы") Тогда
		
		СтруктураРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ОбъектСсылка, "Редактирует, Зашифрован");
		Редактирует = СтруктураРеквизитов.Редактирует;
		Зашифрован = СтруктураРеквизитов.Зашифрован;
		
		Если ЗначениеЗаполнено(Редактирует) Тогда
			СтрокаИсключения = НСтр("ru = 'Нельзя подписать занятый файл: ""'") + Строка(ОбъектСсылка) + """";
			ВызватьИсключение СтрокаИсключения;
		КонецЕсли;
		
		Зашифрован = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбъектСсылка, "Зашифрован");
		Если Зашифрован Тогда
			СтрокаИсключения = НСтр("ru = 'Нельзя подписать зашифрованный файл: ""'") + Строка(ОбъектСсылка) + """";
			ВызватьИсключение СтрокаИсключения;
		КонецЕсли;
		
	КонецЕсли;
	
	ПодписываемыйОбъектСсылка = ОбъектСсылка;
	Если ТипЗнч(ОбъектСсылка) = Тип("СправочникСсылка.Файлы") Тогда
		ПодписываемыйОбъектСсылка = ОбъектСсылка.ТекущаяВерсия;
	КонецЕсли;
	
	НачатьТранзакцию();
	Попытка
		
		ПодписываемыйОбъект = ПодписываемыйОбъектСсылка.ПолучитьОбъект();
		ЗаблокироватьДанныеДляРедактирования(ПодписываемыйОбъектСсылка, , УникальныйИдентификатор);
		
		ОбъектСТЧЭП = ПодписываемыйОбъектСсылка.Метаданные().ТабличныеЧасти.Найти("ЭлектронныеЦифровыеПодписи") <> Неопределено;
		
		Если Не ОбъектСТЧЭП Тогда
			Выборка = ПолучитьЭлектронныеПодписи(ПодписываемыйОбъектСсылка);
		КонецЕсли;
		
		// проверим права на удаление подписей
		Если ОбъектСТЧЭП Тогда
			Если НЕ ПользователиСерверПовтИсп.ЭтоПолноправныйПользовательИБ() Тогда 
				Для Каждого Подпись Из ПодписываемыйОбъект.ЭлектронныеЦифровыеПодписи Цикл
					
					ПодписьУдаляется = Истина;
					
					Для Каждого Данные Из МассивДанныхДляЗанесенияВРегистр Цикл
						Если Подпись.ДатаПодписи = Данные.ДатаПодписи
							И Подпись.Комментарий = Данные.Комментарий
							И Подпись.КомуВыданСертификат = Данные.КомуВыданСертификат
							И Подпись.УстановившийПодпись = Данные.УстановившийПодпись
							И Подпись.Отпечаток = Данные.Отпечаток
							И Подпись.Подпись.Получить() = Данные.НоваяПодписьДвоичныеДанные
							И Подпись.Сертификат.Получить() = Данные.ДвоичныеДанныеСертификата Тогда
							
							ПодписьУдаляется = Ложь;
							Прервать;
							
						КонецЕсли;
					КонецЦикла;
					
					Если ПодписьУдаляется Тогда
						Если Подпись.УстановившийПодпись <> Пользователи.ТекущийПользователь() Тогда
							ВызватьИсключение НСтр("ru = 'У вас нет прав на удаление подписи.'");
						КонецЕсли;
					КонецЕсли;
					
				КонецЦикла;
			КонецЕсли;
		Иначе
			Если НЕ ПользователиСерверПовтИсп.ЭтоПолноправныйПользовательИБ() Тогда
				Пока Выборка.Следующий() Цикл
					
					Подпись = Выборка;
					ПодписьУдаляется = Истина;
					
					Для Каждого Данные Из МассивДанныхДляЗанесенияВРегистр Цикл
						Если Подпись.ДатаПодписи = Данные.ДатаПодписи
							И Подпись.Комментарий = Данные.Комментарий
							И Подпись.КомуВыданСертификат = Данные.КомуВыданСертификат
							И Подпись.УстановившийПодпись = Данные.УстановившийПодпись
							И Подпись.Отпечаток = Данные.Отпечаток
							И Подпись.Подпись.Получить() = Данные.НоваяПодписьДвоичныеДанные
							И Подпись.Сертификат.Получить() = Данные.ДвоичныеДанныеСертификата Тогда
							
							ПодписьУдаляется = Ложь;
							Прервать;
							
						КонецЕсли;
					КонецЦикла;
					
					Если ПодписьУдаляется Тогда
						Если Подпись.УстановившийПодпись <> Пользователи.ТекущийПользователь() Тогда
							ВызватьИсключение НСтр("ru = 'У вас нет прав на удаление подписи.'");
						КонецЕсли;
					КонецЕсли;
					
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
		Если ОбъектСТЧЭП Тогда
			ПодписываемыйОбъект.ЭлектронныеЦифровыеПодписи.Очистить();
			Для Каждого Данные Из МассивДанныхДляЗанесенияВРегистр Цикл
				НоваяЗапись = ПодписываемыйОбъект.ЭлектронныеЦифровыеПодписи.Добавить();
				НоваяЗапись.КомуВыданСертификат = Данные.КомуВыданСертификат;
				НоваяЗапись.ДатаПодписи = Данные.ДатаПодписи;
				НоваяЗапись.ИмяФайлаПодписи = Данные.ИмяФайлаПодписи;
				НоваяЗапись.Комментарий = Данные.Комментарий;
				НоваяЗапись.Отпечаток = Данные.Отпечаток;
				НоваяЗапись.Подпись = Новый ХранилищеЗначения(Данные.НоваяПодписьДвоичныеДанные);
				НоваяЗапись.УстановившийПодпись = Данные.УстановившийПодпись;
				НоваяЗапись.Сертификат = Новый ХранилищеЗначения(Данные.ДвоичныеДанныеСертификата);
			КонецЦикла;
		Иначе
			РаботаСЭП.УдалитьПодписиОбъекта(ПодписываемыйОбъектСсылка);
			Для Каждого Данные Из МассивДанныхДляЗанесенияВРегистр Цикл
				СвойстваПодписи = Новый Структура;
				СвойстваПодписи.Вставить("Подпись", Данные.НоваяПодписьДвоичныеДанные);
				СвойстваПодписи.Вставить("Отпечаток", Данные.Отпечаток);
				СвойстваПодписи.Вставить("ДатаПодписи", Данные.ДатаПодписи);
				СвойстваПодписи.Вставить("Комментарий", Данные.Комментарий);
				СвойстваПодписи.Вставить("КомуВыданСертификат", Данные.КомуВыданСертификат);
				СвойстваПодписи.Вставить("Сертификат", Данные.ДвоичныеДанныеСертификата);
				СвойстваПодписи.Вставить("УстановившийПодпись", Данные.УстановившийПодпись);
				РаботаСЭП.ЗанестиИнформациюОПодписи(ПодписываемыйОбъектСсылка, СвойстваПодписи);
			КонецЦикла;
		КонецЕсли;
		
		ПодписываемыйОбъект.ПодписанЭП = (МассивДанныхДляЗанесенияВРегистр.Количество() <> 0);
		ПодписываемыйОбъект.ДополнительныеСвойства.Вставить("ЗаписьПодписанногоОбъекта", Истина); // чтобы прошла запись ранее подписанного объекта
		
		УстановитьПривилегированныйРежим(Истина);
		ПодписываемыйОбъект.Записать();
		РазблокироватьДанныеДляРедактирования(ПодписываемыйОбъектСсылка, УникальныйИдентификатор);
		
		Если ТипЗнч(ОбъектСсылка) = Тип("СправочникСсылка.Файлы") Тогда
			ФайлСсылка = ОбъектСсылка;
			ВерсияПодписана = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПодписываемыйОбъектСсылка, "ПодписанЭП");
			
			ФайлСсылкаПодписан = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ФайлСсылка, "ПодписанЭП");
			Если ФайлСсылкаПодписан <> ВерсияПодписана Тогда
				ФайлОбъект = ФайлСсылка.ПолучитьОбъект(); 
				ЗаблокироватьДанныеДляРедактирования(ФайлСсылка, , УникальныйИдентификатор);
				ФайлОбъект.ПодписанЭП = ВерсияПодписана;
				
				ФайлОбъект.Записать();
				РазблокироватьДанныеДляРедактирования(ФайлСсылка, УникальныйИдентификатор);
			КонецЕсли;
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Проверяет изменены ли ключевые поля и бросает исключение, если изменены.
//
Процедура ПроверитьИзмененностьКлючевыхПолей(МассивИмен, ПроверяемыйОбъект, Ссылка) Экспорт
	
	РеквизитыОбъектаДоИзменения = Ссылка.ПолучитьОбъект().Метаданные().Реквизиты;
	
	Для Каждого ИмяПоля Из МассивИмен Цикл
		
		ПозицияТочки = Найти(ИмяПоля, ".");
		Если ПозицияТочки = 0 Тогда
			
			Если РеквизитыОбъектаДоИзменения.Найти(ИмяПоля) = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Если ПроверяемыйОбъект[ИмяПоля] <> Ссылка[ИмяПоля] Тогда
				ВызватьИсключение НСтр("ru = 'Подписанный документ нельзя редактировать.'");
			КонецЕсли;
			
		Иначе  // табличные части отдельно обработать
			
			ИмяТабличнойЧасти = Лев(ИмяПоля, ПозицияТочки - 1);
			ИмяКолонки = Сред(ИмяПоля, ПозицияТочки + 1);
			
			Если ПроверяемыйОбъект[ИмяТабличнойЧасти].Количество() <> Ссылка[ИмяТабличнойЧасти].Количество() Тогда
				ВызватьИсключение НСтр("ru = 'Подписанный документ нельзя редактировать.'");
			КонецЕсли;
			
			НомерСтроки = 0;
			Для Каждого Строка Из ПроверяемыйОбъект[ИмяТабличнойЧасти] Цикл
				
				СтрокаВБазе = Ссылка[ИмяТабличнойЧасти][НомерСтроки];
				
				Если Строка[ИмяКолонки] <> СтрокаВБазе[ИмяКолонки] Тогда 
					ВызватьИсключение НСтр("ru = 'Подписанный документ нельзя редактировать.'");
				КонецЕсли;
				
				НомерСтроки = НомерСтроки + 1;
			КонецЦикла;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Возвращает признак того что объект может быть подписан ЭП.
//
Функция ОбъектМожетБытьПодписанЭП(ОбъектСсылка) Экспорт
	
	ТипОбъекта = ТипЗнч(ОбъектСсылка);
	Если ТипОбъекта = Тип("СправочникСсылка.ВнутренниеДокументы") Тогда
		ВидДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбъектСсылка, "ВидДокумента");
		Если ЗначениеЗаполнено(ВидДокумента) И ВидДокумента.ЯвляетсяКомплектомДокументов Тогда
			Возврат Ложь;
		КонецЕсли;
		Возврат Истина;
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.ВходящиеДокументы") Тогда
		Возврат Истина;
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.ИсходящиеДокументы") Тогда
		Возврат Истина;
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.Файлы") Тогда
		Возврат Истина;
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.ВизыСогласования") Тогда
		Возврат Истина;
	ИначеЕсли ТипОбъекта = Тип("СправочникСсылка.Резолюции") Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

// Возвращает подписи, принадлежащие объекту.
//
Функция ПолучитьЭлектронныеПодписи(ПодписанныйОбъектСсылка, УстановившийПодпись = Неопределено,
	ДатаПодписи = Неопределено, УникальныйИдентификатор = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЭП.УникальныйИдентификатор,
		|	ЭП.ДатаПодписи,
		|	ЭП.Объект,
		|	ЭП.УстановившийПодпись,
		|	ЭП.Версия,
		|	ЭП.ДатаПроверкиПодписи,
		|	ЭП.СрокДействияПоследнейМеткиВремени,
		|	ЭП.ИмяФайлаПодписи,
		|	ЭП.Комментарий,
		|	ЭП.КомуВыданСертификат,
		|	ЭП.Отпечаток,
		|	ЭП.Подпись,
		|	ЭП.ПодписьВерна,
		|	ЭП.Сертификат,
		|	ЭП.СертификатДействителен,
		|	ЭП.ТекстОшибкиПроверкиПодписи,
		|	ЭП.ТекстОшибкиПроверкиСертификата,
		|	ВЫБОР
		|		КОГДА КешИнформацииОбОбъектах.СтатусЭП = ЗНАЧЕНИЕ(Перечисление.СтатусПроверкиЭП.ПодписиНет)
		|			ТОГДА 0
		|		КОГДА КешИнформацииОбОбъектах.СтатусЭП = ЗНАЧЕНИЕ(Перечисление.СтатусПроверкиЭП.ПодписьНеПроверена)
		|			ТОГДА 1
		|		КОГДА КешИнформацииОбОбъектах.СтатусЭП = ЗНАЧЕНИЕ(Перечисление.СтатусПроверкиЭП.ПодписьДействительна)
		|			ТОГДА 2
		|		КОГДА КешИнформацииОбОбъектах.СтатусЭП = ЗНАЧЕНИЕ(Перечисление.СтатусПроверкиЭП.ПодписьНедействительна)
		|			ТОГДА 3
		|		ИНАЧЕ ВЫБОР
		|				КОГДА ЭП.Объект.ПодписанЭП
		|					ТОГДА 1
		|				ИНАЧЕ 0
		|			КОНЕЦ
		|	КОНЕЦ КАК СтатусПроверкиЭП
		|ИЗ
		|	РегистрСведений.ЭлектронныеПодписи КАК ЭП
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КешИнформацииОбОбъектах КАК КешИнформацииОбОбъектах
		|		ПО ЭП.Объект = КешИнформацииОбОбъектах.Объект
		|ГДЕ
		|	ЭП.Объект = &ОбъектСсылка";
	Запрос.Параметры.Вставить("ОбъектСсылка", ПодписанныйОбъектСсылка);
	Если УстановившийПодпись <> Неопределено Тогда
		Запрос.Текст = Запрос.Текст + " И ЭП.УстановившийПодпись = &УстановившийПодпись";
		Запрос.Параметры.Вставить("УстановившийПодпись", УстановившийПодпись);
	КонецЕсли;
	Если ДатаПодписи <> Неопределено Тогда
		Запрос.Текст = Запрос.Текст + " И ЭП.ДатаПодписи = &ДатаПодписи";
		Запрос.Параметры.Вставить("ДатаПодписи", ДатаПодписи);
	КонецЕсли;
	Если УникальныйИдентификатор <> Неопределено Тогда
		Запрос.Текст = Запрос.Текст + " И ЭП.УникальныйИдентификатор = &УникальныйИдентификатор";
		Запрос.Параметры.Вставить("УникальныйИдентификатор", УникальныйИдентификатор);
	КонецЕсли;
	
	Результат = Запрос.Выполнить().Выбрать();
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Результат;
	
КонецФункции

// Возвращает количество подписей объекта.
//
Функция КоличествоПодписей(ПодписанныйОбъект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ИСТИНА КАК Поле1
		|ИЗ
		|	РегистрСведений.ЭлектронныеПодписи КАК ЭП
		|ГДЕ
		|	ЭП.Объект = &Объект";
	Запрос.Параметры.Вставить(
		"Объект", 
		?(ОбщегоНазначения.ЗначениеСсылочногоТипа(ПодписанныйОбъект), ПодписанныйОбъект, ПодписанныйОбъект.Ссылка));
	
	Возврат Запрос.Выполнить().Выбрать().Количество();
	
КонецФункции

// Возвращает первую найденную подпись к объекту, в ином случае возвращает Неопределено.
//
Функция ПолучитьЭлектроннуюПодпись(ПодписанныйОбъект,
	УстановившийПодпись = Неопределено, ДатаПодписи = Неопределено, УникальныйИдентификатор = Неопределено) Экспорт
	
	Выборка = ПолучитьЭлектронныеПодписи(ПодписанныйОбъект.Ссылка, УстановившийПодпись, ДатаПодписи, УникальныйИдентификатор);
	Если Выборка.Следующий() Тогда
		Подпись = РегистрыСведений.ЭлектронныеПодписи.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(Подпись, Выборка);
		Подпись.Прочитать();
		Если Не Подпись.Выбран() Тогда
			Возврат Неопределено;
		КонецЕсли;
		Возврат Подпись;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Обновляет информацию о статусе проверки подписи в РС ЭлектронныеПодписи.
//
Процедура ОбновитьСтатусПроверкиПодписи(ДанныеПодписи, ДатаПроверки = Неопределено,
	ОбщийСтатусПроверки = Неопределено) Экспорт
	
	Если НЕ ДанныеПодписи.Свойство("Объект")
		ИЛИ НЕ ДанныеПодписи.Свойство("УстановившийПодпись")
		ИЛИ НЕ ДанныеПодписи.Свойство("ДатаПодписи") Тогда
		ВызватьИсключение НСтр("ru = 'Подпись не найдена.'");
	КонецЕсли;
	
	ПодписанныйОбъект = ДанныеПодписи.Объект;
	УстановившийПодпись = ДанныеПодписи.УстановившийПодпись;
	ДатаПодписи = ДанныеПодписи.ДатаПодписи;
	УникальныйИдентификатор = ДанныеПодписи.УникальныйИдентификатор;
	
	НаборЗаписей = РегистрыСведений.ЭлектронныеПодписи.СоздатьНаборЗаписей();
	
	НаборЗаписей.Отбор.Объект.Установить(ПодписанныйОбъект);
	
	Если ДатаПодписи <> Неопределено Тогда
		НаборЗаписей.Отбор.ДатаПодписи.Установить(ДатаПодписи);
	КонецЕсли;	
	Если УстановившийПодпись <> Неопределено Тогда
		НаборЗаписей.Отбор.УстановившийПодпись.Установить(УстановившийПодпись);
	КонецЕсли;	
	Если УникальныйИдентификатор <> Неопределено Тогда
		НаборЗаписей.Отбор.УникальныйИдентификатор.Установить(УникальныйИдентификатор);
	КонецЕсли;	
	
	НаборЗаписей.Прочитать();
	
	Если НаборЗаписей.Количество() = 0 Тогда
		ВызватьИсключение НСтр("ru = 'Подпись не найдена.'");
	КонецЕсли;
	
	Подпись = НаборЗаписей[0];
	
	ЗаполнитьЗначенияСвойств(Подпись, ДанныеПодписи);
	Подпись.ДатаПроверкиПодписи = ТекущаяДатаСеанса();
	
	НаборЗаписей.Записать();
	
	ДатаПроверки = Подпись.ДатаПроверкиПодписи;
	СрокПроверкиСертификата = СрокПроверкиДействияСертификата(
		Подпись.Сертификат.Получить(), Подпись.СрокДействияПоследнейМеткиВремени);
	ОбщийСтатусПроверки = РаботаСЭПКлиентСервер.ПолучитьОбщийСтатусПроверкиПодписи(
		Подпись.ПодписьВерна, Подпись.СертификатДействителен, Подпись.ДатаПроверкиПодписи, СрокПроверкиСертификата);
	
КонецПроцедуры

// Обновляет информацию о статусе проверки подписей в РС ЭлектронныеПодписи
//
// Параметры:
//  ДанныеПодписей  - Массив из Структура - содержит:
//         * УникальныйИдентификатор  - УникальныйИдентификатор - УИД подписи
//         * Объект  - ОпределяемыйТип.ПодписанныйОбъект - Подписанный объект
//         * УстановившийПодпись  - СправочникСсылка.Пользователи - Пользователь, поставивший подпись
//         * ДатаПодписи  - Дата - Дата подписи
//         * ПодписьВерна  - Булево - Указывает, верна ли подпись
//         * ТекстОшибкиПроверкиПодписи  - Строка - Описание ошибки проверки подписи
//         * СертификатДействителен  - Булево - Указывает, действителен ли сертификат подписи
//         * ТекстОшибкиПроверкиСертификата  - Строка - Описание ошибки проверки сертификата
//
Процедура ОбновитьСтатусыПроверкиПодписей(ДанныеПодписей) Экспорт
	
	Для Каждого ДанныеПодписи Из ДанныеПодписей Цикл
		ДанныеПодписиДляЗаписи = Новый Структура(
			"УникальныйИдентификатор,
			|Объект,
			|УстановившийПодпись,
			|ДатаПодписи,
			|ПодписьВерна,
			|ТекстОшибкиПроверкиПодписи,
			|СертификатДействителен,
			|ТекстОшибкиПроверкиСертификата");
		
		ЗаполнитьЗначенияСвойств(ДанныеПодписиДляЗаписи, ДанныеПодписи);
		
		ОбновитьСтатусПроверкиПодписи(ДанныеПодписиДляЗаписи);
	КонецЦикла;
	
КонецПроцедуры // ОбновитьСтатусыПроверкиПодписей()

// Сохраняет информацию о ЭП.
//
Процедура ЗанестиИнформациюОПодписи(ПодписанныеДанные, СвойстваПодписиАргумент) Экспорт
	
	СвойстваПодписи = СвойстваПодписиАргумент;
	
	Если ТипЗнч(СвойстваПодписи) = Тип("Строка") И ЭтоАдресВременногоХранилища(СвойстваПодписи) Тогда
		СвойстваПодписи = ПолучитьИзВременногоХранилища(СвойстваПодписи);
	КонецЕсли; 
	
	ДатаПодписиИзФайла = ЭлектроннаяПодпись.ДатаПодписания(СвойстваПодписи.Подпись);
	
	ПодписьЗагруженаИзФайла = Не СвойстваПодписи.Свойство("ДатаПодписи") 
		Или Не ЗначениеЗаполнено(СвойстваПодписи.ДатаПодписи);
	
	// Устанавливаем текущую дату сеанса в качестве даты создания подписи,
	// если последнюю не получилось извлечь из двоичных данных.
	Если ЗначениеЗаполнено(ДатаПодписиИзФайла) Тогда
		ДатаПодписи = ДатаПодписиИзФайла;
	ИначеЕсли Не ЗначениеЗаполнено(СвойстваПодписи.ДатаПодписи) Тогда
		ДатаПодписи = ТекущаяДатаСеанса();
	ИначеЕсли ЗначениеЗаполнено(СвойстваПодписи.ДатаПодписи) Тогда
		ДатаПодписи = СвойстваПодписи.ДатаПодписи;
	КонецЕсли;
	
	ИдентификаторПодписи = Новый УникальныйИдентификатор;
	
	МенеджерЗаписи = РегистрыСведений.ЭлектронныеПодписи.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.УникальныйИдентификатор = ИдентификаторПодписи;
	МенеджерЗаписи.ДатаПодписи = ДатаПодписи;
	МенеджерЗаписи.Объект = ПодписанныеДанные;
	МенеджерЗаписи.УстановившийПодпись =  СвойстваПодписи.УстановившийПодпись;
	// Актуальная версия подписываемых данных на текущий момент
	МенеджерЗаписи.Версия = АктуальнаяВерсияПодписи();
	СвойстваПодписи.Свойство("Комментарий",МенеджерЗаписи.Комментарий);
	МенеджерЗаписи.КомуВыданСертификат = СвойстваПодписи.КомуВыданСертификат;
	МенеджерЗаписи.Отпечаток = СвойстваПодписи.Отпечаток;
	МенеджерЗаписи.Подпись = Новый ХранилищеЗначения(СвойстваПодписи.Подпись);
	МенеджерЗаписи.Сертификат = Новый ХранилищеЗначения(СвойстваПодписи.Сертификат);
	Если СвойстваПодписи.Свойство("ТипПодписи") Тогда
		МенеджерЗаписи.ТипПодписи = СвойстваПодписи.ТипПодписи;
	КонецЕсли;
	Если СвойстваПодписи.Свойство("СрокДействияПоследнейМеткиВремени") Тогда
		МенеджерЗаписи.СрокДействияПоследнейМеткиВремени = СвойстваПодписи.СрокДействияПоследнейМеткиВремени;
	КонецЕсли;
	
	// Сразу устанавливаем статус проверки подписи, созданной в программе.
	Если Не ПодписьЗагруженаИзФайла Тогда
		МенеджерЗаписи.ДатаПроверкиПодписи = ДатаПодписи;
		МенеджерЗаписи.ПодписьВерна = Истина;
		МенеджерЗаписи.СертификатДействителен = Истина;
	Иначе
		МенеджерЗаписи.ДатаПроверкиПодписи = ТекущаяДатаСеанса();
		МенеджерЗаписи.ПодписьВерна = СвойстваПодписи.Свойство("ПодписьВерна") И СвойстваПодписи.ПодписьВерна;
		Если Не СвойстваПодписи.Свойство("СертификатДействителен") Тогда
			МенеджерЗаписи.СертификатДействителен = МенеджерЗаписи.ПодписьВерна;
		Иначе	
			МенеджерЗаписи.СертификатДействителен = СвойстваПодписи.СертификатДействителен;
		КонецЕсли;	
	КонецЕсли;
	
	МенеджерЗаписи.Записать();
	
	Если ДелопроизводствоКлиентСервер.ЭтоДокумент(ПодписанныеДанные) Тогда
		ПротоколированиеРаботыПользователей.ЗаписатьПодписаниеЭП(ПодписанныеДанные, СвойстваПодписи.КомуВыданСертификат, МенеджерЗаписи.Комментарий);
	Иначе	
		ПолныйКомментарий = МенеджерЗаписи.Комментарий + " " + ПолучитьНавигационнуюСсылку(ПодписанныеДанные);
		ПротоколированиеРаботыПользователей.ЗаписатьПодписаниеЭП(ПодписанныеДанные, СвойстваПодписи.КомуВыданСертификат, ПолныйКомментарий);
	КонецЕсли;
	
	Доверенность = Неопределено;
	Если СвойстваПодписи.Свойство("Доверенность") Тогда
		Доверенность = СвойстваПодписи.Доверенность;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Доверенность) Тогда
		ЗанестиИнформациюОДоверенностиЭП(ИдентификаторПодписи, Доверенность, СвойстваПодписи);
	КонецЕсли;
	
КонецПроцедуры

// Сохраняет информацию о массиве ЭП.
//
Процедура ЗанестиИнформациюОПодписях(ПодписанныеДанные, ИдентификаторФормы = Неопределено) Экспорт
	
	// Подписываемые файлы должны быть не заняты и не зашифрованы.
	Для Каждого Данные Из ПодписанныеДанные Цикл
		
		ПодписанныйОбъект = Данные.ПодписанныйОбъект;
		
		Если ДелопроизводствоКлиентСервер.ЭтоФайл(ПодписанныйОбъект) Тогда
			
			СтруктураРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПодписанныйОбъект, "Редактирует, Зашифрован");
			
			Если ЗначениеЗаполнено(СтруктураРеквизитов.Редактирует) Тогда
				ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Нельзя подписать занятый файл: ""%1""'"),
					ПодписанныйОбъект);
			КонецЕсли;
				
			Если СтруктураРеквизитов.Зашифрован Тогда
				ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Нельзя подписать зашифрованный файл: ""%1""'"),
					ПодписанныйОбъект);
			КонецЕсли;
				
		КонецЕсли;
		
	КонецЦикла;
	
	НачатьТранзакцию();
	Попытка
	
		Для Каждого Данные Из ПодписанныеДанные Цикл
			
			ПодписанныйОбъект = Данные.ПодписанныйОбъект;
			СвойстваПодписи = Данные.СвойстваПодписи;
			
			Если ДелопроизводствоКлиентСервер.ЭтоФайл(ПодписанныйОбъект) Тогда
				
				Если ТипЗнч(СвойстваПодписи) = Тип("Строка")
					И ЭтоАдресВременногоХранилища(СвойстваПодписи) Тогда
					
					СвойстваПодписи = ПолучитьИзВременногоХранилища(СвойстваПодписи);
				КонецЕсли;
				
				Если ТипЗнч(СвойстваПодписи) = Тип("Структура")
					И СвойстваПодписи.Свойство("ДанныеИзменены")
					И СвойстваПодписи.ДанныеИзменены = Истина Тогда
					
					ОбновитьФайлПередЗанесениемПодписи(ПодписанныйОбъект, СвойстваПодписи);
					
				КонецЕсли;
				
				РаботаСФайламиСлужебныйВызовСервера.ДобавитьПодписьКФайлу(
					ПодписанныйОбъект,
					СвойстваПодписи,
					ИдентификаторФормы);
				
			Иначе
				
				Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(ПодписанныйОбъект)) Тогда
					ПодписываемыйОбъект = ПодписанныйОбъект.ПолучитьОбъект();
					ЗаблокироватьДанныеДляРедактирования(ПодписанныйОбъект,, ИдентификаторФормы);
				Иначе
					ПодписываемыйОбъект = ПодписанныйОбъект;
				КонецЕсли;
				
				ЗанестиИнформациюОПодписи(ПодписанныйОбъект, СвойстваПодписи);
				
				Если ДелопроизводствоКлиентСервер.ЭтоДокумент(ПодписываемыйОбъект)
					Или ДелопроизводствоКлиентСервер.ЭтоФайл(ПодписываемыйОбъект) Тогда
					
					ПодписываемыйОбъект.ПодписанЭП = Истина;
					Если ДелопроизводствоКлиентСервер.ЭтоВнутреннийДокумент(ПодписываемыйОбъект) Тогда
						ПараметрыВыполнения = Неопределено;
						Данные.Свойство("ПараметрыВыполнения", ПараметрыВыполнения);
						
						Если ТипЗнч(СвойстваПодписи) = Тип("Структура") 
							И СвойстваПодписи.Свойство("ВидПодписи") Тогда 
							Если СвойстваПодписи.ВидПодписи = "ПодписьДокумента" Тогда
								ПодписываемыйОбъект.Подписал = СвойстваПодписи.УстановившийПодпись;
								ПодписываемыйОбъект.РезультатПодписания = Перечисления.РезультатыПодписания.Подписан;
								ПодписываемыйОбъект.ДатаПодписания = СвойстваПодписи.ДатаПодписи;
							ИначеЕсли СвойстваПодписи.ВидПодписи = "УтверждениеДокумента" Тогда
								ГрифУтверждения = ПодписываемыйОбъект.ГрифыУтверждения.Добавить();
								ГрифУтверждения.АвторУтверждения = СвойстваПодписи.УстановившийПодпись;
								ГрифУтверждения.Результат = Перечисления.РезультатыУтверждения.Утверждено;
								ГрифУтверждения.ДатаУтверждения = СвойстваПодписи.ДатаПодписи;
							КонецЕсли;
							
						ИначеЕсли ТипЗнч(ПараметрыВыполнения) = Тип("Структура")
							И ПараметрыВыполнения.Свойство("ПодписатьЭД")
							И ПараметрыВыполнения.Свойство("НаправлениеЭД")
							И ПараметрыВыполнения.ПодписатьЭД = Истина Тогда
							
							Если ПараметрыВыполнения.НаправлениеЭД = Перечисления.НаправленияЭДО.Входящий Тогда
								ЗанестиИнформациюПодписанта =
									ОбменСКонтрагентамиДОСервер.ДокументГотовКОбработкеВходящегоЭД(
										ПодписываемыйОбъект.Ссылка);
							Иначе
								ЗанестиИнформациюПодписанта =
									ОбменСКонтрагентамиДОСервер.ПоставитьДокументВОчередьФормированияЭД(
										ПодписываемыйОбъект.Ссылка, Неопределено, Ложь, Истина);
							КонецЕсли;
							
							Если ЗанестиИнформациюПодписанта И ПодписываемыйОбъект.Стороны.Количество() > 0 Тогда
								ОписаниеПодписи = СвойстваПодписи;
								Если ЭтоАдресВременногоХранилища(СвойстваПодписи) Тогда
									ОписаниеПодписи = ПолучитьИзВременногоХранилища(СвойстваПодписи);
								КонецЕсли;
								
								Подписал = Пользователи.АвторизованныйПользователь();
								Если ТипЗнч(ОписаниеПодписи) = Тип("Структура")
									И ОписаниеПодписи.Свойство("УстановившийПодпись") Тогда
									
									Подписал = ОписаниеПодписи.УстановившийПодпись;
								КонецЕсли;
								
								ДатаПодписи = ТекущаяДатаСеанса();
								Если ТипЗнч(ОписаниеПодписи) = Тип("Структура")
									И ОписаниеПодписи.Свойство("ДатаПодписи") Тогда
									
									ДатаПодписи = ОписаниеПодписи.ДатаПодписи;
								КонецЕсли;
								
								СтрокаОрганизации = ПодписываемыйОбъект.Стороны[0];
								СтрокаОрганизации.Подписан = Истина;
								СтрокаОрганизации.ДатаПодписи = ДатаПодписи;
								СтрокаОрганизации.Подписал = Подписал;
								
							Иначе
								ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									НСтр("ru = 'Не удалось подписать ""%1""'"),
									ПодписываемыйОбъект.Ссылка);
							КонецЕсли;
							
						КонецЕсли;
					КонецЕсли;
					
					ПодписываемыйОбъект.ДополнительныеСвойства.Вставить("ЗаписьПодписанногоОбъекта", Истина);
					
				ИначеЕсли ТипЗнч(ПодписываемыйОбъект) = Тип("СправочникСсылка.ВизыСогласования")
					Или ТипЗнч(ПодписываемыйОбъект) = Тип("СправочникОбъект.ВизыСогласования")
					Или ТипЗнч(ПодписываемыйОбъект) = Тип("СправочникСсылка.Резолюции")
					Или ТипЗнч(ПодписываемыйОбъект) = Тип("СправочникОбъект.Резолюции") Тогда
					
					ПодписываемыйОбъект.Подписана = Истина;
					
				КонецЕсли;
				
				Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(ПодписанныйОбъект)) Тогда
					УстановитьПривилегированныйРежим(Истина);
					ПодписываемыйОбъект.Записать();
					РазблокироватьДанныеДляРедактирования(ПодписанныйОбъект, ИдентификаторФормы);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
	
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Создает копию электронной подписи от источника приемнику.
//
// Параметры:
//  Источник - Структура
//    Объект
//    УстановившийПодпись
//    ДатаПодписи
//  ПриемникСсылка
//
// Возвращаемое значение:
//  Истина, если копирование прошло успешно, иначе Ложь.
//
Функция СкопироватьПодпись(Источник, ПриемникСсылка) Экспорт
	
	Если Не Источник.Свойство("Объект")
		ИЛИ Не Источник.Свойство("УстановившийПодпись") 
		ИЛИ Не Источник.Свойство("ДатаПодписи") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ПодписьИсточника = ПолучитьЭлектроннуюПодпись(Источник.Объект, Источник.УстановившийПодпись, Источник.ДатаПодписи);
	Если ПодписьИсточника = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Попытка
		ПодписьПриемника = РегистрыСведений.ЭлектронныеПодписи.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(ПодписьПриемника, ПодписьИсточника);
		ПодписьПриемника.УникальныйИдентификатор = Новый УникальныйИдентификатор;
		ПодписьПриемника.Объект = ПриемникСсылка;
		ПодписьПриемника.Записать();
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

// Создает копии всех подписей от источника приемнику.
//
// Возвращаемое значение:
//  Количество скопированных подписей.
//
Функция СкопироватьВсеПодписи(ИсточникСсылка, ПриемникСсылка) Экспорт
	
	КоличествоПодписей = 0;
	
	Выборка = ПолучитьЭлектронныеПодписи(ИсточникСсылка);
	Пока Выборка.Следующий() Цикл
		Попытка
			ПодписьПриемника = РегистрыСведений.ЭлектронныеПодписи.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(ПодписьПриемника, Выборка);
			ПодписьПриемника.УникальныйИдентификатор = Новый УникальныйИдентификатор;
			ПодписьПриемника.Объект = ПриемникСсылка;
			ПодписьПриемника.Записать();
			КоличествоПодписей = КоличествоПодписей + 1;
		Исключение
		КонецПопытки;
	КонецЦикла;
	
	Возврат КоличествоПодписей;
	
КонецФункции

// Проверяет существование хотя бы одной подписи для любого документа.
//
Функция СуществуютПодписиКДокументам() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	Истина
	|ИЗ
	|	РегистрСведений.ЭлектронныеПодписи
	|ГДЕ
	|	Объект ССЫЛКА Справочник.ВнутренниеДокументы
	|	ИЛИ Объект ССЫЛКА Справочник.ВходящиеДокументы
	|	ИЛИ Объект ССЫЛКА Справочник.ИсходящиеДокументы";

	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

// Записывает в кеш информацию о статусе проверки ЭП.
//
// Параметры:
//  Ссылка - ссылка на документы или версии файлов.
//
Процедура УстановитьСтатусПроверки(Знач Ссылка, Статус) Экспорт
	
	Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.ВерсииФайлов") Тогда
		Ссылка = Ссылка.Владелец;
	КонецЕсли;
	
	Если Не Ссылка.Пустая() И Ссылка.ПолучитьОбъект() = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РегистрыСведений.КешИнформацииОбОбъектах.УстановитьПризнак(Ссылка, "СтатусЭП", Статус);
		
	РегистрыСведений.КешИнформацииОбОбъектах.УстановитьПризнак(Ссылка, "ДатаПроверкиЭП", ТекущаяДатаСеанса());
	
КонецПроцедуры

// Определяет и записывает в кеш информацию об общем статусе проверки ЭП под текущей версией файла.
// Общий статус подписи файла действителен только в том случае, когда действительны все
// подписи файла.
//
// Параметры:
//  ВерсияФайла - СправочникСсылка.ВерсииФайлов
//
Процедура УстановитьСтатусПроверкиДляФайла(ВерсияФайла) Экспорт
	
	ЭПФайла = РаботаСЭП.ПолучитьЭлектронныеПодписи(ВерсияФайла);
	
	Если ЭПФайла.Количество() = 0 Тогда
		УстановитьСтатусПроверки(ВерсияФайла, ПредопределенноеЗначение("Перечисление.СтатусПроверкиЭП.ПодписиНет"));
		Возврат;
	КонецЕсли;
	
	Пока ЭПФайла.Следующий() Цикл
		
		Если Не ЗначениеЗаполнено(ЭПФайла.ДатаПроверкиПодписи) Тогда
			УстановитьСтатусПроверки(ВерсияФайла, ПредопределенноеЗначение("Перечисление.СтатусПроверкиЭП.ПодписьНеПроверена"));
			Возврат;
		КонецЕсли;
		
		Если Не ЭПФайла.ПодписьВерна Тогда
			УстановитьСтатусПроверки(ВерсияФайла, ПредопределенноеЗначение("Перечисление.СтатусПроверкиЭП.ПодписьНедействительна"));
			Возврат;
		КонецЕсли;
		
	КонецЦикла;
	
	УстановитьСтатусПроверки(ВерсияФайла, ПредопределенноеЗначение("Перечисление.СтатусПроверкиЭП.ПодписьДействительна"));
	
КонецПроцедуры

// Определяет и записывает в кеш информацию об общем статусе проверки ЭП под документом с учетом
// статусов подписей подчиненных файлов.
// Общий статус подписи документа действителен только в том случае, когда действительны все
// подписи самого документа и его подчиненных файлов.
//
// Параметры:
//  Документ - СправочникСсылка.ВнутренниеДокументы
//             СправочникСсылка.ВходящиеДокументы
//             СправочникСсылка.ИсходящиеДокументы
//
Процедура УстановитьСтатусПроверкиДляДокумента(Документ) Экспорт
	
	ЭПДокумента = РаботаСЭП.ПолучитьЭлектронныеПодписи(Документ);
	
	// Если под самим документом нет ни одной подписи, то наличие подписей под подчиненными файлами
	// не учитываем.
	Если ЭПДокумента.Количество() = 0 Тогда
		УстановитьСтатусПроверки(Документ, ПредопределенноеЗначение("Перечисление.СтатусПроверкиЭП.ПодписиНет"));
		Возврат;
	КонецЕсли;
	
	Пока ЭПДокумента.Следующий() Цикл
		
		Если Не ЗначениеЗаполнено(ЭПДокумента.ДатаПроверкиПодписи) Тогда
			УстановитьСтатусПроверки(Документ, ПредопределенноеЗначение("Перечисление.СтатусПроверкиЭП.ПодписьНеПроверена"));
			Возврат;
		КонецЕсли;
		
		Если Не ЭПДокумента.ПодписьВерна Тогда
			УстановитьСтатусПроверки(Документ, ПредопределенноеЗначение("Перечисление.СтатусПроверкиЭП.ПодписьНедействительна"));
			Возврат;
		КонецЕсли;
		
	КонецЦикла;
	
	// Проверяем статусы подписей подчиненных файлов.
	ПодчиненныеФайлы = РаботаСФайламиВызовСервера.ПолучитьВсеПодчиненныеФайлы(Документ, Ложь);
	
	Для Каждого Файл Из ПодчиненныеФайлы Цикл
		
		ЭПФайла = РаботаСЭП.ПолучитьЭлектронныеПодписи(Файл.ТекущаяВерсия);
		
		Если ЭПФайла.Количество() = 0 Тогда
			УстановитьСтатусПроверки(Документ, ПредопределенноеЗначение("Перечисление.СтатусПроверкиЭП.ПодписиНет"));
			Возврат;
		КонецЕсли;
	
		Пока ЭПФайла.Следующий() Цикл
			
			Если Не ЗначениеЗаполнено(ЭПФайла.ДатаПроверкиПодписи) Тогда
				УстановитьСтатусПроверки(Документ, ПредопределенноеЗначение("Перечисление.СтатусПроверкиЭП.ПодписьНеПроверена"));
				Возврат;
			КонецЕсли;
			
			Если Не ЭПФайла.ПодписьВерна Тогда
				УстановитьСтатусПроверки(Документ, ПредопределенноеЗначение("Перечисление.СтатусПроверкиЭП.ПодписьНедействительна"));
				Возврат;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	УстановитьСтатусПроверки(Документ, ПредопределенноеЗначение("Перечисление.СтатусПроверкиЭП.ПодписьДействительна"));
	
КонецПроцедуры

#Область ОбработкаСобытийРегистровЭП

// Вызывается при записи набора записей РС ЭлектронныеПодписи и обновляет связанные данные.
//
// Параметры:
//   НаборЗаписей - РегистрСведенийНаборЗаписей.ЭлектронныеПодписи.
//
Процедура ПередЗаписьюЭлектронныхПодписей(НаборЗаписей) Экспорт
	
	ОбъектЭП = НаборЗаписей.Отбор.Объект.Значение;
	
	Если ТипЗнч(ОбъектЭП) = Тип("СправочникСсылка.ВерсииФайлов") Тогда
		
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ОбъектЭП,
			"Владелец, Владелец.ВладелецФайла");
		ВладелецФайла = ЗначенияРеквизитов.ВладелецВладелецФайла;
		Если ДелопроизводствоКлиентСервер.ЭтоДокумент(ВладелецФайла) Тогда
			
			Если ПолучитьФункциональнуюОпцию("АвтовизуализацияЭП") Тогда
				
				ИспользоватьImageMagick = РаботаСФайламиВызовСервера.ПолучитьИспользоватьImageMagickДляРаспознаванияPDF();
				
				Если ИспользоватьImageMagick Тогда
	
					Если НаборЗаписей.Количество() = 0 Тогда
						НаборЗаписей.ДополнительныеСвойства.Вставить("ЭтоУдаление", Истина);
					Иначе
						
						ПрежниеЗначения = Новый Массив;
						
						Выборка = ПолучитьЭлектронныеПодписи(ОбъектЭП, 
							НаборЗаписей.Отбор.УстановившийПодпись.Значение,
							НаборЗаписей.Отбор.ДатаПодписи.Значение,
							НаборЗаписей.Отбор.УникальныйИдентификатор.Значение);
						Пока Выборка.Следующий() Цикл
								
							ОписаниеЭП = Новый Структура(
								"Объект, ДатаПодписи, УстановившийПодпись, Отпечаток, Подпись",
								Выборка.Объект, Выборка.ДатаПодписи, 
								Выборка.УстановившийПодпись, 
								Выборка.Отпечаток, Выборка.Подпись);
							ПрежниеЗначения.Добавить(ОписаниеЭП);
								
						КонецЦикла;
						
						НаборЗаписей.ДополнительныеСвойства.Вставить("ПрежниеЗначения", ПрежниеЗначения);
						
					КонецЕсли;
					
				КонецЕсли;
			
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

// Вызывается при записи набора записей РС ЭлектронныеПодписи и обновляет связанные данные.
//
// Параметры:
//   НаборЗаписей - РегистрСведенийНаборЗаписей.ЭлектронныеПодписи.
//
Процедура ПриЗаписиЭлектронныхПодписей(НаборЗаписей) Экспорт
	
	ОбъектЭП = НаборЗаписей.Отбор.Объект.Значение;
	
	Если ТипЗнч(ОбъектЭП) = Тип("СправочникСсылка.ВерсииФайлов") Тогда
		
		УстановитьСтатусПроверкиДляФайла(ОбъектЭП);
		
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ОбъектЭП,
			"Владелец, Владелец.ВладелецФайла");
		Файл = ЗначенияРеквизитов.Владелец;
		ВладелецФайла = ЗначенияРеквизитов.ВладелецВладелецФайла;
		Если ДелопроизводствоКлиентСервер.ЭтоДокумент(ВладелецФайла) Тогда
			
			УстановитьСтатусПроверкиДляДокумента(ВладелецФайла);
			
			Если ПолучитьФункциональнуюОпцию("АвтовизуализацияЭП") Тогда
				
				ИспользоватьImageMagick = РаботаСФайламиВызовСервера.ПолучитьИспользоватьImageMagickДляРаспознаванияPDF();
				
				Если ИспользоватьImageMagick Тогда

					ДелатьОбработку = Истина;
					
					Если НаборЗаписей.ДополнительныеСвойства.Свойство("ЭтоУдаление") Тогда
						ДелатьОбработку = Истина;
						
					ИначеЕсли НаборЗаписей.ДополнительныеСвойства.Свойство("ПрежниеЗначения") Тогда
						ПрежниеЗначения = НаборЗаписей.ДополнительныеСвойства.ПрежниеЗначения;
						
						Если ПрежниеЗначения.Количество() <> НаборЗаписей.Количество() Тогда
							ДелатьОбработку = Истина;
						Иначе
							
							Для Каждого ЭП Из НаборЗаписей Цикл
								
								НашлиИдентичную = Ложь;
								ПодписьДанные = ЭП.Подпись.Получить();
								
								Для Каждого ЭППрежняя Из ПрежниеЗначения Цикл
									
									Если ЭП.Объект = ЭППрежняя.Объект
										И ЭП.ДатаПодписи = ЭППрежняя.ДатаПодписи
										И ЭП.УстановившийПодпись = ЭППрежняя.УстановившийПодпись
										И ЭП.Отпечаток = ЭППрежняя.Отпечаток
										И ПодписьДанные = ЭППрежняя.Подпись.Получить() Тогда
										
										НашлиИдентичную = Истина;
										Прервать;
										
									КонецЕсли;	
									
								КонецЦикла;	
							
							КонецЦикла;
							
							Если НашлиИдентичную Тогда
								ДелатьОбработку = Ложь;
							КонецЕсли;			
							
						КонецЕсли;		
						
					КонецЕсли;	
					
					Если ДелатьОбработку Тогда
						Если НаборЗаписей.Количество() = 0 Тогда
							УдалитьОтметкиЭП(ВладелецФайла, Файл);
						Иначе
							УдалитьОтметкиЭП(ВладелецФайла, Файл);
							ОбновитьОтметкуЭП(ВладелецФайла, Файл, ОбъектЭП);
						КонецЕсли;
					КонецЕсли;
					
				КонецЕсли;
			
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ДелопроизводствоКлиентСервер.ЭтоДокумент(ОбъектЭП) Тогда
		
		УстановитьСтатусПроверкиДляДокумента(ОбъектЭП);
		
	КонецЕсли;
	
	Если НаборЗаписей.Количество() = 0 Тогда
		ИдентификаторПодписи = НаборЗаписей.Отбор.УникальныйИдентификатор.Значение;
		Если ЗначениеЗаполнено(ИдентификаторПодписи) Тогда
			УдалитьЗаписьОДоверенностиЭП(ИдентификаторПодписи);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// При записи доверенностей электронных подписей
// 
// Параметры:
//  НаборЗаписей - РегистрСведенийНаборЗаписей.ДоверенностиЭлектронныхПодписей - Набор записей
Процедура ПриЗаписиДоверенностейЭлектронныхПодписей(НаборЗаписей) Экспорт
	
	ОбновитьОтметкуЭППриЗаписиДоверенностей(НаборЗаписей);
	
КонецПроцедуры

#КонецОбласти

#Область ПолучениеПодписей

Функция ПодписиОбъектаСУчетомДоверенностей(ПодписанныйОбъект, УникальныйИдентификаторФормы) Экспорт
	
	ПодписиОбъекта = Новый Массив;
	
	ИдентификаторыПодписей = Новый Массив;
	
	ВыборкаПодписей = ПолучитьЭлектронныеПодписи(ПодписанныйОбъект);
	Пока ВыборкаПодписей.Следующий() Цикл
		
		ДанныеПодписи = НовыеДанныеПодписиСУчетомДоверенности();
		ЗаполнитьЗначенияСвойств(ДанныеПодписи, ВыборкаПодписей);
		
		ДвоичныеДанныеПодписи = ВыборкаПодписей.Подпись.Получить();
		Если ДвоичныеДанныеПодписи <> Неопределено Тогда
			ДанныеПодписи.АдресПодписи = ПоместитьВоВременноеХранилище(
				ДвоичныеДанныеПодписи, УникальныйИдентификаторФормы);
		КонецЕсли;
		
		ДвоичныеДанныеСертификата = ВыборкаПодписей.Сертификат.Получить();
		Если ДвоичныеДанныеСертификата <> Неопределено Тогда
			ДанныеПодписи.АдресСертификата = ПоместитьВоВременноеХранилище(
				ДвоичныеДанныеСертификата, УникальныйИдентификаторФормы);
		КонецЕсли;
		
		ПодписиОбъекта.Добавить(ДанныеПодписи);
		
		ИдентификаторыПодписей.Добавить(ДанныеПодписи.УникальныйИдентификатор);
	КонецЦикла;
	
	ДанныеДоверенностей = ДанныеДоверенностейПодписей(ИдентификаторыПодписей);
	
	Для Каждого ДанныеПодписи Из ПодписиОбъекта Цикл
		
		ДанныеДоверенности = ДанныеДоверенностей[ДанныеПодписи.УникальныйИдентификатор];
		Если ДанныеДоверенности = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ДанныеПодписи, ДанныеДоверенности);
		
	КонецЦикла;
	
	Возврат ПодписиОбъекта;
	
КонецФункции

Функция УстановленныеПодписиПоИдентификаторам(ИдентификаторыПодписей) Экспорт
	
	ПодписиПоИдентификаторам = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЭлектронныеПодписи.УникальныйИдентификатор КАК УникальныйИдентификатор,
		|	ЭлектронныеПодписи.ДатаПодписи КАК ДатаПодписи,
		|	ЭлектронныеПодписи.Объект КАК ПодписанныйОбъект,
		|	ЭлектронныеПодписи.УстановившийПодпись КАК УстановившийПодпись,
		|	ЭлектронныеПодписи.Версия КАК Версия,
		|	ЭлектронныеПодписи.ДатаПроверкиПодписи КАК ДатаПроверкиПодписи,
		|	ЭлектронныеПодписи.ИмяФайлаПодписи КАК ИмяФайлаПодписи,
		|	ЭлектронныеПодписи.Комментарий КАК Комментарий,
		|	ЭлектронныеПодписи.КомуВыданСертификат КАК КомуВыданСертификат,
		|	ЭлектронныеПодписи.Отпечаток КАК Отпечаток,
		|	ЭлектронныеПодписи.Подпись КАК Подпись,
		|	ЭлектронныеПодписи.ПодписьВерна КАК ПодписьВерна,
		|	ЭлектронныеПодписи.Сертификат КАК Сертификат,
		|	ЭлектронныеПодписи.СертификатДействителен КАК СертификатДействителен,
		|	ЭлектронныеПодписи.СрокДействияПоследнейМеткиВремени КАК СрокДействияПоследнейМеткиВремени,
		|	ЭлектронныеПодписи.ТекстОшибкиПроверкиПодписи КАК ТекстОшибкиПроверкиПодписи,
		|	ЭлектронныеПодписи.ТекстОшибкиПроверкиСертификата КАК ТекстОшибкиПроверкиСертификата,
		|	ЭлектронныеПодписи.ТипПодписи КАК ТипПодписи,
		|	ЭлектронныеПодписи.ОшибкаПриАвтоматическомПродлении КАК ОшибкаПриАвтоматическомПродлении,
		|	ВЫБОР
		|		КОГДА КешИнформацииОбОбъектах.СтатусЭП = ЗНАЧЕНИЕ(Перечисление.СтатусПроверкиЭП.ПодписиНет)
		|			ТОГДА 0
		|		КОГДА КешИнформацииОбОбъектах.СтатусЭП = ЗНАЧЕНИЕ(Перечисление.СтатусПроверкиЭП.ПодписьНеПроверена)
		|			ТОГДА 1
		|		КОГДА КешИнформацииОбОбъектах.СтатусЭП = ЗНАЧЕНИЕ(Перечисление.СтатусПроверкиЭП.ПодписьДействительна)
		|			ТОГДА 2
		|		КОГДА КешИнформацииОбОбъектах.СтатусЭП = ЗНАЧЕНИЕ(Перечисление.СтатусПроверкиЭП.ПодписьНедействительна)
		|			ТОГДА 3
		|		ИНАЧЕ ВЫБОР
		|			КОГДА ЭлектронныеПодписи.Объект.ПодписанЭП
		|				ТОГДА 1
		|			ИНАЧЕ 0
		|		КОНЕЦ
		|	КОНЕЦ КАК СтатусПроверкиЭП
		|ИЗ
		|	РегистрСведений.ЭлектронныеПодписи КАК ЭлектронныеПодписи
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КешИнформацииОбОбъектах КАК КешИнформацииОбОбъектах
		|		ПО ЭлектронныеПодписи.Объект = КешИнформацииОбОбъектах.Объект
		|ГДЕ
		|	ЭлектронныеПодписи.УникальныйИдентификатор В (&ИдентификаторыПодписей)";
	Запрос.УстановитьПараметр("ИдентификаторыПодписей", ИдентификаторыПодписей);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		СвойстваПодписи = ЭлектроннаяПодписьКлиентСервер.НовыеСвойстваПодписи();
		
		ЗаполнитьЗначенияСвойств(СвойстваПодписи, Выборка);
		СвойстваПодписи.Подпись = СвойстваПодписи.Подпись.Получить();
		СвойстваПодписи.Сертификат = СвойстваПодписи.Сертификат.Получить();
		
		ПодписиПоИдентификаторам.Вставить(СвойстваПодписи.УникальныйИдентификатор, СвойстваПодписи);
		
	КонецЦикла;
	
	Возврат ПодписиПоИдентификаторам;
	
КонецФункции

#КонецОбласти

#Область ОтметкаЭП

// Получив сертфикаты подписей, обновляет изображения отметок ЭП в служебных файлах документа.
//
// Параметры:
//   Документ - СправочникСсылка.ВходящиеДокументы,
//              СправочникСсылка.ИсходящиеДокументы,
//              СправочникСсылка.ВнутренниеДокументы.
//   Версия - СправочникСсылка.ВерсииФайлов - подписываемая версия.
//   ЭП - РегистрСведенийЗапись.ЭлектронныеПодписи - электронная подпись.
//
Процедура ОбновитьОтметкуЭП(Документ, Файл, ОбъектЭП) Экспорт
	
	МассивФайловЭП = Новый Массив;
	
	НаборЗаписей = РегистрыСведений.ЭлектронныеПодписи.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Объект.Установить(ОбъектЭП);
	НаборЗаписей.Прочитать();
	
	Для Каждого ЭП Из НаборЗаписей Цикл
		ОписаниеЭП = ОписаниеЭПДляОтметки(ЭП);
		ПутьКОтметкеЭП = СоздатьОтметкуЭП(ОписаниеЭП);
		МассивФайловЭП.Добавить(ПутьКОтметкеЭП);
	КонецЦикла;
	
	Если МассивФайловЭП.Количество() > 1 Тогда
		НовыйПутьКОтметкеЭП = РаботаСКартинками.СобратьКартинки(МассивФайловЭП, "PNG");
	Иначе	
		НовыйПутьКОтметкеЭП = МассивФайловЭП[0];
	КонецЕсли;	
	
	ЗаписатьОтметкуЭП(Документ, Файл, НовыйПутьКОтметкеЭП);
	
	Для Каждого ПутьКОтметкеЭП Из МассивФайловЭП Цикл
		УдалитьФайлы(ПутьКОтметкеЭП);
	КонецЦикла;	
	
	Если МассивФайловЭП.Количество() > 1 Тогда
		УдалитьФайлы(НовыйПутьКОтметкеЭП);
	КонецЕсли;	
	
КонецПроцедуры

// Возвращает описание ЭП в виде структуры для отметки ЭП.
//
// Параметры:
//   ЭП - РегистрСведенийЗапись.ЭлектронныеПодписи - электронная подпись.
//
// Возвращаемое значение:
//   Структура со свойствами:
//     Номер - Строка - серийный номер
//     Владелец - Строка - владелец сертификата (CN).
//     ДатаНачала - Дата - дата начала действия сертификата.
//     ДатаОкончания - Дата - дата окончания действия сертификата.
//
Функция ОписаниеЭПДляОтметки(ЭП) Экспорт
	
	ОписаниеЭП = НовоеОписаниеОтметкиЭП();
	
	Если ЭП = Неопределено Тогда
		Возврат ОписаниеЭП;
	КонецЕсли;
	
	Если ТипЗнч(ЭП.Сертификат) = Тип("ХранилищеЗначения") Тогда
		ДанныеСертификата = ЭП.Сертификат.Получить();
	Иначе
		ДанныеСертификата = ЭП.Сертификат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДанныеСертификата) Тогда
		Возврат ОписаниеЭП;
	КонецЕсли;
	
	Сертификат = Новый СертификатКриптографии(ДанныеСертификата);
	
	ОписаниеЭП.Номер = СтрЗаменить(Сертификат.СерийныйНомер, " ", "");
	ОписаниеЭП.Владелец = ЭП.КомуВыданСертификат;
	ОписаниеЭП.Владелец = СтрЗаменить(ОписаниеЭП.Владелец, """", " ");
	ОписаниеЭП.ДатаНачала = Сертификат.ДатаНачала;
	ОписаниеЭП.ДатаОкончания = Сертификат.ДатаОкончания;
	
	ОписаниеЭП.ДанныеДоверенности = ОписаниеДоверенностиЭПДляОтметки(ЭП.УникальныйИдентификатор);
	
	Возврат ОписаниеЭП;
	
КонецФункции

// Возвращает полный путь к изображению отметки ЭП по описанию ЭП.
//
// Параметры:
//   ОписаниеЭП - Структура со свойствами:
//     Номер - Строка - серийный номер
//     Владелец - Строка - владелец сертификата (CN).
//     ДатаНачала - Дата - дата начала действия сертификата.
//     ДатаОкончания - Дата - дата окончания действия сертификата.
//
// Возвращаемое значение:
//   Строка - полный путь к файлу с изображением отметки ЭП.
//
Функция СоздатьОтметкуЭП(ОписаниеЭП) Экспорт
	
	ПутьКОтметкеЭП = РаботаСЭППереопределяемый.СоздатьОтметкуЭП(ОписаниеЭП);
	Если ЗначениеЗаполнено(ПутьКОтметкеЭП) Тогда
		Возврат ПутьКОтметкеЭП;
	КонецЕсли;
	
	Если ОписаниеЭП.ДанныеДоверенности.ПодписьПоДоверенности = Истина Тогда
		ПутьКОтметкеЭП = РаботаСКартинками.СформироватьШтампЭПСДоверенностью(ОписаниеЭП);
	Иначе
		ПутьКОтметкеЭП = РаботаСКартинками.СформироватьШтампЭП(ОписаниеЭП);
	КонецЕсли;
	
	Возврат ПутьКОтметкеЭП;
	
КонецФункции

// Записывает отметку ЭП в служебные файлы документа.
//
// Параметры:
//   Документ - СправочникСсылка.ВходящиеДокументы,
//              СправочникСсылка.ИсходящиеДокументы,
//              СправочникСсылка.ВнутренниеДокументы.
//   ИдентификаторЭП - УникальныйИдентификатор,СправочникСсылка.Файлы - идентификатор ЭП или Файл-владелец.
//   ПутьКОтметкеЭП - Строка - полный путь к файлу с изображением отметки ЭП.
//   НомерПодписи - Число - Порядковый номер подписи, когда их может быть несколько
//   Положение - см. НовыйПоложениеИзображения.
//
Процедура ЗаписатьОтметкуЭП(Документ, ИдентификаторЭП, ПутьКОтметкеЭП, НомерПодписи = 1, Положение = Неопределено) Экспорт
	
	ДанныеОтметки = Новый ДвоичныеДанные(ПутьКОтметкеЭП);
	АдресОтметки = ПоместитьВоВременноеХранилище(ДанныеОтметки);
	
	СведенияОФайле = РаботаСФайламиКлиентСервер.СведенияОФайле("ФайлСВерсией");
	СведенияОФайле.АдресВременногоХранилищаФайла = АдресОтметки;
	СведенияОФайле.ИмяБезРасширения = "ОтметкаЭП";
	СведенияОФайле.РасширениеБезТочки = "png";
	СведенияОФайле.ВремяИзменения = ТекущаяДатаСеанса();
	СведенияОФайле.ВремяИзмененияУниверсальное = ТекущаяУниверсальнаяДата();
	СведенияОФайле.Размер = ДанныеОтметки.Размер();
	СведенияОФайле.Вставить("ЗаписьПодписанногоОбъекта", Истина);
	СведенияОФайле.Вставить("ДобавлениеРегШтампа", Истина);

	ОтметкаЭП = ПолучитьОтметкуЭП(Документ, ИдентификаторЭП, Положение);
	Если ЗначениеЗаполнено(ОтметкаЭП) Тогда
		Версия = РаботаСФайламиВызовСервера.СоздатьВерсию(ОтметкаЭП, СведенияОФайле);
		РаботаСФайламиВызовСервера.ОбновитьВерсиюВФайле(ОтметкаЭП, Версия, СведенияОФайле.АдресВременногоХранилищаТекста);
	Иначе
		ОтметкаЭП = РаботаСФайламиВызовСервера.СоздатьФайлСВерсией(Документ, СведенияОФайле);
		
		Если Положение = Неопределено Тогда
			Положение = НовыйПоложениеИзображения();
			// По умолчанию:
			Положение.Страница = 1;
			Положение.Ширина = Цел(1207*25.4/300); // 1207 пикселов при 300 dpi
			Положение.Высота = Цел(173*25.4/300); // 173 пиксела при 300 dpi
			Положение.Сверху = 297 - 10 - НомерПодписи * Положение.Высота;
			Положение.Слева = 10;
		КонецЕсли;
		
		РегистрыСведений.СлужебныеФайлыДокументов.ЗаписатьФайл(
			Документ, 
			?(ТипЗнч(ИдентификаторЭП) = Тип("УникальныйИдентификатор"), "" + ИдентификаторЭП, ИдентификаторЭП),
			Перечисления.ОтношенияСлужебныхФайлов.ОтметкаЭП,
			ОтметкаЭП,
			Положение);
	КонецЕсли;
	
КонецПроцедуры

// Получает отметку ЭП из служебных файлов документа.
//
// Параметры:
//   Документ - СправочникСсылка.ВходящиеДокументы,
//              СправочникСсылка.ИсходящиеДокументы,
//              СправочникСсылка.ВнутренниеДокументы.
//   ИдентификаторЭП - УникальныйИдентификатор,СправочникСсылка.Файлы - идентификатор ЭП или Файл-владелец.
//   Положение - Структура - неявно возвращаемое значение, положение штампа; см. НовыйПоложениеИзображения.
//
//  Возвращаемое значение:
//    СправочникСсылка.Файлы - файл-изображение отметки ЭП.
//
Функция ПолучитьОтметкуЭП(Документ, ИдентификаторЭП, Положение = Неопределено) Экспорт
	
	ДополнительныеСведения = Неопределено;
	ОтметкаЭП = РегистрыСведений.СлужебныеФайлыДокументов.ПолучитьСлужебныйФайл(
		Документ,
		ИдентификаторЭП,
		Перечисления.ОтношенияСлужебныхФайлов.ОтметкаЭП,
		ДополнительныеСведения);
	
	Если ДополнительныеСведения <> Неопределено Тогда
		Положение = НовыйПоложениеИзображения();
		ЗаполнитьЗначенияСвойств(Положение, ДополнительныеСведения);
	КонецЕсли;
	
	Возврат ОтметкаЭП;
	
КонецФункции

// Возвращает пустую структуру - положение изображения для размещения в документе.
// 
// Возвращаемое значение:
//  Структура - Новый положение изображения:
// * Страница - Число -
// * Слева - Число -
// * Сверху - Число -
// * Ширина - Число -
// * Высота - Число -
Функция НовыйПоложениеИзображения() Экспорт
	
	Данные = Новый Структура();
	Данные.Вставить("Страница", 1);
	Данные.Вставить("Слева", 1);
	Данные.Вставить("Сверху", 1);
	Данные.Вставить("Ширина", 1);
	Данные.Вставить("Высота", 1);
	
	Возврат Данные;
	
КонецФункции

// Удаляет отметки ЭП из служебных файлов документа.
//
// Параметры:
//   Документ - СправочникСсылка.ВходящиеДокументы,
//              СправочникСсылка.ИсходящиеДокументы,
//              СправочникСсылка.ВнутренниеДокументы.
//   Версия - СправочникСсылка.ВерсииФайлов - подписанная версия файла.
//
Процедура УдалитьОтметкиЭП(Документ, ФайлВладелец) Экспорт
	
	РегистрыСведений.СлужебныеФайлыДокументов.УдалитьПоДокументуИВладельцу(Документ, ФайлВладелец);
	
	// Удалим текущую версию из кэша, поскольку мы имеем неактуальные подписи, а значит и неактуальные штампы ЭП.
	ТекущаяВерсия = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ФайлВладелец, "ТекущаяВерсия");
	РаботаСФайламиВызовСервера.УдалитьИзРегистраЗаписиДляВсехКомпьютеров(ТекущаяВерсия);
	
КонецПроцедуры

// Удаляет отметку ЭП из служебных файлов документа.
//
// Параметры:
//   Документ - СправочникСсылка.ВходящиеДокументы,
//              СправочникСсылка.ИсходящиеДокументы,
//              СправочникСсылка.ВнутренниеДокументы.
//   ИдентификаторЭП - УникальныйИдентификатор - идентификатор ЭП.
//
Процедура УдалитьОтметкуЭП(Документ, ИдентификаторЭП) Экспорт
	
	ОтметкаЭП = ПолучитьОтметкуЭП(Документ, ИдентификаторЭП);
	Если ЗначениеЗаполнено(ОтметкаЭП) Тогда
		РегистрыСведений.СлужебныеФайлыДокументов.УдалитьФайл(Документ, ОтметкаЭП);
	КонецЕсли;
	
КонецПроцедуры

// Возвращает массив идентификаторов ЭП объекта.
//
// Параметры:
//   ОбъектЭП - ЛюбаяСсылка - подписанный объект.
//
// Возвращаемое значение:
//   Массив - массив идентификаторов ЭП из РС ЭлектронныеПодписи.
//
Функция ИдентификаторыЭП(ОбъектЭП) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЭлектронныеПодписи.УникальныйИдентификатор КАК УникальныйИдентификатор
		|ИЗ
		|	РегистрСведений.ЭлектронныеПодписи КАК ЭлектронныеПодписи
		|ГДЕ
		|	ЭлектронныеПодписи.Объект = &ОбъектЭП");
	Запрос.УстановитьПараметр("ОбъектЭП", ОбъектЭП);
	ИдентификаторыЭП = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("УникальныйИдентификатор");
	
	Возврат ИдентификаторыЭП;
	
КонецФункции

Функция АктуальнаяВерсияПодписи() Экспорт
	
	Возврат 5;
	
КонецФункции

Функция СрокПроверкиДействияСертификата(Знач Сертификат, СрокДействияПоследнейМеткиВремени) Экспорт
	
	Если Сертификат = Неопределено Тогда
		Возврат ТекущаяДатаСеанса();
	КонецЕсли;
	
	Если ТипЗнч(Сертификат) = Тип("Строка")
		И ЭтоАдресВременногоХранилища(Сертификат) Тогда
		
		ДвоичныеДанныеСертификата = ПолучитьИзВременногоХранилища(Сертификат);
		Сертификат = Новый СертификатКриптографии(ДвоичныеДанныеСертификата);
	ИначеЕсли ТипЗнч(Сертификат) = Тип("ДвоичныеДанные") Тогда
		Сертификат = Новый СертификатКриптографии(Сертификат);
	КонецЕсли;
	
	ДатаДействительностиСертификата = Сертификат.ДатаОкончания;
	
	Если ДатаДействительностиСертификата > СрокДействияПоследнейМеткиВремени Тогда
		Возврат ДатаДействительностиСертификата;
	Иначе
		Возврат СрокДействияПоследнейМеткиВремени;
	КонецЕсли;
	
КонецФункции

// Текст подписи УЭП для вывода в печатную форму.
// 
// Параметры:
//  ОбъектПодписания  - СправочникСсылка.ВизыСогласования, СправочникСсылка.ДокументыПредприятия, 
//  					СправочникСсылка.Резолюции - подписанный объект
//  УстановилПодпись - СправочникСсылка.Пользователи, Неопределено - тот, кто установил подпись.
// 
// Возвращаемое значение:
//  Строка - Текст подписи УЭП 
Функция ТекстПодписиУЭПДляВывода(ОбъектПодписания, УстановилПодпись = Неопределено) Экспорт
	
	ТекстПодписи = "";
	
	ЭПОбъекта = РаботаСЭП.ПолучитьЭлектронныеПодписи(ОбъектПодписания);
	
	Пока ЭПОбъекта.Следующий() Цикл
		Если ЗначениеЗаполнено(УстановилПодпись) 
			И УстановилПодпись <> ЭПОбъекта.УстановившийПодпись Тогда
			Продолжить;
		КонецЕсли;
		
		ОписаниеЭП = РаботаСЭП.ОписаниеЭПДляОтметки(ЭПОбъекта);
		ТекстПодписи  = ТекстПодписи + СтрШаблон(НСтр("ru = 'Сертификат: %1
		|Владелец: %2
		|Действителен: с %3 по %4'"),
		ОписаниеЭП.Номер,
		ОписаниеЭП.Владелец,
		Формат(ОписаниеЭП.ДатаНачала, "ДФ=dd.MM.yyyy;"),
		Формат(ОписаниеЭП.ДатаОкончания, "ДФ=dd.MM.yyyy;"));
			
	КонецЦикла;
		
	СтрокаВозврата = НСтр("ru = 'ПОДПИСАНО УСИЛЕННОЙ ЭЛЕКТРОННОЙ ПОДПИСЬЮ'")
		+ Символы.ПС + ТекстПодписи;
		 		
	Возврат СтрокаВозврата;		
	
КонецФункции

#Область ПроверкаПодписей

Функция ДанныеОбъектовДляПроверкиПодписей(ПараметрыПолучения) Экспорт
	
	ДанныеОбъектов = Новый Соответствие;
	
	ДанныеПодписейОбъектов = ПараметрыПолучения.ДанныеПодписейОбъектов;
	
	Для Каждого ЭлементОбъекта Из ДанныеПодписейОбъектов Цикл
		
		Объект = ЭлементОбъекта.Ключ;
		ДанныеПодписейОбъекта = ЭлементОбъекта.Значение;
		
		Если ДелопроизводствоКлиентСервер.ЭтоДокумент(Объект)
			Или ДелопроизводствоКлиентСервер.ЭтоВизаСогласования(Объект)
			Или ДелопроизводствоКлиентСервер.ЭтоРезолюция(Объект) Тогда
			
			ВерсииПодписей = ВерсииПодписейОбъекта(ДанныеПодписейОбъекта);
			ДанныеОбъекта = ДанныеОбъектаДОДляПроверкиПодписей(Объект, ВерсииПодписей, ПараметрыПолучения);
			
		ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.Файлы")
			Или ТипЗнч(Объект) = Тип("СправочникСсылка.ВерсииФайлов") Тогда
			
			ДанныеОбъекта = ДанныеФайлаДляПроверкиПодписей(Объект, ПараметрыПолучения);
			
		КонецЕсли;
		
		ДанныеОбъектов.Вставить(Объект, ДанныеОбъекта);
		
	КонецЦикла;
	
	Возврат ДанныеОбъектов;
	
КонецФункции

Процедура ОбновитьСтатусыПроверкиПодписейВРегистре(ДанныеПодписей, ДатаПроверки = Неопределено) Экспорт
	
	Для Каждого ДанныеПодписи Из ДанныеПодписей Цикл
		ДанныеДляЗаписи = ДанныеПодписиДляЗаписи(ДанныеПодписи);
		ОбновитьСтатусПроверкиПодписиВРегистре(ДанныеДляЗаписи, ДатаПроверки);
	КонецЦикла;
	
КонецПроцедуры

// Результат проверки подписей
// 
// Параметры:
//  ДанныеПроверок  - Соответствие из КлючИЗначение:
//    * Ключ - УникальныйИдентификатор - УИД подписи
//    * Значение см. РаботаСЭП.НовыеДанныеПроверокПодписи
// 
// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//    * Ключ - УникальныйИдентификатор - УИД подписи
//    * Значение см. НовыйРезультатПроверкиПодписи
//
Функция РезультатПроверкиПодписей(ДанныеПроверок) Экспорт
	
	РезультатПроверки = Новый Соответствие;
	
	Для Каждого Элемент Из ДанныеПроверок Цикл
		
		ИдентификаторПодписи = Элемент.Ключ;
		ДанныеПроверокПодписи = Элемент.Значение;
		
		РезультатПодписи = РезультатПроверокПодписи(ДанныеПроверокПодписи);
		РезультатПроверки.Вставить(ИдентификаторПодписи, РезультатПодписи);
		
	КонецЦикла;
	
	Возврат РезультатПроверки;
	
КонецФункции

// Новые данные проверок подписей
// 
// Возвращаемое значение:
//  Структура:
// * ДанныеДляПроверки см. РаботаСЭПКлиентСервер.НовыеДанныеПодписиДляПроверки
// * РезультатПроверкиПодписи см. РаботаСЭПКлиентСервер.НовыйРезультатПроверкиПодписи
// * РезультатПроверкиДоверенности - Структура, Неопределено - Протокол проверки доверенности см МашиночитаемыеДоверенности.НовыйПротоколПроверкиПодписи
// * ДатаПроверки - Дата - ДатаВыполненияПроверки
//
Функция НовыеДанныеПроверокПодписи() Экспорт
	
	НовыеДанныеДляПроверки = РаботаСЭПКлиентСервер.НовыеДанныеПодписиДляПроверки();
	НовыйРезультатПроверкиПодписи = РаботаСЭПКлиентСервер.НовыйРезультатПроверкиПодписи();
	
	ДанныеПроверок = Новый Структура;
	ДанныеПроверок.Вставить("ДанныеДляПроверки", НовыеДанныеДляПроверки);
	ДанныеПроверок.Вставить("РезультатПроверкиПодписи", НовыйРезультатПроверкиПодписи);
	ДанныеПроверок.Вставить("РезультатПроверкиДоверенности", Новый Структура);
	ДанныеПроверок.Вставить("ДатаПроверки", Дата(1, 1, 1));
	
	Возврат ДанныеПроверок;
	
КонецФункции

Функция ПроверитьПодписиНаСервере(ДанныеПодписейОбъектов, ДанныеОбъектов) Экспорт
	
	РезультатПроверкиПодписей = ПроверитьДействительностьЭПНаСервере(ДанныеПодписейОбъектов, ДанныеОбъектов);
	
	Возврат РаботаСЭПВызовСервера.ЗавершитьПроверкуПодписейНаСервере(ДанныеПодписейОбъектов, РезультатПроверкиПодписей);
	
КонецФункции

#КонецОбласти

#Область РаботаСМЧД

// Проверяет доверенности электронных подписей
// 
// Параметры:
//  ДанныеДляПроверки - Массив из Структура - см РаботаСЭПКлиентСервер.НовыеДанныеДляПроверкиДоверенностиПодписи
// 
// Возвращаемое значение:
//  Структура - Проверить доверенности электронных подписей:
//   * РезультатыПроверкиДоверенностей - Соответствие из КлючИЗначение:
//     ** Ключ - УникальныйИдентификатор - Уникальный идентификатор подписи
//     ** Значение - Структура - Протокол проверки доверенности см МашиночитаемыеДоверенности.НовыйПротоколПроверкиПодписи
//   * ОшибкиПриПроверке - Соответствие из КлючИЗначение:
//     ** Ключ - УникальныйИдентификатор - Уникальный идентификатор подписи
//     ** Значение - Строка - Описание проблемы при проверке подписи
//
Функция ПроверитьДоверенностиЭлектронныхПодписей(ДанныеДляПроверки) Экспорт
	
	РезультатПроверки = Новый Структура;
	РезультатПроверки.Вставить("РезультатыПроверкиДоверенностей", Новый Соответствие);
	РезультатПроверки.Вставить("ОшибкиПриПроверке", Новый Соответствие);
	
	ДанныеДоверенностей = ДанныеДоверенностейВДанныхДляПроверкиДоверенностей(ДанныеДляПроверки);
	
	Для Каждого Данные Из ДанныеДляПроверки Цикл
		
		ИдентификаторПодписи = Данные.ДанныеПодписи.УникальныйИдентификатор;
		Доверенность = Данные.Доверенность;
		
		Если Не ЗначениеЗаполнено(Доверенность) Тогда
			СообщениеОшибки = СтрШаблон(НСтр("ru = 'Для подписи %1 к объекту %2 не указана доверенность'"),
				ИдентификаторПодписи, Данные.ДанныеПодписи.Объект);
			РезультатПроверки.ОшибкиПриПроверке.Вставить(ИдентификаторПодписи, СообщениеОшибки);
			Продолжить;
		КонецЕсли;
		
		ДанныеДоверенности = ДанныеДоверенностей[Доверенность];
		Если ДанныеДоверенности = Неопределено Тогда
			СообщениеОшибки = СтрШаблон(НСтр("ru = 'Не удалось получить данные доверенности %1'"),
				Доверенность);
			РезультатПроверки.ОшибкиПриПроверке.Вставить(ИдентификаторПодписи, СообщениеОшибки);
			Продолжить;
		КонецЕсли;
		
		ПараметрыПроверки = ПараметрыПроверкиДоверенностиЭП(Данные, ДанныеДоверенности);
		Результат = МашиночитаемыеДоверенности.ПроверитьПодпись(ПараметрыПроверки);
		
		РезультатПроверки.РезультатыПроверкиДоверенностей.Вставить(ИдентификаторПодписи, Результат.ПротоколПроверки);
		
	КонецЦикла;
	
	Возврат РезультатПроверки;
	
КонецФункции

Процедура ОбновитьСтатусыПроверкиДоверенностей(РезультатыПроверки, ДатаПроверки = Неопределено) Экспорт
	
	ДатаПроверкиДоверенности = ТекущаяДатаСеанса();
	Если ДатаПроверки <> Неопределено Тогда
		ДатаПроверкиДоверенности = ДатаПроверки;
	КонецЕсли;
	
	Для Каждого Элемент Из РезультатыПроверки Цикл
		
		ИдентификаторПодписи = Элемент.Ключ;
		ПротоколПроверки = Элемент.Значение;
		
		НаборЗаписей = РегистрыСведений.ДоверенностиЭлектронныхПодписей.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.УникальныйИдентификаторПодписи.Установить(ИдентификаторПодписи);
		НаборЗаписей.Прочитать();
		
		Если НаборЗаписей.Количество() = 0 Тогда
			СообщениеПользователю = СтрШаблон(
				НСтр("ru = 'Не удалось обновить статус проверки доверенности подписи %1: запись о доверенности не найдена'"),
				ИдентификаторПодписи);
			ВызватьИсключение СообщениеПользователю;
		КонецЕсли;
		
		Запись = НаборЗаписей[0];
		
		РезультатПроверки = РаботаСЭПКлиентСервер.РезультатПроверкиДоверенностиПоПротоколу(ПротоколПроверки);
		
		ПротоколПроверкиJSON = ОбщегоНазначенияБЭД.JSONСтрока(ПротоколПроверки);
		
		Запись.ПротоколПроверки = Новый ХранилищеЗначения(ПротоколПроверкиJSON, Новый СжатиеДанных(9));
		Запись.ДатаПроверки = ДатаПроверкиДоверенности;
		Запись.ДоверенностьВерна = РезультатПроверки.ДоверенностьДействительна;
		
		НаборЗаписей.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗанестиИнформациюОДоверенностиЭП(ИдентификаторПодписи, Доверенность, Знач СвойстваПодписи) Экспорт
	
	СвойстваПодписи.Вставить("УникальныйИдентификатор", ИдентификаторПодписи);
	
	ДанныеДляПроверкиДоверенности = РаботаСЭПКлиентСервер.НовыеДанныеДляПроверкиДоверенностиПодписи();
	ЗаполнитьЗначенияСвойств(ДанныеДляПроверкиДоверенности.ДанныеПодписи, СвойстваПодписи);
	ДанныеДляПроверкиДоверенности.Доверенность = Доверенность;
	
	ДанныеДляПроверки = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДанныеДляПроверкиДоверенности);
	РезультатПроверки = ПроверитьДоверенностиЭлектронныхПодписей(ДанныеДляПроверки);
	ПротоколПроверки = РезультатПроверки.РезультатыПроверкиДоверенностей[ИдентификаторПодписи];
	ДоверенностьВерна =
		РаботаСЭПКлиентСервер.РезультатПроверкиДоверенностиПоПротоколу(ПротоколПроверки).ДоверенностьДействительна;
	
	ПротоколПроверкиJSON = ОбщегоНазначенияБЭД.JSONСтрока(ПротоколПроверки);
	
	МенеджерЗаписи = РегистрыСведений.ДоверенностиЭлектронныхПодписей.СоздатьМенеджерЗаписи();
	
	МенеджерЗаписи.УникальныйИдентификаторПодписи = ИдентификаторПодписи;
	МенеджерЗаписи.Доверенность = Доверенность;
	МенеджерЗаписи.ДоверенностьВерна = ДоверенностьВерна;
	МенеджерЗаписи.ДатаПроверки = ТекущаяДатаСеанса();
	МенеджерЗаписи.ПротоколПроверки = Новый ХранилищеЗначения(ПротоколПроверкиJSON, Новый СжатиеДанных(9));
	
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

Процедура УдалитьЗаписьОДоверенностиЭП(ИдентификаторПодписи) Экспорт
	
	НаборЗаписей = РегистрыСведений.ДоверенностиЭлектронныхПодписей.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.УникальныйИдентификаторПодписи.Установить(ИдентификаторПодписи);
	НаборЗаписей.Прочитать();
	
	Если НаборЗаписей.Количество() > 0 Тогда
		НаборЗаписей.Очистить();
		НаборЗаписей.Записать();
	КонецЕсли;
	
КонецПроцедуры

Функция ДоступныеДоверенностиОрганизацийДляПодписанияСертификатом(Сертификат) Экспорт
	
	Доверенности = Новый Массив;
	
	СвойстваСубъекта = КриптографияБЭД.СвойстваСубъектаСертификатаПоСсылке(Сертификат);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	МашиночитаемыеДоверенностиОрганизаций.Ссылка КАК Ссылка,
		|	МашиночитаемыеДоверенностиОрганизаций.ДатаВыдачи,
		|	МашиночитаемыеДоверенностиОрганизаций.ДатаОкончания,
		|	МашиночитаемыеДоверенностиОрганизаций.СтатусВРеестреФНС,
		|	МашиночитаемыеДоверенностиОрганизаций.Верна,
		|	МашиночитаемыеДоверенностиОрганизаций.Отозвана,
		|	МашиночитаемыеДоверенностиОрганизаций.ДатаОтзыва
		|ИЗ
		|	Справочник.МашиночитаемыеДоверенностиОрганизаций КАК МашиночитаемыеДоверенностиОрганизаций
		|ГДЕ
		|	МашиночитаемыеДоверенностиОрганизаций.ПредставительФЛ_ИНН = &ПредставительИНН
		|	И НЕ МашиночитаемыеДоверенностиОрганизаций.ПометкаУдаления
		|	И МашиночитаемыеДоверенностиОрганизаций.СтатусВРеестреФНС В (&СтатусВРеестреФНС)";
	
	Статусы = Новый Массив;
	Статусы.Добавить(Перечисления.СтатусыМашиночитаемойДоверенностиВРеестреФНС.Зарегистрировано);
	Статусы.Добавить(Перечисления.СтатусыМашиночитаемойДоверенностиВРеестреФНС.ДатаНачалаДействияНеНаступила);
	Статусы.Добавить(Перечисления.СтатусыМашиночитаемойДоверенностиВРеестреФНС.ИстекСрокДействия);
	Статусы.Добавить(Перечисления.СтатусыМашиночитаемойДоверенностиВРеестреФНС.ПустаяСсылка());
	
	Запрос.УстановитьПараметр("СтатусВРеестреФНС", Статусы);
	Запрос.УстановитьПараметр("ПредставительИНН", СвойстваСубъекта.ИНН);
	
	ТекущаяДата = ТекущаяДатаСеанса();
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ДействуетПоСроку = (ТекущаяДата > Выборка.ДатаВыдачи)
			И (Выборка.ДатаОкончания > ТекущаяДата);
		
		Если ДействуетПоСроку И Не Выборка.Отозвана Тогда
			Доверенности.Добавить(Выборка.Ссылка);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Доверенности;
	
КонецФункции

Функция ДоступныеДоверенностиКонтрагентовПоСертификатуКриптографии(Сертификат) Экспорт
	
	Доверенности = Новый Массив;
	
	СвойстваСубъекта = КриптографияБЭД.СвойстваСубъектаСертификата(Сертификат);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	МашиночитаемыеДоверенностиКонтрагентов.Ссылка КАК Ссылка,
		|	МашиночитаемыеДоверенностиКонтрагентов.ДатаВыдачи,
		|	МашиночитаемыеДоверенностиКонтрагентов.ДатаОкончания,
		|	МашиночитаемыеДоверенностиКонтрагентов.СтатусВРеестреФНС,
		|	МашиночитаемыеДоверенностиКонтрагентов.Верна,
		|	МашиночитаемыеДоверенностиКонтрагентов.Отозвана,
		|	МашиночитаемыеДоверенностиКонтрагентов.ДатаОтзыва
		|ИЗ
		|	Справочник.МашиночитаемыеДоверенностиКонтрагентов КАК МашиночитаемыеДоверенностиКонтрагентов
		|ГДЕ
		|	МашиночитаемыеДоверенностиКонтрагентов.ПредставительИНН = &ПредставительИНН
		|	И НЕ МашиночитаемыеДоверенностиКонтрагентов.ПометкаУдаления
		|	И МашиночитаемыеДоверенностиКонтрагентов.СтатусВРеестреФНС В (&СтатусВРеестреФНС)";
	
	Статусы = Новый Массив;
	Статусы.Добавить(Перечисления.СтатусыМашиночитаемойДоверенностиВРеестреФНС.Зарегистрировано);
	Статусы.Добавить(Перечисления.СтатусыМашиночитаемойДоверенностиВРеестреФНС.ДатаНачалаДействияНеНаступила);
	Статусы.Добавить(Перечисления.СтатусыМашиночитаемойДоверенностиВРеестреФНС.ИстекСрокДействия);
	Статусы.Добавить(Перечисления.СтатусыМашиночитаемойДоверенностиВРеестреФНС.ПустаяСсылка());
	
	Запрос.УстановитьПараметр("СтатусВРеестреФНС", Статусы);
	Запрос.УстановитьПараметр("ПредставительИНН", СвойстваСубъекта.ИНН);
	
	ТекущаяДата = ТекущаяДатаСеанса();
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ДействуетПоСроку = (ТекущаяДата > Выборка.ДатаВыдачи)
			И (Выборка.ДатаОкончания > ТекущаяДата);
		
		Если ДействуетПоСроку И Не Выборка.Отозвана Тогда
			Доверенности.Добавить(Выборка.Ссылка);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Доверенности;
	
КонецФункции

Функция СертификатуНужнаДоверенность(Сертификат) Экспорт
	
	Если ТипЗнч(Сертификат) = Тип("СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования") Тогда
		СвойстваСубъекта = КриптографияБЭД.СвойстваСубъектаСертификатаПоСсылке(Сертификат);
		СвойстваИздателя = КриптографияБЭД.СвойстваИздателяСертификатаПоСсылке(Сертификат);
	Иначе
		СвойстваСубъекта = КриптографияБЭД.СвойстваСубъектаСертификата(Сертификат);
		СвойстваИздателя = КриптографияБЭД.СвойстваИздателяСертификата(Сертификат);
	КонецЕсли;
	
	ЭтоСертификатИП = (СвойстваСубъекта.ОГРНИП <> Неопределено);
	ЭтоСертификатФизЛица = МашиночитаемыеДоверенностиКлиентСервер.ЭтоСертификатФизическогоЛица(
		СвойстваСубъекта, СвойстваИздателя);
	
	Возврат ЭтоСертификатФизЛица И Не ЭтоСертификатИП;
	
КонецФункции

Функция ДанныеДоверенностейПодписей(ИдентификаторыПодписей) Экспорт
	
	ДанныеДоверенностей = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДоверенностиЭлектронныхПодписей.УникальныйИдентификаторПодписи,
		|	ДоверенностиЭлектронныхПодписей.ДатаПроверки КАК ДатаПроверкиДоверенности,
		|	ДоверенностиЭлектронныхПодписей.Доверенность КАК Доверенность,
		|	ДоверенностиЭлектронныхПодписей.ДоверенностьВерна КАК ДоверенностьВерна,
		|	ДоверенностиЭлектронныхПодписей.ПротоколПроверки КАК ПротоколПроверкиДоверенности
		|ИЗ
		|	РегистрСведений.ДоверенностиЭлектронныхПодписей КАК ДоверенностиЭлектронныхПодписей
		|ГДЕ
		|	ДоверенностиЭлектронныхПодписей.УникальныйИдентификаторПодписи В (&ИдентификаторыПодписей)";
	Запрос.УстановитьПараметр("ИдентификаторыПодписей", ИдентификаторыПодписей);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ДанныеДоверенности = НовыеДанныеДоверенности();
		ЗаполнитьЗначенияСвойств(ДанныеДоверенности, Выборка, "Доверенность, ДатаПроверкиДоверенности, ДоверенностьВерна");
		
		ПротоколПроверкиJSON = Выборка.ПротоколПроверкиДоверенности.Получить();
		Если ПротоколПроверкиJSON <> Неопределено Тогда
			ЧтениеJSON = Новый ЧтениеJSON;
			ЧтениеJSON.УстановитьСтроку(ПротоколПроверкиJSON);
			ДанныеДоверенности.ПротоколПроверкиДоверенности = ПрочитатьJSON(ЧтениеJSON,,"ДатаПроверки");
			ЧтениеJSON.Закрыть();
		КонецЕсли;
		
		ДанныеДоверенностей.Вставить(Выборка.УникальныйИдентификаторПодписи, ДанныеДоверенности);
		
	КонецЦикла;
	
	Возврат ДанныеДоверенностей;
	
КонецФункции

Функция ДоступноУказаниеДоверенностиПодписи(ИдентификаторПопдиси, Знач Пользователь = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(Пользователь) Тогда
		Пользователь = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
	Если Пользователи.ЭтоПолноправныйПользователь(Пользователь) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Идентификаторы = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ИдентификаторПопдиси);
	ПодписиПоИдентификаторам = УстановленныеПодписиПоИдентификаторам(Идентификаторы);
	СвойстваПодписи = ПодписиПоИдентификаторам[ИдентификаторПопдиси];
	
	Если ТипЗнч(СвойстваПодписи) = Тип("Структура")
		И СвойстваПодписи.УстановившийПодпись = Пользователь Тогда
		
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Функция ДанныеДоверенностейДляВыгрузки(Доверенности, ИдентификаторФормы) Экспорт
	
	ДанныеДоверенностей = Новый Соответствие;
	
	Для Каждого Доверенность Из Доверенности Цикл
		
		ДанныеДоверенности = ДанныеДоверенностей[Доверенность];
		Если ДанныеДоверенности <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ДанныеДоверенности = НовыеДанныеДоверенностиДляВыгрузки();
		
		Если ТипЗнч(Доверенность) = Тип("СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций") Тогда
			РезультатВыгрузки = МашиночитаемыеДоверенности.ВыгрузитьДанныеДоверенности(Доверенность);
		Иначе
			РезультатВыгрузки = ВыгрузитьДанныеДоверенностиКонтрагента(Доверенность);
		КонецЕсли;
		
		Если РезультатВыгрузки.Ошибка Тогда
			ДанныеДоверенности.ОшибкаВыгрузки = РезультатВыгрузки.ТекстОшибки;
		Иначе
			ДанныеДоверенности.Выгружена = Истина;
			ДанныеДоверенности.АдресДанных = ПоместитьВоВременноеХранилище(
				РезультатВыгрузки.ОписаниеФайла.ДвоичныеДанные, ИдентификаторФормы);
			ДанныеДоверенности.ИмяФайла = РезультатВыгрузки.ОписаниеФайла.ИмяФайла;
		КонецЕсли;
		
		ДанныеДоверенностей.Вставить(Доверенность, ДанныеДоверенности);
		
	КонецЦикла;
	
	Возврат ДанныеДоверенностей;
	
КонецФункции

Функция ВыгрузитьДанныеДоверенностиКонтрагента(Ссылка)
	
	РезультатВыгрузки = Новый Структура;
	РезультатВыгрузки.Вставить("ОписаниеФайла", РаботаСФайламиБЭДКлиентСервер.НовоеОписаниеФайла());
	РезультатВыгрузки.Вставить("Ошибка", Ложь);
	РезультатВыгрузки.Вставить("ТекстОшибки", "");
	
	Если Ссылка.Пустая() Тогда
		Возврат РезультатВыгрузкиНеподписаннойДоверенности(РезультатВыгрузки);
	КонецЕсли;
	
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "ЭлектроннаяПодпись, XMLфайлМЧД");
	Подпись = ЗначенияРеквизитов.ЭлектроннаяПодпись.Получить();
	Если Подпись = Неопределено Тогда
		Возврат РезультатВыгрузкиНеподписаннойДоверенности(РезультатВыгрузки);
	КонецЕсли;
	
	ДвоичныеДанныеДоверенности = ЗначенияРеквизитов.XMLфайлМЧД.Получить();
	Если ДвоичныеДанныеДоверенности = Неопределено Тогда
		Возврат РезультатВыгрузкиНеподписаннойДоверенности(РезультатВыгрузки);
	КонецЕсли;
	
	Файлы = Новый Массив;
	
	ИмяВременногоКаталога = РаботаСФайламиБЭД.ВременныйКаталог();
	ИмяФайлаДоверенностьБезРасширения = ИмяФайлаМЧДКонтрагента(Ссылка);
	ИмяФайлаДоверенность = ИмяВременногоКаталога + ИмяФайлаДоверенностьБезРасширения + ".xml";
	ИмяФайлаПодпись = ИмяВременногоКаталога + ИмяФайлаДоверенностьБезРасширения + ".p7s";
	
	ДвоичныеДанныеДоверенности.Записать(ИмяФайлаДоверенность);
	Файлы.Добавить(ИмяФайлаДоверенность);
	
	Подпись.Записать(ИмяФайлаПодпись);
	Файлы.Добавить(ИмяФайлаПодпись);
		
	ИмяФайлаВизуализация = ИмяВременногоКаталога + ИмяФайлаДоверенностьБезРасширения + ".pdf";
	РезультатФормирования = МашиночитаемыеДоверенности.ТабличныйДокументМЧД(ДвоичныеДанныеДоверенности);
	
	ТабличныйДокумент = РезультатФормирования.ПредставлениеДокумента;
	Если ТабличныйДокумент <> Неопределено Тогда
		ТабличныйДокумент.Записать(ИмяФайлаВизуализация, ТипФайлаТабличногоДокумента.PDF);
		Файлы.Добавить(ИмяФайлаВизуализация);
	КонецЕсли;
	
	Архив = РаботаСФайламиБЭД.СформироватьАрхивФайлов(Файлы);
	
	УдалитьФайлы(ИмяВременногоКаталога);
	
	РезультатВыгрузки.ОписаниеФайла.ИмяФайла = ИмяФайлаДоверенностьБезРасширения + ".zip";
	РезультатВыгрузки.ОписаниеФайла.ДвоичныеДанные = Архив;
	
	Возврат РезультатВыгрузки;
	
КонецФункции

#КонецОбласти

#Область ПолучениеСертификатовИзПодписи

// Получает сертификаты из подписей (использование криптопровайдера на сервере).
// 
// Параметры:
// ПодписиБезСертификатов - Соответствие Из КлючИЗначение:
//   * Ключ - УникальныйИдентификатор - Идентификатор подписи
//   * Значение - Строка - Адрес двоичных данных подписи во временном хранилище
// ИдентификаторФормы - УникальныйИдентификатор - Идентификатор формы для помещения данных во временное хранилище
// 
// Возвращаемое значение:
//  Структура:
// * ДанныеСертификатовПоПодписям - Соответствие Из КлючИЗначение:
//   ** Ключ - УникальныйИдентификатор - Идентификатор подписи
//   ** Значение - см. РаботаСЭПКлиентСервер.НовыйРезультатПолученияДанныхСертификатаИзПодписи
// * ОшибкиПриПолученииСертификатов - Соответствие Из КлючИЗначение:
//   ** Ключ - УникальныйИдентификатор - Идентификатор подписи
//   ** Значение - Строка - Описание ошибки при получении сертификата из подписи
Функция ПолучитьСертификатыИзПодписейНаСервере(ПодписиБезСертификатов, ИдентификаторФормы = Неопределено) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ДанныеСертификатовПоПодписям", Новый Соответствие);
	Результат.Вставить("ОшибкиПриПолученииСертификатов", Новый Соответствие);
	
	Для Каждого Элемент Из ПодписиБезСертификатов Цикл
		
		ИдентификаторПодписи = Элемент.Ключ;
		АдресДанныхПодписи = Элемент.Значение;
		
		РезультатДляПодписи = ПолучитьДанныеСертификатаИзПодписи(АдресДанныхПодписи, ИдентификаторФормы);
		
		Если РезультатДляПодписи.Успех Тогда
			Результат.ДанныеСертификатовПоПодписям.Вставить(
				ИдентификаторПодписи, РезультатДляПодписи.ДанныеСертификата);
		Иначе
			Результат.ОшибкиПриПолученииСертификатов.Вставить(
				ИдентификаторПодписи, РезультатДляПодписи.ОписаниеОшибки);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Заносит в регистр сведений ЭП данные о сертификатах
// 
// Параметры:
//  ДанныеСертификатовПоПодписям - Соответствие Из КлючИЗначение:
//    * Ключ - УникальныйИдентификатор - Идентификатор подписи
//    * Значение см. РаботаСЭПКлиентСервер.НовыйРезультатПолученияДанныхСертификатаИзПодписи
//
Процедура ЗанестиДанныеСертификатовПодписей(ДанныеСертификатовПоПодписям) Экспорт
	
	Для Каждого Элемент Из ДанныеСертификатовПоПодписям Цикл
		
		ИдентификаторПодписи = Элемент.Ключ;
		ДанныеСертификата = Элемент.Значение;
		
		ЗанестиДанныеСертификатаПодписи(ИдентификаторПодписи, ДанныеСертификата);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает настройки текущего пользователя для работы с электронной подписью.
//
// Возвращаемое значение:
//   ПерсональныеНастройки (Структура)
//       |- ДействияПриСохраненииСЭП (Строка)
//       |- ПутьМодуляКриптографии (Строка)
//       |- РасширениеДляФайловПодписи (Строка)
//       |- РасширениеДляЗашифрованныхФайлов (Строка)
//       |- ОтпечатокЛичногоСертификатаДляШифрования (Строка)
//
Функция ПерсональныеНастройки(ТолькоЗначенияПоУмолчанию = Ложь) Экспорт
	
	ПерсональныеНастройки = Новый Структура;
	
	// Значения "по умолчанию".
	ПерсональныеНастройки.Вставить("ДействияПриСохраненииСЭП", Перечисления.ДействияПриСохраненииСЭП.Спрашивать);
	ПерсональныеНастройки.Вставить("ПутьМодуляКриптографии", "");
	ПерсональныеНастройки.Вставить("РасширениеДляФайловПодписи", "p7s");
	ПерсональныеНастройки.Вставить("РасширениеДляЗашифрованныхФайлов", "p7m");
	ПерсональныеНастройки.Вставить("ОтпечатокЛичногоСертификатаДляШифрования", "");
	
	Если ТолькоЗначенияПоУмолчанию Тогда
		Возврат ПерсональныеНастройки;
	КонецЕсли;
	
	КлючПодсистемы = "ЭП";
	
	Для Каждого КлючИЗначение Из ПерсональныеНастройки Цикл
		СохраненноеЗначение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(КлючПодсистемы, КлючИЗначение.Ключ);
		Если ЗначениеЗаполнено(СохраненноеЗначение) Тогда
			ПерсональныеНастройки.Вставить(КлючИЗначение.Ключ, СохраненноеЗначение);
		КонецЕсли;
	КонецЦикла;
	
	Если ПерсональныеНастройки.ДействияПриСохраненииСЭП = Перечисления.ДействияПриСохраненииСЭП.Спрашивать Тогда
		ПерсональныеНастройки.ДействияПриСохраненииСЭП = "Спрашивать";
	ИначеЕсли ПерсональныеНастройки.ДействияПриСохраненииСЭП = Перечисления.ДействияПриСохраненииСЭП.СохранятьВсеПодписи Тогда
		ПерсональныеНастройки.ДействияПриСохраненииСЭП = "СохранятьВсеПодписи";
	КонецЕсли;
	
	Возврат ПерсональныеНастройки;
	
КонецФункции

Функция ПолучитьДанныеОбъектаСтрокой(Объект, Имя)
	
	СтроковоеПредставлениеОбъекта = "";
	
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.УстановитьСтроку();
	
	ЗаписатьXML(ЗаписьXML, Объект, Имя, НазначениеТипаXML.Явное); 
	
	СтроковоеПредставлениеОбъекта = ЗаписьXML.Закрыть();
	
	Возврат СтроковоеПредставлениеОбъекта;
	
КонецФункции

// Добавляет ЭП из сообщения почты.
//
Процедура ДобавитьЭПИзСообщения(ОбъектСсылка, МассивФайловПодписей) Экспорт
	
	ДатаПодписи = ТекущаяДатаСеанса();
	
	МассивДанныхДляЗанесенияВБазу = Новый Массив;
	
	Для Каждого ОбъектДляПодписи Из МассивФайловПодписей Цикл
		
		НоваяПодписьДвоичныеДанные = ОбъектДляПодписи.ДвоичныеДанные;
		ИмяФайлаПодписи = ОбъектДляПодписи.Имя;
		
		ОбъектСсылкаДляПодписи = ОбъектСсылка;
		
		Отпечаток = ОбъектДляПодписи.Отпечаток;
		КомуВыданСертификат = ОбъектДляПодписи.КомуВыданСертификат;
		
		ДвоичныеДанныеСертификата = ОбъектДляПодписи.ДвоичныеДанныеСертификата;
		
		ДанныеДляЗанесенияВБазу = Новый Структура;
		ДанныеДляЗанесенияВБазу.Вставить("ПодписанныйОбъект", ОбъектСсылкаДляПодписи);
		
		СвойстваПодписи = Новый Структура;
		СвойстваПодписи.Вставить("Подпись", НоваяПодписьДвоичныеДанные);
		СвойстваПодписи.Вставить("Отпечаток", Отпечаток);
		СвойстваПодписи.Вставить("ДатаПодписи", ДатаПодписи);
		СвойстваПодписи.Вставить("Комментарий", "");
		СвойстваПодписи.Вставить("КомуВыданСертификат", КомуВыданСертификат);
		СвойстваПодписи.Вставить("Сертификат", ДвоичныеДанныеСертификата);
		
		ДанныеДляЗанесенияВБазу.Вставить("СвойстваПодписи", СвойстваПодписи);
		
		МассивДанныхДляЗанесенияВБазу.Добавить(ДанныеДляЗанесенияВБазу);
		
	КонецЦикла;
	
	МассивАдресов = Новый Массив;
	ЗанестиИнформациюОПодписях(МассивДанныхДляЗанесенияВБазу);
	
КонецПроцедуры

Процедура ОбновитьФайлПередЗанесениемПодписи(ПодписанныйОбъект, СвойстваПодписи)
	
	ДанныеФайла = РаботаСФайламиСлужебныйВызовСервера.ДанныеФайла(ПодписанныйОбъект);
	
	АдресНовыхДанных = ПоместитьВоВременноеХранилище(СвойстваПодписи.ИзмененныеДанные);
	
	СведенияОФайле = РаботаСФайламиКлиентСервер.СведенияОФайле("ФайлСВерсией");
	СведенияОФайле.АдресВременногоХранилищаФайла = АдресНовыхДанных;
	СведенияОФайле.ИмяБезРасширения = ДанныеФайла.ПолноеНаименованиеВерсии;
	СведенияОФайле.РасширениеБезТочки = ДанныеФайла.Расширение;
	СведенияОФайле.ВремяИзменения = ТекущаяДатаСеанса();
	СведенияОФайле.ВремяИзмененияУниверсальное = ТекущаяУниверсальнаяДата();
	СведенияОФайле.Комментарий = СвойстваПодписи.ПричинаИзмененияДанных;
	
	НоваяВерсия = РаботаСФайламиСлужебныйВызовСервера.ОбновитьВерсиюФайла(ПодписанныйОбъект, СведенияОФайле);
	РаботаСФайламиСлужебныйВызовСервера.ОбновитьВерсиюВФайле(ПодписанныйОбъект, НоваяВерсия, );
	
КонецПроцедуры

#Область ФормированиеНабораКлючевыхРеквизитов

Функция ПараметрыФормированияНабораВажныхПолей(Версия, ДополнительныеПараметры)
	
	ПараметрыФормирования = Новый Структура;
	ПараметрыФормирования.Вставить("Версия", ?(ЗначениеЗаполнено(Версия), Версия, АктуальнаяВерсияПодписи()));
	ПараметрыФормирования.Вставить("ИсключаемыеРеквизиты", Новый Соответствие);
	ПараметрыФормирования.Вставить("ДанныеПодписанта", Неопределено);
	ПараметрыФормирования.Вставить("ОсобыеДанныеПодписания", Новый Соответствие);
	ПараметрыФормирования.Вставить("ЗаполнятьПодписантаВФайлах", Ложь);
	ПараметрыФормирования.Вставить("ОтпечатокСертификата", Неопределено);
	ПараметрыФормирования.Вставить("РеквизитОрганизацияНеЗаполнен", Ложь);
	ПараметрыФормирования.Вставить("ПодписалПользователь", Ложь);
	ПараметрыФормирования.Вставить("РасшифрованныеДанные", Новый Соответствие);
	ПараметрыФормирования.Вставить("ДоверенностиПриПодписании", Новый Массив);
	
	Если ТипЗнч(ДополнительныеПараметры) <> Тип("Структура") Тогда
		Возврат ПараметрыФормирования;
	КонецЕсли;
	
	ПараметрыФормирования.ИсключаемыеРеквизиты = ИсключаемыеРеквизитыНабораВажныхПолей(ДополнительныеПараметры);
	ПараметрыФормирования.ДанныеПодписанта = ДанныеПодписантаДляНабораВажныхПолей(ДополнительныеПараметры);
	ПараметрыФормирования.ОсобыеДанныеПодписания = ОсобыеДанныеПодписанияДляНабораВажныхПолей(ДополнительныеПараметры);
	
	Если ДополнительныеПараметры.Свойство("ЗаполнятьПодписантаВФайле")
		И ДополнительныеПараметры.ЗаполнятьПодписантаВФайле = Истина Тогда
		
		ПараметрыФормирования.ЗаполнятьПодписантаВФайлах = Истина;
		ПараметрыФормирования.ОтпечатокСертификата = ДополнительныеПараметры.Отпечаток;
	КонецЕсли;
	
	Если ДополнительныеПараметры.Свойство("РеквизитОрганизацияНеЗаполнен")
		И ДополнительныеПараметры.РеквизитОрганизацияНеЗаполнен = Истина Тогда
		
		ПараметрыФормирования.РеквизитОрганизацияНеЗаполнен = Истина;
	КонецЕсли;
	
	Если ДополнительныеПараметры.Свойство("ПодписалПользователь")
		И ДополнительныеПараметры.ПодписалПользователь = Истина Тогда
		
		ПараметрыФормирования.ПодписалПользователь = Истина;
	КонецЕсли;
	
	Если ДополнительныеПараметры.Свойство("РасшифрованныеДанные")
		И ТипЗнч(ДополнительныеПараметры.РасшифрованныеДанные) = Тип("Соответствие") Тогда
		
		ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(
			ПараметрыФормирования.РасшифрованныеДанные,
			ДополнительныеПараметры.РасшифрованныеДанные);
	КонецЕсли;
	
	Если ДополнительныеПараметры.Свойство("ДоверенностиПриПодписании")
		И ТипЗнч(ДополнительныеПараметры.ДоверенностиПриПодписании) = Тип("Массив") Тогда
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
			ПараметрыФормирования.ДоверенностиПриПодписании,
			ДополнительныеПараметры.ДоверенностиПриПодписании);
	КонецЕсли;
	
	Возврат ПараметрыФормирования;
	
КонецФункции

Функция ИсключаемыеРеквизитыНабораВажныхПолей(ДополнительныеПараметры)
	
	ИсключаемыеРеквизиты = Новый Соответствие;
	
	Если ДополнительныеПараметры.Свойство("ИсключаемыеКлючевыеРеквизиты")
		И ТипЗнч(ДополнительныеПараметры.ИсключаемыеКлючевыеРеквизиты) = Тип("Массив") Тогда
		
		Для Каждого ИмяРеквизита Из ДополнительныеПараметры.ИсключаемыеКлючевыеРеквизиты Цикл
			ИсключаемыеРеквизиты.Вставить(ИмяРеквизита, Истина);
		КонецЦикла;
	
	КонецЕсли;
	
	Возврат ИсключаемыеРеквизиты;
	
КонецФункции

Функция ДанныеПодписантаДляНабораВажныхПолей(ДополнительныеПараметры)
	
	Если ДополнительныеПараметры.Свойство("ДанныеПодписанта") Тогда
		
		Возврат ДополнительныеПараметры.ДанныеПодписанта;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция ОсобыеДанныеПодписанияДляНабораВажныхПолей(ДополнительныеПараметры)
	
	ДанныеПодписания = Новый Соответствие;
	
	Если ДополнительныеПараметры.Свойство("ОсобыеДанныеПодписания")
		И ТипЗнч(ДополнительныеПараметры.ОсобыеДанныеПодписания) = Тип("Массив") Тогда
		
		ОсобыеДанныеПодписания = ДополнительныеПараметры.ОсобыеДанныеПодписания;
		
		Для Каждого Элемент Из ОсобыеДанныеПодписания Цикл
			ДанныеСтроки = Новый Структура("Подписал, Подписан, ДатаПодписи");
			ЗаполнитьЗначенияСвойств(ДанныеСтроки, Элемент);
			ДанныеПодписания.Вставить(Элемент.НомерСтроки, ДанныеСтроки);
		КонецЦикла;
	КонецЕсли;
	
	Возврат ДанныеПодписания;
	
КонецФункции

Процедура ДобавитьЗаписьПоляОбъектаВНаборВажныхПолей(Объект, ИмяПоля, МассивПолей, ПараметрыФормирования)
	
	Если ИмяПоля = "Файлы" Тогда
		ДобавитьПоляФайловВНаборВажныхПолей(Объект, МассивПолей, ПараметрыФормирования);
		Возврат;
	КонецЕсли;
	
	Если ИмяПоля = "Организация"
		И ПараметрыФормирования.РеквизитОрганизацияНеЗаполнен Тогда
		
		ДобавитьПустуюОрганизациюВНаборВажныхПолей(МассивПолей);
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Объект) = Тип("Структура") Тогда
		ЗначениеПоля = Объект[ИмяПоля];
	Иначе
		ЗначениеПоля = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект, ИмяПоля);
	КонецЕсли;
	
	СтрокаЗначения = ПолучитьДанныеОбъектаСтрокой(
		ЗначениеПоля, ИмяПоля);
	МассивПолей.Добавить(СтрокаЗначения);
	
КонецПроцедуры

Процедура ДобавитьПоляФайловВНаборВажныхПолей(Объект, МассивПолей, ПараметрыФормирования)
	
	МассивФайлов = РаботаСФайламиВызовСервера.ПолучитьВсеПодчиненныеФайлы(Объект, Ложь);
	
	МинимальнаяВерсияХешированияФайлов = 3;
	
	НомерЯчейки = 1;
	Для Каждого ФайлСсылка Из МассивФайлов Цикл
		
		Если ПараметрыФормирования.Версия >= МинимальнаяВерсияХешированияФайлов Тогда
			ХешДанных = Новый ХешированиеДанных(ХешФункция.CRC32);
			
			Если ПараметрыФормирования.ЗаполнятьПодписантаВФайлах Тогда
				ДанныеФайлаСПодписантом =
					ОбменСКонтрагентамиДОВызовСервера.ПолучитьДанныеФайлаСУчетомПодписания(
						ФайлСсылка,
						ПараметрыФормирования.ОтпечатокСертификата,
						ПараметрыФормирования.ДоверенностиПриПодписании);
				
				ДвоичныеДанные = ДанныеФайлаСПодписантом.ДвоичныеДанные;
			Иначе
				
				СведенияФайла = РаботаСФайламиВызовСервера.ДанныеФайлаИДвоичныеДанные(ФайлСсылка);
				ТекущаяВерсия = СведенияФайла.ДанныеФайла.ТекущаяВерсия;
				РасшифрованныеДанные = ПараметрыФормирования.РасшифрованныеДанные;
				
				Если РасшифрованныеДанные[ФайлСсылка] <> Неопределено Тогда
					ДвоичныеДанные = ПолучитьИзВременногоХранилища(РасшифрованныеДанные[ФайлСсылка]);
				ИначеЕсли РасшифрованныеДанные[ТекущаяВерсия] <> Неопределено Тогда
					ДвоичныеДанные = ПолучитьИзВременногоХранилища(РасшифрованныеДанные[ТекущаяВерсия]);
				Иначе
					ДвоичныеДанные = СведенияФайла.ДвоичныеДанные;
				КонецЕсли;
				
			КонецЕсли;
			
			ХешДанных.Добавить(ДвоичныеДанные);
			ЗначениеПоля = ХешДанных.ХешСумма;
		Иначе
			ЗначениеПоля = ФайлСсылка;
		КонецЕсли;
		
		ИмяЯчейки = "Файл" + НомерЯчейки;
		СтрокаЗначения = ПолучитьДанныеОбъектаСтрокой(ЗначениеПоля, ИмяЯчейки);
		МассивПолей.Добавить(СтрокаЗначения);
		НомерЯчейки = НомерЯчейки + 1;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьПустуюОрганизациюВНаборВажныхПолей(МассивПолей)
	
	ПустаяОрганизация = Справочники.Организации.ПустаяСсылка();
	СтрокаЗначения = ПолучитьДанныеОбъектаСтрокой(ПустаяОрганизация, "Организация");
	МассивПолей.Добавить(СтрокаЗначения);
	
КонецПроцедуры

Функция ЗначениеПоляТаблицыДляНабораВажныхПолей(СтрокаТаблицы, ИмяТаблицы, ИмяКолонки,
		НомерСтроки, ПараметрыФормирования)
	
	Если ИмяТаблицы = "Стороны" Тогда
		
		// Если это первая строка -- это наша организация, для нее могут быть заданы особые параметры подписанта
		Если НомерСтроки = 0 Тогда
			
			ЗначениеДанныхПодписанта = ЗначениеИзДанныхПодписанта(ИмяКолонки, ПараметрыФормирования);
			Если ЗначениеДанныхПодписанта <> Неопределено Тогда
				Возврат ЗначениеДанныхПодписанта;
			КонецЕсли;
			
		КонецЕсли;
		
		ЗначениеИзОсобыхДанныхПодписания = 
			ЗначениеИзОсобыхДанныхПодписания(ИмяКолонки, НомерСтроки, ПараметрыФормирования);
		Если ЗначениеИзОсобыхДанныхПодписания <> Неопределено Тогда
			Возврат ЗначениеИзОсобыхДанныхПодписания;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СтрокаТаблицы[ИмяКолонки];
	
КонецФункции

Функция ЗначениеИзДанныхПодписанта(ИмяПоля, ПараметрыФормирования)
	
	Если ПараметрыФормирования.ДанныеПодписанта = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДанныеПодписанта = ПараметрыФормирования.ДанныеПодписанта;
	
	Если ИмяПоля = "Подписал" Тогда
		Возврат ДанныеПодписанта.Подписал;
	ИначеЕсли ИмяПоля = "ДатаПодписи" Тогда
		Возврат НачалоДня(ДанныеПодписанта.ДатаПодписи);
	ИначеЕсли ИмяПоля = "Подписан" Тогда
		Возврат ДанныеПодписанта.Подписан;
	Иначе
		// Неизвестное имя поля, которого нет в данных подписанта. Делать ничего не надо.
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция ЗначениеИзОсобыхДанныхПодписания(ИмяПоля, НомерСтроки, ПараметрыФормирования)
	
	ДанныеПодписанияСтроки = ПараметрыФормирования.ОсобыеДанныеПодписания.Получить(НомерСтроки);
	
	Если ДанныеПодписанияСтроки <> Неопределено Тогда
		Если ИмяПоля = "Подписал" Тогда
			Возврат ДанныеПодписанияСтроки.Подписал;
		ИначеЕсли ИмяПоля = "Подписан" Тогда
			Возврат ДанныеПодписанияСтроки.Подписан;
		ИначеЕсли ИмяПоля = "ДатаПодписи" Тогда
			Возврат ДанныеПодписанияСтроки.ДатаПодписи;
		Иначе
			// Неизвестное имя поля. Не делаем ничего.
		КонецЕсли;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#Область РаботаСМЧД

Функция НовыеДанныеПодписиСУчетомДоверенности()
	
	СвойстваПодписи = Новый Структура;
	СвойстваПодписи.Вставить("УникальныйИдентификатор", УникальныйИдентификаторПустой());
	СвойстваПодписи.Вставить("ДатаПодписи", Дата(1, 1, 1));
	СвойстваПодписи.Вставить("Объект", Неопределено);
	СвойстваПодписи.Вставить("УстановившийПодпись", Справочники.Пользователи.ПустаяСсылка());
	СвойстваПодписи.Вставить("Версия", "");
	СвойстваПодписи.Вставить("ДатаПроверкиПодписи", Дата(1, 1, 1));
	СвойстваПодписи.Вставить("СрокДействияПоследнейМеткиВремени", Дата(1, 1, 1));
	СвойстваПодписи.Вставить("ИмяФайлаПодписи", "");
	СвойстваПодписи.Вставить("Комментарий", "");
	СвойстваПодписи.Вставить("КомуВыданСертификат", "");
	СвойстваПодписи.Вставить("Отпечаток", "");
	СвойстваПодписи.Вставить("АдресПодписи", Неопределено);
	СвойстваПодписи.Вставить("ПодписьВерна", Ложь);
	СвойстваПодписи.Вставить("АдресСертификата", Неопределено);
	СвойстваПодписи.Вставить("СертификатДействителен", Ложь);
	СвойстваПодписи.Вставить("ТекстОшибкиПроверкиПодписи", "");
	СвойстваПодписи.Вставить("ТекстОшибкиПроверкиСертификата", "");
	СвойстваПодписи.Вставить("СтатусПроверкиЭП", 0);
	
	СвойстваПодписи.Вставить("АвторПодписи", "");
	СвойстваПодписи.Вставить("Статус", "");
	
	СвойстваПодписи.Вставить("Доверенность", Неопределено);
	СвойстваПодписи.Вставить("ДатаПроверки", Дата(1, 1, 1));
	СвойстваПодписи.Вставить("ДоверенностьВерна", Ложь);
	СвойстваПодписи.Вставить("ПротоколПроверки", Неопределено);
	
	Возврат СвойстваПодписи;
	
КонецФункции

Функция НовыеДанныеДоверенности()
	
	ДанныеДоверенности = Новый Структура;
	ДанныеДоверенности.Вставить("Доверенность", Неопределено);
	ДанныеДоверенности.Вставить("ДатаПроверкиДоверенности", Дата(1, 1, 1));
	ДанныеДоверенности.Вставить("ДоверенностьВерна", Ложь);
	ДанныеДоверенности.Вставить("ПротоколПроверкиДоверенности", Неопределено);
	
	Возврат ДанныеДоверенности;
	
КонецФункции

Процедура ЗаполнитьСтатусыПодписейИДоверенностей(ДанныеПодписей)
	
	Доверенности = Новый Массив;
	Для Каждого ДанныеПодписи Из ДанныеПодписей Цикл
		Если ЗначениеЗаполнено(ДанныеПодписи.Доверенность) Тогда
			Доверенности.Добавить(ДанныеПодписи.Доверенность);
		КонецЕсли;
	КонецЦикла;
	
	ДоверителиПоДоверенностям = ДоверителиДоверенностей(Доверенности);
	
	Для Каждого ДанныеПодписи Из ДанныеПодписей Цикл
		
		ДанныеДляСтатуса = РаботаСЭПКлиентСервер.НовыеДанныеДляПолученияСтатусаПодписи();
		ДанныеДляСтатуса.ПодписьВерна = ДанныеПодписи.ПодписьВерна;
		ДанныеДляСтатуса.СертификатДействителен = ДанныеПодписи.СертификатДействителен;
		ДанныеДляСтатуса.ТекстОшибкиПроверкиПодписи = ДанныеПодписи.ТекстОшибкиПроверкиПодписи;
		ДанныеДляСтатуса.ТекстОшибкиПроверкиСертификата = ДанныеПодписи.ТекстОшибкиПроверкиСертификата;
		
		Если Не ЗначениеЗаполнено(ДанныеПодписи.Доверенность) Тогда
			ДанныеДляСтатуса.ПодписьПоДоверенности = Ложь;
		Иначе
			ДанныеДляСтатуса.ПодписьПоДоверенности = Истина;
			ДанныеДляСтатуса.ДоверенностьДействительна = ДанныеПодписи.ДоверенностьВерна;
		КонецЕсли;
		
		ДанныеДляСтатуса.ДатаПроверки = ДанныеПодписи.ДатаПроверкиПодписи;
		
		Если ЗначениеЗаполнено(ДанныеПодписи.АдресСертификата) Тогда
			ДвоичныеДанныеСертификата = ПолучитьИзВременногоХранилища(ДанныеПодписи.АдресСертификата);
			
			ДанныеДляСтатуса.СрокПроверкиСертификата = СрокПроверкиДействияСертификата(
				ДвоичныеДанныеСертификата, ДанныеПодписи.СрокДействияПоследнейМеткиВремени);
		Иначе
			ДанныеДляСтатуса.СрокПроверкиСертификата = ТекущаяДатаСеанса();
		КонецЕсли;
		
		ДанныеПодписи.Статус = РаботаСЭПКлиентСервер.ОбщийСтатусПроверкиПодписи(ДанныеДляСтатуса);
		Если ЗначениеЗаполнено(ДанныеПодписи.Доверенность) Тогда
			ДанныеПодписи.АвторПодписи = СтрШаблон(
				НСтр("ru = '%1, от имени %2 по доверенности %3'"),
				ДанныеПодписи.КомуВыданСертификат,
				ДоверителиПоДоверенностям[ДанныеПодписи.Доверенность],
				ДанныеПодписи.Доверенность);
		Иначе
			ДанныеПодписи.АвторПодписи = ДанныеПодписи.КомуВыданСертификат;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ДоверителиДоверенностей(Доверенности)
	
	ДоверенностиОрганизаций = Новый Массив;
	ДоверенностиКонтрагентов = Новый Массив;
	
	Для Каждого Доверенность Из Доверенности Цикл
		Если ТипЗнч(Доверенность) = Тип("СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций") Тогда
			ДоверенностиОрганизаций.Добавить(Доверенность);
		ИначеЕсли ТипЗнч(Доверенность) = Тип("СправочникСсылка.МашиночитаемыеДоверенностиКонтрагентов") Тогда
			ДоверенностиКонтрагентов.Добавить(Доверенность);
		КонецЕсли;
	КонецЦикла;
	
	РеквизитыДоверенностейОрганизаций =
		ОбщегоНазначения.ЗначенияРеквизитовОбъектов(ДоверенностиОрганизаций, "Организация");
	РеквизитыДоверенностейКонтрагентов =
		ОбщегоНазначения.ЗначенияРеквизитовОбъектов(ДоверенностиКонтрагентов, "Доверитель");
	
	ДоверителиПоДоверенностям = Новый Соответствие;
	
	Для Каждого Элемент Из РеквизитыДоверенностейОрганизаций Цикл
		
		Доверенность = Элемент.Ключ;
		Реквизиты = Элемент.Значение;
		
		ДоверителиПоДоверенностям.Вставить(Доверенность, Реквизиты.Организация);
		
	КонецЦикла;
	
	Для Каждого Элемент Из РеквизитыДоверенностейКонтрагентов Цикл
		
		Доверенность = Элемент.Ключ;
		Реквизиты = Элемент.Значение;
		
		ДоверителиПоДоверенностям.Вставить(Доверенность, Реквизиты.Доверитель);
		
	КонецЦикла;
	
	Возврат ДоверителиПоДоверенностям;
	
КонецФункции

Функция ДанныеДоверенностейВДанныхДляПроверкиДоверенностей(ДанныеДляПроверки)
	
	ДоверенностиОрганизаций = Новый Массив;
	ДоверенностиКонтрагентов = Новый Массив;
	
	Для Каждого ЭлементДанных Из ДанныеДляПроверки Цикл
		Доверенность = ЭлементДанных.Доверенность;
		Если Не ЗначениеЗаполнено(Доверенность) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТипЗнч(Доверенность) = Тип("СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций") Тогда
			ДоверенностиОрганизаций.Добавить(Доверенность);
		ИначеЕсли ТипЗнч(Доверенность) = Тип("СправочникСсылка.МашиночитаемыеДоверенностиКонтрагентов") Тогда
			ДоверенностиКонтрагентов.Добавить(Доверенность);
		КонецЕсли;
	КонецЦикла;
	
	ДанныеДоверенностейОрганизаций = ДанныеДоверенностейОрганизацийДляПроверки(ДоверенностиОрганизаций);
	ДанныеДоверенностейКонтрагентов = ДанныеДоверенностейКонтрагентовДляПроверки(ДоверенностиКонтрагентов);
	
	ДанныеДоверенностей = Новый Соответствие;
	ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(ДанныеДоверенностей, ДанныеДоверенностейОрганизаций);
	ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(ДанныеДоверенностей, ДанныеДоверенностейКонтрагентов);
	
	Возврат ДанныеДоверенностей;
	
КонецФункции

Функция ДанныеДоверенностейОрганизацийДляПроверки(Доверенности)
	
	ДанныеДоверенностей = Новый Соответствие;
	
	РеквизитыДоверенностей = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(
		Доверенности, "НомерДоверенности, ДоверительЮЛ_ИНН");
	
	Для Каждого Доверенность Из Доверенности Цикл
		
		ДанныеДоверенности = НовыеДанныеДоверенностиДляПроверки();
		
		РеквизитыДоверенности = РеквизитыДоверенностей[Доверенность];
		Если РеквизитыДоверенностей = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ИННДоверителя = РеквизитыДоверенности.ДоверительЮЛ_ИНН;
		НомерДоверенности = РеквизитыДоверенности.НомерДоверенности;
		
		СведенияМЧД = Справочники.МашиночитаемыеДоверенностиОрганизаций.СведенияМЧД(Доверенность);
		
		ДанныеДоверенности.ИННДоверителя = ИННДоверителя;
		ДанныеДоверенности.НомерДоверенности = НомерДоверенности;
		ДанныеДоверенности.Сведения = СведенияМЧД;
		
		ДанныеДоверенностей.Вставить(Доверенность, ДанныеДоверенности);
		
	КонецЦикла;
	
	Возврат ДанныеДоверенностей;
	
КонецФункции

Функция ДанныеДоверенностейКонтрагентовДляПроверки(Доверенности)
	
	ДанныеДоверенностей = Новый Соответствие;
	
	РеквизитыДоверенностей = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(
		Доверенности, "НомерДоверенности, ДоверительИНН");
	
	Для Каждого Доверенность Из Доверенности Цикл
		
		ДанныеДоверенности = НовыеДанныеДоверенностиДляПроверки();
		
		РеквизитыДоверенности = РеквизитыДоверенностей[Доверенность];
		Если РеквизитыДоверенностей = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ИННДоверителя = РеквизитыДоверенности.ДоверительИНН;
		НомерДоверенности = РеквизитыДоверенности.НомерДоверенности;
		
		СведенияМЧД = Справочники.МашиночитаемыеДоверенностиКонтрагентов.СведенияМЧД(Доверенность);
		
		ДанныеДоверенности.ИННДоверителя = ИННДоверителя;
		ДанныеДоверенности.НомерДоверенности = НомерДоверенности;
		ДанныеДоверенности.Сведения = СведенияМЧД;
		
		ДанныеДоверенностей.Вставить(Доверенность, ДанныеДоверенности);
		
	КонецЦикла;
	
	Возврат ДанныеДоверенностей;
	
КонецФункции

Функция НовыеДанныеДоверенностиДляПроверки()
	
	ДанныеДоверенности = Новый Структура;
	ДанныеДоверенности.Вставить("ИННДоверителя", "");
	ДанныеДоверенности.Вставить("НомерДоверенности", "");
	ДанныеДоверенности.Вставить("Сведения", Новый Структура);
	
	Возврат ДанныеДоверенности;
	
КонецФункции

Функция ПараметрыПроверкиДоверенностиЭП(ДанныеПроверки, ДанныеДоверенности)
	
	СвойстваПодписи = Новый Структура;
	СвойстваПодписи.Вставить("ДатаПроверкиПодписи", Дата(1, 1, 1));
	СвойстваПодписи.Вставить("ПодписьВерна", Ложь);
	СвойстваПодписи.Вставить("ДатаПодписи", Дата(1, 1, 1));
	СвойстваПодписи.Вставить("Сертификат", Неопределено);
	СвойстваПодписи.Вставить("Комментарий", "");
	
	ЗаполнитьЗначенияСвойств(СвойстваПодписи, ДанныеПроверки.ДанныеПодписи);
	
	Если Не ЗначениеЗаполнено(СвойстваПодписи.Сертификат) Тогда
		СвойстваПодписи.Сертификат = ПолучитьИзВременногоХранилища(ДанныеПроверки.ДанныеПодписи.АдресСертификата);
	КонецЕсли;
	
	ПараметрыПроверки = МашиночитаемыеДоверенности.НовыеПараметрыПроверкиПодписи();
	ПараметрыПроверки.СвойстваПодписи = СвойстваПодписи;
	ПараметрыПроверки.ИННДоверителя = ДанныеДоверенности.ИННДоверителя;
	ПараметрыПроверки.СведенияМЧД = ДанныеДоверенности.Сведения;
	
	Возврат ПараметрыПроверки;
	
КонецФункции

Функция НовыеДанныеДоверенностиДляВыгрузки()
	
	ДанныеДоверенности = Новый Структура;
	ДанныеДоверенности.Вставить("Выгружена", Ложь);
	ДанныеДоверенности.Вставить("ОшибкаВыгрузки", "");
	ДанныеДоверенности.Вставить("ИмяФайла", "");
	ДанныеДоверенности.Вставить("АдресДанных", "");
	
	Возврат ДанныеДоверенности;
	
КонецФункции

Функция РезультатВыгрузкиНеподписаннойДоверенности(РезультатВыгрузки)
	
	РезультатВыгрузки.Ошибка = Истина;
	РезультатВыгрузки.ТекстОшибки = НСтр("ru = 'Выгружать в файл можно только подписанные доверенности. Подпишите и повторите выгрузку.'");
	Возврат РезультатВыгрузки;
	
КонецФункции

Функция ИмяФайлаМЧДКонтрагента(Ссылка)
	
	Реквизиты = "ДатаВыдачи, НомерДоверенности";
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, Реквизиты);

	ЭлементыИмениФайла = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФорматыЭДО_ФНС.ПространствоИмен_МЧД());
	ДатаФайла = ?(ЗначениеЗаполнено(ЗначенияРеквизитов.ДатаВыдачи), ЗначенияРеквизитов.ДатаВыдачи,
		ТекущаяДатаСеанса());
	ЭлементыИмениФайла.Добавить(Формат(ДатаФайла, "ДФ=ггггММдд;"));
	ЭлементыИмениФайла.Добавить(ЗначенияРеквизитов.НомерДоверенности);

	Возврат СтрСоединить(ЭлементыИмениФайла, "_");
	
КонецФункции

#КонецОбласти

#Область ПроверкаПодписей

#Область ЗаполнениеДанныхДляПроверки

Функция ВерсииПодписейОбъекта(ДанныеПодписейОбъекта)
	
	ВерсииПодписей = Новый Массив;
	
	Для Каждого Элемент Из ДанныеПодписейОбъекта Цикл
		ДанныеПодписи = Элемент.Значение;
		ВерсииПодписей.Добавить(ДанныеПодписи.Версия);
	КонецЦикла;
	
	ВерсииПодписей = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ВерсииПодписей);
	
	Возврат ВерсииПодписей;
	
КонецФункции

Функция ДанныеОбъектаДОДляПроверкиПодписей(Объект, Версии, ПараметрыПолучения)
	
	ДанныеОбъекта = РаботаСЭПКлиентСервер.НовыеДанныеОбъектаДляПроверкиПодписи();
	ДанныеОбъекта.Зашифрован = Ложь;
	ДанныеОбъекта.РазныеВерсииПодписей = Истина;
	
	Для Каждого Версия Из Версии Цикл
		
		ДвоичныеДанныеВерсии = ДанныеОбъекта.ДвоичныеДанныеПоВерсиям[Версия];
		Если ДвоичныеДанныеВерсии = Неопределено Тогда
			ДвоичныеДанныеВерсии = Новый Массив;
			ДанныеОбъекта.ДвоичныеДанныеПоВерсиям.Вставить(Версия, ДвоичныеДанныеВерсии);
		КонецЕсли;
		
		ВариантыДополнительныхПроверок = ВариантыДополнительныхПроверокПодписи(Версия, Объект);
		
		Для Каждого ВариантПроверки Из ВариантыДополнительныхПроверок Цикл
			ПараметрыПолученияДанных = Новый Структура;
			ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ПараметрыПолученияДанных, ВариантПроверки);
			ПараметрыПолученияДанных.Вставить("РасшифрованныеДанные", ПараметрыПолучения.РасшифрованныеДанные);
			
			ДвоичныеДанные = ПолучитьДвоичныеДанныеОбъекта(Объект, Версия, ПараметрыПолученияДанных);
			АдресДанных = ПоместитьВоВременноеХранилище(
				ДвоичныеДанные, ПараметрыПолучения.УникальныйИдентификаторФормы);
			ДвоичныеДанныеВерсии.Добавить(АдресДанных);
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ДанныеОбъекта;
	
КонецФункции

Функция ДанныеФайлаДляПроверкиПодписей(Объект, ПараметрыПолучения)
	
	ДанныеОбъекта = РаботаСЭПКлиентСервер.НовыеДанныеОбъектаДляПроверкиПодписи();
	
	РасшифрованныеДанные = ПараметрыПолучения.РасшифрованныеДанные[Объект];
	Если РасшифрованныеДанные <> Неопределено Тогда
		ДанныеОбъекта.АдресДанных = РасшифрованныеДанные;
		Возврат ДанныеОбъекта;
	КонецЕсли;
	
	Если ТипЗнч(Объект) = Тип("СправочникСсылка.Файлы") Тогда
		СведенияФайла = РаботаСФайламиВызовСервера.ДанныеФайлаИДвоичныеДанные(Объект);
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.ВерсииФайлов") Тогда
		СведенияФайла = РаботаСФайламиВызовСервера.ДанныеФайлаИДвоичныеДанные(,Объект);
	КонецЕсли;
	
	ДанныеОбъекта.Зашифрован = СведенияФайла.ДанныеФайла.Зашифрован;
	
	Если ТипЗнч(Объект) = Тип("СправочникСсылка.Файлы") Тогда
		ДанныеОбъекта.ОбъектШифрования = Объект;
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.ВерсииФайлов") Тогда
		ДанныеОбъекта.ОбъектШифрования = СведенияФайла.ДанныеФайла.Ссылка;
	КонецЕсли;
	
	ДанныеОбъекта.АдресДанных = ПоместитьВоВременноеХранилище(
		СведенияФайла.ДвоичныеДанные, ПараметрыПолучения.УникальныйИдентификаторФормы);
	
	Возврат ДанныеОбъекта;
	
КонецФункции

#КонецОбласти

#Область ДополнительныеВариантыПроверок

Функция ВариантыДополнительныхПроверокПодписи(Версия, Объект)
	
	ВозможныеВариантыПроверок = Новый Структура;
	ИменаПараметров = Новый Массив;
	
	ЗаполнитьВозможныеВариантыПроверок(Объект, Версия, ВозможныеВариантыПроверок, ИменаПараметров);
	
	Если ИменаПараметров.Количество() = 0 Тогда
		ВариантыДополнительныхПроверок = Новый Массив;
		ВариантыДополнительныхПроверок.Добавить(Новый Структура);
		Возврат ВариантыДополнительныхПроверок;
	КонецЕсли;
	
	НомераЗначенийПараметров = Новый Структура;
	Для Каждого Элемент Из ВозможныеВариантыПроверок Цикл
		НомераЗначенийПараметров.Вставить(Элемент.Ключ, 0);
	КонецЦикла;
	
	НомераЗначенийПараметров[ИменаПараметров[0]] = -1;
	
	ВариантыДополнительныхПроверок = Новый Массив;
	
	ВсеВариантыПеребраны = Ложь;
	
	Пока Не ВсеВариантыПеребраны Цикл
		
		НомерПараметра = 0;
		
		Пока НомерПараметра < НомераЗначенийПараметров.Количество() Цикл
			
			ИмяПараметра = ИменаПараметров[НомерПараметра];
			КоличествоЗначенийПараметра = ВозможныеВариантыПроверок[ИмяПараметра].Количество();
			
			Если НомераЗначенийПараметров[ИмяПараметра] < КоличествоЗначенийПараметра - 1 Тогда
				
				НомераЗначенийПараметров[ИмяПараметра] =
					НомераЗначенийПараметров[ИмяПараметра] + 1;
				
				Для Инд = 0 По НомерПараметра - 1 Цикл
					НомераЗначенийПараметров[ИменаПараметров[Инд]] = 0;
				КонецЦикла;
				
				Прервать;
				
			Иначе
				НомерПараметра = НомерПараметра + 1;
			КонецЕсли;
			
		КонецЦикла;
		
		Если НомерПараметра >= НомераЗначенийПараметров.Количество() Тогда
			ВсеВариантыПеребраны = Истина;
		Иначе
			
			ВариантПроверки = Новый Структура;
			
			Для Каждого Элемент Из ВозможныеВариантыПроверок Цикл
				
				ВариантПроверки.Вставить(
					Элемент.Ключ,
					Элемент.Значение[НомераЗначенийПараметров[Элемент.Ключ]]);
			
			КонецЦикла;
			
			ВариантыДополнительныхПроверок.Добавить(ВариантПроверки);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ВариантыДополнительныхПроверок;
	
КонецФункции

Процедура ЗаполнитьВозможныеВариантыПроверок(Объект, Версия, ВозможныеВариантыПроверок, ИменаПараметров)
	
	// Повторная проверка подписи для случаев, когда подпись была
	// создана до включения учета по организациям.
	Если ДелопроизводствоКлиентСервер.ЭтоДокумент(Объект) И Версия > 1 Тогда
		ДобавитьПроверкиУчетаПоОрганизациям(Объект, ВозможныеВариантыПроверок, ИменаПараметров);
	КонецЕсли;
	
	// Повторная проверка документов предприятия для версии подписи 3.
	Если ДелопроизводствоКлиентСервер.ЭтоВнутреннийДокумент(Объект) И Версия = 3 Тогда
		ДобавитьПроверкиПоСтарымВерсиямПодписей(Объект, ВозможныеВариантыПроверок, ИменаПараметров);
	КонецЕсли;
	
	// Повторная проверка 5-й версии подписи для внутренних документов, отправленных по ЭДО.
	Если ДелопроизводствоКлиентСервер.ЭтоВнутреннийДокумент(Объект)
		И Версия = 5 Тогда
		
		ДобавитьПроверкиДляИсходящихЭДО(Объект, ВозможныеВариантыПроверок, ИменаПараметров);
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьПроверкиУчетаПоОрганизациям(Объект, ВозможныеВариантыПроверок, ИменаПараметров)
	
	ВозможныеЗначенияПараметра = Новый Массив;
	
	ВозможныеЗначенияПараметра.Добавить(Ложь);
	ВозможныеЗначенияПараметра.Добавить(Истина);
	
	ИмяПараметра = "РеквизитОрганизацияНеЗаполнен";
	
	ВозможныеВариантыПроверок.Вставить(ИмяПараметра, ВозможныеЗначенияПараметра);
	
	ИменаПараметров.Добавить(ИмяПараметра);
	
КонецПроцедуры

Процедура ДобавитьПроверкиПоСтарымВерсиямПодписей(Объект, ВозможныеВариантыПроверок, ИменаПараметров)
	
	ИсключаемыеРеквизиты_2_1_7 = Новый Массив;
	ИсключаемыеРеквизиты_2_1_7.Добавить("Стороны.Подписал");
	ИсключаемыеРеквизиты_2_1_7.Добавить("Стороны.ДатаПодписи");
	ИсключаемыеРеквизиты_2_1_7.Добавить("Стороны.Сторона");
	ИсключаемыеРеквизиты = Новый Массив;
	ИсключаемыеРеквизиты.Добавить("Стороны.Подписал");
	ИсключаемыеРеквизиты.Добавить("Стороны.ДатаПодписи");
	
	ВозможныеЗначенияПараметра = Новый Массив;
	ВозможныеЗначенияПараметра.Добавить(Новый Массив);
	ВозможныеЗначенияПараметра.Добавить(ИсключаемыеРеквизиты);
	ВозможныеЗначенияПараметра.Добавить(ИсключаемыеРеквизиты_2_1_7);
	
	ИмяПараметра = "ИсключаемыеКлючевыеРеквизиты";
	
	ВозможныеВариантыПроверок.Вставить(ИмяПараметра, ВозможныеЗначенияПараметра);
	
	ИменаПараметров.Добавить(ИмяПараметра);
	
КонецПроцедуры

Процедура ДобавитьПроверкиДляИсходящихЭДО(Объект, ВозможныеВариантыПроверок, ИменаПараметров)
	
	ДанныеСостояния = ОбменСКонтрагентамиДОСлужебный.СостояниеДокумента(Объект);
	
	Если Не (ДанныеСостояния.Направление = Перечисления.НаправленияЭДО.Исходящий
		И ДанныеСостояния.Состояние = Перечисления.СостоянияЭДОДокументооборот.ОбменЗавершен) Тогда
		
		Возврат;
	КонецЕсли;
	
	// Если это исходящий документ и обмен завершен, то при подписании в таблице сторон со стороны контрагента
	// не было данных о подписании. Это необходимо указать в доп проверках
	ПустоеКонтактноеЛицо = ПредопределенноеЗначение("Справочник.КонтактныеЛица.ПустаяСсылка");
	
	ВозможныеЗначенияПараметра = Новый Массив;
	ВозможныеЗначенияПараметра.Добавить(Новый Массив);
	
	ДанныеПодписанияКонтрагентаДоОбмена = Новый Структура;
	ДанныеПодписанияКонтрагентаДоОбмена.Вставить("НомерСтроки", 1);
	ДанныеПодписанияКонтрагентаДоОбмена.Вставить("Подписал", ПустоеКонтактноеЛицо);
	ДанныеПодписанияКонтрагентаДоОбмена.Вставить("Подписан", Ложь);
	ДанныеПодписанияКонтрагентаДоОбмена.Вставить("ДатаПодписи", Дата(1, 1, 1));
	ДанныеПодписанияДоОбмена =
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДанныеПодписанияКонтрагентаДоОбмена);
		
	ВозможныеЗначенияПараметра.Добавить(ДанныеПодписанияДоОбмена);
	
	ДанныеПодписанияКонтрагентаДоОбмена = Новый Структура;
	ДанныеПодписанияКонтрагентаДоОбмена.Вставить("НомерСтроки", 1);
	ДанныеПодписанияКонтрагентаДоОбмена.Вставить("Подписал", "");
	ДанныеПодписанияКонтрагентаДоОбмена.Вставить("Подписан", Ложь);
	ДанныеПодписанияКонтрагентаДоОбмена.Вставить("ДатаПодписи", Дата(1, 1, 1));
	ДанныеПодписанияДоОбмена =
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДанныеПодписанияКонтрагентаДоОбмена);
		
	ВозможныеЗначенияПараметра.Добавить(ДанныеПодписанияДоОбмена);
	
	ИмяПараметра = "ОсобыеДанныеПодписания";
	
	ВозможныеВариантыПроверок.Вставить(ИмяПараметра, ВозможныеЗначенияПараметра);
	
	ИменаПараметров.Добавить(ИмяПараметра);
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеДанныхВРегистре

Функция ДанныеПодписиДляЗаписи(ДанныеПодписи)
	
	ДанныеДляЗаписи = Новый Структура;
	ДанныеДляЗаписи.Вставить("УникальныйИдентификатор", УникальныйИдентификаторПустой());
	ДанныеДляЗаписи.Вставить("Объект", Неопределено);
	ДанныеДляЗаписи.Вставить("УстановившийПодпись", Справочники.Пользователи.ПустаяСсылка());
	ДанныеДляЗаписи.Вставить("ДатаПодписи", Дата(1, 1, 1));
	ДанныеДляЗаписи.Вставить("ПодписьВерна", Ложь);
	ДанныеДляЗаписи.Вставить("ТекстОшибкиПроверкиПодписи", "");
	ДанныеДляЗаписи.Вставить("СертификатДействителен", Ложь);
	ДанныеДляЗаписи.Вставить("ТекстОшибкиПроверкиСертификата", "");
	
	ЗаполнитьЗначенияСвойств(ДанныеДляЗаписи, ДанныеПодписи);
	
	Возврат ДанныеДляЗаписи;
	
КонецФункции

Процедура ОбновитьСтатусПроверкиПодписиВРегистре(ДанныеПодписиДляЗаписи, ДатаПроверки = Неопределено)
	
	НаборЗаписей = РегистрыСведений.ЭлектронныеПодписи.СоздатьНаборЗаписей();
	
	НаборЗаписей.Отбор.Объект.Установить(ДанныеПодписиДляЗаписи.Объект);
	НаборЗаписей.Отбор.УникальныйИдентификатор.Установить(ДанныеПодписиДляЗаписи.УникальныйИдентификатор);
	НаборЗаписей.Отбор.УстановившийПодпись.Установить(ДанныеПодписиДляЗаписи.УстановившийПодпись);
	НаборЗаписей.Отбор.ДатаПодписи.Установить(ДанныеПодписиДляЗаписи.ДатаПодписи);
	
	НаборЗаписей.Прочитать();
	
	Если НаборЗаписей.Количество() = 0 Тогда
		СообщениеПользователю = СтрШаблон(
			НСтр("ru = 'Не удалось обновить статус проверки подписи %1 к объекту %2: подпись не найдена'"),
			ДанныеПодписиДляЗаписи.УникальныйИдентификатор,
			ДанныеПодписиДляЗаписи.Объект);
		ВызватьИсключение СообщениеПользователю;
	КонецЕсли;
	
	Подпись = НаборЗаписей[0];
	
	ДатаПроверкиПодписи = ТекущаяДатаСеанса();
	Если ДатаПроверки <> Неопределено Тогда
		ДатаПроверки = ДатаПроверкиПодписи;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Подпись, ДанныеПодписиДляЗаписи);
	Подпись.ДатаПроверкиПодписи = ДатаПроверки;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область ВозвратРезультатаПроверкиПодписей

Функция РезультатПроверокПодписи(ДанныеПроверок)
	
	ДанныеДляПроверки = ДанныеПроверок.ДанныеДляПроверки;
	РезультатПроверкиПодписи = ДанныеПроверок.РезультатПроверкиПодписи;
	
	РезультатПроверок = НовыйРезультатПроверкиПодписи();
	
	РезультатПроверок.ПодписьВерна = РезультатПроверкиПодписи.ПодписьВерна;
	РезультатПроверок.СертификатДействителен = РезультатПроверкиПодписи.СертификатДействителен;
	РезультатПроверок.ДатаПроверкиПодписи = ДанныеПроверок.ДатаПроверки;
	
	Доверенность = ДанныеДляПроверки.Доверенность;
	Если Не ЗначениеЗаполнено(Доверенность) Тогда
		РезультатПроверок.ДоверенностьВерна = Ложь;
	Иначе
		ПротоколПроверки = ДанныеПроверок.РезультатПроверкиДоверенности;
		РезультатПоПротоколу = РаботаСЭПКлиентСервер.РезультатПроверкиДоверенностиПоПротоколу(ПротоколПроверки);
		РезультатПроверок.ДоверенностьВерна = РезультатПоПротоколу.ДоверенностьДействительна;
	КонецЕсли;
	
	РезультатПроверок.Статус = СтатусПроверкиПодписиПоДаннымПроверок(ДанныеПроверок);
	
	Возврат РезультатПроверок;
	
КонецФункции

Функция СтатусПроверкиПодписиПоДаннымПроверок(ДанныеПроверок)
	
	ДанныеДляСтатуса = РаботаСЭПКлиентСервер.НовыеДанныеДляПолученияСтатусаПодписи();
	
	ДанныеДляПроверки = ДанныеПроверок.ДанныеДляПроверки;
	РезультатПроверкиПодписи = ДанныеПроверок.РезультатПроверкиПодписи;
	
	ЗаполнитьЗначенияСвойств(ДанныеДляСтатуса, РезультатПроверкиПодписи);
	
	Доверенность = ДанныеДляПроверки.Доверенность;
	
	Если Не ЗначениеЗаполнено(Доверенность) Тогда
		ДанныеДляСтатуса.ПодписьПоДоверенности = Ложь;
		ДанныеДляСтатуса.ДоверенностьДействительна = Ложь;
		ДанныеДляСтатуса.ПротоколПроверкиДоверенности = Неопределено;
	Иначе
		ДанныеДляСтатуса.ПодписьПоДоверенности = Истина;
		
		ПротоколПроверки = ДанныеПроверок.РезультатПроверкиДоверенности;
		РезультатПоПротоколу = РаботаСЭПКлиентСервер.РезультатПроверкиДоверенностиПоПротоколу(ПротоколПроверки);
		
		ДанныеДляСтатуса.ДоверенностьДействительна = РезультатПоПротоколу.ДоверенностьДействительна;
		ДанныеДляСтатуса.ПротоколПроверкиДоверенности = ПротоколПроверки;
	КонецЕсли;
	
	ДанныеДляСтатуса.ДатаПроверки = ДанныеПроверок.ДатаПроверки;
	Сертификат = ПолучитьИзВременногоХранилища(ДанныеДляПроверки.АдресСертификата);
	ДанныеДляСтатуса.СрокПроверкиСертификата =
		СрокПроверкиДействияСертификата(Сертификат, ДанныеДляПроверки.СрокДействияПоследнейМеткиВремени);
	
	Возврат РаботаСЭПКлиентСервер.ОбщийСтатусПроверкиПодписи(ДанныеДляСтатуса);
	
КонецФункции

Функция НовыйРезультатПроверкиПодписи()
	
	ПустаяДата = Дата(1, 1, 1);
	
	РезультатПроверки = Новый Структура;
	РезультатПроверки.Вставить("ПодписьВерна", Ложь);
	РезультатПроверки.Вставить("СертификатДействителен", Ложь);
	РезультатПроверки.Вставить("ДатаПроверкиПодписи", ПустаяДата);
	РезультатПроверки.Вставить("ДоверенностьВерна", Ложь);
	
	РезультатПроверки.Вставить("Статус", "");
	
	Возврат РезультатПроверки;
	
КонецФункции

#КонецОбласти

#Область ПроверкаЭПНаСервере

Функция ПроверитьДействительностьЭПНаСервере(ДанныеПодписейОбъектов, ДанныеОбъектов)
	
	РезультатыПроверкиПодписей = Новый Соответствие;
	
	Для Каждого ЭлементОбъекта Из ДанныеПодписейОбъектов Цикл
		
		Объект = ЭлементОбъекта.Ключ;
		ДанныеПодписейОбъекта = ЭлементОбъекта.Значение;
		
		Для Каждого ЭлементПодписи Из ДанныеПодписейОбъекта Цикл
			
			ИдентификаторПодписи = ЭлементПодписи.Ключ;
			ДанныеПодписи = ЭлементПодписи.Значение;
			
			РезультатПроверки = ПроверитьПодписьОбъекта(ДанныеОбъектов, Объект, ДанныеПодписи);
			
			РезультатыПроверкиПодписей.Вставить(ИдентификаторПодписи, РезультатПроверки);
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат РезультатыПроверкиПодписей;
	
КонецФункции

Функция ПроверитьПодписьОбъекта(ДанныеОбъектов, Объект, ДанныеПодписи)
	
	РезультатПроверкиПодписи = РаботаСЭПКлиентСервер.НовыйРезультатПроверкиПодписи();
	
	ДанныеОбъектаСУчетомДополнительныхПроверок = Новый Массив;
	
	ДанныеОбъекта = ДанныеОбъектов[Объект];
	Версия = ДанныеПодписи.Версия;
	
	Если Не ДанныеОбъекта.РазныеВерсииПодписей Тогда
		ДанныеОбъектаСУчетомДополнительныхПроверок.Добавить(ДанныеОбъекта.АдресДанных);
	Иначе
		ДанныеОбъектаСУчетомДополнительныхПроверок = ДанныеОбъекта.ДвоичныеДанныеПоВерсиям[Версия];
	КонецЕсли;
	
	Для Каждого Данные Из ДанныеОбъектаСУчетомДополнительныхПроверок Цикл
		
		ОписаниеОшибки = "";
		
		РезультатПроверкиБСП = ЭлектроннаяПодпись.ПроверитьПодпись(, Данные, ДанныеПодписи.АдресПодписи,
			ОписаниеОшибки, ДанныеПодписи.ДатаПодписи);
		
		Если ТипЗнч(РезультатПроверкиБСП) = Тип("Булево") Тогда
			РезультатПроверкиПодписи.ПодписьВерна = РезультатПроверкиБСП;
			РезультатПроверкиПодписи.СертификатДействителен = РезультатПроверкиБСП;
			
			Если Не РезультатПроверкиБСП Тогда
				РезультатПроверкиПодписи.ТекстОшибкиПроверкиПодписи = ОписаниеОшибки;
			КонецЕсли;
		ИначеЕсли ТипЗнч(РезультатПроверкиБСП) = Тип("Структура") Тогда
			ЗаполнитьЗначенияСвойств(РезультатПроверкиПодписи, РезультатПроверкиБСП);
		КонецЕсли;
		
		Если РезультатПроверкиПодписи.ПодписьВерна = Истина Тогда
			Возврат РезультатПроверкиПодписи;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат РезультатПроверкиПодписи;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область ПолучениеСертификатовИзПодписи

Функция ПолучитьДанныеСертификатаИзПодписи(АдресДанныхПодписи, ИдентификаторФормы)
	
	Результат = Новый Структура;
	Результат.Вставить("Успех", Ложь);
	Результат.Вставить("ОписаниеОшибки", "");
	Результат.Вставить("ДанныеСертификата", РаботаСЭПКлиентСервер.НовыйРезультатПолученияДанныхСертификатаИзПодписи());
	
	Если Не ЭлектроннаяПодпись.ПроверятьЭлектронныеПодписиНаСервере() Тогда
		Результат.ОписаниеОшибки = НСтр("ru = 'Некорректный вызов метода: электронные подписи проверяются только на клиенте'");
		Возврат Результат;
	КонецЕсли;
	
	МенеджерКриптографии = Неопределено;
	
	Если Не (ТипЗнч(АдресДанныхПодписи) = Тип("Строка")
		И ЭтоАдресВременногоХранилища(АдресДанныхПодписи)) Тогда
		
		Результат.ОписаниеОшибки = НСтр("ru = 'Некорректный вызов метода: переданный параметр не является адресом данных временного хранилища'");
		Возврат Результат;
	КонецЕсли;
	
	ДанныеПодписи = ПолучитьИзВременногоХранилища(АдресДанныхПодписи);
	Если ТипЗнч(ДанныеПодписи) <> Тип("ДвоичныеДанные") Тогда
		Результат.ОписаниеОшибки = НСтр("ru = 'Не удалось получить данные подписи из хранилища'");
		Возврат Результат;
	КонецЕсли;
	
	Попытка
		МенеджерКриптографии = ЭлектроннаяПодпись.МенеджерКриптографии("ПолучениеСертификатов");
	Исключение
		Результат.ОписаниеОшибки = СтрШаблон(НСтр("ru = 'Не удалось получить менеджер криптографии по причине: %1'"),
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	Если ТипЗнч(МенеджерКриптографии) <> Тип("МенеджерКриптографии") Тогда
		
		Если Не ЗначениеЗаполнено(Результат.ОписаниеОшибки) Тогда
			Результат.ОписаниеОшибки = НСтр("ru = 'Не удалось получить менеджер криптографии'");
		КонецЕсли;
		
		Возврат Результат;
		
	КонецЕсли;
	
	Попытка
		Сертификаты = МенеджерКриптографии.ПолучитьСертификатыИзПодписи(ДанныеПодписи);
	Исключение
		Результат.ОписаниеОшибки = СтрШаблон(НСтр("ru = 'Не удалось получить сертификаты из подписи по причине: %1'"),
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат Результат;
	КонецПопытки;
	
	Если ТипЗнч(Сертификаты) <> Тип("Массив") Тогда
		Результат.ОписаниеОшибки = НСтр("ru = 'Не удалось получить сертификаты из подписи'");
		Возврат Результат;
	КонецЕсли;
	
	Сертификат = КриптографияБЭДСлужебныйКлиентСервер.НайтиСертификатПодписавшейСтороныРекурсивно(Сертификаты);
	
	Если ТипЗнч(Сертификат) <> Тип("СертификатКриптографии") Тогда
		Результат.ОписаниеОшибки = НСтр("ru = 'Не удалось получить сертификат из данных подписи'");
		Возврат Результат;
	КонецЕсли;
	
	Результат.Успех = Истина;
	
	Результат.ДанныеСертификата.АдресСертификата =
		ПоместитьВоВременноеХранилище(Сертификат.Выгрузить(), ИдентификаторФормы);
	Результат.ДанныеСертификата.КомуВыданСертификат = ЭлектроннаяПодпись.ПредставлениеСубъекта(Сертификат);
	Результат.ДанныеСертификата.Отпечаток = Base64Строка(Сертификат.Отпечаток);
	
	Возврат Результат;
	
КонецФункции

Процедура ЗанестиДанныеСертификатаПодписи(ИдентификаторПодписи, ДанныеСертификата)
	
	НаборЗаписей = РегистрыСведений.ЭлектронныеПодписи.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.УникальныйИдентификатор.Установить(ИдентификаторПодписи);
	НаборЗаписей.Прочитать();
	
	Если НаборЗаписей.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ДвоичныеДанныеСертификата = ПолучитьИзВременногоХранилища(ДанныеСертификата.АдресСертификата);
	Если ТипЗнч(ДвоичныеДанныеСертификата) <> Тип("ДвоичныеДанные") Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Запись Из НаборЗаписей Цикл
		Запись.Сертификат = Новый ХранилищеЗначения(ДвоичныеДанныеСертификата);
		Запись.КомуВыданСертификат = ДанныеСертификата.КомуВыданСертификат;
		Запись.Отпечаток = ДанныеСертификата.Отпечаток;
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область ОтметкаЭП

Функция НовоеОписаниеОтметкиЭП()
	
	ОписаниеЭП = Новый Структура;
	ОписаниеЭП.Вставить("Номер", "");
	ОписаниеЭП.Вставить("Владелец", "");
	ОписаниеЭП.Вставить("ДатаНачала", "");
	ОписаниеЭП.Вставить("ДатаОкончания", "");
	
	НовыеДанныеДоверенности = НовоеОписаниеДоверенностиЭПДляОтметки();
	
	ОписаниеЭП.Вставить("ДанныеДоверенности", НовыеДанныеДоверенности);
	
	Возврат ОписаниеЭП;
	
КонецФункции

Функция НовоеОписаниеДоверенностиЭПДляОтметки()
	
	ДанныеДляОтметки = Новый Структура;
	ДанныеДляОтметки.Вставить("ПодписьПоДоверенности", Ложь);
	ДанныеДляОтметки.Вставить("НомерДоверенности", "");
	ДанныеДляОтметки.Вставить("ДоверительПредставление", "");
	ДанныеДляОтметки.Вставить("ДатаВыдачи", Дата(1, 1, 1));
	
	Возврат ДанныеДляОтметки;
	
КонецФункции

Функция ОписаниеДоверенностиЭПДляОтметки(ИдентификаторПодписи)
	
	ДанныеДляОтметки = НовоеОписаниеДоверенностиЭПДляОтметки();
	
	ИдентификаторыПодписей = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ИдентификаторПодписи);
	ДанныеДоверенностей = ДанныеДоверенностейПодписей(ИдентификаторыПодписей);
	ДанныеДоверенности = ДанныеДоверенностей[ИдентификаторПодписи];
	
	Если ДанныеДоверенности = Неопределено Тогда
		Возврат ДанныеДляОтметки;
	КонецЕсли;
	
	Доверенность = ДанныеДоверенности.Доверенность;
	
	Если Не ЗначениеЗаполнено(Доверенность) Тогда
		Возврат ДанныеДляОтметки;
	КонецЕсли;
	
	Если ТипЗнч(Доверенность) = Тип("СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций") Тогда
		
		РеквизитыДоверенности = Общегоназначения.ЗначенияРеквизитовОбъекта(Доверенность,
			"НомерДоверенности, ДатаВыдачи, ДоверительЮЛ_НаимОрг");
		
		ДанныеДляОтметки.ПодписьПоДоверенности = Истина;
		ДанныеДляОтметки.НомерДоверенности = РеквизитыДоверенности.НомерДоверенности;
		ДанныеДляОтметки.ДоверительПредставление = РеквизитыДоверенности.ДоверительЮЛ_НаимОрг;
		ДанныеДляОтметки.ДатаВыдачи = РеквизитыДоверенности.ДатаВыдачи;
		
	ИначеЕсли ТипЗнч(Доверенность) = Тип("СправочникСсылка.МашиночитаемыеДоверенностиКонтрагентов") Тогда
		
		РеквизитыДоверенности = Общегоназначения.ЗначенияРеквизитовОбъекта(Доверенность,
			"НомерДоверенности, ДатаВыдачи, Доверитель");
		
		ДанныеДляОтметки.ПодписьПоДоверенности = Истина;
		ДанныеДляОтметки.НомерДоверенности = РеквизитыДоверенности.НомерДоверенности;
		ДанныеДляОтметки.ДоверительПредставление = РеквизитыДоверенности.Доверитель;
		ДанныеДляОтметки.ДатаВыдачи = РеквизитыДоверенности.ДатаВыдачи;
		
	КонецЕсли;
	
	Возврат ДанныеДляОтметки;
	
КонецФункции

Процедура ОбновитьОтметкуЭППриЗаписиДоверенностей(НаборЗаписей)
	
	Если Не ПолучитьФункциональнуюОпцию("АвтовизуализацияЭП") Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторПопдиси = НаборЗаписей.Отбор.УникальныйИдентификаторПодписи.Значение;
	
	Если Не ЗначениеЗаполнено(ИдентификаторПопдиси) Тогда
		Возврат;
	КонецЕсли;
	
	Идентификаторы = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ИдентификаторПопдиси);
	ДанныеПодписей = УстановленныеПодписиПоИдентификаторам(Идентификаторы);
	ДанныеПодписи = ДанныеПодписей[ИдентификаторПопдиси];
	
	Если ДанныеПодписи = Неопределено Тогда
		Возврат
	КонецЕсли;
	
	Объект = ДанныеПодписи.ПодписанныйОбъект;
	Если ТипЗнч(Объект) <> Тип("СправочникСсылка.ВерсииФайлов") Тогда
		Возврат;
	КонецЕсли;
	
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект,
		"Владелец, Владелец.ВладелецФайла");
	Файл = ЗначенияРеквизитов.Владелец;
	ВладелецФайла = ЗначенияРеквизитов.ВладелецВладелецФайла;
	
	Если Не ДелопроизводствоКлиентСервер.ЭтоДокумент(ВладелецФайла) Тогда
		Возврат;
	КонецЕсли;
		
	ИспользоватьImageMagick = РаботаСФайламиВызовСервера.ПолучитьИспользоватьImageMagickДляРаспознаванияPDF();
	Если Не ИспользоватьImageMagick Тогда
		Возврат;
	КонецЕсли;
	
	УдалитьОтметкиЭП(ВладелецФайла, Файл);
	ОбновитьОтметкуЭП(ВладелецФайла, Файл, Объект);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти