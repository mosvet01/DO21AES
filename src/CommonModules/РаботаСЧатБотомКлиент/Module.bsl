////////////////////////////////////////////////////////////////////////////////
// Модуль для работы с чат-ботом (клиент).
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограмныйИнтерфейс

//Подключает обработчики отправки сообщения, действия сообщения 
// и создает обсуждение с пользователем, если еще такого нет.
//
Процедура СоздатьЧатБота() Экспорт
	
	// Если параметры уже прочитаны, то не делаем КС вызов.
	ПараметрыРаботыКлиента = СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиентаПриЗапуске();
	Если ЗначениеЗаполнено(ПараметрыРаботыКлиента) 
		И ПараметрыРаботыКлиента.Свойство("СистемаВзаимодействияЗарегистрирована") Тогда
		Если Не ПараметрыРаботыКлиента.СистемаВзаимодействияЗарегистрирована Тогда 
			Возврат;
		КонецЕсли;
		
	ИначеЕсли Не РаботаСЧатБотомВызовСервера.ИнформационнаяБазаЗарегистрирована() Тогда 
		Возврат;
	КонецЕсли;
	
	// Если чат-бот отключен, то ничего не делаем.
	Если ЗначениеЗаполнено(ПараметрыРаботыКлиента) 
		И ПараметрыРаботыКлиента.Свойство("ЧатБотИспользуется")
		И Не ПараметрыРаботыКлиента.ЧатБотИспользуется Тогда
		Возврат;
	КонецЕсли;
	
	ИмяПараметра = "СтандартныеПодсистемыДокументооборота.СостоянияДляУточнения";
	Если ПараметрыПриложения[ИмяПараметра] = Неопределено Тогда
		ПараметрыПриложения.Вставить(ИмяПараметра, Новый Массив);
	КонецЕсли;
	
	ИмяПараметра = "СтандартныеПодсистемыДокументооборота.ЗапрашиваемыеПараметрыСостоянияЧатБота";
	Если ПараметрыПриложения[ИмяПараметра] = Неопределено Тогда
		ПараметрыПриложения.Вставить(ИмяПараметра, Новый Структура());
	КонецЕсли;
	
	ИмяПараметра = "СтандартныеПодсистемыДокументооборота.ТекущееСостояниеЧатБота";
	Если ПараметрыПриложения[ИмяПараметра] = Неопределено Тогда
		ПараметрыПриложения.Вставить(ИмяПараметра,
			ПредопределенноеЗначение("Справочник.СостоянияЧатБота.ПустаяСсылка"));
	КонецЕсли;
	
	ИмяПараметра = "СтандартныеПодсистемыДокументооборота.ЗаписьБеседыСЧатБотом";
	Если ПараметрыПриложения[ИмяПараметра] = Неопределено Тогда
		ПараметрыПриложения.Вставить(ИмяПараметра, РаботаСЧатБотомКлиентСервер.НоваяЗаписьБеседы());
	КонецЕсли;
	
	ОбработчикПослеОтправки = Новый ОписаниеОповещения("ОбработкаОтправкиСообщения", ЭтотОбъект);
	СистемаВзаимодействия.ПодключитьОбработчикПослеОтправкиСообщения(ОбработчикПослеОтправки);
	ОбработчикДействия = Новый ОписаниеОповещения("ОбработкаДействияСообщения", ЭтотОбъект);
	СистемаВзаимодействия.ПодключитьОбработчикДействияСообщения(ОбработчикДействия);
	
	// Если параметры уже прочитаны, то не делаем КС вызов.
	Попытка
		Если ЗначениеЗаполнено(ПараметрыРаботыКлиента) И ПараметрыРаботыКлиента.Свойство("НеСоздаватьБота") Тогда 
			Если Не ПараметрыРаботыКлиента.НеСоздаватьБота Тогда 
				ОбсуждениеСЧатБотом = РаботаСЧатБотомВызовСервера.СоздатьОбсуждениеСБотом();
			Иначе
				ОбсуждениеСЧатБотом = РаботаСЧатБотомВызовСервера.СоздатьОбсуждениеСБотом(Истина);
			КонецЕсли;
			
		Иначе 
			ОбсуждениеСЧатБотом = РаботаСЧатБотомВызовСервера.СоздатьОбсуждениеСБотом(Истина);
		КонецЕсли;
		
		ИмяПараметра = "СтандартныеПодсистемыДокументооборота.ОбсуждениеПользователяСЧатБотом";
		Если ПараметрыПриложения[ИмяПараметра] = Неопределено Тогда
			ПараметрыПриложения.Вставить(ИмяПараметра, ОбсуждениеСЧатБотом);
		КонецЕсли;

	Исключение
		ТекстСообщения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		РаботаСЧатБотомВызовСервера.ЗаписьЖурналаРегистрацииСервер(ТекстСообщения);
	КонецПопытки;
	
КонецПроцедуры

//Обрабатывает сообщение отправленное пользователем.
//
//Параметры: 
//	Сообщение - СообщениеСистемыВзаимодействия - Сообщение отправленное пользователем
//	Обсуждение - ОбсуждениеСистемыВзаимодейтвия - Обсуждение в котором было написано сообщение
//	Дополнительные параметры - значение, которое было указано при создании объекта ОписаниеОповещения
//		см.СоздатьЧатБота 
//
Процедура ОбработкаОтправкиСообщения(Сообщение, Обсуждение, ДополнительныеПараметры) Экспорт
	
	Если Обсуждение.Идентификатор <> 
		ПараметрыПриложения["СтандартныеПодсистемыДокументооборота.ОбсуждениеПользователяСЧатБотом"] Тогда
	
		Возврат;
	
	КонецЕсли;
	
	СледующееСостояние = РаботаСЧатБотомВызовСервера.СледующееСостояние(
		ПараметрыПриложения["СтандартныеПодсистемыДокументооборота.ТекущееСостояниеЧатБота"], 
		Сообщение.Текст, 
		ПараметрыПриложения["СтандартныеПодсистемыДокументооборота.СостоянияДляУточнения"],
		ПараметрыПриложения["СтандартныеПодсистемыДокументооборота.ЗаписьБеседыСЧатБотом"]);
	
	Если Не ЗначениеЗаполнено(СледующееСостояние) Тогда 
		Возврат;
	КонецЕсли;
	
	Если СледующееСостояние.Предопределенный Тогда
		Если ЗначениеЗаполнено(СледующееСостояние.Действие) Тогда 
			#Если НЕ ВебКлиент Тогда
				Выполнить(СледующееСостояние.Действие);
			#Иначе
				ЗаписьБеседыСЧатБотом = РаботаСЧатБотомКлиентСервер.НоваяЗаписьБеседы();
				ЗаписьБеседыСЧатБотом.СостояниеИз =
					ПараметрыПриложения["СтандартныеПодсистемыДокументооборота.ТекущееСостояниеЧатБота"];
				ПараметрыПриложения["СтандартныеПодсистемыДокументооборота.ЗаписьБеседыСЧатБотом"] = ЗаписьБеседыСЧатБотом;
				ПредставлениеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
				РаботаСЧатБотомВызовСервера.СообщитьОбОшибке(
					ПредставлениеОшибки,
					ПараметрыПриложения["СтандартныеПодсистемыДокументооборота.ЗаписьБеседыСЧатБотом"],
					НСтр("ru = 'Это действие недоступно в веб-клиенте.
	                      |Работайте в тонком клиенте.'"));
			#КонецЕсли
		КонецЕсли;
		Возврат;
		
	КонецЕсли;
	
	Если СледующееСостояние.Ссылка <> ПараметрыПриложения["СтандартныеПодсистемыДокументооборота.ТекущееСостояниеЧатБота"] Тогда
		
		ПараметрыПриложения["СтандартныеПодсистемыДокументооборота.ТекущееСостояниеЧатБота"] = СледующееСостояние.Ссылка;
		ПодключитьОбработчикОжидания("ОбработатьСостояниеОтложенно", 0.2, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

//Обрабатывает нажатие кнопки пользователем.
//
//Параметры: 
//	Сообщение - СообщениеСистемыВзаимодействия - Сообщение отправленное пользователем.
//	Действие - Произвольный - Значение, заданное для нажатой гиперссылки.
//	Дополнительные параметры - значение, которое было указано при создании объекта ОписаниеОповещения
//		см.СоздатьЧатБота 
//
Процедура ОбработкаДействияСообщения(Сообщение, Действие, ДополнительныеПараметры) Экспорт
	
	Если Сообщение.Обсуждение <> 
		ПараметрыПриложения["СтандартныеПодсистемыДокументооборота.ОбсуждениеПользователяСЧатБотом"]
		ИЛИ Не РаботаСЧатБотомВызовСервера.ЧатБотЗапущен()Тогда
	
		Возврат;
	
	КонецЕсли;
		
	ЗаписьБеседыСЧатБотом = РаботаСЧатБотомКлиентСервер.НоваяЗаписьБеседы();
	ЗаписьБеседыСЧатБотом.СообщениеПользователя = "";
	ЗаписьБеседыСЧатБотом.СостояниеИз = ПараметрыПриложения["СтандартныеПодсистемыДокументооборота.ТекущееСостояниеЧатБота"];
	ПараметрыПриложения["СтандартныеПодсистемыДокументооборота.ЗаписьБеседыСЧатБотом"] = ЗаписьБеседыСЧатБотом;
	СостояниеДоИзменений = ПараметрыПриложения["СтандартныеПодсистемыДокументооборота.ТекущееСостояниеЧатБота"];
	
	Попытка
		
		Если ЭтоСсылкаНаСостояние(Действие) Тогда
			
			ПараметрыПриложения["СтандартныеПодсистемыДокументооборота.ТекущееСостояниеЧатБота"] = Действие;
			ПараметрыПриложения["СтандартныеПодсистемыДокументооборота.СостоянияДляУточнения"] = Новый Массив;
			ОбработатьСостояние();
		
		Иначе
			
			ВыполнитьДействиеБезСостояния(Действие);
			
		КонецЕсли;
	
	Исключение
		
		ПредставлениеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		РаботаСЧатБотомВызовСервера.СообщитьОбОшибке(ПредставлениеОшибки, 
			ПараметрыПриложения["СтандартныеПодсистемыДокументооборота.ЗаписьБеседыСЧатБотом"]);
		ПараметрыПриложения["СтандартныеПодсистемыДокументооборота.ТекущееСостояниеЧатБота"] = СостояниеДоИзменений;
	
	КонецПопытки;
	
КонецПроцедуры

//Пишет высказывание и выполняет действие текущего состояния.
//
Процедура ОбработатьСостояние() Экспорт

	РаботаСЧатБотомВызовСервера.НаписатьПользователю(
		ПараметрыПриложения["СтандартныеПодсистемыДокументооборота.ТекущееСостояниеЧатБота"], 
		ПараметрыПриложения["СтандартныеПодсистемыДокументооборота.ЗаписьБеседыСЧатБотом"]);
	ПодключитьОбработчикОжидания("ВыполнитьДействиеОтложенно", 0.2, Истина);

КонецПроцедуры

//Выполняет действие текущего состояния.
//
Процедура ВыполнитьДействие() Экспорт
	
	Состояние = РаботаСЧатБотомВызовСервера.ДействиеСостояния(
		ПараметрыПриложения["СтандартныеПодсистемыДокументооборота.ТекущееСостояниеЧатБота"]);
	
	Если Состояние.ТипСостояния <> ПредопределенноеЗначение("Перечисление.ТипыСостоянийЧатБота.Действие") Тогда
		
		Если Состояние.ТипСостояния = ПредопределенноеЗначение("Перечисление.ТипыСостоянийЧатБота.Вопрос") Тогда
		
			ОткрытьФормуЗаполненияПараметра();
		
		КонецЕсли;
		
		Возврат;
		
	КонецЕсли;
	
	#Если НЕ ВебКлиент Тогда
		
		Попытка
			
			Выполнить(ИнициализацияПараметров(Состояние.Параметры) + Состояние.Действие);
			
		Исключение
			
			ЗаписьБеседыСЧатБотом = РаботаСЧатБотомКлиентСервер.НоваяЗаписьБеседы();
			ЗаписьБеседыСЧатБотом.СостояниеИз = 
				ПараметрыПриложения["СтандартныеПодсистемыДокументооборота.ТекущееСостояниеЧатБота"];
			ПараметрыПриложения["СтандартныеПодсистемыДокументооборота.ЗаписьБеседыСЧатБотом"] = ЗаписьБеседыСЧатБотом;
			ПредставлениеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
			РаботаСЧатБотомВызовСервера.СообщитьОбОшибке(ПредставлениеОшибки, 
				ПараметрыПриложения["СтандартныеПодсистемыДокументооборота.ЗаписьБеседыСЧатБотом"]);
			
		КонецПопытки;
	#Иначе
		ЗаписьБеседыСЧатБотом = РаботаСЧатБотомКлиентСервер.НоваяЗаписьБеседы();
		ЗаписьБеседыСЧатБотом.СостояниеИз = 
			ПараметрыПриложения["СтандартныеПодсистемыДокументооборота.ТекущееСостояниеЧатБота"];
		ПараметрыПриложения["СтандартныеПодсистемыДокументооборота.ЗаписьБеседыСЧатБотом"] = ЗаписьБеседыСЧатБотом;
		ПредставлениеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		РаботаСЧатБотомВызовСервера.СообщитьОбОшибке(
			ПредставлениеОшибки,
			ПараметрыПриложения["СтандартныеПодсистемыДокументооборота.ЗаписьБеседыСЧатБотом"],
			НСтр("ru = 'Это действие недоступно в веб-клиенте.
                  |Работайте в тонком клиенте.'"));
	#КонецЕсли
	
	ПараметрыПриложения["СтандартныеПодсистемыДокументооборота.ЗапрашиваемыеПараметрыСостоянияЧатБота"] = Неопределено;
	ПараметрыПриложения["СтандартныеПодсистемыДокументооборота.СостоянияДляУточнения"] = Новый Массив;

КонецПроцедуры

//Записывает значение запрашиваемого параметра в ЗапрашиваемыеПараметрыСостоянияЧатБота
//
//Параметры: 
//	ЗначениеПараметра - Строка, Число, Булево, Дата, ЛюбаяСсылка - значение параметра, 
//		запрашиваемого в типе состояния "Вопрос"
//	Ключ - Строка - Имя запрашиваемого параметра.
//
Процедура ЗакончитьЗаполнениеПараметра(ЗначениеПараметра, Ключ) Экспорт

	Если ПараметрыПриложения["СтандартныеПодсистемыДокументооборота.ЗапрашиваемыеПараметрыСостоянияЧатБота"] = Неопределено Тогда
	
		ПараметрыПриложения["СтандартныеПодсистемыДокументооборота.ЗапрашиваемыеПараметрыСостоянияЧатБота"] = Новый Структура();
	
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеПараметра) Тогда
	
		ПараметрыПриложения["СтандартныеПодсистемыДокументооборота.ЗапрашиваемыеПараметрыСостоянияЧатБота"].Вставить(
			Ключ, ЗначениеПараметра);
		ОбсуждениеСПользователем = РаботаСЧатБотомВызовСервера.ОтправитьСообщениеОтИмениПользователя(ЗначениеПараметра);
		Сообщение = Новый Структура("Текст", Строка(ЗначениеПараметра)); 
		ОбработкаОтправкиСообщения(Сообщение, ОбсуждениеСПользователем, Неопределено);
		
	Иначе
		
		УточнитьНезаполненныйПараметр(Ключ);
		
	КонецЕсли;
	
КонецПроцедуры

//Вызывается из оповещения, после процедуры "УточнитьНезаполненныйПараметр"
//
//Параметры:
//	РезультатВопроса - КодВозвратаДиалога - Код возврата диалога (Да, Нет);
//	ДополнительныеПараметры - Любой - Значение из оповещения в "УточнитьНезаполненныйПараметр"
//
Процедура ПослеУточненияПараметра(РезультатВопроса, ДополнительныеПараметры) Экспорт 

	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
	
		ОткрытьФормуЗаполненияПараметра();
		
	Иначе
		
		ПараметрыПриложения["СтандартныеПодсистемыДокументооборота.ТекущееСостояниеЧатБота"] = ПредопределенноеЗначение(
			"Справочник.СостоянияЧатБота.Стоп");
		ОбработатьСостояниеОтложенно();
		ПараметрыПриложения["СтандартныеПодсистемыДокументооборота.ЗапрашиваемыеПараметрыСостоянияЧатБота"] = Неопределено;
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ВыполнитьДействиеБезСостояния(Действие)

	Попытка
		
		Выполнить (Действие);
		
	Исключение
		
		ЗаписьБеседыСЧатБотом = РаботаСЧатБотомКлиентСервер.НоваяЗаписьБеседы();
		ЗаписьБеседыСЧатБотом.СостояниеИз = ПараметрыПриложения["СтандартныеПодсистемыДокументооборота.ТекущееСостояниеЧатБота"];
		ПараметрыПриложения["СтандартныеПодсистемыДокументооборота.ЗаписьБеседыСЧатБотом"] = ЗаписьБеседыСЧатБотом;
		ПредставлениеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		РаботаСЧатБотомВызовСервера.СообщитьОбОшибке(ПредставлениеОшибки, 
			ПараметрыПриложения["СтандартныеПодсистемыДокументооборота.ЗаписьБеседыСЧатБотом"]);
		
	КонецПопытки;
	
	ПараметрыПриложения["СтандартныеПодсистемыДокументооборота.ТекущееСостояниеЧатБота"] = ПредопределенноеЗначение(
		"Справочник.СостоянияЧатБота.ПустаяСсылка");
	ПараметрыПриложения["СтандартныеПодсистемыДокументооборота.СостоянияДляУточнения"] = Новый Массив;

КонецПроцедуры

//Возвращает строку для инициализации параметров
// 
//Параметры:
//	Параметры - Структура - где:
//		* Ключ - Строка - Имя параметра
//		* Значение - Число, Булево, Дата, Строка, ЛюаяСсылка - Значение параметра
//	
//Возвращаемое значение:
//	Строка - Строка с инициализацией параметров.
//
//Пример:
//	ИнициализацияПараметров(Новый Структура("ШаблонДокумента", Заявление на отпуск))
//		= "ШаблонДокумента = Заявление на отпуск;"
//
Функция ИнициализацияПараметров(Параметры)
	
	ИнициализацияПараметров = "Параметры = Состояние.Параметры;" + Символы.ПС;
	 
	Для Каждого Параметр Из Параметры Цикл
		
		ИнициализацияПараметров = ИнициализацияПараметров
			+ Параметр.Ключ + " = " + "Параметры." + Параметр.Ключ + ";" + Символы.ПС;
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ПараметрыПриложения["СтандартныеПодсистемыДокументооборота.ЗапрашиваемыеПараметрыСостоянияЧатБота"]) Тогда
	
		Для Каждого Параметр Из ПараметрыПриложения["СтандартныеПодсистемыДокументооборота.ЗапрашиваемыеПараметрыСостоянияЧатБота"] Цикл
	
			ИнициализацияПараметров = ИнициализацияПараметров
				+ Параметр.Ключ + " = "
				+ "ПараметрыПриложения[""СтандартныеПодсистемыДокументооборота.ЗапрашиваемыеПараметрыСостоянияЧатБота""]."
				+ Параметр.Ключ + ";" + Символы.ПС;
	
		КонецЦикла;
	
	КонецЕсли;

	Возврат ИнициализацияПараметров;

КонецФункции

Процедура ОткрытьФормуЗаполненияПараметра()

	ПараметрыФормы = РаботаСЧатБотомВызовСервера.ПараметрДляЗаполнения(
		ПараметрыПриложения["СтандартныеПодсистемыДокументооборота.ТекущееСостояниеЧатБота"]);
	ПараметрыФормы.Вставить("ТекущееСостояние", 
		ПараметрыПриложения["СтандартныеПодсистемыДокументооборота.ТекущееСостояниеЧатБота"]);
	Ключ = ПараметрыФормы.ИмяЗаполняемогоПараметра;
	
	Если ПараметрыФормы.ТипЗначения = "Строка" Тогда
	
		ПараметрыФормы.Вставить("КлючНазначенияИспользования", "Многострочный");
		
	Иначе
		
		ПараметрыФормы.Вставить("КлючНазначенияИспользования", "Однострочный");
	
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗакончитьЗаполнениеПараметра", ЭтотОбъект, Ключ);
	ОткрытьФорму("Справочник.СостоянияЧатБота.Форма.ФормаЗаполненияПараметра", ПараметрыФормы,,,,, ОписаниеОповещения);
	
КонецПроцедуры

Процедура УточнитьНезаполненныйПараметр(ИмяПараметра)

	Сообщение = СтрШаблон(НСтр("ru = 'Параметр ""%1"" не заполнен. 
		|Хотите указать его повторно'"), 
		ИмяПараметра);
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеУточненияПараметра", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения, Сообщение, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да);

КонецПроцедуры

Функция ЭтоСсылкаНаСостояние(Ссылка)
	
	Возврат ТипЗнч(Ссылка) = Тип("СправочникСсылка.СостоянияЧатБота");
	
КонецФункции

#КонецОбласти
