////////////////////////////////////////////////////////////////////////////////
// Методы, обслуживающие подписки на события.
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Обработчик подписки на событие "СинхронизацияКалендарейПередЗаписью".
//
// Параметры:
//  Источник - СправочникОбъект.ЗаписиРабочегоКалендаря - Записываем запись календаря.
//  Отказ    - Булево                                   - Отказ от записи.
//
Процедура СинхронизацияКалендарейПередЗаписью(Источник, Отказ) Экспорт
	
	Если МиграцияПриложенийПереопределяемый.ЭтоЗагрузка(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСинхронизациюКалендарей") Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	УзлыДляВыгрузки = СинхронизацияКалендарей.УзлыДляВыгрузки(Источник.Пользователь);
	ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияЗначенияИзМассива(
		УзлыДляВыгрузки,
		Источник.ОбменДанными.Отправитель);
	Если Не ЗначениеЗаполнено(УзлыДляВыгрузки) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого УзелДляВыгрузки Из УзлыДляВыгрузки Цикл
		Источник.ОбменДанными.Получатели.Добавить(УзелДляВыгрузки);
	КонецЦикла;
	
	УдалитьУстаревшуюРегистрациюИзменений(Источник.Ссылка, УзлыДляВыгрузки);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Удаляет устаревшую регистрацию изменений по узлам, которые перестали быть актуальными.
//
// Параметры:
//  ЗаписьКалендаря - СправочникСсылка.ЗаписиРабочегоКалендаря           - Запись календаря.
//  УзлыДляВыгрузки - Массив из ПланОбменаСсылка.СинхронизацияКалендарей - Актуальные узлы для выгрузки.
//
Процедура УдалитьУстаревшуюРегистрациюИзменений(ЗаписьКалендаря, УзлыДляВыгрузки)
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ЗаписиРабочегоКалендаряИзменения.Узел КАК Узел
		|ИЗ
		|	Справочник.ЗаписиРабочегоКалендаря.Изменения КАК ЗаписиРабочегоКалендаряИзменения
		|ГДЕ
		|	ЗаписиРабочегоКалендаряИзменения.Ссылка = &ЗаписьКалендаря
		|	И НЕ ЗаписиРабочегоКалендаряИзменения.Узел В (&УзлыДляВыгрузки)
		|	И ЗаписиРабочегоКалендаряИзменения.Узел ССЫЛКА ПланОбмена.СинхронизацияКалендарей");
	
	Запрос.УстановитьПараметр("ЗаписьКалендаря", ЗаписьКалендаря);
	Запрос.УстановитьПараметр("УзлыДляВыгрузки", УзлыДляВыгрузки);
	
	ЛишниеУзлы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Узел");
	Если Не ЗначениеЗаполнено(ЛишниеУзлы) Тогда
		Возврат;
	КонецЕсли;
	
	ПланыОбмена.УдалитьРегистрациюИзменений(ЛишниеУзлы, ЗаписьКалендаря);
	
КонецПроцедуры

#КонецОбласти