
#Область ПрограммныйИнтерфейс

// Изменяет текст заголовка надписи в зависимости от видимости таблицы формы.
//
// Параметры:
//  ЭлементТаблицаФормы - ТаблицаФормы - Элемент формы, видимость которого учитывается.
//  ДанныеТаблицаФормы - ДанныеФормыДерево, ДинамическийСписок - Данные таблицы, для подсчета количества строк.
//  ДекорацияФормы - ДекорацияФормы - Декорация, заголовок которой нужно оформить.
//
Процедура ОформитьНадписьДляСкрытияТаблицы(ЭлементТаблицаФормы, ДанныеТаблицаФормы, ДекорацияФормы) Экспорт
	
	Треугольник = ?(ЭлементТаблицаФормы.Видимость = Ложь, Символ(9658),Символ(9660));
	ШаблонЗаголовка = "%1 %2%3";
	Количество = "";
	Если ТипЗнч(ДанныеТаблицаФормы) = Тип("ДанныеФормыДерево") Тогда
		ШаблонЗаголовка = "%1 %2 (%3)";
		Количество = Формат(ДанныеТаблицаФормы.ПолучитьЭлементы().Количество(), "ЧН=0; ЧГ=");
	ИначеЕсли ТипЗнч(ДанныеТаблицаФормы) = Тип("ДанныеФормыКоллекция") Тогда
		ШаблонЗаголовка = "%1 %2 (%3)";
		Количество = Формат(ДанныеТаблицаФормы.Количество(), "ЧН=0; ЧГ=");
	КонецЕсли;
	ДекорацияФормы.Заголовок = СтрШаблон(ШаблонЗаголовка, Треугольник, ДекорацияФормы.Подсказка, Количество);
	
КонецПроцедуры

// Обработчик команды формы.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма, из которой выполняется команда.
//   Команда - КомандаФормы, Строка - Выполняемая команда.
//   Источник - ТаблицаФормы, ДанныеФормыСтруктура, Ссылка, Массив - Объект или список формы с полем "Ссылка",
//                                                                   Ссылка или массив ссылок.
//
Процедура ВыполнитьКоманду(Форма, Команда, Источник) Экспорт
	
	Если ТипЗнч(Команда) = Тип("КомандаФормы") Тогда
		ИмяКоманды = Команда.Имя;
	Иначе
		ИмяКоманды = Команда;
	КонецЕсли;
	
	Если ИмяКоманды = "ПереключитьСтраницуВлевоМК" 
		Или ИмяКоманды = "ПереключитьСтраницуВправоМК" Тогда
		
		Страницы = Форма.Элементы[Форма.ПараметрыМобильныйКлиент.ЭлементСтраницыИмя];
		
		ВидимыеСтраницы = Новый Массив();
		
		Для Каждого Страница Из Страницы.ПодчиненныеЭлементы Цикл
			Если Страница.Видимость Тогда
				ВидимыеСтраницы.Добавить(Страница);
			КонецЕсли;
		КонецЦикла;
		
		ТекущийИндекс = ВидимыеСтраницы.Найти(Страницы.ТекущаяСтраница);
		// Переключим на следующую страницу и обработаем переполнение.
		Если ИмяКоманды = "ПереключитьСтраницуВправоМК" Тогда
			Если ТекущийИндекс = ВидимыеСтраницы.Количество() - 1 Тогда
				ТекущийИндекс = 0;
			Иначе
				ТекущийИндекс = ТекущийИндекс + 1;
			КонецЕсли;
		Иначе
			Если ТекущийИндекс = 0 Тогда
				ТекущийИндекс = ВидимыеСтраницы.Количество() - 1;
			Иначе
				ТекущийИндекс = ТекущийИндекс - 1;
			КонецЕсли;
		КонецЕсли;
		
		Страницы.ТекущаяСтраница = ВидимыеСтраницы.Получить(ТекущийИндекс);
		
		МобильныйКлиентАдаптацияИнтерфейсаКлиентСервер.ОбновитьСтраницыИПереключатели(Форма);
		
		
	ИначеЕсли ИмяКоманды = "ПереключитьВкладкуМК" Тогда
		
		// получим индекс нажатой вкладки
		ИндексСтрокой = СтрЗаменить(Источник.Имя, "ВкладкаМКПодпись", "");
		ИндексСтрокой = СтрЗаменить(ИндексСтрокой, "ВкладкаМККартинка", "");
		Индекс = Число(ИндексСтрокой);
		
		СтраницыДляВкладок = Форма.Элементы[Форма.ПараметрыМобильныйКлиент.ЭлементВкладкиСтраницыИмя];
		ПереключателиВкладок = Форма.Элементы[Форма.ПараметрыМобильныйКлиент.ЭлементВкладкиПереключателиИмя];
		
		Если СтраницыДляВкладок.ТекущаяСтраница <> СтраницыДляВкладок.ПодчиненныеЭлементы[Индекс] Тогда
			
			СтраницыДляВкладок.ТекущаяСтраница = СтраницыДляВкладок.ПодчиненныеЭлементы[Индекс];
			МобильныйКлиентАдаптацияИнтерфейсаКлиентСервер.ОбновитьОформлениеВкладок(
				Форма, СтраницыДляВкладок, ПереключателиВкладок);
			
			КонецЕсли;
			
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти
