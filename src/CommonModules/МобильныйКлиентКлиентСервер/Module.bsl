#Область ПрограммныйИнтерфейс

// Возвращает имя собыития для записи в журнал регистрации по push уведомлениям.
// 
// Возвращаемое значение:
//  Строка - Имя события для записи в журнал регистрации для push уведомлений.
//
Функция ИмяСобытияPushУведомления() Экспорт
	
#Если Клиент Тогда
	КодЯзыка = ОбщегоНазначенияКлиент.КодОсновногоЯзыка();
#Иначе
	КодЯзыка = ОбщегоНазначения.КодОсновногоЯзыка();
#КонецЕсли

	ИмяСобытия = НСтр("ru = 'Push-уведомления'", КодЯзыка);
	
	Возврат ИмяСобытия;

КонецФункции

// Распределяет html текст по экрану мобильного устройства, если еще не распределен
// Параметры
//  HtmlТекст - Строка - Исходный HTML текст
//
Процедура АдаптироватьHtmlПодЭкранПриНеобходимости(ТекстHTML) Экспорт

	Если (СтрНайти(ТекстHTML, ТекстHtmlАдаптацияКонтентаПоШиринеЭкрана()) <> 0) Тогда
		Возврат;
	КонецЕсли;
	
	ИсходныйТекстHTML = ТекстHTML;
	
	ТекстДоОткрТэгаHead = Лев(ТекстHTML, СтрНайти(ТекстHTML, "<head") + СтрДлина("<head") - 1);
	ИсходныйТекстHTML =
		Прав(ИсходныйТекстHTML, СтрДлина(ТекстHTML) - СтрНайти(ТекстHTML, "<head") - СтрДлина("<head") + 1);
	ТекстДоЗакрТэгаHead = Лев(ИсходныйТекстHTML, СтрНайти(ИсходныйТекстHTML, ">"));
	
	РезультирующийТекстHtml = Прав(ИсходныйТекстHTML, СтрДлина(ИсходныйТекстHTML) - СтрНайти(ИсходныйТекстHTML, ">"));
	
	ТекстHTML = СтрШаблон("%1%2%3%4",
		ТекстДоОткрТэгаHead,
		ТекстДоЗакрТэгаHead,
		ТекстHtmlАдаптацияКонтентаПоШиринеЭкрана(),
		РезультирующийТекстHtml);

КонецПроцедуры

// Возвращает необходимый текст для адаптации html по ширине экрана
//  Возвращаемое значение
//  Текст - Строка - Текст текст для адаптации html по ширине экрана
//
Функция ТекстHtmlАдаптацияКонтентаПоШиринеЭкрана() Экспорт
	
	возврат "<meta name=""viewport"" content=""viewport-fit=cover, width=device-width, initial-scale=1"" />";

КонецФункции

// Возвращает Истина, если клиентское приложение является мобильным клиентом.
//
// Возвращаемое значение:
//  Булево - если нет клиентского приложения, возвращается Ложь.
//
Функция ЭтоМобильныйКлиент() Экспорт
	
#Если МобильныйКлиент Тогда
	Возврат Истина;
#ИначеЕсли Сервер Или ТолстыйКлиентОбычноеПриложение Тогда
	УстановитьПривилегированныйРежим(Истина);
	
	ЭтоМобильныйКлиент = СтандартныеПодсистемыСервер.ПараметрыКлиентаНаСервере().Получить("ЭтоМобильныйКлиент");
	
	Если ЭтоМобильныйКлиент = Неопределено Тогда
		Возврат Ложь; // Нет клиентского приложения.
	КонецЕсли;
	
	Возврат ЭтоМобильныйКлиент;
#Иначе
	Возврат Ложь;
#КонецЕсли
	
КонецФункции

#КонецОбласти
