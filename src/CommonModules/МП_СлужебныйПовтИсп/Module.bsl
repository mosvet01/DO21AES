///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2022, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает данные сообщения
//
// Параметры:
// Сообщение	 - Ссылка на элемент справочника СообщенияИнтегрированныхСистем.
Функция ДанныеСообщения(Сообщение) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Сообщение, "ДанныеСообщения").Получить();
	
КонецФункции

// Получает параметры синхронизации для того чтобы их не читать множество раз
//
// Параметры:
//  МобильноеПриложение	 - Справочник.ПользователиМобильногоПриложения
// 
// Возвращаемое значение:
//  Структура - Кешированные параметры синхронизации и объекты для обеспечения скорости выгрузки
//
Функция ПараметрыСинхронизации(МобильноеПриложение) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Пользователь = ПользователиКлиентСервер.ТекущийПользователь();

	ПараметрыСинхронизации = Новый Структура;

	ПараметрыСинхронизации.Вставить("МобильноеПриложение", МобильноеПриложение);
	
	ПараметрыСинхронизации.Вставить("ТекущийПользователь", Пользователь);

	ПараметрыСинхронизации.Вставить("КоличествоОбъектовВсего", 0);
	
	Сведения = СведенияОМобильномПриложении(МобильноеПриложение);
	
	ПараметрыСинхронизации.Вставить("ПредыдущаяГраницаСборки",
		?(Сведения = Неопределено, 0, Сведения.ГраницаСборкиДанных));
	
	ПараметрыСинхронизации.Вставить("ТекущаяГраницаСборки", ОтметкиВремени.ТекущаяГраница());
	
	// часто используемые настройки:
	Для Каждого КлючИЗначение Из НайстройкиПользователя(Пользователь) Цикл
		
		ПараметрыСинхронизации.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
		
	КонецЦикла;

	Возврат ПараметрыСинхронизации;

КонецФункции

// Получает персональные настройки пользователя мобильного приложения
//
// Параметры:
//  Пользователь - Справочник.Пользователи (по умолчанию - текущий пользователь)
// 
// Возвращаемое значение:
//  Структура - Кешированные данные настроек мобильного приложения
//
Функция НайстройкиПользователя(Пользователь = Неопределено) Экспорт
	
	Если Пользователь = Неопределено Тогда
		Пользователь = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
	Настройки = Новый Структура;
	
	ВсеНастройки = Метаданные.Перечисления.ОбменСМобильнымиТипыНастроекПользователей.ЗначенияПеречисления;
	
	Для Каждого Настройка Из ВсеНастройки Цикл
		
		ИмяНастройки = Настройка.Имя;
		ЗначениеНастройки = РегистрыСведений.ОбменСМобильнымиНастройкиПользователей.ПолучитьНастройку(Пользователь,
			Перечисления.ОбменСМобильнымиТипыНастроекПользователей[ИмяНастройки]);
		Настройки.Вставить(ИмяНастройки, ЗначениеНастройки);
			
		Если ИмяНастройки = "ПодробныйПротоколОбменаСМобильнымУстройством" Тогда
			
			Настройки.Вставить("ПодробныйПротоколОбмена", ЗначениеНастройки);
			
			Если ЗначениеНастройки = Истина Тогда
				
				ПротоколируемыеТипы = Новый Массив;
				ПротоколируемыеТипы.Добавить(МП_СлужебныйПовтИсп.ТипВходящееПисьмо());
				ПротоколируемыеТипы.Добавить(МП_СлужебныйПовтИсп.ТипИсходящееПисьмо());
				ПротоколируемыеТипы.Добавить(МП_СлужебныйПовтИсп.ТипЗадача());
				ПротоколируемыеТипы.Добавить(МП_СлужебныйПовтИсп.ТипКонтроль());
				ПротоколируемыеТипы.Добавить(МП_СлужебныйПовтИсп.ТипЗаписьКалендаря());
				ПротоколируемыеТипы.Добавить(МП_СлужебныйПовтИсп.ТипПроцессИсполнения());
				ПротоколируемыеТипы.Добавить(МП_СлужебныйПовтИсп.ТипМоеОтсутствие());
				
				Настройки.Вставить("ПротоколируемыеТипы", ПротоколируемыеТипы);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Настройки;
	
КонецФункции

// Получает сведения о мобильном приложении
//
// Параметры:
//  МобильноеПриложение	 - Справочник.ПользователиМобильногоПриложения
// 
// Возвращаемое значение:
//  Структура - Кешированные сведения о мобильном приложении
//
Функция СведенияОМобильномПриложении(МобильноеПриложение) Экспорт
	
	Возврат РегистрыСведений.СведенияОМобильныхКлиентах.СведенияОМобильномПриложении(МобильноеПриложение);

КонецФункции

// Получает папки синхронизации текущего пользователя
// Возвращаемое значение:
//  Массив - закешированный массив папок текущего пользователя
//
Функция ПапкиСинхронизацииТекущегоПользователя() Экспорт
	
	Возврат РегистрыСведений.СинхронизацияПапокПисемСМобильнымКлиентом.ПолучитьПапкиДляСинхронизации(
		ПользователиКлиентСервер.ТекущийПользователь());

КонецФункции

#КонецОбласти

#Область ВерификацияПользователяМобильногоПриложения

Функция ВходПользователюРазрешен(ОписаниеОшибкиВхода, ТекущийПользователь) Экспорт

	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьМобильныеКлиенты") Тогда
		
		ОписаниеОшибкиВхода = НСтр("ru='Работа с мобильными приложениями выключена.'");
		
		Возврат Ложь;
		
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьОтметкиВремени") Тогда
		
		ОписаниеОшибкиВхода = НСтр("ru='Для работы с данной версией мобильного приложения 
			|необходимо включить опцию ""Использовать отметки времени""'");
		
		Возврат Ложь;
		
	КонецЕсли;

	ПодключениеВозможно = ПроверитьВозможностьПодключенияМобильногоПриложения(ТекущийПользователь);
	
	Если Не ПодключениеВозможно Тогда
		
		ОписаниеОшибкиВхода = НСтр("ru='Вам не разрешен доступ к программе с использованием мобильного приложения. 
			|Обратитесь к администратору.'");
		
		Возврат Ложь;
		
	КонецЕсли;
		
	Возврат Истина;
	
КонецФункции

Функция МобильноеПриложение(ОписаниеОшибкиВхода, ClientCode, ТекущийПользователь) Экспорт
	
	Если Не ЗначениеЗаполнено(ClientCode) Тогда
		ОписаниеОшибкиВхода = НСтр("ru='Неизвестное мобильное приложение, либо старая версия'");
		Возврат Неопределено;
	КонецЕсли;
	
	МобильноеПриложение =
		Справочники.ПользователиМобильногоПриложения.ПолучитьПриложение(ClientCode, ТекущийПользователь);
		
	Если Не ЗначениеЗаполнено(МобильноеПриложение) Тогда
		ОписаниеОшибкиВхода = НСтр("ru='Неизвестное мобильное приложение, либо старая версия.'");
		Возврат Неопределено;
	КонецЕсли;
	
	Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(МобильноеПриложение, "ПометкаУдаления") Тогда
		ОписаниеОшибкиВхода = НСтр("ru='Ваше устройство отключено. Дальнейшая синхронизация невозможна'");
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат МобильноеПриложение;
	
КонецФункции

// Проверяет возможность работы с мобильного приложения для текущего пользователя.
// Возвращает ложь / истину в зависимости от того добавлен пользовател в разрешенные мобильные 
//	пользователи или нет.
// Параметры
// 	Пользователь - ссылка на пользователя
Функция ПроверитьВозможностьПодключенияМобильногоПриложения(ТекущийПользователь) Экспорт

	Если Не РегистрыСведений.РазрешенныеМобильныеПользователи.ИспользованиеМобильногоКлиентаРазрешено(
		ТекущийПользователь) Тогда
		Возврат Ложь;
	КонецЕсли;

	Возврат Истина;

КонецФункции

#КонецОбласти

#Область ТипыДанных
// Получает представление типа объекта для выгрузки
// Параметры:
//  ЭлементДанных - ДокументСсылка.ВходящееПисьмо - Выгружаемый элемент данных
// 
// Возвращаемое значение:
//  Строка - тип объекта строкой подготовленный к выгрузке
//
Функция ТипПоСсылке(Ссылка) Экспорт
	
	ТипXML = XMLТипЗнч(Ссылка);
	
	Если ТипXML = Неопределено Или ТипXML.ИмяТипа = "Null" Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ИмяТипа = СтрРазделить(ТипXML.ИмяТипа, ".")[1];
	
	Если ИмяТипа = "ВходящееПисьмо" Тогда
		Тип = ТипВходящееПисьмо();

	ИначеЕсли ИмяТипа = "ИсходящееПисьмо" Тогда
		Тип = ТипИсходящееПисьмо();

	ИначеЕсли ИмяТипа = "ЗаписиРабочегоКалендаря" Тогда
		Тип = ТипЗаписьКалендаря();

	ИначеЕсли ИмяТипа = "ЗадачаИсполнителя" Тогда
		Тип = ТипЗадача();

	ИначеЕсли ИмяТипа = "Контроль" Тогда
		Тип = ТипКонтроль();

	ИначеЕсли ИмяТипа = "Исполнение" Тогда
		Тип = ТипПроцессИсполнения();

	ИначеЕсли ИмяТипа = "УчетныеЗаписиЭлектроннойПочты" Тогда
		Тип = ТипУчетнаяЗаписьЭлектроннойПочты();

	ИначеЕсли ИмяТипа = "ПапкиПисем" Тогда
		Тип = ТипПапкаПисем();

	ИначеЕсли ИмяТипа = "Пользователи" Тогда
		Тип = ТипПользователь();

	ИначеЕсли ИмяТипа = "РолиИсполнителей" Тогда
		Тип = ТипРольИсполнителя();

	ИначеЕсли ИмяТипа = "Файлы" Тогда
		Тип = ТипФайл();

	ИначеЕсли ИмяТипа = "ВеткиПереписки" Тогда
		Тип = ТипВеткаПереписки();

	ИначеЕсли ИмяТипа = "АдресатыПочтовыхСообщений" Тогда
		Тип = ТипАдресат();
		
	ИначеЕсли ИмяТипа = "ШаблоныТекстов" Тогда
		Тип = ТипШаблонТекста();
		
	ИначеЕсли ИмяТипа = "Отсутствие" Тогда
		Тип = ТипМоеОтсутствие();
		
	ИначеЕсли ИмяТипа = "СамочувствиеСотрудника" Тогда
		Тип = ТипМоеСамочувствие();
		
	ИначеЕсли ИмяТипа = "ГрафикУчетаСамочувствияСотрудников" Тогда
		Тип = ТипВремяЗамера();
		
	ИначеЕсли ИмяТипа = "Мероприятия" Тогда
		Тип = ТипМероприятие();
	
	ИначеЕсли ИмяТипа = "РешениеВопросовВыполненияЗадач" Тогда
		Тип = ТипПроцессРВВЗ();
		
	Иначе
		Тип = ТипПредметПроцесса();
	КонецЕсли;

	Возврат Тип;

КонецФункции

Функция ТипСсылкиПоТипуМобильного(ТипМобильного) Экспорт

	Если ТипМобильного = ТипВходящееПисьмо() Тогда 
		
		Возврат Тип("ДокументСсылка.ВходящееПисьмо");

	ИначеЕсли ТипМобильного = ТипИсходящееПисьмо() Тогда
		Возврат Тип("ДокументСсылка.ИсходящееПисьмо");

	ИначеЕсли ТипМобильного = ТипЗаписьКалендаря() Тогда
		Возврат Тип("СправочникСсылка.ЗаписиРабочегоКалендаря");

	ИначеЕсли ТипМобильного = ТипМероприятие() Тогда
		Возврат Тип("СправочникСсылка.Мероприятия");

	ИначеЕсли ТипМобильного = ТипЗадача() Тогда
		Возврат Тип("ЗадачаСсылка.ЗадачаИсполнителя");

	ИначеЕсли ТипМобильного = ТипКонтроль() Тогда
		Возврат Тип("СправочникСсылка.Контроль");

	ИначеЕсли ТипМобильного = ТипПроцессИсполнения() Тогда
		Возврат Тип("БизнесПроцессСсылка.Исполнение");

	ИначеЕсли ТипМобильного = ТипПроцессРВВЗ() Тогда
		Возврат Тип("БизнесПроцессСсылка.РешениеВопросовВыполненияЗадач");

	ИначеЕсли ТипМобильного = ТипУчетнаяЗаписьЭлектроннойПочты()Тогда
		Возврат Тип("СправочникСсылка.УчетныеЗаписиЭлектроннойПочты");

	ИначеЕсли ТипМобильного = ТипПапкаПисем() Тогда
		Возврат Тип("СправочникСсылка.ПапкиПисем");

	ИначеЕсли ТипМобильного = ТипПользователь() Тогда
		Возврат Тип("СправочникСсылка.Пользователи");

	ИначеЕсли ТипМобильного = ТипРольИсполнителя() Тогда
		Возврат Тип("СправочникСсылка.РолиИсполнителей");

	ИначеЕсли ТипМобильного = ТипФайл() Тогда
		Возврат Тип("СправочникСсылка.Файлы");

	ИначеЕсли ТипМобильного = ТипВеткаПереписки() Тогда
		Возврат Тип("СправочникСсылка.ВеткиПереписки");
		
	ИначеЕсли ТипМобильного = ТипАдресат() Тогда
		Возврат Тип("СправочникСсылка.АдресатыПочтовыхСообщений");
		
	ИначеЕсли ТипМобильного = ТипШаблонТекста() Тогда
		Возврат Тип("СправочникСсылка.ШаблоныТекстов");

	ИначеЕсли ТипМобильного = ТипМоеОтсутствие() Тогда
		Возврат Тип("ДокументСсылка.Отсутствие");
		
	ИначеЕсли ТипМобильного = ТипМоеСамочувствие() Тогда
		Возврат Тип("ДокументСсылка.СамочувствиеСотрудника");
			
	КонецЕсли;

КонецФункции

// Получает ссылку на объект указанного типа
//
// Параметры:
//  ТипМобильного		 - Строка - Описатель типа объекта
//  СтрокаИдентификатора - Строка, УникальныйИдентификатор - идентификатор объекта.
// 
// Возвращаемое значение:
//  Ссылка - ссылка на объект 
//
Функция СсылкаПоТипуИИД(ТипМобильного, СтрокаИдентификатора) Экспорт

	Если Не ЗначениеЗаполнено(ТипМобильного) Или Не ЗначениеЗаполнено(СтрокаИдентификатора) Тогда
		Возврат Неопределено;
	КонецЕсли;

	Если ТипЗнч(СтрокаИдентификатора) = Тип("Строка") Тогда
		Идентификатор = Новый УникальныйИдентификатор(СтрокаИдентификатора)
	ИначеЕсли ТипЗнч(СтрокаИдентификатора) = Тип("УникальныйИдентификатор") Тогда
		Идентификатор = СтрокаИдентификатора;
	КонецЕсли;

	Если Идентификатор = УникальныйИдентификаторПустой() Тогда
		Возврат Неопределено;
	КонецЕсли;

	Если ТипМобильного = ТипРольИсполнителя() Тогда
		Возврат Справочники.ПолныеРоли.НайтиСоздатьПолнуюРоль(
			Справочники.РолиИсполнителей.ПолучитьСсылку(Идентификатор),
			Неопределено,
			Неопределено)
	КонецЕсли;
	
	ТипСсылки = ТипСсылкиПоТипуМобильного(ТипМобильного);
	XMLТип = XMLТип(ТипСсылки);
	
	Если XMLТип = Неопределено Или XMLТип.ИмяТипа = "Null" Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	КлассИТип = СтрРазделить(XMLТип.ИмяТипа, ".");
	Класс = КлассИТип[0];
	ИмяТипа = КлассИТип[1];
	
	Если СтрСравнить(Класс, "DocumentRef") = 0 Тогда
		
		Возврат Документы[ИмяТипа].ПолучитьСсылку(Идентификатор);
		
	ИначеЕсли СтрСравнить(Класс, "CatalogRef") = 0 Тогда
		
		Возврат Справочники[ИмяТипа].ПолучитьСсылку(Идентификатор);
		
	ИначеЕсли СтрСравнить(Класс, "TaskRef") = 0 Тогда
		
		Возврат Задачи[ИмяТипа].ПолучитьСсылку(Идентификатор);
		
	ИначеЕсли СтрСравнить(Класс, "BusinessProcessRef") = 0 Тогда
		
		Возврат БизнесПроцессы[ИмяТипа].ПолучитьСсылку(Идентификатор);
		
	КонецЕсли;
	 
КонецФункции

Функция ТипСобытиеПротокола() Экспорт
	
	Возврат "LogRecord";
	
КонецФункции

Функция ТипПисьмо() Экспорт
	
	Возврат "Mail";
	
КонецФункции

Функция ТипПисьмоСодержимое() Экспорт
	
	Возврат "MailContent";
	
КонецФункции

Функция ТипПисьмоЗаголовки() Экспорт
	
	Возврат "MailHeaders";
	
КонецФункции

Функция ТипВходящееПисьмо() Экспорт
	
	Возврат "IncomingMail"
	
КонецФункции

Функция ТипИсходящееПисьмо() Экспорт
	
	Возврат "OutgoingMail"
	
КонецФункции

функция ТипЗадача() Экспорт
	
	возврат "Task"

КонецФункции

Функция ТипЗадачаЗаголовки() Экспорт
	
	Возврат "TaskHeaders";
	
КонецФункции

Функция ТипЗадачаСодержимое() Экспорт
	
	Возврат "TaskContent";
	
КонецФункции

Функция ТипПроцессИсполнения() Экспорт
	
	Возврат "ExecutionProcess";
	
КонецФункции

Функция ТипПроцессИсполненияЗаголовки() Экспорт
	
	Возврат "ExecutionProcessHeaders";
	
КонецФункции

Функция ТипПроцессИсполненияСодержимое() Экспорт
	
	Возврат "ExecutionProcessContent";
	
КонецФункции

Функция ТипЗаписьКалендаря() Экспорт
	
	Возврат "CalendarEvent";
	
КонецФункции

Функция ТипКонтроль() Экспорт
	
	Возврат "Control";
	
КонецФункции

Функция ТипУчетнаяЗаписьЭлектроннойПочты() Экспорт
	
	Возврат "MailAccount";
	
КонецФункции

Функция ТипПапкаПисем() Экспорт
	
	Возврат "MailFolder";
	
КонецФункции

Функция ТипПользователь() Экспорт
	
	Возврат "User";
	
КонецФункции

Функция ТипРольИсполнителя() Экспорт
	
	Возврат "Role";
	
КонецФункции

Функция ТипФайл() Экспорт
	
	Возврат "File";
	
КонецФункции

Функция ТипВеткаПереписки() Экспорт
	
	Возврат "MailThread";
	
КонецФункции

Функция ТипАдресат() Экспорт
	
	Возврат "Addressee";
	
КонецФункции

Функция ТипШаблонТекста() Экспорт
	
	Возврат "MailTextTemplate";
	
КонецФункции

Функция ТипПредметПроцесса() Экспорт
	
	Возврат "ActionTarget";
	
КонецФункции

Функция ТипИнформацияОКлиенте() Экспорт
	
	Возврат "ClientInfo";
	
КонецФункции

Функция ТипПодключениеНовогоУстройства() Экспорт
	
	Возврат "Connect";
	
КонецФункции

Функция ТипОтключениеУстройства() Экспорт
	
	Возврат "Disconnect";
	
КонецФункции

Функция ТипКлючеваяОперация() Экспорт
	
	Возврат "KeyOperation";
	
КонецФункции

Функция ТипНастройкиСинхронизации() Экспорт
	
	Возврат "SynchronizationSettings";

КонецФункции

Функция ТипМоеОтсутствие() Экспорт
	
	Возврат "MyAbsence";

КонецФункции

Функция ТипВидОтсутствия() Экспорт
	
	Возврат "TypeOfAbsence";
	
КонецФункции

Функция ТипМоеСамочувствие() Экспорт
	
	Возврат "MyHealth";
	
КонецФункции

функция ТипВремяЗамера() Экспорт
	
	Возврат "MeasurementTime"
	
КонецФункции

функция ТипМероприятие() Экспорт
	
	Возврат "Event";
	
КонецФункции

Функция ТипСостояниеПриглашения() Экспорт
	
	Возврат "InvitationStatus";
	
КонецФункции

Функция ТипТрудозатраты() Экспорт
	
	Возврат "WorkTimeRecords";
	
КонецФункции

функция ТипВидРабот() Экспорт
	
	Возврат "KindOfWorks";
	
КонецФункции

Функция ТипЕжедневныйОтчет() Экспорт
	
	Возврат "DailyReport";
	
Конецфункции

Функция ТипЕженедельныйОтчет() Экспорт
	
	Возврат "WeeklyReport";
	
Конецфункции

функция ТипПроект() Экспорт
	
	Возврат "Project";
	
КонецФункции

Функция ТипПроцессРВВЗ() Экспорт
	
	Возврат "IssueSolvingProcess";
	
КонецФункции

Функция ТипПроцессРВВЗЗаголовки() Экспорт 
	
	возврат "IssueSolvingProcessHeaders";
	
КонецФункции 	

Функция ТипПроцессРВВЗСодержимое() Экспорт 
	
	возврат "IssueSolvingProcessContent";
	
КонецФункции

Функция ТипыДанныхPUSH() Экспорт
	
	ПоддерживаемыеТипы = 
		Метаданные.РегистрыСведений.СообщенияPushУведомлений.Измерения.ОбъектУведомления.Тип.Типы();
	
	Возврат ПоддерживаемыеТипы;
	
КонецФункции

// Возвращает закешированный массив поддерживаемых типов ссылочных данных мобильного приложения
//
// Возвращаемое значение:
//  Массив - Массив поддерживаемых типов ссылочных данных
//
Функция ПоддерживаемыеТипыСсылочныхДанных() Экспорт
	
	Возврат Метаданные.ОпределяемыеТипы.МП_Ссылки.Тип.Типы(); 

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Получает флаг объекта по представлению идентификатора
//
// Параметры:
//  Представление - Строка -  Идентификатор перечисления в обмене.
// 
// Возвращаемое значение:
//  Перечисления.ФлагиОбъектов - Значение перечисление полученное по идентификатору.
//
Функция ФлагПоПредставлению(Представление) Экспорт

	Если СтрСравнить(Представление, "red") = 0 Тогда
		Флаг = Перечисления.ФлагиОбъектов.Красный;

	ИначеЕсли СтрСравнить(Представление, "blue") = 0 Тогда
		Флаг = Перечисления.ФлагиОбъектов.Синий;

	ИначеЕсли СтрСравнить(Представление, "yellow") = 0 Тогда
		Флаг = Перечисления.ФлагиОбъектов.Желтый;

	ИначеЕсли СтрСравнить(Представление, "green") = 0 Тогда
		Флаг = Перечисления.ФлагиОбъектов.Зеленый;

	ИначеЕсли СтрСравнить(Представление, "orange") = 0 Тогда
		Флаг = Перечисления.ФлагиОбъектов.Оранжевый;

	ИначеЕсли СтрСравнить(Представление, "purple") = 0 Тогда
		Флаг = Перечисления.ФлагиОбъектов.Лиловый;

	Иначе
		Флаг = Перечисления.ФлагиОбъектов.ПустаяСсылка();
	КонецЕсли;
	
	Возврат Флаг;
	
КонецФункции

//Возвращает возможность поддержки push оповещений
функция PUSHЧерезFCM() Экспорт
	
	СистемнаяИнформация = Новый СистемнаяИнформация;
	
	Возврат Число(СтрЗаменить(Лев(СистемнаяИнформация.ВерсияПриложения, 7), ".", "")) > 8312
	
КонецФункции

Функция ПустаяТаблицаЗаписейОТрудозатратах() Экспорт
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("Пользователь");
	Таблица.Колонки.Добавить("ДатаДобавления");
	Таблица.Колонки.Добавить("НомерДобавления");
	
	Возврат Таблица;
	
КонецФункции

Функция ВерсияБольше2_2_5(МобильноеПриложение) Экспорт
	
	ВерсияЧисло = СтрокаВерсииВЧисло(МобильноеПриложение);
	Если ВерсияЧисло = 0 Тогда
		Возврат Истина;
	КонецЕсли;

	Возврат ВерсияЧисло > 225;
	
КонецФункции

Функция СтрокаВерсииВЧисло(МобильноеПриложение)
	
	ВерсияСтрока = СтрЗаменить(
		Лев(РегистрыСведений.СведенияОМобильныхКлиентах.ВерсияМобильногоПриложения(МобильноеПриложение), 5), ".", "");
		
	ВерсияЧисло = 0;
	
	Если ЗначениеЗаполнено(ВерсияСтрока) Тогда
		
		ВерсияЧисло = Число(ВерсияСтрока);
		
	КонецЕсли;
	
	Возврат ВерсияЧисло;
	
КонецФункции

Функция ИспользуетсяПриложение21() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Мобильный.Ссылка КАК Ссылка
		|ИЗ
		|	ПланОбмена.Мобильный КАК Мобильный
		|ГДЕ
		|	НЕ Мобильный.ЭтотУзел
		|	И НЕ Мобильный.ПометкаУдаления";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции
#КонецОбласти