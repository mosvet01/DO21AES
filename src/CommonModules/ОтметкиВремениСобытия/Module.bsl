#Область ОбработчикиСобытий

// Ссылочные объекты при записи.
// 
// Параметры:
//  Источник - ОпределяемыйТип.ОтметкиВремениСсылочныеОбъекты - записываемый объект.
//  Отказ - Булево - Отказ
Процедура СсылочныеОбъектыПриЗаписи(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьОтметкиВремени")
		Или Источник.ДополнительныеСвойства.Свойство("ОтключитьОтметкиВремени") Тогда
		Возврат;
	КонецЕсли;
	
	Набор = РегистрыСведений["ОтметкиВремениОчередь" + ТекущийНомерОчереди(Источник)].СоздатьНаборЗаписей();
		
	Запись = Набор.Добавить();
	Запись.Ключ = Источник.Ссылка;
	Запись.Объект = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ТипЗнч(Источник));
	Запись.ТипКлюча = 0;
	
	Источник.ДополнительныеСвойства.Свойство("ОтметкиВремениИсточник", Запись.Источник);
	Источник.ДополнительныеСвойства.Свойство("ОтметкиВремениВладелец", Запись.Владелец);
	Если Источник.ДополнительныеСвойства.Свойство("ОтметкаВремени") Тогда
		Запись.Отметка = Источник.ДополнительныеСвойства.ОтметкаВремени;
	Иначе
		Запись.Отметка = ТекущаяУниверсальнаяДатаВМиллисекундах();
	КонецЕсли;
					
	Набор.Отбор.Отметка.Установить(Запись.Отметка);
	Набор.Отбор.Ключ.Установить(Запись.Ключ);
	Набор.Отбор.Объект.Установить(Запись.Объект);
	
	ОтметкиВремени.ЗаполнитьРежимЗаписи(Набор);
		
	Набор.Записать();
			
КонецПроцедуры

// Ссылочные объекты перед удалением.
// 
// Параметры:
//  Источник - ОпределяемыйТип.ОтметкиВремениСсылочныеОбъекты - записываемый объект.
//  Отказ - Булево - Отказ
Процедура СсылочныеОбъектыПередУдалением(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьОтметкиВремени")
		Или Источник.ДополнительныеСвойства.Свойство("ОтключитьОтметкиВремени") Тогда
		Возврат;
	КонецЕсли;
		
	Набор = РегистрыСведений["ОтметкиВремениОчередь" + ТекущийНомерОчереди(Источник)].СоздатьНаборЗаписей();
		
	Запись = Набор.Добавить();
	Запись.Ключ = Источник.Ссылка;
	Запись.Объект = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ТипЗнч(Источник));
	Запись.ТипКлюча = 0;
	Запись.Удаление = Истина;
	
	Источник.ДополнительныеСвойства.Свойство("ОтметкиВремениИсточник", Запись.Источник);
	Источник.ДополнительныеСвойства.Свойство("ОтметкиВремениВладелец", Запись.Владелец);
	Если Источник.ДополнительныеСвойства.Свойство("ОтметкаВремени") Тогда
		Запись.Отметка = Источник.ДополнительныеСвойства.ОтметкаВремени;
	Иначе
		Запись.Отметка = ТекущаяУниверсальнаяДатаВМиллисекундах();
	КонецЕсли;
	
	Набор.Отбор.Отметка.Установить(Запись.Отметка);
	Набор.Отбор.Ключ.Установить(Запись.Ключ);
	Набор.Отбор.Объект.Установить(Запись.Объект);
	
	ОтметкиВремени.ЗаполнитьРежимЗаписи(Набор);
		
	Набор.Записать();
	
КонецПроцедуры

// Регистры перед записью.
// 
// Параметры:
//  Источник - ОпределяемыйТип.ОтметкиВремениРегистры - записываемый объект.
//  Отказ - Булево - Отказ
//  Замещение - Булево - Замещение
Процедура РегистрыПередЗаписью(Источник, Отказ, Замещение) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьОтметкиВремени")
		Или Источник.ДополнительныеСвойства.Свойство("ОтключитьОтметкиВремени")
		Или Не Замещение И Источник.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Набор = РегистрыСведений["ОтметкиВремениОчередь" + ТекущийНомерОчереди(Источник)].СоздатьНаборЗаписей();
		
	Запись = Набор.Добавить();
	Запись.Объект = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ТипЗнч(Источник));
	Запись.Удаление = Источник.Количество() = 0;
	
	Источник.ДополнительныеСвойства.Свойство("ОтметкиВремениИсточник", Запись.Источник);
	Источник.ДополнительныеСвойства.Свойство("ОтметкиВремениВладелец", Запись.Владелец);
	Если Источник.ДополнительныеСвойства.Свойство("ОтметкаВремени") Тогда
		Запись.Отметка = Источник.ДополнительныеСвойства.ОтметкаВремени;
	Иначе
		Запись.Отметка = ТекущаяУниверсальнаяДатаВМиллисекундах();
	КонецЕсли;
	
	ПолнотаОтбора = ПолнотаОтбораНабораЗаписей(Источник);
	
	Если ПолнотаОтбора = 0 Тогда
		//Нет отборов. Регистр без измерений.
		Запись.Ключ = Справочники.ИдентификаторыОбъектовМетаданных.ПустаяСсылка();
		Запись.ТипКлюча = 8;
		
	ИначеЕсли Источник.Отбор[0].Имя = "Регистратор" Тогда
		//По регистратору.
		Запись.Ключ = Источник.Отбор[0].Значение;
		Запись.ТипКлюча = 1;
		
	ИначеЕсли Источник.ВыгрузитьКолонки().Колонки.Найти("ОтметкиВремениИдентификаторНабораЗаписей") <> Неопределено Тогда
		//Общий реквизиит.
		
		Запись.ТипКлюча = 3;
		Если Запись.Удаление Тогда
			Если ПолнотаОтбора > 1 Тогда
				Запись.ИдентификаторКлюча = ИдентификаторНабораЗаписей(Источник);
				Запись.ЗначенияКлюча = Новый ХранилищеЗначения(Источник.Отбор, Новый СжатиеДанных(9));
			КонецЕсли;
		ИначеЕсли ПолнотаОтбора = 3 Тогда
			//Запись или удаление уникальной записи.
			Запись.ИдентификаторКлюча = ИдентификаторНабораЗаписей(Источник);
			Для Каждого Строка Из Источник Цикл
				Строка.ОтметкиВремениИдентификаторНабораЗаписей = Запись.ИдентификаторКлюча;
			КонецЦикла;
		Иначе
			Запись.ИдентификаторКлюча = ЗаполнитьИдентификаторНабораЗаписей(Источник, Замещение);
			Если Источник.Количество() > 1 Тогда
				ВсеКлючи = Источник.Выгрузить(,"ОтметкиВремениИдентификаторНабораЗаписей");
				ВсеКлючи.Свернуть("ОтметкиВремениИдентификаторНабораЗаписей");
				ВсеКлючи.Колонки.Вставить(0, "Ключ");
				Для Каждого Строка Из ВсеКлючи Цикл
					Строка[0] = Справочники.ИдентификаторыОбъектовМетаданных.ПолучитьСсылку(Строка[1]);
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		Запись.Ключ = Справочники.ИдентификаторыОбъектовМетаданных.ПолучитьСсылку(Запись.ИдентификаторКлюча);
			
	Иначе //Вердущее измерение ссылочного типа.
		
		Запись.ТипКлюча = 2;
		ПолеКлюча = Неопределено;
			
		Если Источник.Отбор[0].Имя <> "Период" Тогда
			ПолеКлюча = Источник.Отбор[0].Имя;
		ИначеЕсли Источник.Отбор.Количество() > 1 Тогда
			ПолеКлюча = Источник.Отбор[1].Имя;
		Иначе
			ВызватьИсключение СтрШаблон("%1 %2 ""%3"" [%4]",
				НСтр("ru = 'Не найдено ключевое поле'"),
				НСтр("ru = 'для'"),
				Запись.Объект,
				Источник.Отбор);
		КонецЕсли;
		
		Если Источник.Отбор[ПолеКлюча].Использование Тогда
			Ключ = Источник.Отбор[ПолеКлюча].Значение;
			
		ИначеЕсли Источник.Количество() = 1 Тогда
			Ключ = Источник[0][ПолеКлюча];
			
		ИначеЕсли Источник.Количество() > 1 Тогда
			ВсеКлючи = Источник.Выгрузить(,ПолеКлюча);
			ВсеКлючи.Свернуть(ПолеКлюча);
			Ключ = ВсеКлючи[0][0];
			
		ИначеЕсли ПолнотаОтбора = 1 Тогда
			Ключ = Справочники.ИдентификаторыОбъектовМетаданных.ПустаяСсылка();
			
		Иначе
			ВызватьИсключение СтрШаблон("%1 ""%5"" %2 ""%3"" [%4]",
				НСтр("ru = 'Не найдено значение ключевого поля'"),
				НСтр("ru = 'для'"),
				Запись.Объект,
				Источник.Отбор,
				ПолеКлюча);
		КонецЕсли;	
		
		Запись.Ключ = Ключ;
		
		Если Запись.Ключ <> Ключ Тогда
			ВызватьИсключение СтрШаблон("%1 ""%5"" %2 ""%3"" [%4]",
				НСтр("ru = 'Недопустимый тип значения ключевого поля'"),
				НСтр("ru = 'для'"),
				Запись.Объект,
				Источник.Отбор,
				ПолеКлюча);
		КонецЕсли;
		
	КонецЕсли;
				
	Набор.Отбор.Отметка.Установить(Запись.Отметка);
	Набор.Отбор.Ключ.Установить(Запись.Ключ);
	Набор.Отбор.Объект.Установить(Запись.Объект);
	
	ОтметкиВремени.ЗаполнитьРежимЗаписи(Набор);
		
	Набор.Записать();
	
	Если ЗначениеЗаполнено(ВсеКлючи) Тогда
		Для Каждого Строка Из ВсеКлючи Цикл
			Если Строка[0] <> Запись.Ключ Тогда
				Запись.Ключ = Строка[0];
				Если Запись.ТипКлюча = 3 Тогда
					Запись.ИдентификаторКлюча = Запись.Ключ.УникальныйИдентификатор(); 	
				КонецЕсли;
				Набор.Отбор.Ключ.Значение = Запись.Ключ;
				Набор.Записать();
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если Замещение И Не Запись.Удаление И ПолнотаОтбора <> 3 И ПолнотаОтбора <> 0 Тогда
		//Замещение множества записей. Отмечаем общий ключ удаления.
		Если ПолнотаОтбора = 1 Тогда
			Запись.Ключ = Справочники.ИдентификаторыОбъектовМетаданных.ПустаяСсылка();
			Запись.ИдентификаторКлюча = Неопределено;
			Запись.Удаление = Истина;
			Набор.Отбор.Ключ.Значение = Запись.Ключ;
			Набор.Записать();
		ИначеЕсли Запись.ТипКлюча = 3 Тогда
			Запись.ИдентификаторКлюча = ИдентификаторНабораЗаписей(Источник);
			Запись.Ключ = Справочники.ИдентификаторыОбъектовМетаданных.ПолучитьСсылку(Запись.ИдентификаторКлюча);
			Запись.Удаление = Истина;
			Запись.ЗначенияКлюча = Новый ХранилищеЗначения(Источник.Отбор, Новый СжатиеДанных(9));
			Набор.Отбор.Ключ.Значение = Запись.Ключ;
			Набор.Записать();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Константы при записи.
// 
// Параметры:
//  Источник - ОпределяемыйТип.ОтметкиВремениКонстанты - записываемый объект.
//  Отказ - Булево - Отказ
Процедура КонстантыПриЗаписи(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьОтметкиВремени")
		Или Источник.ДополнительныеСвойства.Свойство("ОтключитьОтметкиВремени") Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Набор = РегистрыСведений["ОтметкиВремениОчередь" + ТекущийНомерОчереди(Источник)].СоздатьНаборЗаписей();
		
	Запись = Набор.Добавить();
	Запись.Ключ = Справочники.ИдентификаторыОбъектовМетаданных.ПустаяСсылка();
	Запись.Объект = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ТипЗнч(Источник));
	Запись.ТипКлюча = 9;
	
	Источник.ДополнительныеСвойства.Свойство("ОтметкиВремениИсточник", Запись.Источник);
	Источник.ДополнительныеСвойства.Свойство("ОтметкиВремениВладелец", Запись.Владелец);
	Если Источник.ДополнительныеСвойства.Свойство("ОтметкаВремени") Тогда
		Запись.Отметка = Источник.ДополнительныеСвойства.ОтметкаВремени;
	Иначе
		Запись.Отметка = ТекущаяУниверсальнаяДатаВМиллисекундах();
	КонецЕсли;
			
	Набор.Отбор.Отметка.Установить(Запись.Отметка);
	Набор.Отбор.Ключ.Установить(Запись.Ключ);
	Набор.Отбор.Объект.Установить(Запись.Объект);
	
	ОтметкиВремени.ЗаполнитьРежимЗаписи(Набор);
	Набор.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Необходимость регистрации отметки при записи источника
//
// Параметры:
//  Источник - записываемый объект. 
// 
// Возвращаемое значение:
//   - Булево
//
Функция Использование(Источник) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьОтметкиВремени")
		Или Источник.ДополнительныеСвойства.Свойство("ОтключитьОтметкиВремени") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Программная запись отметки по ссылке на объект.
//
// Параметры:
//  Ссылка	 - ЛюбаяСсылка - ссылка на объект.
//  Источник - РегистрСведений.ОтметкиВремениСсылочныхОбъектов.Источник - ссылка на узел-источник.
//  Владелец - РегистрСведений.ОтметкиВремениСсылочныхОбъектов.Владелец - ссылка на группировочный объект. 
//  Отметка	 - Число - фиксированная отметка времени.
//
Процедура ОтметитьСсылочныйОбъект(Ссылка, Источник = Неопределено, Владелец = Неопределено, Отметка = Неопределено) Экспорт
		
	Набор = РегистрыСведений["ОтметкиВремениОчередь" + ТекущийНомерОчереди(Источник)].СоздатьНаборЗаписей();
		
	Запись = Набор.Добавить();
	Запись.Ключ = Ссылка;
	Запись.Объект = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ТипЗнч(Ссылка));
	Запись.ТипКлюча = 0;
	
	Запись.Источник = Источник;
	Запись.Владелец = Владелец;
	Если ЗначениеЗаполнено(Отметка) Тогда
		Запись.Отметка = Отметка;
	Иначе
		Запись.Отметка = ТекущаяУниверсальнаяДатаВМиллисекундах();
	КонецЕсли;
					
	Набор.Отбор.Отметка.Установить(Запись.Отметка);
	Набор.Отбор.Ключ.Установить(Запись.Ключ);
	Набор.Отбор.Объект.Установить(Запись.Объект);
	
	ОтметкиВремени.ЗаполнитьРежимЗаписи(Набор);
		
	Набор.Записать();
			
КонецПроцедуры

#КонецОбласти

#Область Служебные

// Текущий номер очереди.
// 
// Параметры:
//  Источник - СправочникОбъект.ВидыБизнесСобытий, КонстантаМенеджерЗначения.ВидЦитированияПриОтветеВФорматеHTML, БизнесПроцессОбъект.РешениеВопросовВыполненияЗадач, СправочникОбъект.ШаблоныПодписания, СправочникОбъект.Организации, ПланВидовХарактеристикОбъект.ДополнительныеРеквизитыИСведения, БизнесПроцессОбъект.Ознакомление, СправочникОбъект.Мероприятия, СправочникОбъект.МестаХраненияДел, СправочникОбъект.СтраныМира, СправочникОбъект.ШаблоныСогласования, БизнесПроцессОбъект.КомплексныйПроцесс, КонстантаМенеджерЗначения.ИспользоватьПолнотекстовыйПоиск, РегистрСведенийНаборЗаписей.АвтоматическиеОтветыПоАдресам, ДокументОбъект.ТранспортныйКонтейнерЭДО, СправочникОбъект.ДополнительныеОтчетыИОбработки, ДокументОбъект.Отсутствие, СправочникОбъект.ДетекторыБизнесСобытий, КонстантаМенеджерЗначения.СокращатьИнформациюОПисьмеПриОтвете, КонстантаМенеджерЗначения.ХранитьФайлыВТомахНаДиске, КонстантаМенеджерЗначения.СписокРасширенийТекстовыхФайлов, КонстантаМенеджерЗначения.ДлинаТемыОтветногоПисьма, КонстантаМенеджерЗначения.ПользовательРегламентногоЗаданияПолучениеИОтправкаПисем, КонстантаМенеджерЗначения.OAuth_redirection_uri, РегистрСведенийНаборЗаписей.ФайлыНаРаспознавании, СправочникОбъект.Контроль, СправочникОбъект.НоменклатураКонтрагентов, СправочникОбъект.ПапкиПроектов, СправочникОбъект.ЗначенияСвойствОбъектовИерархия, СправочникОбъект.ШаблоныРассмотрения, РегистрСведенийНаборЗаписей.ПапкиПисемЧастоИспользуемые, РегистрСведенийНаборЗаписей.РазмерыФайловТомов, КонстантаМенеджерЗначения.ОчищатьДанныеДляПолнотекстовогоПоиска, РегистрСведенийНаборЗаписей.ОбъектыДляАвтоподбораАдресатов, КонстантаМенеджерЗначения.ОтветственныйЗаУдалениеНеактивныхВерсий, РегистрСведенийНаборЗаписей.ИдентификаторыПочтовыхВложений, СправочникОбъект.УчетныеЗаписиЭлектроннойПочты, КонстантаМенеджерЗначения.КаталогСохраненияКопииПротоколаРаботыПользователейWindows, ДокументОбъект.ЭлектронныйДокументВходящийЭДО, СправочникОбъект.ПапкиДокументов, КонстантаМенеджерЗначения.СписокЗапрещенныхРасширений, РегистрСведенийНаборЗаписей.НастройкиОповещенийОПисьмах, РегистрСведенийНаборЗаписей.ПисьмаВПроцессеОтправкиПоВнутреннейМаршрутизации, СправочникОбъект.ВопросыОбращений, СправочникОбъект.ВидыОтсутствий, РегистрСведенийНаборЗаписей.УсловияОповещенийОПисьмах, СправочникОбъект.ШаблоныКомплексныхБизнесПроцессов, СправочникОбъект.РолиИсполнителей, РегистрСведенийНаборЗаписей.ПапкиПисемБыстрогоДоступа, РегистрСведенийНаборЗаписей.ФайлыВРабочемКаталогеКомпьютера, БизнесПроцессОбъект.Рассмотрение, СправочникОбъект.ВнешниеКомпоненты, КонстантаМенеджерЗначения.ИспользоватьВнутреннююМаршрутизацию, РегистрСведенийНаборЗаписей.ВложенныеПисьма, СправочникОбъект.ЛичныеАдресаты, ДокументОбъект.ИсходящееСообщениеСВД, РегистрСведенийНаборЗаписей.ФайлыКУдалению, СправочникОбъект.ГруппыКонтрольныхТочек, ДокументОбъект.ЕжедневныйОтчет, КонстантаМенеджерЗначения.ВестиПротоколДоставкиПочты, КонстантаМенеджерЗначения.СокращатьИнформациюОПисьмеПриПересылке, СправочникОбъект.ШаблоныПриглашения, РегистрСведенийНаборЗаписей.ДочерниеБизнесПроцессы, СправочникОбъект.ИсточникиДанных, БизнесПроцессОбъект.ОбработкаИсходящегоДокумента, СправочникОбъект.ГрифыДоступа, СправочникОбъект.ВизыСогласования, КонстантаМенеджерЗначения.МаксимальныйРазмерФайла, КонстантаМенеджерЗначения.ЗапрещатьЗагрузкуФайловПоРасширению, СправочникОбъект.ШаблоныТекстов, БизнесПроцессОбъект.Исполнение, ДокументОбъект.СамочувствиеСотрудника, КонстантаМенеджерЗначения.РасположениеШтампаЭПВPdf, СправочникОбъект.Контрагенты, КонстантаМенеджерЗначения.ЧислоАдресатовДляКраткогоПредставления, КонстантаМенеджерЗначения.СрокХраненияПротоколаДоставкиПочты, СправочникОбъект.Виджеты, ПланВидовХарактеристикОбъект.ОбъектыАдресацииЗадач, СправочникОбъект.ГруппыДоступаФизическихЛиц, РегистрСведенийНаборЗаписей.ДанныеБизнесПроцессов, РегистрСведенийНаборЗаписей.ОчередьВерсийФайловДляРазмещенийВТомах, СправочникОбъект.ГруппыКонтактовПользователей, СправочникОбъект.ВнешниеПользователи, БизнесПроцессОбъект.Регистрация, ДокументОбъект.ВыгрузкаВССТУ, СправочникОбъект.ШаблоныРегистрации, РегистрСведенийНаборЗаписей.ДатыЗакрытияКарточекПисем, СправочникОбъект.ДелегированиеПрав, СправочникОбъект.ПравилаАвтозаполненияФайлов, СправочникОбъект.Номенклатура, СправочникОбъект.ГруппыВнешнихПользователей, КонстантаМенеджерЗначения.ИзменениеФайловMSWordТолькоНаСервере, СправочникОбъект.ПредметыИнструкций, ДокументОбъект.ЕженедельныйОтчет, СправочникОбъект.ВидыМероприятий, СправочникОбъект.Инструкции, РегистрСведенийНаборЗаписей.НеполученныеВходящиеПисьма, СправочникОбъект.ВеткиПереписки, БизнесПроцессОбъект.ОбработкаВходящегоДокумента, КонстантаМенеджерЗначения.ИспользоватьРаспознавание, РегистрСведенийНаборЗаписей.РезультатыПотоковогоСканирования, КонстантаМенеджерЗначения.СрокХраненияНеактивныхВерсий, СправочникОбъект.ГруппыДоступаКонтрагентов, ДокументОбъект.СправкиСПАРКРиски, СправочникОбъект.ШаблоныПисем, РегистрСведенийНаборЗаписей.СлужебныеФайлыДокументов, РегистрСведенийНаборЗаписей.БезопасноеХранилищеДанныхОбластейДанных, РегистрСведенийНаборЗаписей.СведенияОФайлах, ДокументОбъект.ВходящееПисьмо, РегистрСведенийНаборЗаписей.НастройкаШаблоновБизнесПроцессов, СправочникОбъект.ДелаХраненияДокументов, БизнесПроцессОбъект.ОбработкаВнутреннегоДокумента, КонстантаМенеджерЗначения.ПериодХраненияHTMLПредставленияСодержанияПисем, СправочникОбъект.ШаблоныКонтрольныхТочек, ЗадачаОбъект.ЗадачаИсполнителя, КонстантаМенеджерЗначения.КаталогСохраненияКопииПротоколаРаботыПользователейLinux, ПланВидовХарактеристикОбъект.ВидыДоступа, СправочникОбъект.Должности, СправочникОбъект.ПапкиМероприятий, СправочникОбъект.ПапкиПоиска, КонстантаМенеджерЗначения.АвтовизуализацияЭП, РегистрСведенийНаборЗаписей.ОповещенияОПисьмах, СправочникОбъект.ВидыСостоянийДокументовВСВД, РегистрСведенийНаборЗаписей.НастройкиАвтоочисткиПапокПисем, РегистрСведенийНаборЗаписей.СведенияОФайлахДокументооборот, РегистрСведенийНаборЗаписей.СтатистикаОтправкаPushУведомлений, СправочникОбъект.ТомаХраненияФайлов, СправочникОбъект.Пользователи, СправочникОбъект.Валюты, КонстантаМенеджерЗначения.ИспользоватьШифрование, ДокументОбъект.Бронь, РегистрСведенийНаборЗаписей.ПодготовленныеКОтправкеПисьма, РегистрСведенийНаборЗаписей.НеотправленныеИсходящиеПисьма, РегистрСведенийНаборЗаписей.ИспользованиеШаблонов, РегистрСведенийНаборЗаписей.СведенияОПрочтении, КонстантаМенеджерЗначения.СотрудникДляЗаданияРаспознавания, КонстантаМенеджерЗначения.OAuthАвторизацияКлиент, СправочникОбъект.ПравилаРазмещенияФайловВТомах, ДокументОбъект.ПередачаДелВАрхив, ДокументОбъект.ЭлектронныйДокументИсходящийЭДО, КонстантаМенеджерЗначения.ПомечатьКаждуюСтрокуИсходногоПисьмаПриПересылке, КонстантаМенеджерЗначения.СписокРасширенийСканКопийОригиналов, РегистрСведенийНаборЗаписей.ФайлыСозданныеПоШаблону, СправочникОбъект.ДокументыПредприятия, СправочникОбъект.ПапкиФайлов, ДокументОбъект.УничтожениеДел, СправочникОбъект.СпискиАдресовЭлектроннойПочты, СправочникОбъект.ПравилаАвтоматическойКатегоризацииДанных, КонстантаМенеджерЗначения.РазрешитьПеренаправлениеПисем, КонстантаМенеджерЗначения.РасширенияФайловДляПроверкиБлокировкиФайлаНаДиске, ДокументОбъект.ОтзывСогласияНаОбработкуПерсональныхДанных, СправочникОбъект.ЗаметкиДокументооборота, ДокументОбъект.ВходящееСообщениеСВД, СправочникОбъект.ГруппыДоступа, СправочникОбъект.ДескрипторыДоступаРегистров, СправочникОбъект.КонтактныеЛица, РегистрСведенийНаборЗаписей.БезопасноеХранилищеДанных, СправочникОбъект.ШаблоныДокументов, СправочникОбъект.ГрафикУчетаСамочувствияСотрудников, КонстантаМенеджерЗначения.ПутьКПрограммеКонвертацииPDF, ДокументОбъект.УдалитьПроизвольныйЭД, РегистрСведенийНаборЗаписей.РабочиеКаталогиФайловКомпьютера, СправочникОбъект.ПроектныеЗадачи, КонстантаМенеджерЗначения.ИспользоватьInternetExplorerДляПолученияТекстаИзHTML, КонстантаМенеджерЗначения.СрокХраненияБизнесСобытий, ДокументОбъект.СогласиеНаОбработкуПерсональныхДанных, СправочникОбъект.ВопросыДеятельности, СправочникОбъект.ШаблоныУтверждения, СправочникОбъект.Проекты, РегистрСведенийНаборЗаписей.СведенияОПользователяхДокументооборот, РегистрСведенийНаборЗаписей.СведенияОПросмотреПисем, РегистрСведенийНаборЗаписей.ПапкиУчетныхЗаписей, БизнесПроцессОбъект.Утверждение, РегистрСведенийНаборЗаписей.ОбращенияКВерсиямФайлов, СправочникОбъект.ИменаПредметов, СправочникОбъект.БанковскиеСчета, СправочникОбъект.ВидыКонтактнойИнформации, ДокументОбъект.УведомлениеПоSMS, СправочникОбъект.ШаблоныИсполнения, БизнесПроцессОбъект.Согласование, СправочникОбъект.ВидыДокументовЭДО, СправочникОбъект.СпискиРассылкиПоКонтрагентам, РегистрСведенийНаборЗаписей.КоличествоПисемВПапках, РегистрСведенийНаборЗаписей.НаличиеФайлов, КонстантаМенеджерЗначения.ИспользоватьБизнесСобытия, РегистрСведенийНаборЗаписей.ОбращенияКОбъектам, РегистрСведенийНаборЗаписей.ПутиКПрограммамЭлектроннойПодписиИШифрованияНаСерверахLinux, СправочникОбъект.ВидыДокументов, КонстантаМенеджерЗначения.ИзвлекатьТекстыФайловНаСервере, СправочникОбъект.ВидыРабот, РегистрСведенийНаборЗаписей.ДвоичныеДанныеФайлов, КонстантаМенеджерЗначения.СокращатьПредставлениеАдресатов, СправочникОбъект.Нумераторы, СправочникОбъект.ФизическиеЛица, КонстантаМенеджерЗначения.ИспользуетсяРаспознаваниеПриПомощиСервиса, КонстантаМенеджерЗначения.ПрефиксДляПересылаемыхПисем, КонстантаМенеджерЗначения.СписокРасширенийФайловOpenDocument, СправочникОбъект.ТипыСвязей, КонстантаМенеджерЗначения.ПомечатьКаждуюСтрокуИсходногоПисьмаПриОтвете, РегистрСведенийНаборЗаписей.ПапкиПисемВиджетаПочта, КонстантаМенеджерЗначения.ИспользоватьХранилищеФайлов, КонстантаМенеджерЗначения.ИспользуетсяРаспознаваниеПриПомощиCuneiForm, СправочникОбъект.ВерсииФайлов, БизнесПроцессОбъект.Поручение, ДокументОбъект.СообщениеЭДО, КонстантаМенеджерЗначения.УдалятьНеактивныеВерсии, СправочникОбъект.ПапкиФорума, КонстантаМенеджерЗначения.ВыполнятьОтправкуВОтдельномРегламентномЗадании, РегистрСведенийНаборЗаписей.Штрихкоды, СправочникОбъект.ПравилаОбработкиПисем, РегистрСведенийНаборЗаписей.КодировкиФайлов, РегистрСведенийНаборЗаписей.ИдентификаторыПолученныхПисем, РегистрСведенийНаборЗаписей.ТемыЗаметокДокументооборота, РегистрСведенийНаборЗаписей.ПроверкаПоступленияНовыхПисем, СправочникОбъект.ПапкиПисем, СправочникОбъект.Файлы, БизнесПроцессОбъект.Приглашение, КонстантаМенеджерЗначения.ИспользоватьВстроеннуюПочту, ДокументОбъект.ИсходящееПисьмо, КонстантаМенеджерЗначения.ПрефиксДляОтветныхПисем, СправочникОбъект.ЗначенияСвойствОбъектов, КонстантаМенеджерЗначения.ОтображатьФотографииОбщаяНастройка, КонстантаМенеджерЗначения.МаксимальныйРазмерВнешнегоИсходящегоПисьма, КонстантаМенеджерЗначения.МаксимальноеЧислоПринимаемыхПисем, КонстантаМенеджерЗначения.СрокХраненияПротоколаРаботыПользователей, СправочникОбъект.ШаблоныПоручения, СправочникОбъект.ШаблоныОзнакомления, РегистрСведенийНаборЗаписей.ПодпискиНаПоказателиПроцессов, РегистрНакопленияНаборЗаписей.РабочееВремяСотрудников, КонстантаМенеджерЗначения.ПутьСохраненияУдаляемыхВерсийWindows, РегистрСведенийНаборЗаписей.ПисьмаВПапках, КонстантаМенеджерЗначения.ПутьСохраненияУдаляемыхВерсийLinux, СправочникОбъект.ГрафикиРаботы, РегистрСведенийНаборЗаписей.ИспользованиеПочты, СправочникОбъект.ЗаписиРабочегоКалендаря, СправочникОбъект.ВычисляемыеПараметрыЧатБота, РегистрСведенийНаборЗаписей.ИдентификаторыИмпортированныхПисем, СправочникОбъект.ВидыДокументовФизическихЛиц, РегистрСведенийНаборЗаписей.ПроизошедшиеБизнесСобытия, КонстантаМенеджерЗначения.ИспользоватьШтрихкоды, СправочникОбъект.ВидыПроектов, СправочникОбъект.НоменклатураДел, СправочникОбъект.Календари, КонстантаМенеджерЗначения.ВестиУчетСканКопийОригиналовДокументов, КонстантаМенеджерЗначения.ЯзыкРаспознавания, СправочникОбъект.АдресатыПочтовыхСообщений, КонстантаМенеджерЗначения.OAuthАвторизацияКлиентСекретныйКод, СправочникОбъект.ГруппыЛичныхАдресатов, СправочникОбъект.ШаблоныСоставныхБизнесПроцессов, СправочникОбъект.ДескрипторыДоступаОбъектов, РегистрСведенийНаборЗаписей.НастройкиОтложеннойОтправкиПисем, СправочникОбъект.СтруктураПредприятия, РегистрСведенийНаборЗаписей.ЗадачиСПодзадачами, КонстантаМенеджерЗначения.ИспользоватьImageMagickДляРаспознаванияPDF, РегистрСведенийНаборЗаписей.ПисьмаВеток, РегистрСведенийНаборЗаписей.ПолучателиУведомленийОПроблемах - Источник
// 
// Возвращаемое значение:
//  Число - Текущий номер очереди
Функция ТекущийНомерОчереди(Источник) Экспорт
	
	Возврат (НомерСоединенияИнформационнойБазы() % 3) + 1;
	
КонецФункции

// Идентификатор набора записей.
// 
// Параметры:
//  Источник - РегистрСведенийНаборЗаписей.ПапкиПисемВиджетаПочта, РегистрСведенийНаборЗаписей.ПодготовленныеКОтправкеПисьма, РегистрСведенийНаборЗаписей.НеотправленныеИсходящиеПисьма, РегистрСведенийНаборЗаписей.ДанныеБизнесПроцессов, РегистрСведенийНаборЗаписей.ИспользованиеШаблонов, РегистрСведенийНаборЗаписей.СведенияОПрочтении, РегистрСведенийНаборЗаписей.ОчередьВерсийФайловДляРазмещенийВТомах, РегистрСведенийНаборЗаписей.АвтоматическиеОтветыПоАдресам, РегистрСведенийНаборЗаписейИмяРегистраСведений, РегистрСведенийНаборЗаписей.Штрихкоды, РегистрСведенийНаборЗаписей.ФайлыСозданныеПоШаблону, РегистрСведенийНаборЗаписей.КодировкиФайлов, РегистрСведенийНаборЗаписей.ДатыЗакрытияКарточекПисем, РегистрСведенийНаборЗаписей.ИдентификаторыПолученныхПисем, РегистрСведенийНаборЗаписей.ТемыЗаметокДокументооборота, РегистрСведенийНаборЗаписей.ПроверкаПоступленияНовыхПисем, РегистрСведенийНаборЗаписей.ФайлыНаРаспознавании, РегистрСведенийНаборЗаписей.НеполученныеВходящиеПисьма, РегистрСведенийНаборЗаписей.ПапкиПисемЧастоИспользуемые, РегистрСведенийНаборЗаписей.БезопасноеХранилищеДанных, РегистрСведенийНаборЗаписей.РазмерыФайловТомов, РегистрСведенийНаборЗаписей.ОбъектыДляАвтоподбораАдресатов, РегистрСведенийНаборЗаписей.РезультатыПотоковогоСканирования, РегистрСведенийНаборЗаписей.ИдентификаторыПочтовыхВложений, РегистрСведенийНаборЗаписей.РабочиеКаталогиФайловКомпьютера, РегистрСведенийНаборЗаписей.СлужебныеФайлыДокументов, РегистрСведенийНаборЗаписей.БезопасноеХранилищеДанныхОбластейДанных, РегистрСведенийНаборЗаписей.СведенияОФайлах, РегистрСведенийНаборЗаписей.НастройкаШаблоновБизнесПроцессов, РегистрСведенийНаборЗаписей.НастройкиОповещенийОПисьмах, РегистрСведенийНаборЗаписей.ПисьмаВПроцессеОтправкиПоВнутреннейМаршрутизации, РегистрСведенийНаборЗаписей.ПодпискиНаПоказателиПроцессов, РегистрНакопленияНаборЗаписей.РабочееВремяСотрудников, РегистрСведенийНаборЗаписей.УсловияОповещенийОПисьмах, РегистрСведенийНаборЗаписей.ПисьмаВПапках, РегистрСведенийНаборЗаписей.ИспользованиеПочты, РегистрСведенийНаборЗаписей.СведенияОПользователяхДокументооборот, РегистрСведенийНаборЗаписей.СведенияОПросмотреПисем, РегистрСведенийНаборЗаписей.ПапкиПисемБыстрогоДоступа, РегистрСведенийНаборЗаписей.ФайлыВРабочемКаталогеКомпьютера, РегистрСведенийНаборЗаписей.ИдентификаторыИмпортированныхПисем, РегистрСведенийНаборЗаписей.ПапкиУчетныхЗаписей, РегистрСведенийНаборЗаписей.ОбращенияКВерсиямФайлов, РегистрСведенийНаборЗаписей.ПроизошедшиеБизнесСобытия, РегистрСведенийНаборЗаписей.ВложенныеПисьма, РегистрСведенийНаборЗаписей.ФайлыКУдалению, РегистрСведенийНаборЗаписей.ОповещенияОПисьмах, РегистрСведенийНаборЗаписей.НастройкиАвтоочисткиПапокПисем, РегистрСведенийНаборЗаписей.КоличествоПисемВПапках, РегистрСведенийНаборЗаписей.ДочерниеБизнесПроцессы, РегистрСведенийНаборЗаписей.СведенияОФайлахДокументооборот, РегистрСведенийНаборЗаписей.НаличиеФайлов, РегистрСведенийНаборЗаписей.ОбращенияКОбъектам, РегистрСведенийНаборЗаписей.ПутиКПрограммамЭлектроннойПодписиИШифрованияНаСерверахLinux, РегистрСведенийНаборЗаписей.СтатистикаОтправкаPushУведомлений, РегистрСведенийНаборЗаписей.НастройкиОтложеннойОтправкиПисем, РегистрСведенийНаборЗаписей.ЗадачиСПодзадачами, РегистрСведенийНаборЗаписей.ДвоичныеДанныеФайлов, РегистрСведенийНаборЗаписей.ПисьмаВеток, РегистрСведенийНаборЗаписей.ПолучателиУведомленийОПроблемах - Источник
// 
// Возвращаемое значение:
//  УникальныйИдентификатор - Идентификатор набора записей
Функция ИдентификаторНабораЗаписей(Источник) Экспорт
	
	Хэширование = Новый ХешированиеДанных(ХешФункция.MD5);
	Хэширование.Добавить(XMLСтрока(Новый ХранилищеЗначения(Источник.Отбор, Новый СжатиеДанных(0))));
	Хеш = СтрЗаменить(Хэширование.ХешСумма, " ", "");
	Хэширование = Неопределено;
	
	Возврат Новый УникальныйИдентификатор(СтрШаблон("%1-%2-%3-%4-%5",
		Лев(Хеш, 8), Сред(Хеш, 9, 4), Сред(Хеш, 13, 4), Сред(Хеш, 17, 4), Сред(Хеш, 21)));
			
КонецФункции

// Заполнить идентификатор набора записей.
// 
// Параметры:
//  Источник - РегистрСведенийНаборЗаписей.ПапкиПисемВиджетаПочта, РегистрСведенийНаборЗаписей.ПодготовленныеКОтправкеПисьма, РегистрСведенийНаборЗаписей.НеотправленныеИсходящиеПисьма, РегистрСведенийНаборЗаписей.ДанныеБизнесПроцессов, РегистрСведенийНаборЗаписей.ИспользованиеШаблонов, РегистрСведенийНаборЗаписей.СведенияОПрочтении, РегистрСведенийНаборЗаписей.ОчередьВерсийФайловДляРазмещенийВТомах, РегистрСведенийНаборЗаписей.АвтоматическиеОтветыПоАдресам, РегистрСведенийНаборЗаписей.Штрихкоды, РегистрСведенийНаборЗаписей.ФайлыСозданныеПоШаблону, РегистрСведенийНаборЗаписей.КодировкиФайлов, РегистрСведенийНаборЗаписей.ДатыЗакрытияКарточекПисем, РегистрСведенийНаборЗаписей.ИдентификаторыПолученныхПисем, РегистрСведенийНаборЗаписей.ТемыЗаметокДокументооборота, РегистрСведенийНаборЗаписей.ПроверкаПоступленияНовыхПисем, РегистрСведенийНаборЗаписей.ФайлыНаРаспознавании, РегистрСведенийНаборЗаписей.НеполученныеВходящиеПисьма, РегистрСведенийНаборЗаписей.ПапкиПисемЧастоИспользуемые, РегистрСведенийНаборЗаписей.БезопасноеХранилищеДанных, РегистрСведенийНаборЗаписей.РазмерыФайловТомов, РегистрСведенийНаборЗаписей.ОбъектыДляАвтоподбораАдресатов, РегистрСведенийНаборЗаписей.РезультатыПотоковогоСканирования, РегистрСведенийНаборЗаписей.ИдентификаторыПочтовыхВложений, РегистрСведенийНаборЗаписей.РабочиеКаталогиФайловКомпьютера, РегистрСведенийНаборЗаписей.СлужебныеФайлыДокументов, РегистрСведенийНаборЗаписей.БезопасноеХранилищеДанныхОбластейДанных, РегистрСведенийНаборЗаписей.СведенияОФайлах, РегистрСведенийНаборЗаписей.НастройкаШаблоновБизнесПроцессов, РегистрСведенийНаборЗаписей.НастройкиОповещенийОПисьмах, РегистрСведенийНаборЗаписей.ПисьмаВПроцессеОтправкиПоВнутреннейМаршрутизации, РегистрСведенийНаборЗаписей.ПодпискиНаПоказателиПроцессов, РегистрНакопленияНаборЗаписей.РабочееВремяСотрудников, РегистрСведенийНаборЗаписей.УсловияОповещенийОПисьмах, РегистрСведенийНаборЗаписей.ПисьмаВПапках, РегистрСведенийНаборЗаписей.ИспользованиеПочты, РегистрСведенийНаборЗаписей.СведенияОПользователяхДокументооборот, РегистрСведенийНаборЗаписей.СведенияОПросмотреПисем, РегистрСведенийНаборЗаписей.ПапкиПисемБыстрогоДоступа, РегистрСведенийНаборЗаписей.ФайлыВРабочемКаталогеКомпьютера, РегистрСведенийНаборЗаписей.ИдентификаторыИмпортированныхПисем, РегистрСведенийНаборЗаписей.ПапкиУчетныхЗаписей, РегистрСведенийНаборЗаписей.ОбращенияКВерсиямФайлов, РегистрСведенийНаборЗаписей.ПроизошедшиеБизнесСобытия, РегистрСведенийНаборЗаписей.ВложенныеПисьма, РегистрСведенийНаборЗаписей.ФайлыКУдалению, РегистрСведенийНаборЗаписей.ОповещенияОПисьмах, РегистрСведенийНаборЗаписей.НастройкиАвтоочисткиПапокПисем, РегистрСведенийНаборЗаписей.КоличествоПисемВПапках, РегистрСведенийНаборЗаписей.ДочерниеБизнесПроцессы, РегистрСведенийНаборЗаписей.СведенияОФайлахДокументооборот, РегистрСведенийНаборЗаписей.НаличиеФайлов, РегистрСведенийНаборЗаписей.ОбращенияКОбъектам, РегистрСведенийНаборЗаписей.ПутиКПрограммамЭлектроннойПодписиИШифрованияНаСерверахLinux, РегистрСведенийНаборЗаписей.СтатистикаОтправкаPushУведомлений, РегистрСведенийНаборЗаписей.НастройкиОтложеннойОтправкиПисем, РегистрСведенийНаборЗаписей.ЗадачиСПодзадачами, РегистрСведенийНаборЗаписей.ДвоичныеДанныеФайлов, РегистрСведенийНаборЗаписей.ПисьмаВеток, РегистрСведенийНаборЗаписей.ПолучателиУведомленийОПроблемах - Источник
//  Замещение - Булево - Замещение
// 
// Возвращаемое значение:
//  УникальныйИдентификатор, Неопределено - Заполнить идентификатор набора записей
Функция ЗаполнитьИдентификаторНабораЗаписей(Источник, Замещение) Экспорт

	//Запись и удаление множества записей.
	Ключ = Неопределено;
	КлючевойНабор = РегистрыСведений[Источник.Метаданные().Имя].СоздатьНаборЗаписей();
	Для каждого Поле Из КлючевойНабор.Отбор Цикл
		Поле.Использование = Истина;
	КонецЦикла;
	
	Для Каждого Запись Из Источник Цикл
		Для каждого Поле Из КлючевойНабор.Отбор Цикл
			Поле.Значение = Запись[Поле.Имя];
		КонецЦикла;
		
		Запись.ОтметкиВремениИдентификаторНабораЗаписей = ИдентификаторНабораЗаписей(КлючевойНабор);
		Если Ключ = Неопределено Тогда
			Ключ = Запись.ОтметкиВремениИдентификаторНабораЗаписей;
		КонецЕсли;
	КонецЦикла;
	
	КлючевойНабор = Неопределено;
	Возврат Ключ;
				
КонецФункции

// Полнота отбора набора записей.
// 
// Параметры:
//  Источник - РегистрСведенийНаборЗаписей.ПапкиПисемВиджетаПочта, РегистрСведенийНаборЗаписей.ПодготовленныеКОтправкеПисьма, РегистрСведенийНаборЗаписей.НеотправленныеИсходящиеПисьма, РегистрСведенийНаборЗаписей.ДанныеБизнесПроцессов, РегистрСведенийНаборЗаписей.ИспользованиеШаблонов, РегистрСведенийНаборЗаписей.СведенияОПрочтении, РегистрСведенийНаборЗаписей.ОчередьВерсийФайловДляРазмещенийВТомах, РегистрСведенийНаборЗаписей.АвтоматическиеОтветыПоАдресам, РегистрСведенийНаборЗаписей.Штрихкоды, РегистрСведенийНаборЗаписей.ФайлыСозданныеПоШаблону, РегистрСведенийНаборЗаписей.КодировкиФайлов, РегистрСведенийНаборЗаписей.ДатыЗакрытияКарточекПисем, РегистрСведенийНаборЗаписей.ИдентификаторыПолученныхПисем, РегистрСведенийНаборЗаписей.ТемыЗаметокДокументооборота, РегистрСведенийНаборЗаписей.ПроверкаПоступленияНовыхПисем, РегистрСведенийНаборЗаписей.ФайлыНаРаспознавании, РегистрСведенийНаборЗаписей.НеполученныеВходящиеПисьма, РегистрСведенийНаборЗаписей.ПапкиПисемЧастоИспользуемые, РегистрСведенийНаборЗаписей.БезопасноеХранилищеДанных, РегистрСведенийНаборЗаписей.РазмерыФайловТомов, РегистрСведенийНаборЗаписей.ОбъектыДляАвтоподбораАдресатов, РегистрСведенийНаборЗаписей.РезультатыПотоковогоСканирования, РегистрСведенийНаборЗаписей.ИдентификаторыПочтовыхВложений, РегистрСведенийНаборЗаписей.РабочиеКаталогиФайловКомпьютера, РегистрСведенийНаборЗаписей.СлужебныеФайлыДокументов, РегистрСведенийНаборЗаписей.БезопасноеХранилищеДанныхОбластейДанных, РегистрСведенийНаборЗаписей.СведенияОФайлах, РегистрСведенийНаборЗаписей.НастройкаШаблоновБизнесПроцессов, РегистрСведенийНаборЗаписей.НастройкиОповещенийОПисьмах, РегистрСведенийНаборЗаписей.ПисьмаВПроцессеОтправкиПоВнутреннейМаршрутизации, РегистрСведенийНаборЗаписей.ПодпискиНаПоказателиПроцессов, РегистрНакопленияНаборЗаписей.РабочееВремяСотрудников, РегистрСведенийНаборЗаписей.УсловияОповещенийОПисьмах, РегистрСведенийНаборЗаписей.ПисьмаВПапках, РегистрСведенийНаборЗаписей.ИспользованиеПочты, РегистрСведенийНаборЗаписей.СведенияОПользователяхДокументооборот, РегистрСведенийНаборЗаписей.СведенияОПросмотреПисем, РегистрСведенийНаборЗаписей.ПапкиПисемБыстрогоДоступа, РегистрСведенийНаборЗаписей.ФайлыВРабочемКаталогеКомпьютера, РегистрСведенийНаборЗаписей.ИдентификаторыИмпортированныхПисем, РегистрСведенийНаборЗаписей.ПапкиУчетныхЗаписей, РегистрСведенийНаборЗаписей.ОбращенияКВерсиямФайлов, РегистрСведенийНаборЗаписей.ПроизошедшиеБизнесСобытия, РегистрСведенийНаборЗаписей.ВложенныеПисьма, РегистрСведенийНаборЗаписей.ФайлыКУдалению, РегистрСведенийНаборЗаписей.ОповещенияОПисьмах, РегистрСведенийНаборЗаписей.НастройкиАвтоочисткиПапокПисем, РегистрСведенийНаборЗаписей.КоличествоПисемВПапках, РегистрСведенийНаборЗаписей.ДочерниеБизнесПроцессы, РегистрСведенийНаборЗаписей.СведенияОФайлахДокументооборот, РегистрСведенийНаборЗаписей.НаличиеФайлов, РегистрСведенийНаборЗаписей.ОбращенияКОбъектам, РегистрСведенийНаборЗаписей.ПутиКПрограммамЭлектроннойПодписиИШифрованияНаСерверахLinux, РегистрСведенийНаборЗаписей.СтатистикаОтправкаPushУведомлений, РегистрСведенийНаборЗаписей.НастройкиОтложеннойОтправкиПисем, РегистрСведенийНаборЗаписей.ЗадачиСПодзадачами, РегистрСведенийНаборЗаписей.ДвоичныеДанныеФайлов, РегистрСведенийНаборЗаписей.ПисьмаВеток, РегистрСведенийНаборЗаписей.ПолучателиУведомленийОПроблемах - Источник
// 
// Возвращаемое значение:
//  Число - Полнота отбора набора записей
Функция ПолнотаОтбораНабораЗаписей(Источник)
	
	Н = 0;
	Для Каждого Поле Из Источник.Отбор Цикл
		Если Поле.Использование Тогда
			Н = 3;
		ИначеЕсли Н = 3 Тогда
			Возврат 2;
		Иначе
			Н = 1;
		КонецЕсли;
	КонецЦикла;
	Возврат Н;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытийСтарые

Процедура ЗаполнитьВладельцаСведенийПисем(Источник, Отказ, Замещение) Экспорт
	
	//ПриЗаписи РегистрСведенийНаборЗаписей.СвязиДокументов, РегистрСведенийНаборЗаписей.СведенияОПрочтении, РегистрСведенийНаборЗаписей.ФлагиОбъектов
	
	Если Не Использование(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	ТипыПисем = Новый ОписаниеТипов("ДокументСсылка.ВходящееПисьмо, ДокументСсылка.ИсходящееПисьмо");	
	Условие = ?(Источник.Отбор.Найти("Документ") = Неопределено, Источник.Отбор.Объект, Источник.Отбор.Документ);
	
	Если Условие.Использование Тогда
		Если ТипыПисем.СодержитТип(ТипЗнч(Условие.Значение)) И ЗначениеЗаполнено(Условие.Значение) Тогда
			Источник.ДополнительныеСвойства.Вставить("ОтметкиВремениВладелец",
				МП_СлужебныйПовтИспНаВремяВызова.УчетнаяЗаписьПисьма(Условие.Значение));
		КонецЕсли;

	ИначеЕсли Источник.Количество() = 0 И Замещение Тогда
		//Удаление множества владельцев.
		Набор = РегистрыСведений[Источник.Метаданные.Имя].СоздатьНаборЗаписей();
		Для Каждого Строка Из Источник.Отбор Цикл
			Если Строка.Использование Тогда
				Набор.Отбор.Установить(Строка.Значение);
			КонецЕсли;
		КонецЦикла; 
		Набор.Прочитать();
		
	ИначеЕсли Источник.Количество() = 1 Тогда
		Для Каждого Строка Из Источник Цикл
			Если ТипыПисем.СодержитТип(ТипЗнч(Строка[Условие.Имя]))	И ЗначениеЗаполнено(Строка[Условие.Имя]) Тогда
				Источник.ДополнительныеСвойства.Вставить("ОтметкиВремениВладелец",
					МП_СлужебныйПовтИспНаВремяВызова.УчетнаяЗаписьПисьма(Строка[Условие.Имя]));
			КонецЕсли;
		КонецЦикла;
	
	ИначеЕсли Источник.Количество() > 1 Тогда
		Набор = Источник;
		
	КонецЕсли;
	
	Если Набор <> Неопределено Тогда
		Значения = Новый Соответствие;
		Владельцы = Новый Структура(Условие.Имя, Значения);
		Для Каждого Строка Из Набор Цикл
			Если ТипыПисем.СодержитТип(ТипЗнч(Строка[Условие.Имя]))	И ЗначениеЗаполнено(Строка[Условие.Имя]) Тогда
				Значения.Вставить(Строка[Условие.Имя],
					МП_СлужебныйПовтИспНаВремяВызова.УчетнаяЗаписьПисьма(Строка[Условие.Имя]));
			КонецЕсли;
		КонецЦикла;
		Если Значения.Количество() = 1 Тогда
			Для Каждого Строка Из Значения Цикл
				Источник.ДополнительныеСвойства.Вставить("ОтметкиВремениВладелец", Строка.Значение);
			КонецЦикла;
		ИначеЕсли Значения.Количество() > 1 Тогда
			Источник.ДополнительныеСвойства.Вставить("ОтметкиВремениВладелецы", Владельцы);
		КонецЕсли;
	КонецЕсли;
			
КонецПроцедуры

// Отметки времени изменения сведений писем.
// 
// Параметры:
//  Источник - РегистрСведенийНаборЗаписей.ФлагиОбъектов, РегистрСведенийНаборЗаписей.СведенияОПрочтении, РегистрСведенийНаборЗаписей.СвязиОбъектов - Источник
//  Отказ - Булево - Отказ
//  Замещение - Булево - Замещение
Процедура ОтметкиВремениИзмененияСведенийПисем(Источник, Отказ, Замещение) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьОтметкиВремени") Тогда
		Возврат;
	КонецЕсли;
	
	Если МиграцияПриложенийПереопределяемый.ЭтоЗагрузка(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	МассивТиповПисьма = Новый Массив;
	МассивТиповПисьма.Добавить(Тип("ДокументСсылка.ВходящееПисьмо"));
	МассивТиповПисьма.Добавить(Тип("ДокументСсылка.ИсходящееПисьмо"));
	
	Для каждого ЭлементОтбора Из Источник.Отбор Цикл
		
		ЗначениеЭлементаОтбора = ЭлементОтбора.Значение;
		
		Если (ЭлементОтбора.Имя = "Объект" Или ЭлементОтбора.Имя = "Документ") 
			И (МассивТиповПисьма.Найти(ТипЗнч(ЗначениеЭлементаОтбора)) <> Неопределено
			И ЗначениеЗаполнено(ЗначениеЭлементаОтбора)) Тогда
			
			УчетнаяЗапись = МП_СлужебныйПовтИспНаВремяВызова.УчетнаяЗаписьПисьма(ЗначениеЭлементаОтбора);
			
			РегистрыСведений.ОтметкиВремениИзмененияСведенийПисем.ДобавитьЗапись(УчетнаяЗапись, ЗначениеЭлементаОтбора);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Отметки времени изменения писем.
// 
// Параметры:
//  Источник - ДокументОбъект.ВходящееПисьмо, ДокументОбъект.ИсходящееПисьмо - Источник
//  Отказ - Булево - Отказ
//  Замещение Булево
Процедура ОтметкиВремениИзмененияПисем(Источник, Отказ, Замещение) Экспорт
	
КонецПроцедуры

// Отметки времени изменения цвета и флага.
// 
// Параметры:
//  Источник - РегистрСведенийНаборЗаписей.ФлагиОбъектов, РегистрСведенийНаборЗаписей.НастройкиОтображенияЗаписейРабочегоКалендаря - Источник
//  Отказ - Булево - Отказ
//  Замещение - Булево - Замещение
Процедура ОтметкиВремениИзмененияЦветаИФлага(Источник, Отказ, Замещение) Экспорт
	
КонецПроцедуры

// Отметки времени изменения настроек обмена с мобильным.
// 
// Параметры:
//  Источник - РегистрСведенийНаборЗаписей.ИзмененныеНастройкиСинхронизацииСМобильнымКлиентом - Источник
//  Отказ - Булево - Отказ
//  Замещение - Булево - Замещение
Процедура ОтметкиВремениИзмененияНастроекОбменаСМобильным(Источник, Отказ, Замещение) Экспорт
	
КонецПроцедуры

// Отметки времени изменения максимального размера файла при записи.
// 
// Параметры:
//  Источник - КонстантаМенеджерЗначения.МаксимальныйРазмерФайла - Источник
//  Отказ - Булево - Отказ
Процедура ОтметкиВремениИзмененияМаксимальногоРазмераФайлаПриЗаписи(Источник, Отказ) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьОтметкиВремени") Тогда
		Возврат;
	КонецЕсли;
	Если МиграцияПриложенийПереопределяемый.ЭтоЗагрузка(Источник) Тогда
		Возврат;
	КонецЕсли;

КонецПроцедуры

// Отметки времени изменения состояний приглашения на мероприятие.
// 
// Параметры:
//  Источник - РегистрСведенийНаборЗаписей.УчастникиМероприятия - Источник
//  Отказ - Булево - Отказ
//  Замещение - Булево - Замещение
Процедура ОтметкиВремениИзмененияСостоянийПриглашенияНаМероприятие(Источник, Отказ, Замещение) Экспорт
	
КонецПроцедуры

// Отметки времени изменения трудозатрат.
// 
// Параметры:
//  Источник - РегистрСведенийНаборЗаписей.ФактическиеТрудозатраты - Источник
//  Отказ - Булево - Отказ
//  Замещение - Булево - Замещение
Процедура ОтметкиВремениИзмененияТрудозатрат(Источник, Отказ, Замещение) Экспорт
	
КонецПроцедуры

#КонецОбласти
