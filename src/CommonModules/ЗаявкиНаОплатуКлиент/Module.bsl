#Область ПрограммныйИнтерфейс

// Начинает выбор авансового отчета из формы заявки.
//
Процедура НачатьВыборАвансовогоОтчета(ФормаЗаявки, ЭлементАвансовыйОтчет) Экспорт
	
	ВидДокументаЗаявка = ФормаЗаявки.Объект.ВидДокумента;
	ВидыДокументовАвансовыйОтчет = ЗаявкиНаОплатуВызовСервера.
		ВидыДокументовПоСвязиАвансовыйОтчет(ВидДокументаЗаявка);
	ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияЗначенияИзМассива(ВидыДокументовАвансовыЙОтчет,
		ПредопределенноеЗначение("Справочник.ВидыВнутреннихДокументов.ПустаяСсылка"));
		
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор", Новый Структура);
	Если ВидыДокументовАвансовыйОтчет.Количество() <> 0 Тогда
		ПараметрыФормы.Отбор.Вставить("ВидДокумента", ВидыДокументовАвансовыйОтчет);
	КонецЕсли;
	ОткрытьФорму("Справочник.ВнутренниеДокументы.ФормаВыбора",
		ПараметрыФормы,
		ЭлементАвансовыйОтчет,,,,,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры
	
// Начинает создание авансового отчета из формы заявки.
//
Процедура НачатьСозданиеАвансовогоОтчета(ФормаЗаявки, ЭлементАвансовыйОтчет) Экспорт
	
	ВидДокументаЗаявка = ФормаЗаявки.Объект.ВидДокумента;
	ВидыДокументовАвансовыйОтчет = ЗаявкиНаОплатуВызовСервера.
		ВидыДокументовПоСвязиАвансовыйОтчет(ВидДокументаЗаявка);
	ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияЗначенияИзМассива(ВидыДокументовАвансовыЙОтчет,
		ПредопределенноеЗначение("Справочник.ВидыВнутреннихДокументов.ПустаяСсылка"));
		
	Если ВидыДокументовАвансовыйОтчет.Количество() = 1 Тогда
		
		ЗначенияЗаполнения = Новый Структура;
		ЗначенияЗаполнения.Вставить("ВидДокумента", ВидыДокументовАвансовыйОтчет[0]);
		ЗначенияЗаполнения.Вставить("Основание", ФормаЗаявки.Объект.Ссылка);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
		ПараметрыФормы.Вставить("РежимВыбора", Истина);
		
		Контекст = Новый Структура("ФормаЗаявки, ЭлементАвансовыйОтчет",
			ФормаЗаявки, ЭлементАвансовыйОтчет);
		
		ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина,
			"ВнутренниеДокументыВыполнениеКомандыСоздать");
		
		ОткрытьФорму("Справочник.ВнутренниеДокументы.ФормаОбъекта",
			ПараметрыФормы,
			ЭлементАвансовыйОтчет);
		
	Иначе
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ТипШаблонаДокумента", "ШаблоныВнутреннихДокументов");
		ПараметрыФормы.Вставить("ВозможностьСозданияПустогоДокумента", Истина);
		
		Контекст = Новый Структура("ФормаЗаявки, ЭлементАвансовыйОтчет",
			ФормаЗаявки, ЭлементАвансовыйОтчет);
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"СозданиеДокументаПоШаблонуЗавершение", ЭтотОбъект, Контекст);
		
		Попытка
			
			ОткрытьФорму(
				"ОбщаяФорма.СозданиеДокументаПоШаблону",
				ПараметрыФормы,,,,,
				ОписаниеОповещения,
				РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
				
		Исключение
			
			Инфо = ИнформацияОбОшибке();
			Если Инфо.Описание = "СоздатьПустойДокумент" Тогда
				ВыполнитьОбработкуОповещения(ОписаниеОповещения,
					"СоздатьПустойДокумент");
			Иначе
				ВызватьИсключение;
			КонецЕсли;
			
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СозданиеДокументаПоШаблонуЗавершение(Результат, Контекст) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Результат) ИЛИ Результат = "ПрерватьОперацию" Тогда
		Возврат;
	КонецЕсли;
	
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина,
		"ВнутренниеДокументыВыполнениеКомандыСоздать");
	
	Если (ТипЗнч(Результат) <> Тип("Строка")) Тогда 
		ШаблонДокумента = Результат.ШаблонДокумента;
	Иначе
		ШаблонДокумента = Результат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ШаблонДокумента", ШаблонДокумента);
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	
	Открытьформу(
		"Справочник.ВнутренниеДокументы.ФормаОбъекта",
		ПараметрыФормы,
		Контекст.ЭлементАвансовыйОтчет);
	
КонецПроцедуры
	
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти