#Область ПрограммныйИнтерфейс

// Преобразует длительность интервала времени в строку вида час:мин.
//
// Параметры
//  Длительность – Число – длительность интервала времени в секундах. 
//
//  ПредставлениеНуля – Строка – представление интервала нулевой длительности. 
//
// Возвращаемое значение:
//   Строка – представление интервала в виде час:мин.
//
Функция ЧислоВСтроку(Длительность, ПредставлениеНуля = "") Экспорт 
	
	Если Длительность = 0 Тогда 
		Возврат ПредставлениеНуля;
	КонецЕсли;	
	
	Час = Цел(Длительность / 3600);
	Мин = (Длительность - Час*3600) / 60;
	
	Если Час < 100 Тогда
		Возврат Формат(Час, "ЧЦ=2; ЧН=; ЧВН=; ЧГ=") + ":" + Формат(Мин, "ЧЦ=2; ЧН=; ЧВН=; ЧГ=");
	Иначе
		Возврат Формат(Час, "ЧГ=") + ":" + Формат(Мин, "ЧЦ=2; ЧН=; ЧВН=; ЧГ=");
	КонецЕсли;
	
КонецФункции

// Преобразует строку вида час:мин в длительность интервала времени.
//
// Параметры
//  ДлительностьСтр – Строка – представление интервала времени в виде строки час:мин. 
//
// Возвращаемое значение:
//   Число – длительность интервала времени в секундах.
//
Функция ЧислоИзСтроки(ДлительностьСтр) Экспорт 
	
	Если Не ЗначениеЗаполнено(ДлительностьСтр) Тогда 
		Возврат 0;
	КонецЕсли;

	Поз = Найти(ДлительностьСтр, ":");
	Если Поз = 0 Тогда 
		Возврат 0;
	КонецЕсли;

	Час = Лев(ДлительностьСтр, Поз-1);
	Мин = Сред(ДлительностьСтр, Поз+1);
	
	Возврат Число(Час) * 3600 + Число(Мин) * 60;
	
КонецФункции

// Выполняет проверку строки на соответствие представлению интервала времени в виде час:мин.
//
// Параметры
//  ДлительностьСтр – Строка – проверяемая строка. 
//
// Возвращаемое значение:
//   Булево – Истина, если переданная строка соответствует виду час:мин.
//			  Ложь, если переданная строка не соответствует виду час:мин.
//
Функция ПроверитьФормат(ДлительностьСтр) Экспорт
	
	Поз = Найти(ДлительностьСтр, ":");
	Если Поз = 0 Тогда 
		Возврат Ложь;
	КонецЕсли;

	ЧасСтр = Лев(ДлительностьСтр, Поз-1);
	МинСтр = Сред(ДлительностьСтр, Поз+1);
	
	Если Не ЗначениеЗаполнено(ЧасСтр) Тогда
		ЧасСтр = "00";
	КонецЕсли;
	Если Не ЗначениеЗаполнено(МинСтр) Тогда
		МинСтр = "00";
	КонецЕсли;
	
	Час = Число(ЧасСтр);
	Мин = Число(МинСтр);
	Если (Час >= 24) Или (Мин >= 60) Тогда
		Возврат Ложь;
    КонецЕсли;

	Если Час < 10 Тогда 
		ЧасСтр = "0" + Час;
	КонецЕсли;
	Если Мин < 10 Тогда 
		МинСтр = "0" + Мин;
	КонецЕсли;
	
    ДлительностьСтр = ЧасСтр + ":" + МинСтр;
	Возврат Истина;

КонецФункции

// разность дат в формате час:мин
Функция РазностьДат(Знач Дата1, Знач Дата2) Экспорт 
	
	Дата1 = Дата1 - Секунда(Дата1);
	Дата2 = Дата2 - Секунда(Дата2);
	
	Час = Цел((Дата2 - Дата1) / 3600);
	Мин = (Дата2 - Дата1 - Час*3600) / 60;
	
	Возврат Дата(1,1,1, Час, Мин, 0);
	
КонецФункции

// дата в формате час:мин
Функция ПолучитьДату(Секунды) Экспорт 
	
	Час = Цел(Секунды / 3600);
	Мин = (Секунды - Час*3600) / 60;
	
	Если Час < 24 Тогда 
		Возврат Дата(1,1,1, Час, Мин, 0);
	Иначе
		Возврат Дата(1,1,1, 23, Мин, 0);
	КонецЕсли;	
	
КонецФункции

//строка в формате час:мин
Функция ПолучитьВВидеДаты(Секунды) Экспорт 
	
	Если Секунды = 0 Тогда 
		Возврат "";
	КонецЕсли;	
	
	Час = Цел(Секунды / 3600);
	Мин = (Секунды - Час*3600) / 60;
	
	Возврат Формат(Час, "ЧН=; ЧГ=")+":"+Формат(Мин, "ЧЦ=2; ЧН=; ЧВН=; ЧГ=");
	
КонецФункции

//строка в формате час:мин
Функция ПолучитьВВидеДатыИзМин(Минуты) Экспорт 
	
	Если Минуты = 0 Тогда 
		Возврат "";
	КонецЕсли;	
	
	Час = Цел(Минуты / 60);
	Мин = (Минуты - Час*60);
	
	Возврат Формат(Час, "ЧН=; ЧГ=")+":"+Формат(Мин, "ЧЦ=2; ЧН=; ЧВН=; ЧГ=");
	
КонецФункции

// Возвращает начало понедельника недели указанной даты. Аналог НачалоНедели(), но возвращает
// начало понедельника вне зависимости от региональных настроек.
//
Функция НачалоПонедельника(Дата) Экспорт
	Возврат НачалоДня(Дата - (ДеньНедели(Дата) - 1) * 86400);
КонецФункции

// Возвращает конец воскресенья недели указанной даты. Аналог КонецНедели(), но возвращает
// конец воскресенья вне зависимости от региональных настроек.
//
Функция КонецВоскресенья(Дата) Экспорт
	Возврат КонецДня(Дата + (7 - ДеньНедели(Дата)) * 86400);
КонецФункции

// Возвращает дату начала еженедельного отчета с указанной датой.
//
Функция ДатаНачалаОтчетаЗаНеделю(Дата) Экспорт
	Если ДеньНедели(Дата) > 5 Тогда 
		Возврат КонецВоскресенья(Дата) + 1 - 3600 * 24 * 2;
	Иначе
		Возврат НачалоПонедельника(Дата) - 3600 * 24 * 2;
	КонецЕсли;
КонецФункции

#КонецОбласти