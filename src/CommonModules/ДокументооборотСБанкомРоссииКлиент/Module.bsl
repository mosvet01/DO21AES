////////////////////////////////////////////////////////////////////////////////
// Подсистема "Документооборот с Банком России".
// 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Выполняет отправку отчета на портал Банка России.
//
// Параметры:
//  Организация   - СправочникСсылка.Организации - организация, по которой осуществляется выгрузка пакета.
//  СсылкаНаОтчет - Ссылка - ссылка на отчет, который необходимо выгрузить в пакет.
//  ВладелецФормы - ФормаКлиентскогоПриложения - форма, которая будет использования в качестве
//                                               владельца открываемых служебных окон.
//
Процедура ОтправитьОтчет(Организация, СсылкаНаОтчет, ВладелецФормы = Неопределено) Экспорт
	
	ДокументооборотСБанкомРоссииСлужебныйКлиент.ОтправитьОтчет(Организация, СсылкаНаОтчет, ВладелецФормы);
	
КонецПроцедуры

// Проверяет разрешение на выполнение операции.
//
// Параметры:
//  ОповещениеОЗавершении   - ОписаниеОповещения - описание процедуры, принимающей результат.
//    Результат - Структура:
//      * Выполнено           - Булево - если Истина, то процедура успешно выполнена и получен результат,
//                                       иначе - была ошибка при выполнении проверки.
//
//  Отправка   - СправочникСсылка.ОтправкиБанкРоссии - ссылка на отправку, для которой необходимо обновить статус.
//  ВладелецФормы - ФормаКлиентскогоПриложения - форма, которая будет использования в качестве
//                                               владельца открываемых служебных окон.
//
Процедура ПолучитьСтатусОтчета(ОповещениеОЗавершении, Отправка, ВладелецФормы = Неопределено) Экспорт
	
	ДокументооборотСБанкомРоссииСлужебныйКлиент.ПолучитьСтатусОтчета(ОповещениеОЗавершении, Отправка, ВладелецФормы);
	
КонецПроцедуры

// Проверяет разрешение на выполнение операции.
//
// Параметры:
//  ОповещениеОЗавершении   - ОписаниеОповещения - описание процедуры, принимающей результат.
//    Результат - Структура:
//      * Выполнено           - Булево - если Истина, то процедура успешно выполнена и получен результат,
//                                       иначе - была ошибка при выполнении проверки.
//      * ВыполнениеРазрешено - Булево - если Истина, то продолжение выполнения разрешено.
//
//  ВладелецФормы           - ФормаКлиентскогоПриложения - форма, которая будет использования в качестве
//                                                         владельца открываемых служебных окон.
//
//  ПараметрыАутентификации - Структура - параметры доступа к сайту поддержки пользователей.
//    * Логин  - Строка - логин пользователя.
//    * Пароль - Строка - пароль пользователя.
//
Процедура ПроверитьВозможностьВыполненияОперации(ОповещениеОЗавершении, ВладелецФормы = Неопределено, ПараметрыАутентификации = Неопределено) Экспорт
	
	ДокументооборотСБанкомРоссииСлужебныйКлиент.ПроверитьВозможностьВыполненияОперации(
		ОповещениеОЗавершении, ВладелецФормы, ПараметрыАутентификации);
	
КонецПроцедуры

Процедура ПоказатьПротокол(ИсточникСсылка, ПечатьВозможна = Истина) Экспорт
	
	ПараметрыФормы = Новый Структура("ИсточникСсылка, ПечатьВозможна", ИсточникСсылка, ПечатьВозможна);
	ОткрытьФорму("ОбщаяФорма.ПротоколОбработкиБанкРоссии", ПараметрыФормы);
	
КонецПроцедуры

Функция ПолучитьНеЗавершенныеОтправки(Организация) Экспорт
	
	Возврат ДокументооборотСБанкомРоссииВызовСервера.ПолучитьНеЗавершенныеОтправки(Организация);
	
КонецФункции

Процедура ОткрытьФормуНастройки(Организация, ФормаВладелец = Неопределено, Окно = Неопределено) Экспорт 
	
	Если ЗначениеЗаполнено(Организация) Тогда
		ПараметрыФормы = Новый Структура("ОрганизацияСсылка", Организация);
		ОткрытьФорму(
			"РегистрСведений.НастройкиОбменаБанкРоссии.ФормаЗаписи", 
			ПараметрыФормы,,,,,, 
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Иначе
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("ВидНастроек", ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.БанкРоссии"));
		ОткрытьФорму(
			"Обработка.ДокументооборотСКонтролирующимиОрганами.Форма.ФормаНастроекДокументооборотаОрганизаций", 
			ПараметрыОткрытия,,
			Новый УникальныйИдентификатор,
			Окно);
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьПоследнююОтправкуОтчета(ОтчетСсылка) Экспорт
	
	Возврат ДокументооборотСБанкомРоссииВызовСервера.ПолучитьПоследнююОтправкуОтчета(ОтчетСсылка);
	
КонецФункции

#КонецОбласти