////////////////////////////////////////////////////////////////////////////////
// Работа с планировщиком (клиент).
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Показывает текущий элемент планировщика.
//
// Параметры:
//  ПолеПланировщика - ПолеПланировщика - Поле планировщика.
//
Процедура ПоказатьТекущийЭлементПланировщика(ПолеПланировщика) Экспорт
	
	ТекущийЭлементПланировщика = ТекущийЭлементПланировщика(ПолеПланировщика);
	Если ТекущийЭлементПланировщика = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПоказатьЗначение(, ТекущийЭлементПланировщика.Значение.Ссылка);
	
КонецПроцедуры

// Возвращает текущий элемент планировщика.
//
// Параметры:
//  ПолеПланировщика - ПолеПланировщика - Поле планировщика.
// 
// Возвращаемое значение:
//  ЭлементПланировщика, Неопределено - Текущий элемент планировщика.
//
Функция ТекущийЭлементПланировщика(ПолеПланировщика) Экспорт
	
	Возврат ?(ПолеПланировщика.ВыделенныеЭлементы.Количество() > 0,
		ПолеПланировщика.ВыделенныеЭлементы.Получить(0),
		Неопределено);
	
КонецФункции

// Возвращает выделенные в планировщике объекты.
//
// Параметры:
//  ПолеПланировщика - ПолеПланировщика - Поле планировщика.
// 
// Возвращаемое значение:
//  Массив из ЛюбаяСсылка - Выделенные в планировщике объекты.
//
Функция ВыделенныеОбъекты(ПолеПланировщика) Экспорт
	
	ВыделенныеОбъекты = Новый Массив;
	Для Каждого ВыделенныйЭлемент Из ПолеПланировщика.ВыделенныеЭлементы Цикл
		
		Если ТипЗнч(ВыделенныйЭлемент.Значение) <> Тип("Структура")
			Или Не ЗначениеЗаполнено(ВыделенныйЭлемент.Значение.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		
		ВыделенныеОбъекты.Добавить(ВыделенныйЭлемент.Значение.Ссылка);
		
	КонецЦикла;
	
	Возврат ВыделенныеОбъекты;
	
КонецФункции

#КонецОбласти