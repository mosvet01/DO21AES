#Область ПрограммныйИнтерфейс

// Вызывается в форме добавления заявления на новый сертификат для заполнения реквизитов организации и при ее выборе.
//
// Параметры:
//  Параметры - Структура:
//
//    * ТипОрганизации - ОписаниеТипов - возвращаемое значение. Содержит ссылочные типы, из которых
//                       можно сделать выбор. Начальное значение ОпределяемыйТип.Организация.
//                     - Неопределено  - возвращаемое значение. Выбор организации не поддерживается.
//
//    * Организация - СправочникСсылка - организация из ТипОрганизации, которую нужно заполнить.
//                    Если организация уже заполнена, требуется перезаполнить ее свойства - например,
//                    при повтором вызове, когда пользователь выбрал другую организацию.
//                  - Неопределено - если ТипОрганизации не настроен.
//                    Пользователю недоступен выбор организации.
//
//    * ЭтоИндивидуальныйПредприниматель - Булево - возвращаемое значение:
//                    Ложь   - начальное значение - указанная организация является юридическим лицом;
//                    Истина - указанная организация является индивидуальным предпринимателем.
//
//    * ЭтоИностраннаяОрганизация - Булево - возвращаемое значение, когда Истина ОГРН не заполняется.
//                                - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * НаименованиеСокращенное  - Строка - возвращаемое значение. Краткое наименование организации.
//                               - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * НаименованиеПолное       - Строка - возвращаемое значение. Краткое наименование организации.
//                               - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * ИНН                      - Строка - возвращаемое значение. ИНН организации.
//                               - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * КПП                      - Строка - возвращаемое значение. КПП организации.
//                               - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * ОГРН                     - Строка - возвращаемое значение. ОГРН организации (кроме иностранных).
//                               - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * РасчетныйСчет            - Строка - возвращаемое значение. Основной расчетный счет организации для договора.
//                               - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * БИК                      - Строка - возвращаемое значение. БИК банка расчетного счета.
//                               - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * КорреспондентскийСчет    - Строка - возвращаемое значение. Корреспондентский счет банка расчетного счета.
//                               - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * Телефон                  - Строка - возвращаемое значение. Телефон организации в формате JSON, как его
//                                 возвращает функция КонтактнаяИнформацияОбъекта общего модуля УправлениеКонтактнойИнформацией.
//
//    * ЮридическийАдрес - Строка - возвращаемое значение. Юридический адрес организации в формате JSON, как его
//                         возвращает функция КонтактнаяИнформацияОбъекта общего модуля УправлениеКонтактнойИнформацией.
//                       - Неопределено - значение не указано, не изменять имеющееся значение.
//
Процедура ПриЗаполненииРеквизитовОрганизацииВЗаявленииНаСертификат(Параметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Параметры.Организация) Тогда
		ОрганизацияПоУмолчанию = Справочники.Организации.ОрганизацияПоУмолчанию();
		Если ЗначениеЗаполнено(ОрганизацияПоУмолчанию) Тогда
			Параметры.Организация = ОрганизацияПоУмолчанию;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Параметры.Организация) Тогда
		Возврат; // Поля можно заполнить только, если организация указана.
	КонецЕсли;
	
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Параметры.Организация,
		"Наименование, НаименованиеПолное, ЮрФизЛицо, ИНН, КПП, ОГРН");
	
	Параметры.НаименованиеСокращенное = Реквизиты.Наименование;
	Параметры.НаименованиеПолное      = Реквизиты.НаименованиеПолное;
	Параметры.ЭтоИндивидуальныйПредприниматель = (Реквизиты.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ИндивидуальныйПредприниматель
		ИЛИ Реквизиты.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо);
	Параметры.ИНН  = Реквизиты.ИНН;
	Параметры.КПП  = Реквизиты.КПП;
	Параметры.ОГРН = Реквизиты.ОГРН;
	
	Объекты = Новый Массив;
	Объекты.Добавить(Параметры.Организация);
	
	ВидыКИ = Новый Массив;
	ВидыКИ.Добавить(Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации);
	ВидыКИ.Добавить(Справочники.ВидыКонтактнойИнформации.ФактАдресОрганизации);
	ВидыКИ.Добавить(Справочники.ВидыКонтактнойИнформации.ТелефонОрганизации);
	
	КонтактнаяИнформация = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъектов(Объекты, , ВидыКИ, ТекущаяДатаСеанса());
	
	Строка = КонтактнаяИнформация.Найти(Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации, "Вид");
	Если Строка <> Неопределено Тогда
		Параметры.ЮридическийАдрес = Строка.Значение;
	КонецЕсли;
	
	Строка = КонтактнаяИнформация.Найти(Справочники.ВидыКонтактнойИнформации.ТелефонОрганизации, "Вид");
	Если Строка <> Неопределено Тогда
		Параметры.Телефон = Строка.Значение
	КонецЕсли;
	
КонецПроцедуры

// Вызывается в форме добавления заявления на новый сертификат для заполнении реквизитов владельца и при его выборе.
//
// Параметры:
//  Параметры - Структура:
//    * ЭтоФизическоеЛицо - Булево - если Истина, тогда заявление заполняется для физического лица, иначе для организации.
// 
//    * Организация  - СправочникСсылка - выбранная организация из ТипОрганизации, на которую оформляется сертификат.
//                   - Неопределено - если ТипОрганизации не настроен или составной и тип организации не выбран.
//
//    * ТипВладельца  - ОписаниеТипов - возвращаемое значение. Содержит ссылочные типы, из которых можно сделать выбор.
//                    - Неопределено  - возвращаемое значение. Выбор владельца не поддерживается.
//
//    * Сотрудник    - СправочникСсылка - возвращаемое значение. Владелец сертификата из ТипВладельца,
//                     которого нужно заполнить. Если уже заполнен (выбран пользователем), его не следует изменять.
//                   - Неопределено - если ТипВладельца не определен, тогда реквизит не доступен пользователю.
//
//    * Директор     - СправочникСсылка - возвращаемое значение. Директор из ТипВладельца, который может быть
//                     выбран, как владелец сертификата. Не учитывается, если заявление для ИП или физического лица.
//                   - Неопределено - начальное значение - скрыть директора из списка выбора.
//
//    * ГлавныйБухгалтер - СправочникСсылка - возвращаемое значение. Главный бухгалтер из ТипВладельца, который может
//                     быть выбран как владелец сертификата. Не учитывается, если заявление для ИП или физического лица.
//                   - Неопределено - начальное значение - скрыть главного бухгалтера из списка выбора.
//
//    * Пользователь - СправочникСсылка.Пользователи - возвращаемое значение. Пользователь - владелец сертификата.
//                     В общем случае может быть не заполнено. Рекомендуется заполнить, если есть возможность.
//                     Записывается в сертификат в поле Пользователь, может быть изменено в дальнейшем.
//
//    * Фамилия            - Строка - возвращаемое значение. Фамилия сотрудника.
//                         - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * Имя                - Строка - возвращаемое значение. Имя сотрудника.
//                         - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * Отчество           - Строка - возвращаемое значение. Отчество сотрудника.
//                         - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * ДатаРождения       - Дата   - возвращаемое значение. Дата рождения сотрудника.
//                         - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * Пол                - Строка - возвращаемое значение. Пол сотрудника "Мужской" или "Женский".
//                         - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * МестоРождения      - Строка - возвращаемое значение. Описание места рождения сотрудника.
//                         - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * Гражданство        - СправочникСсылка.СтраныМира - возвращаемое значение. Гражданство сотрудника.
//                         - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * ИНН                - Строка - возвращаемое значение. ИНН физического лица.
//                           Учитывается только в заявлении для физического лица.
//                         - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * СтраховойНомерПФР  - Строка - возвращаемое значение. СНИЛС сотрудника.
//                         - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * Должность          - Строка - возвращаемое значение. Должность сотрудника в организации.
//                           Не учитывается, если заявление для ИП или физического лица.
//                         - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * Подразделение      - Строка - возвращаемое значение. Обособленное подразделение организации, в котором
//                           работает сотрудник. Не учитывается, если заявление для ИП или физического лица.
//                         - Неопределено - значение не указано, не изменять имеющееся значение.
//
//
//    * ДокументВид        - Строка - возвращаемое значение. Строки "21" или "91". 21 - паспорт гражданина РФ,
//                           91 - иной документ, предусмотренный законодательством РФ (по СПДУЛ).
//                         - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * ДокументНомер      - Строка - возвращаемое значение. Номер документа сотрудника (серия и
//                           номер для паспорта гражданина РФ).
//                         - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * ДокументКемВыдан   - Строка - возвращаемое значение. Кем выдан документ сотрудника.
//                         - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * ДокументКодПодразделения - Строка - возвращаемое значение. Код подразделения, если вид документа 21.
//                               - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * ДокументДатаВыдачи - Дата   - возвращаемое значение. Дата выдачи документа сотрудника.
//                         - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * АдресРегистрации   - Строка - возвращаемое значение. Адрес постоянной или временной регистрации
//                           физического лица (минимум регион, населенный пункт) в формате JSON, как его возвращает
//                           функция КонтактнаяИнформацияОбъекта общего модуля УправлениеКонтактнойИнформацией.
//                           Учитывается только в заявлении для физического лица.
//                         - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * ЭлектроннаяПочта   - Строка - возвращаемое значение. Адрес электронной почты сотрудника в формате JSON, как его
//                           возвращает функция КонтактнаяИнформацияОбъекта общего модуля УправлениеКонтактнойИнформацией.
//                         - Неопределено - значение не указано, не изменять имеющееся значение.
//
//    * Телефон            - Строка - возвращаемое значение. Телефон физического лица в формате JSON, как его
//                           возвращает функция КонтактнаяИнформацияОбъекта общего модуля УправлениеКонтактнойИнформацией.
//                           Учитывается только в заявлении для физического лица.
//
//
Процедура ПриЗаполненииРеквизитовВладельцаВЗаявленииНаСертификат(Параметры) Экспорт
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("СправочникСсылка.ФизическиеЛица"));
	Параметры.ТипВладельца = Новый ОписаниеТипов(МассивТипов);
	
	Если ЗначениеЗаполнено(Параметры.Организация)
		И ТипЗнч(Параметры.Организация) = Тип("СправочникСсылка.Организации") Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ОтветственныеЛицаОрганизацийСрезПоследних.ОтветственноеЛицо КАК ОтветственноеЛицо,
			|	Пользователи.ФизЛицо КАК ФизЛицо,
			|	ОтветственныеЛицаОрганизацийСрезПоследних.Пользователь КАК Пользователь
			|ИЗ
			|	РегистрСведений.ОтветственныеЛицаОрганизаций.СрезПоследних(
			|			,
			|			Организация = &Организация
			|				И ОтветственноеЛицо В (&ОтветственныеЛица)) КАК ОтветственныеЛицаОрганизацийСрезПоследних
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
			|		ПО ОтветственныеЛицаОрганизацийСрезПоследних.Пользователь = Пользователи.Ссылка";
		
		Запрос.УстановитьПараметр("Организация", Параметры.Организация);
		МассивОтветственныхЛиц = Новый Массив;
		МассивОтветственныхЛиц.Добавить(Перечисления.ОтветственныеЛицаОрганизаций.РуководительОрганизации);
		МассивОтветственныхЛиц.Добавить(Перечисления.ОтветственныеЛицаОрганизаций.ГлавныйБухгалтер);
		Запрос.УстановитьПараметр("ОтветственныеЛица", МассивОтветственныхЛиц);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Руководитель = НовоеОписаниеСотрудника();
		ГлавБух = НовоеОписаниеСотрудника();
		
		Пока Выборка.Следующий() Цикл
			Если Выборка.ОтветственноеЛицо = Перечисления.ОтветственныеЛицаОрганизаций.РуководительОрганизации Тогда
				Руководитель.ФизЛицо = Выборка.ФизЛицо;
				Руководитель.Пользователь = Выборка.Пользователь;
			ИначеЕсли Выборка.ОтветственноеЛицо = Перечисления.ОтветственныеЛицаОрганизаций.ГлавныйБухгалтер Тогда
				ГлавБух.ФизЛицо = Выборка.ФизЛицо;
				ГлавБух.Пользователь = Выборка.Пользователь;
			КонецЕсли;
		КонецЦикла;
		
		Если ЗначениеЗаполнено(Руководитель.ФизЛицо) Тогда
			Если Не ЗначениеЗаполнено(Параметры.Сотрудник) Тогда
				Параметры.Сотрудник = Руководитель.ФизЛицо;
				Параметры.Пользователь = Руководитель.Пользователь;
			КонецЕсли;
			
			Параметры.Директор = Руководитель.ФизЛицо
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Главбух.ФизЛицо) Тогда
			Параметры.ГлавныйБухгалтер = ГлавБух.ФизЛицо;
			
			Если Не ЗначениеЗаполнено(Параметры.Сотрудник) Тогда
				Параметры.Сотрудник = ГлавБух.ФизЛицо;
				Параметры.Пользователь = ГлавБух.Пользователь;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Параметры.Сотрудник) Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Сотрудник = Параметры.Директор Тогда
		Параметры.Должность = НСтр("ru = 'Генеральный директор'");
		
	ИначеЕсли Параметры.Сотрудник = Параметры.ГлавныйБухгалтер Тогда
		Параметры.Должность = НСтр("ru = 'Главный бухгалтер'");
	Иначе
		Параметры.Должность = НСтр("ru = 'Менеджер'");
	КонецЕсли;
	
	Если ТипЗнч(Параметры.Сотрудник) <> Тип("СправочникСсылка.ФизическиеЛица") Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыСотрудника = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		Параметры.Сотрудник, "Наименование, ДатаРождения, Пол");
	
	МассивФИО = СтрРазделить(РеквизитыСотрудника.Наименование, " ", Ложь);
	Если МассивФИО.Количество() > 0 Тогда
		Параметры.Фамилия = МассивФИО[0];
	КонецЕсли;
	Если МассивФИО.Количество() > 1 Тогда
		Параметры.Имя = МассивФИО[1];
	КонецЕсли;
	Если МассивФИО.Количество() > 2 Тогда
		Параметры.Отчество = МассивФИО[2];
	КонецЕсли;
	
	Если РеквизитыСотрудника.Пол = Перечисления.ПолФизическогоЛица.Мужской Тогда
		Параметры.Пол = "Мужской";
	ИначеЕсли РеквизитыСотрудника.Пол = Перечисления.ПолФизическогоЛица.Женский Тогда
		Параметры.Пол = "Женский";
	КонецЕсли;
	
	Параметры.ДатаРождения = РеквизитыСотрудника.ДатаРождения;
	
	ДанныеПаспорта = ДанныеПаспортаФизЛица(Параметры.Сотрудник);
	
	Если ЗначениеЗаполнено(ДанныеПаспорта.Серия) И ЗначениеЗаполнено(ДанныеПаспорта.Номер) Тогда
		Параметры.Гражданство = Справочники.СтраныМира.Россия;
		Параметры.ДокументВид = "21";
		
		Параметры.ДокументНомер = СтрШаблон("%1%2", ДанныеПаспорта.Серия, ДанныеПаспорта.Номер);
		Параметры.ДокументКемВыдан = ДанныеПаспорта.КемВыдан;
		Параметры.ДокументДатаВыдачи = ДанныеПаспорта.ДатаВыдачи;
		Параметры.ДокументКодПодразделения = ДанныеПаспорта.КодПодразделения;
	КонецЕсли;
	
	КонтактыФизЛица = КонтактнаяИнформацияФизЛица(Параметры.Сотрудник);
	
	Если ЗначениеЗаполнено(КонтактыФизЛица.ЭлектроннаяПочта) Тогда
		Параметры.ЭлектроннаяПочта = КонтактыФизЛица.ЭлектроннаяПочта;
	КонецЕсли;
	
	Если Не Параметры.ЭтоФизическоеЛицо Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КонтактыФизЛица.АдресРегистрации) Тогда
		Параметры.АдресРегистрации = КонтактыФизЛица.АдресРегистрации;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КонтактыФизЛица.Телефон) Тогда
		Параметры.Телефон = КонтактыФизЛица.Телефон;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НовоеОписаниеСотрудника()
	
	ОписаниеСотрудника = Новый Структура;
	ОписаниеСотрудника.Вставить("Пользователь");
	ОписаниеСотрудника.Вставить("ФизЛицо");
	
	Возврат ОписаниеСотрудника;
	
КонецФункции

Функция КонтактнаяИнформацияФизЛица(ФизЛицо)
	
	КонтактыФизЛица = Новый Структура;
	КонтактыФизЛица.Вставить("ЭлектроннаяПочта", Неопределено);
	КонтактыФизЛица.Вставить("АдресРегистрации", Неопределено);
	КонтактыФизЛица.Вставить("Телефон", Неопределено);
	
	
	Объекты = Новый Массив;
	Объекты.Добавить(ФизЛицо);
	
	ВидыКИ = Новый Массив;
	ВидыКИ.Добавить(Справочники.ВидыКонтактнойИнформации.ДомашнийАдресФизическогоЛица);
	ВидыКИ.Добавить(Справочники.ВидыКонтактнойИнформации.МобильныйТелефонФизическогоЛица);
	ВидыКИ.Добавить(Справочники.ВидыКонтактнойИнформации.EmailФизическогоЛица);
	
	КонтактнаяИнформация = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъектов(Объекты, , ВидыКИ, ТекущаяДатаСеанса());
	
	Строка = КонтактнаяИнформация.Найти(Справочники.ВидыКонтактнойИнформации.EmailФизическогоЛица, "Вид");
	Если Строка <> Неопределено Тогда
		КонтактыФизЛица.ЭлектроннаяПочта = Строка.Значение;
	КонецЕсли;
	
	Строка = КонтактнаяИнформация.Найти(Справочники.ВидыКонтактнойИнформации.ДомашнийАдресФизическогоЛица, "Вид");
	Если Строка <> Неопределено Тогда
		КонтактыФизЛица.АдресРегистрации = Строка.Значение;
	КонецЕсли;
	
	Строка = КонтактнаяИнформация.Найти(Справочники.ВидыКонтактнойИнформации.МобильныйТелефонФизическогоЛица, "Вид");
	Если Строка <> Неопределено Тогда
		КонтактыФизЛица.Телефон = Строка.Значение
	КонецЕсли;
	
	Возврат КонтактыФизЛица;
	
КонецФункции

Функция ДанныеПаспортаФизЛица(ФизЛицо)
	
	ДанныеПаспорта = Новый Структура;
	ДанныеПаспорта.Вставить("Серия", Неопределено);
	ДанныеПаспорта.Вставить("Номер", Неопределено);
	ДанныеПаспорта.Вставить("КемВыдан", Неопределено);
	ДанныеПаспорта.Вставить("ДатаВыдачи", Неопределено);
	ДанныеПаспорта.Вставить("КодПодразделения", Неопределено);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДокументыФизическихЛицСрезПоследних.Серия,
		|	ДокументыФизическихЛицСрезПоследних.Номер,
		|	ДокументыФизическихЛицСрезПоследних.КемВыдан,
		|	ДокументыФизическихЛицСрезПоследних.ДатаВыдачи,
		|	ДокументыФизическихЛицСрезПоследних.КодПодразделения
		|ИЗ
		|	РегистрСведений.ДокументыФизическихЛиц.СрезПоследних(, Физлицо = &ФизЛицо
		|	И ВидДокумента = &ВидДокумента) КАК ДокументыФизическихЛицСрезПоследних";
	
	Запрос.УстановитьПараметр("ФизЛицо", ФизЛицо);
	Запрос.УстановитьПараметр("ВидДокумента", Справочники.ВидыДокументовФизическихЛиц.ПаспортРФ);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(ДанныеПаспорта, Выборка);
	КонецЕсли;
	
	Возврат ДанныеПаспорта;
	
КонецФункции

#КонецОбласти
