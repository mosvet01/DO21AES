////////////////////////////////////////////////////////////////////////////////
// Подсистема "Работа со сканером документов". Клиентская часть.
//
// Модуль для работы с мобильным сканером, который загружает отсканированное в открытую карточку документа.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Обрабатывает добавление скана
//
// Параметры:
//  ВладелецФайла - Ссылка на объект, который будет установлен владельцем файла.
//
// ВозвращаемоеЗначение:
//  Булево - Истина, если скан добавлен.
//
Функция ОбработатьСкан(ВладелецФайла) Экспорт

	ИмяФайла = СформироватьИмяФайла();
	Результат = МП_МобильныйСканерВызовСервера.ОбработатьСкан(ВладелецФайла, ИмяФайла);
	Если Результат.ФайлДобавлен Тогда
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Владелец", ВладелецФайла);
		ПараметрыОповещения.Вставить("Файл",     Результат.ФайлСсылка);
		ПараметрыОповещения.Вставить("Событие",  "СозданФайл");
		
		Оповестить("Запись_Файл", ПараметрыОповещения);
		
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Добавлен скан'"),
			ПолучитьНавигационнуюСсылку(Результат.ФайлСсылка),
			ИмяФайла, 
			БиблиотекаКартинок.Информация32);
		Возврат Истина;
	Иначе
		Если ЗначениеЗаполнено(Результат.ТекстОшибки) Тогда
			ПоказатьПредупреждение(, Результат.ТекстОшибки);
		Иначе
			ПоказатьПредупреждение(, Нстр("ru = 'Неизвестная ошибка добавления файла.'"));
		КонецЕсли;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// Открывает форму для обработки ручного добавления сканов
//
// Параметры:
//  ФормаВладелец - ФормаКлиентскогоПриложения - Будет установлена владельцем открываемой формы.
//  ОбработчикОповещения - ОписаниеОповещения - Обработчик после закрытия формы.
//
Процедура ДобавитьСканСМобильного(ФормаВладелец, ОбработчикОповещения) Экспорт
	
	ОткрытьФорму("ОбщаяФорма.МП_ФормаДобавлениеСканов",,ФормаВладелец,,,,ОбработчикОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СформироватьИмяФайла()
	
	Возврат СтрШаблон(НСтр("ru='Скан от %1'"), Формат(ТекущаяДата(), "ДФ=""гггг-ММ-дд ЧЧ-мм-сс"""));
	
КонецФункции

#КонецОбласти
