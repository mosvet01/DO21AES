///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2022, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Первоначальная выгрузка данных.
//
// Параметры:
//  ПараметрыСинхронизации - инициализированные параметры синхронизации в начале синхронизации
Функция ПервоначальнаяВыгрузкаДанных(ПараметрыСинхронизации) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	МобильноеПриложение = ПараметрыСинхронизации.МобильноеПриложение;
	
	ДатаНачалаВыгрузки = МП_Служебный.ОпределитьДатуУстареванияДанных(ПараметрыСинхронизации.МобильноеПриложение,
		ПараметрыСинхронизации);
	
	Запрос = ИнициализироватьЗапрос(ПараметрыСинхронизации, ДатаНачалаВыгрузки);
	
	СтруктураРезультатовЗапроса = Новый Структура;
	
	ДобавитьНСИПользователиИРоли(СтруктураРезультатовЗапроса, Запрос, МобильноеПриложение);
	
	Если ПараметрыСинхронизации.СинхронизацияПочты И ПолучитьФункциональнуюОпцию("ИспользованиеВстроеннойПочты") Тогда
		
		ДобавитьПочту(ПараметрыСинхронизации, СтруктураРезультатовЗапроса, Запрос);
		
	КонецЕсли;
	
	Если ПараметрыСинхронизации.СинхронизацияКалендаря Тогда
		
		ДобавитьКалендарь(ПараметрыСинхронизации, СтруктураРезультатовЗапроса, Запрос);
		
	КонецЕсли;
	
	Если ПараметрыСинхронизации.СинхронизацияКонтроля Тогда
		
		ДобавитьКонтроль(ПараметрыСинхронизации, СтруктураРезультатовЗапроса, Запрос);
		
	КонецЕсли;
	
	Если ПараметрыСинхронизации.СинхронизацияЗадач Тогда
		
		ДобавитьЗадачиИПоручения(ПараметрыСинхронизации, СтруктураРезультатовЗапроса, Запрос);
		
	КонецЕсли;
	
	Если ПараметрыСинхронизации.СинхронизацияОтсутствий 
		И ПолучитьФункциональнуюОпцию("ИспользоватьОтсутствия") = Истина Тогда
		
		ДобавитьОтсутствия(ПараметрыСинхронизации, СтруктураРезультатовЗапроса, Запрос);
	КонецЕсли;
	
	Если ПараметрыСинхронизации.СинхронизацияСамочувствий
		И ПолучитьФункциональнуюОпцию("УчетСамочувствияСотрудников") = Истина Тогда
		
		ДобавитьСамочувствия(ПараметрыСинхронизации, СтруктураРезультатовЗапроса, Запрос)
	КонецЕсли;
	
	Если ПараметрыСинхронизации.СинхронизацияУчетаВремени Тогда
		ДобавитьОбъектыУчетаВремени(ПараметрыСинхронизации, СтруктураРезультатовЗапроса, Запрос);
	КонецЕсли;
	
	Если ПараметрыСинхронизации.СинхронизацияМероприятий Тогда
		ДобавитьМероприятия(ПараметрыСинхронизации, СтруктураРезультатовЗапроса, Запрос);
	КонецЕсли;
	
	ДобавитьНастройкиВложений(ПараметрыСинхронизации, СтруктураРезультатовЗапроса, Запрос);
	
	ДобавитьПрочиеНастройкиСинхронизации(ПараметрыСинхронизации, СтруктураРезультатовЗапроса);
	
	СтруктураСериализованныхДанных = 
		МП_ФормированиеИсходящегоСообщения.ВыгружаемыеДанныеВJS(СтруктураРезультатовЗапроса);
		
	КоличествоОбъектов = СтруктураСериализованныхДанных.КоличествоОбъектов;
	
	Если КоличествоОбъектов > 0 Тогда
		
		МП_ФормированиеИсходящегоСообщения.ПоместитьДанныеВСообщениеОбмена(ПараметрыСинхронизации.МобильноеПриложение,
			СтруктураСериализованныхДанных.ТекстJSON, 
			МП_ФормированиеИсходящегоСообщения.СведенияОбИсходящемСообщении(КоличествоОбъектов));
		
	КонецЕсли;
	
	Возврат СтруктураРезультатовЗапроса;
	
КонецФункции

#КонецОбласти

#Область РазделыСинхронизации
Процедура ДобавитьНСИПользователиИРоли(СтруктураРезультатовЗапроса, Запрос, МобильноеПриложение)
	
	Запрос.Текст = ТекстЗапросаНСИПользователиИРоли(МобильноеПриложение);
	
	Результаты = Запрос.ВыполнитьПакет();
	
	РезультатПользователи = Результаты[0];
	РезультатРоли = Результаты[1];
	
	Если Не РезультатПользователи.Пустой() Тогда
		
		СтруктураРезультатовЗапроса.Вставить(МП_СлужебныйПовтИсп.ТипПользователь(), РезультатПользователи);
		
	КонецЕсли;
	
	Если Не РезультатРоли.Пустой() Тогда
		
		СтруктураРезультатовЗапроса.Вставить(МП_СлужебныйПовтИсп.ТипРольИсполнителя(), РезультатРоли);
		
	КонецЕсли;

КонецПроцедуры

Процедура ДобавитьПочту(ПараметрыСинхронизации, СтруктураРезультатовЗапроса, Запрос)
	
	ОсновнаяУчетнаяЗапись = ВстроеннаяПочтаСервер.ПолучитьПерсональнуюНастройку("ОсновнаяУчетнаяЗапись");
	
	ПодписьДляНовыхПисем =
		ВстроеннаяПочтаСервер.ПолучитьПерсональнуюНастройку("ПодписьДляНовыхПисем");
	
	ПодписьПриОтветеИПересылке =
		ВстроеннаяПочтаСервер.ПолучитьПерсональнуюНастройку("ПодписьПриОтветеИПересылке");
	
	Запрос.УстановитьПараметр("ОсновнаяУчетнаяЗапись", ОсновнаяУчетнаяЗапись);
	
	Запрос.УстановитьПараметр("ПодписьДляНовыхПисем", ПодписьДляНовыхПисем);
	
	Запрос.УстановитьПараметр("ПодписьПриОтветеИПересылке", ПодписьПриОтветеИПересылке);
	
	Запрос.Текст = ТекстЗапросаНСИпочта();
	
	Запрос.ВыполнитьПакетСПромежуточнымиДанными();
	
	Для каждого ТаблицаВТ Из Запрос.МенеджерВременныхТаблиц.Таблицы Цикл
		
		Результат = ТаблицаВТ.ПолучитьДанные();
		
		Если Не Результат.Пустой() Тогда
			СтруктураРезультатовЗапроса.Вставить(ТаблицаВТ.ПолноеИмя, Результат);
		КонецЕсли;
		
	КонецЦикла;
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.Текст = ТекстЗапросаПапки();
	
	РезультатПапки = Запрос.Выполнить();
	
	Если РезультатПапки.Пустой() тогда
		ВызватьИсключение ("Не указаны папки писем для синхронизации");
	КонецЕсли;
	
	СтруктураРезультатовЗапроса.Вставить(МП_СлужебныйПовтИсп.ТипПапкаПисем(), РезультатПапки);
	
	Запрос.Текст = ТекстЗапросаПочта();
	
	Запрос.ВыполнитьПакетСПромежуточнымиДанными();
	
	Результат = Запрос.МенеджерВременныхТаблиц.Таблицы.Найти("Почта").ПолучитьДанные();
	
	Если Не Результат.Пустой() Тогда
		
		ТаблицаПочта = Результат.Выгрузить();
		
		МП_Изменения.ЗаполнитьСпецифическиеКолонкиПисьма(ТаблицаПочта);
		
		СтруктураРезультатовЗапроса.Вставить(МП_СлужебныйПовтИсп.ТипПисьмо(), ТаблицаПочта);
		
		МП_Изменения.ИзмененияВсеОстальноеПоТаблицамЗаголовковПочта(
		ТаблицаПочта, СтруктураРезультатовЗапроса, ПараметрыСинхронизации);
	КонецЕсли;
	
	МП_Изменения.ОтобратьВложенияПочты(
		СтруктураРезультатовЗапроса, ПараметрыСинхронизации, Запрос.МенеджерВременныхТаблиц);

КонецПроцедуры

Процедура ДобавитьКалендарь(ПараметрыСинхронизации, СтруктураРезультатовЗапроса, Запрос)
	
	Запрос.Текст = ТекстЗапросаКалендарь();
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		
		ТаблицаКалендарь = Результат.Выгрузить();
		
		МП_Изменения.ОтобратьПредметыКалендарь(СтруктураРезультатовЗапроса, ПараметрыСинхронизации, ТаблицаКалендарь);
		
		СтруктураРезультатовЗапроса.Вставить(МП_СлужебныйПовтИсп.ТипЗаписьКалендаря(), ТаблицаКалендарь);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьКонтроль(ПараметрыСинхронизации, СтруктураРезультатовЗапроса, Запрос)

	ТипыБП = БизнесПроцессы.ТипВсеСсылки().Типы();
	
	Запрос.УстановитьПараметр("ТипыБП", ТипыБП);
	
	Запрос.УстановитьПараметр("СостояниеОтветПолучен", НСтр("ru = 'Ответ получен'"));
	
	Запрос.УстановитьПараметр("СостояниеОтветНеПолучен", НСтр("ru = 'Ответ не получен'")); 
	
	Запрос.УстановитьПараметр("СостояниеИсполнено", НСтр("ru = 'Исполнено'"));
		
	Запрос.Текст = ТекстЗапросаКонтроль();
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		
		ТаблицаКонтроль = Результат.Выгрузить();
		
		МП_Изменения.ОтобратьПредметыКонтроль(СтруктураРезультатовЗапроса, ПараметрыСинхронизации, ТаблицаКонтроль);
		
		СтруктураРезультатовЗапроса.Вставить(МП_СлужебныйПовтИсп.ТипКонтроль(), ТаблицаКонтроль);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьЗадачиИПоручения(ПараметрыСинхронизации, СтруктураРезультатовЗапроса, Запрос)

	Запрос.Текст = ТекстЗапросаЗадачиМне();
	
	Запрос.ВыполнитьПакетСПромежуточнымиДанными();
	
	РезультатЗадачи = Запрос.МенеджерВременныхТаблиц.Таблицы.Найти("ИзмененияЗадач").ПолучитьДанные();
	
	Если Не РезультатЗадачи.Пустой() Тогда
		
		СтруктураРезультатовЗапроса.Вставить(МП_СлужебныйПовтИсп.ТипЗадачаЗаголовки(), РезультатЗадачи);
		
		МП_Изменения.ИзмененияВсеОстальноеПоТаблицамЗаголовковЗадач(
			ПараметрыСинхронизации,
			СтруктураРезультатовЗапроса,
			Неопределено,
			Запрос.МенеджерВременныхТаблиц);
			
	КонецЕсли; 
	
	Запрос.Текст = ТекстЗапросаПроцессы();
	
	Запрос.ВыполнитьПакетСПромежуточнымиДанными();
	
	РезультатПроцессыИсполнения =
		Запрос.МенеджерВременныхТаблиц.Таблицы.Найти("ИзмененияПроцессовИсполнения").ПолучитьДанные(); 
	
	Если Не РезультатПроцессыИсполнения.Пустой() Тогда
		
		СтруктураРезультатовЗапроса.Вставить(
		МП_СлужебныйПовтИсп.ТипПроцессИсполненияЗаголовки(),
			РезультатПроцессыИсполнения);
			
		МП_Изменения.ИзмененияВсеОстальноеПоТаблицамЗаголовковПроцессовИсполнения(
			ПараметрыСинхронизации,
			СтруктураРезультатовЗапроса,
			Неопределено,
			Запрос.МенеджерВременныхТаблиц);
			
	КонецЕсли;

	РезультатПроцессыВопросы =
			Запрос.МенеджерВременныхТаблиц.Таблицы.Найти("ИзмененияПроцессовВопросы").ПолучитьДанные(); 
		
		Если Не РезультатПроцессыВопросы.Пустой() Тогда
			
			СтруктураРезультатовЗапроса.Вставить(
				МП_СлужебныйПовтИсп.ТипПроцессРВВЗЗаголовки(),
				РезультатПроцессыВопросы);
			
			МП_Изменения.ИзмененияВсеОстальноеПоТаблицамЗаголовковПроцессовВопросов(
				ПараметрыСинхронизации,
				СтруктураРезультатовЗапроса,
				Неопределено,
				Запрос.МенеджерВременныхТаблиц);
		
		КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьОтсутствия(ПараметрыСинхронизации, СтруктураРезультатовЗапроса, Запрос)
	
	Запрос.Текст = ТекстЗапросаМоиОтсутствияИВидыОтсутствий();
	
	Результаты = Запрос.ВыполнитьПакет();
	РезультатМоиОтсутствия = Результаты[0];
	РезультатВидыОтсутствий = Результаты[1];
	Если Не РезультатМоиОтсутствия.Пустой() Тогда
		
		ТаблицаМоиОтсутствия = РезультатМоиОтсутствия.Выгрузить();
		
		СтруктураРезультатовЗапроса.Вставить(МП_СлужебныйПовтИсп.ТипМоеОтсутствие(), ТаблицаМоиОтсутствия);
		
	КонецЕсли;
	
	Если Не РезультатВидыОтсутствий.Пустой() Тогда
		
		ТаблицаВидыОтсутствий = РезультатВидыОтсутствий.Выгрузить();
		
		СтруктураРезультатовЗапроса.Вставить(МП_СлужебныйПовтИсп.ТипВидОтсутствия(), ТаблицаВидыОтсутствий);
		
	КонецЕсли;

КонецПроцедуры

Процедура ДобавитьСамочувствия(ПараметрыСинхронизации, СтруктураРезультатовЗапроса, Запрос)
	
	Запрос.Текст = ТекстЗапросаМоиСамочувствия();
	
	Результат = Запрос.ВыполнитьПакет();
	
	Если Не Результат[0].Пустой() Тогда
		
		СтруктураРезультатовЗапроса.Вставить(МП_СлужебныйПовтИсп.ТипВремяЗамера(), Результат[0].Выгрузить());
		
	КонецЕсли;
	
	Если Не Результат[1].Пустой() Тогда
		
		ТаблицаМоиСамочувствия = Результат[1].Выгрузить();
		
		СтруктураРезультатовЗапроса.Вставить(МП_СлужебныйПовтИсп.ТипМоеСамочувствие(), ТаблицаМоиСамочувствия);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьМероприятия(ПараметрыСинхронизации, СтруктураРезультатовЗапроса, Запрос)
	
	Запрос.Текст = ТекстЗапросаМероприятия();
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		
		ТаблицаМероприятия = Результат.Выгрузить();
		
		МП_Изменения.ЗаполнитьСпецифическиеКолонкиМероприятие(СтруктураРезультатовЗапроса, ПараметрыСинхронизации, 
			ТаблицаМероприятия);
		
		СтруктураРезультатовЗапроса.Вставить(МП_СлужебныйПовтИсп.ТипМероприятие(), ТаблицаМероприятия);
		
	КонецЕсли;
	
КонецПроцедуры	

Процедура ДобавитьОбъектыУчетаВремени(ПараметрыСинхронизации, СтруктураРезультатовЗапроса, Запрос)
	
	Запрос.Текст = ТекстЗапросаУчетВремени();
	Результаты = Запрос.ВыполнитьПакет();
	РезультатВидыРабот = Результаты[0];
	РезультатЕжедневныеРаботы = Результаты[1];
	РезультатЕженедельныеРаботы = Результаты[2];
	РезультатФактическиеТрудозатраты = Результаты[3];
	РезультатИспользуемыеПроекты = Результаты[4];
	
	Если Не РезультатВидыРабот.Пустой() Тогда
		
		ТаблицаВидыРабот = РезультатВидыРабот.Выгрузить();
		
		СтруктураРезультатовЗапроса.Вставить(МП_СлужебныйПовтИсп.ТипВидРабот(), ТаблицаВидыРабот);
		
	КонецЕсли;
	
	Если Не РезультатЕжедневныеРаботы.Пустой() Тогда
		
		ТаблицаЕжедневныеРаботы = РезультатЕжедневныеРаботы.Выгрузить();
		
		СтруктураРезультатовЗапроса.Вставить(МП_СлужебныйПовтИсп.ТипЕжедневныйОтчет(), ТаблицаЕжедневныеРаботы);
		
	КонецЕсли;
	
	Если Не РезультатЕженедельныеРаботы.Пустой() Тогда
		
		ТаблицаЕженедельныеРаботы = РезультатЕженедельныеРаботы.Выгрузить();
		
		СтруктураРезультатовЗапроса.Вставить(МП_СлужебныйПовтИсп.ТипЕженедельныйОтчет(), ТаблицаЕженедельныеРаботы);
		
	КонецЕсли;
	
	Если Не РезультатФактическиеТрудозатраты.Пустой() Тогда
		
		ТаблицаФактическиеТрудозатраты = РезультатФактическиеТрудозатраты.Выгрузить();
		
		СтруктураРезультатовЗапроса.Вставить(МП_СлужебныйПовтИсп.ТипТрудозатраты(), ТаблицаФактическиеТрудозатраты);
		
	КонецЕсли;
	
	Если Не РезультатИспользуемыеПроекты.Пустой() Тогда
		
		ТаблицаИспользуемыеПроекты = РезультатИспользуемыеПроекты.Выгрузить();
		
		СтруктураРезультатовЗапроса.Вставить(МП_СлужебныйПовтИсп.ТипПроект(), ТаблицаИспользуемыеПроекты);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьНастройкиВложений(ПараметрыСинхронизации, СтруктураРезультатовЗапроса, Запрос)
	
	Запрос.Текст = ТекстЗапросаМаксРазмерВложения();
	
	Результат = Запрос.Выполнить();
	
	ТипНастройкиСинхронизации = МП_СлужебныйПовтИсп.ТипНастройкиСинхронизации();
	
	МП_Изменения.ИнициализироватьТаблицуПоТипу(
		СтруктураРезультатовЗапроса, ТипНастройкиСинхронизации);
	
	СтрокаНастроек = СтруктураРезультатовЗапроса[ТипНастройкиСинхронизации].Добавить();
	
	СтрокаНастроек.ВидНастройки = "МаксимальныйРазмерВложения";
	СтрокаНастроек.Значение = Результат.Выгрузить()[0].Значение;
	СтрокаНастроек.НастройкаСтрокой = Истина;
	
	Если ПараметрыСинхронизации.ОграничениеФорматовПередаваемыхФайлов Тогда
		
		СтрокаНастроек = СтруктураРезультатовЗапроса[ТипНастройкиСинхронизации].Добавить();
		СтрокаНастроек.ВидНастройки = "ФорматыПередаваемыхФайлов";
		СтрокаНастроек.Значение = ПараметрыСинхронизации.ФорматыПередаваемыхФайлов;
		СтрокаНастроек.НастройкаСтрокой = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьПрочиеНастройкиСинхронизации(ПараметрыСинхронизации, СтруктураРезультатовЗапроса)
	
	ТипНастройкиСинхронизации = МП_СлужебныйПовтИсп.ТипНастройкиСинхронизации();

	МП_Изменения.ИнициализироватьТаблицуПоТипу(
		СтруктураРезультатовЗапроса, ТипНастройкиСинхронизации);
	
	СтрокаНастроек = СтруктураРезультатовЗапроса[ТипНастройкиСинхронизации].Добавить();
	СтрокаНастроек.ВидНастройки = "СинхронизацияСамочувствий";
	СтрокаНастроек.Значение = ПараметрыСинхронизации.СинхронизацияСамочувствий И 
		ПолучитьФункциональнуюОпцию("УчетСамочувствияСотрудников") = Истина;
	СтрокаНастроек.НастройкаСтрокой = Истина;
	
	СтрокаНастроек = СтруктураРезультатовЗапроса[ТипНастройкиСинхронизации].Добавить();
	СтрокаНастроек.ВидНастройки = "СинхронизацияОтсутствий";
	СтрокаНастроек.Значение = ПараметрыСинхронизации.СинхронизацияОтсутствий И 
		ПолучитьФункциональнуюОпцию("ИспользоватьОтсутствия") = Истина;
	СтрокаНастроек.НастройкаСтрокой = Истина;
	
	СтрокаНастроек = СтруктураРезультатовЗапроса[ТипНастройкиСинхронизации].Добавить();
	СтрокаНастроек.ВидНастройки = "СинхронизацияМероприятий";
	СтрокаНастроек.Значение = ПараметрыСинхронизации.СинхронизацияМероприятий И 
		ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеМероприятиями") = Истина;
	СтрокаНастроек.НастройкаСтрокой = Истина;
	
	СтрокаНастроек = СтруктураРезультатовЗапроса[ТипНастройкиСинхронизации].Добавить();
	СтрокаНастроек.ВидНастройки = "СинхронизацияУчетаВремени";
	СтрокаНастроек.Значение = ПараметрыСинхронизации.СинхронизацияУчетаВремени;
	СтрокаНастроек.НастройкаСтрокой = Истина;
	
	Если ПараметрыСинхронизации.СинхронизацияУчетаВремени = Истина Тогда
		
		СтрокаНастроек = СтруктураРезультатовЗапроса[ТипНастройкиСинхронизации].Добавить();
		СтрокаНастроек.ВидНастройки = "ПроектПоУмолчанию";
		
		ПроектПоУмолчанию = ХранилищеОбщихНастроек.Загрузить("НастройкиРаботыСПроектами", "ПроектПоУмолчанию");
		Если ЗначениеЗаполнено(ПроектПоУмолчанию) Тогда
			СтрокаНастроек.Значение = XMLСтрока(ПроектПоУмолчанию);
		КонецЕсли;
		СтрокаНастроек.НастройкаСтрокой = Истина;
		
		СтрокаНастроек = СтруктураРезультатовЗапроса[ТипНастройкиСинхронизации].Добавить();
		СтрокаНастроек.ВидНастройки = "СпособУказанияВремени";
		СпособУказанияВремени = УчетВремени.ПолучитьСпособУказанияВремени();
		СтрокаНастроек.Значение = ОбщегоНазначения.ИмяЗначенияПеречисления(СпособУказанияВремени);
		СтрокаНастроек.НастройкаСтрокой = Истина;
		
		ВидРаботПоУмолчанию = ХранилищеОбщихНастроек.Загрузить("НастройкиУчетаВремени", "ВидРабот");
		СтрокаНастроек = СтруктураРезультатовЗапроса[ТипНастройкиСинхронизации].Добавить();
		СтрокаНастроек.ВидНастройки = "ВидРаботПоУмолчанию";
		СтрокаНастроек.Значение =  XMLСтрока(ВидРаботПоУмолчанию);
		СтрокаНастроек.НастройкаСтрокой = Истина;
		
	КонецЕсли;
	
	СтрокаНастроек = СтруктураРезультатовЗапроса[ТипНастройкиСинхронизации].Добавить();
	СтрокаНастроек.ВидНастройки = "СинхронизацияАдреснойКниги";
	СтрокаНастроек.Значение = ПараметрыСинхронизации.СинхронизацияАдреснойКниги;
	СтрокаНастроек.НастройкаСтрокой = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ТекстыЗапросовДляПервоначальнойВыгрузкиДанных

Функция ТекстЗапросаПочта()
	
	// Получение состояний аналогично запросу в ЖурналДокументов.ЭлектроннаяПочта.ФормаСписка
	Возврат
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЭлектроннаяПочта.Ссылка КАК Ссылка,
		|	ЭлектроннаяПочта.ЕстьВложения КАК ЕстьВложения,
		|	ЭлектроннаяПочта.Дата КАК Дата,
		|	ЭлектроннаяПочта.Папка КАК Папка,
		|	ВЫБОР
		|		КОГДА КешИнформацииОбОбъектах.Объект ЕСТЬ NULL
		|			ТОГДА 0
		|		КОГДА КешИнформацииОбОбъектах.ЕстьОшибкиПриемкиОтправкиПочты
		|				И ЭлектроннаяПочта.Отправлено = ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА 6
		|		КОГДА КешИнформацииОбОбъектах.ЕстьОшибкиПриемкиОтправкиПочты
		|				И ТИПЗНАЧЕНИЯ(ЭлектроннаяПочта.Ссылка) = ТИП(Документ.ВходящееПисьмо)
		|			ТОГДА 6
		|		КОГДА КешИнформацииОбОбъектах.Отозван
		|			ТОГДА 9
		|		КОГДА ЭлектроннаяПочта.ОтправкаОтменена
		|			ТОГДА 5
		|		КОГДА КешИнформацииОбОбъектах.ПолученОтвет
		|			ТОГДА 4
		|		КОГДА КешИнформацииОбОбъектах.ОтправленОтвет
		|				И КешИнформацииОбОбъектах.Перенаправлен
		|			ТОГДА 8
		|		КОГДА НЕ КешИнформацииОбОбъектах.ОтправленОтвет
		|				И КешИнформацииОбОбъектах.Перенаправлен
		|			ТОГДА 7
		|		КОГДА КешИнформацииОбОбъектах.ОтправленОтвет
		|				И КешИнформацииОбОбъектах.Переслан
		|			ТОГДА 3
		|		КОГДА КешИнформацииОбОбъектах.ОтправленОтвет
		|				И НЕ КешИнформацииОбОбъектах.Переслан
		|			ТОГДА 1
		|		КОГДА НЕ КешИнформацииОбОбъектах.ОтправленОтвет
		|				И КешИнформацииОбОбъектах.Переслан
		|			ТОГДА 2
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ИндексСостоянияПисьма,
		|	ЭлектроннаяПочта.Важность КАК Важность,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ЭлектроннаяПочта.Отправитель) КАК Отправитель,
		|	ЭлектроннаяПочта.УчетнаяЗапись КАК УчетнаяЗапись,
		|	КешИнформацииОбОбъектах.СрокИсполнения КАК СрокИсполнения,
		|	ЭлектроннаяПочта.Тема КАК Тема,
		|	ЭлектроннаяПочта.ПометкаУдаления КАК ПометкаУдаления,
		|	ФлагиОбъектов.Флаг КАК Флаг,
		|	ЭлектроннаяПочта.ДатаОтправкиПолучения КАК ДатаОтправки,
		|	ОтметкиВремениСсылочныхОбъектов.Граница КАК Граница,
		|	СведенияОПрочтении.Прочтен КАК Прочитан,
		|	ЛОЖЬ КАК ТолькоЗаголовок,
		|	ТИПЗНАЧЕНИЯ(ЭлектроннаяПочта.Ссылка) = ТИП(Документ.ВходящееПисьмо) КАК Входящее,
		|	ЕСТЬNULL(ЭлектроннаяПочта.Ссылка.ТекстХранилище, ЭлектроннаяПочта.Ссылка.ТекстПисьмаПростойТекстХранилище) КАК ТекстХранилище,
		|	ЭлектроннаяПочта.Ссылка.ТекстПисьмаHTMLХранилище КАК ТекстHTMLХранилище,
		|	ЭлектроннаяПочта.Ссылка.ТипТекста КАК ТипТекста,
		|	КешИнформацииОбОбъектах.СостояниеКонтроля КАК СостояниеКонтроля
		|ПОМЕСТИТЬ Почта
		|ИЗ
		|	ЖурналДокументов.ЭлектроннаяПочта КАК ЭлектроннаяПочта
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПапкиПисемДляСинхронизации КАК ПапкиПисемДляСинхронизации
		|		ПО ЭлектроннаяПочта.Папка = ПапкиПисемДляСинхронизации.Папка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КешИнформацииОбОбъектах КАК КешИнформацииОбОбъектах
		|		ПО ЭлектроннаяПочта.Ссылка = КешИнформацииОбОбъектах.Объект
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтметкиВремениСсылочныхОбъектов КАК ОтметкиВремениСсылочныхОбъектов
		|		ПО ЭлектроннаяПочта.Ссылка = ОтметкиВремениСсылочныхОбъектов.Ключ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОПрочтении КАК СведенияОПрочтении
		|		ПО ЭлектроннаяПочта.Ссылка = СведенияОПрочтении.Объект
		|			И (СведенияОПрочтении.Пользователь = &Пользователь)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФлагиОбъектов КАК ФлагиОбъектов
		|		ПО ЭлектроннаяПочта.Ссылка = ФлагиОбъектов.Объект
		|			И (ФлагиОбъектов.Пользователь = &Пользователь)
		|ГДЕ
		|	НЕ ЭлектроннаяПочта.ПометкаУдаления
		|	И ЭлектроннаяПочта.Дата > &ДатаНачалаВыгрузки";
	
КонецФункции

Функция ТекстЗапросаНСИпочта()
	
	Возврат 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ШаблоныТекстовПисем.Ссылка КАК Ссылка,
		|	ПОДСТРОКА(ШаблоныТекстовПисем.Шаблон, 1, 1000) КАК Шаблон,
		|	ШаблоныТекстовПисем.Наименование КАК Наименование,
		|	ВЫБОР
		|		КОГДА ШаблоныТекстовПисем.Ссылка = &ПодписьДляНовыхПисем
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ПодписьДляНовыхПисем,
		|	ВЫБОР
		|		КОГДА ШаблоныТекстовПисем.Ссылка = &ПодписьПриОтветеИПересылке
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ПодписьПриОтветеИПересылке,
		|	ШаблоныТекстовПисем.ПометкаУдаления КАК ПометкаУдаления
		|ПОМЕСТИТЬ MailTextTemplate
		|ИЗ
		|	Справочник.ШаблоныТекстов КАК ШаблоныТекстовПисем
		|ГДЕ
		|	ШаблоныТекстовПисем.Автор = &Пользователь
		|	И НЕ ШаблоныТекстовПисем.ПометкаУдаления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	УчетныеЗаписиЭлектроннойПочты.Ссылка КАК Ссылка,
		|	УчетныеЗаписиЭлектроннойПочты.АдресЭлектроннойПочты КАК Адрес,
		|	УчетныеЗаписиЭлектроннойПочты.ИспользоватьДляОтправки КАК МожетИспользоватьсяДляОтправки,
		|	УчетныеЗаписиЭлектроннойПочты.Ссылка = &ОсновнаяУчетнаяЗапись КАК ОсновнаяУчетнаяЗапись,
		|	УчетныеЗаписиЭлектроннойПочты.Наименование КАК Наименование,
		|	УчетныеЗаписиЭлектроннойПочты.ПометкаУдаления КАК ПометкаУдаления
		|ПОМЕСТИТЬ MailAccount
		|ИЗ
		|	Справочник.УчетныеЗаписиЭлектроннойПочты КАК УчетныеЗаписиЭлектроннойПочты
		|ГДЕ
		|	УчетныеЗаписиЭлектроннойПочты.ВариантИспользования = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияПочты.Встроенная)
		|	И УчетныеЗаписиЭлектроннойПочты.ОтветственныеЗаОбработкуПисем.Пользователь = &Пользователь
		|	И НЕ УчетныеЗаписиЭлектроннойПочты.ПометкаУдаления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	МАКСИМУМ(АдресатыПочтовыхСообщений.Ссылка) КАК Ссылка,
		|	СведенияОбАдресатах.Представление КАК Наименование,
		|	АдресатыПочтовыхСообщений.Адрес КАК Адрес,
		|	АдресатыПочтовыхСообщений.ПометкаУдаления КАК ПометкаУдаления
		|ПОМЕСТИТЬ Addressee
		|ИЗ
		|	Справочник.Пользователи КАК Пользователи
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОбАдресатах КАК СведенияОбАдресатах
		|		ПО Пользователи.Ссылка = СведенияОбАдресатах.Контакт
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.АдресатыПочтовыхСообщений КАК АдресатыПочтовыхСообщений
		|		ПО (СведенияОбАдресатах.АдресатСообщения = АдресатыПочтовыхСообщений.Ссылка)
		|ГДЕ
		|	НЕ Пользователи.ПометкаУдаления
		|	И НЕ Пользователи.Недействителен
		|	И НЕ АдресатыПочтовыхСообщений.ПометкаУдаления
		|
		|СГРУППИРОВАТЬ ПО
		|	АдресатыПочтовыхСообщений.Адрес,
		|	СведенияОбАдресатах.Представление,
		|	АдресатыПочтовыхСообщений.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	МАКСИМУМ(АдресатыПочтовыхСообщений.Ссылка),
		|	СведенияОбАдресатах.Представление,
		|	АдресатыПочтовыхСообщений.Адрес,
		|	АдресатыПочтовыхСообщений.ПометкаУдаления
		|ИЗ
		|	Справочник.РолиИсполнителей КАК РолиИсполнителей
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОбАдресатах КАК СведенияОбАдресатах
		|		ПО РолиИсполнителей.Ссылка = СведенияОбАдресатах.Контакт
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.АдресатыПочтовыхСообщений КАК АдресатыПочтовыхСообщений
		|		ПО (СведенияОбАдресатах.АдресатСообщения = АдресатыПочтовыхСообщений.Ссылка)
		|ГДЕ
		|	НЕ РолиИсполнителей.ПометкаУдаления
		|	И НЕ АдресатыПочтовыхСообщений.ПометкаУдаления
		|
		|СГРУППИРОВАТЬ ПО
		|	АдресатыПочтовыхСообщений.Адрес,
		|	СведенияОбАдресатах.Представление,
		|	АдресатыПочтовыхСообщений.ПометкаУдаления";
		
	
КонецФункции

функция ТекстЗапросаНСИПользователиИРоли(МобильноеПриложение)
	
	ТекстЗапроса =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Пользователи.Ссылка КАК Ссылка,
		|	Пользователи.Наименование КАК Наименование,
		|	ОтметкиВремениСсылочныхОбъектов.Граница КАК Граница,
		|	ФизическиеЛица.ФайлФотографии КАК ФайлФотографии,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(СведенияОПользователяхДокументооборот.Подразделение) КАК Подразделение,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(СведенияОПользователяхДокументооборот.ГрафикРаботы) КАК ГрафикРаботы,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(Пользователи.Помещение) КАК Помещение
		|	,&КонтактнаяИнформацияПользователя
		|ИЗ
		|	Справочник.Пользователи КАК Пользователи
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтметкиВремениСсылочныхОбъектов КАК ОтметкиВремениСсылочныхОбъектов
		|		ПО Пользователи.Ссылка = ОтметкиВремениСсылочныхОбъектов.Ключ
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
		|		ПО Пользователи.ФизЛицо = ФизическиеЛица.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОПользователяхДокументооборот КАК СведенияОПользователяхДокументооборот
		|		ПО Пользователи.Ссылка = СведенияОПользователяхДокументооборот.Пользователь
		|ГДЕ
		|	НЕ Пользователи.ПометкаУдаления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РолиИсполнителей.Ссылка КАК Ссылка,
		|	РолиИсполнителей.Наименование КАК Наименование,
		|	ОтметкиВремениСсылочныхОбъектов.Граница КАК Граница,
		|	РолиИсполнителей.Комментарий КАК Комментарий
		|	,&КонтактнаяИнформацияРолиИсполнителя
		|ИЗ
		|	Справочник.РолиИсполнителей КАК РолиИсполнителей
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтметкиВремениСсылочныхОбъектов КАК ОтметкиВремениСсылочныхОбъектов
		|		ПО РолиИсполнителей.Ссылка = ОтметкиВремениСсылочныхОбъектов.Ключ
		|ГДЕ
		|	НЕ РолиИсполнителей.ПометкаУдаления";
	
	Если МП_СлужебныйПовтИсп.ВерсияБольше2_2_5(МобильноеПриложение) Тогда
		МП_Служебный.ПолеКИВТекстЗапроса(ТекстЗапроса, Метаданные.Справочники.Пользователи.Имя,
			"&КонтактнаяИнформацияПользователя");
		
		МП_Служебный.ПолеКИВТекстЗапроса(ТекстЗапроса, Метаданные.Справочники.РолиИсполнителей.Имя,
			"&КонтактнаяИнформацияРолиИсполнителя");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ",&КонтактнаяИнформацияПользователя", "");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ",&КонтактнаяИнформацияРолиИсполнителя", "");
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаПапки()
	
	Возврат
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СинхронизацияПапокПисемСМобильнымКлиентом.Папка КАК Папка,
		|	ПапкиУчетныхЗаписей.УчетнаяЗапись КАК УчетнаяЗапись
		|ПОМЕСТИТЬ ПапкиПисемДляСинхронизации
		|ИЗ
		|	РегистрСведений.СинхронизацияПапокПисемСМобильнымКлиентом КАК СинхронизацияПапокПисемСМобильнымКлиентом
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПапкиУчетныхЗаписей КАК ПапкиУчетныхЗаписей
		|		ПО СинхронизацияПапокПисемСМобильнымКлиентом.Папка = ПапкиУчетныхЗаписей.Папка
		|ГДЕ
		|	СинхронизацияПапокПисемСМобильнымКлиентом.Пользователь = &Пользователь
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПапкиПисемДляСинхронизации.Папка КАК Ссылка,
		|	ПапкиПисем.ПометкаУдаления КАК ПометкаУдаления,
		|	ПапкиПисем.ВидПапки КАК ВидПапки,
		|	ПапкиПисем.Родитель КАК Родитель,
		|	ИСТИНА КАК Синхронизировать,
		|	ПапкиПисемДляСинхронизации.УчетнаяЗапись КАК УчетнаяЗапись,
		|	ПапкиПисем.Наименование КАК Наименование,
		|	ЕСТЬNULL(ОтметкиВремениСсылочныхОбъектов.Граница, 0) КАК Граница
		|ИЗ
		|	ПапкиПисемДляСинхронизации КАК ПапкиПисемДляСинхронизации
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтметкиВремениСсылочныхОбъектов КАК ОтметкиВремениСсылочныхОбъектов
		|		ПО ПапкиПисемДляСинхронизации.Папка = ОтметкиВремениСсылочныхОбъектов.Ключ
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПапкиПисем КАК ПапкиПисем
		|		ПО ПапкиПисемДляСинхронизации.Папка = ПапкиПисем.Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	МП_ОтправленныеСсылочныеДанные.Ссылка,
		|	ИСТИНА,
		|	NULL,
		|	NULL,
		|	ЛОЖЬ,
		|	NULL,
		|	NULL,
		|	NULL
		|ИЗ
		|	РегистрСведений.МП_ОтправленныеСсылочныеДанные КАК МП_ОтправленныеСсылочныеДанные
		|ГДЕ
		|	МП_ОтправленныеСсылочныеДанные.МобильноеПриложение = &МобильноеПриложение
		|	И МП_ОтправленныеСсылочныеДанные.Ссылка ССЫЛКА Справочник.ПапкиПисем
		|	И НЕ МП_ОтправленныеСсылочныеДанные.Ссылка В
		|				(ВЫБРАТЬ
		|					ПапкиПисемДляСинхронизации.Папка КАК Папка
		|				ИЗ
		|					ПапкиПисемДляСинхронизации КАК ПапкиПисемДляСинхронизации)
		|
		|СГРУППИРОВАТЬ ПО
		|	МП_ОтправленныеСсылочныеДанные.Ссылка";
	
КонецФункции

Функция ТекстЗапросаЗадачиМне()
	
	Возврат
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЗадачиПоИсполнителю.Ссылка КАК Ссылка,
		|	ЗадачиПоИсполнителю.Наименование КАК Наименование,
		|	ЗадачиПоИсполнителю.Автор КАК Автор,
		|	ЗадачиПоИсполнителю.Исполнитель КАК Исполнитель,
		|	ЗадачиПоИсполнителю.СрокИсполнения КАК СрокИсполнения,
		|	ЗадачиПоИсполнителю.Дата КАК ДатаСоздания,
		|	КешИнформацииОбОбъектах.ЕстьФайлы КАК ЕстьВложения,
		|	ВЫБОР
		|		КОГДА КешИнформацииОбОбъектах.СостояниеКонтроля = ЗНАЧЕНИЕ(Перечисление.СостоянияКонтроля.НаКонтроле)
		|			ТОГДА ВЫБОР
		|				КОГДА КешИнформацииОбОбъектах.СрокИсполнения = ДАТАВРЕМЯ(1, 1, 1)
		|				ИЛИ КешИнформацииОбОбъектах.СрокИсполнения >= &ТекущаяДата
		|					ТОГДА 1
		|				ИНАЧЕ 3
		|			КОНЕЦ
		|		КОГДА КешИнформацииОбОбъектах.СостояниеКонтроля = ЗНАЧЕНИЕ(Перечисление.СостоянияКонтроля.СнятсКонтроля)
		|			ТОГДА 4
		|		КОГДА КешИнформацииОбОбъектах.СостояниеКонтроля = ЗНАЧЕНИЕ(Перечисление.СостоянияКонтроля.НаКонтролеНесколько)
		|			ТОГДА ВЫБОР
		|				КОГДА КешИнформацииОбОбъектах.СрокИсполнения = ДАТАВРЕМЯ(1, 1, 1)
		|				ИЛИ КешИнформацииОбОбъектах.СрокИсполнения >= &ТекущаяДата
		|					ТОГДА 5
		|				КОГДА КешИнформацииОбОбъектах.СрокИсполненияОбщий = ДАТАВРЕМЯ(1, 1, 1)
		|				ИЛИ КешИнформацииОбОбъектах.СрокИсполненияОбщий >= &ТекущаяДата
		|					ТОГДА 8
		|				ИНАЧЕ 6
		|			КОНЕЦ
		|		КОГДА КешИнформацииОбОбъектах.СостояниеКонтроля = ЗНАЧЕНИЕ(Перечисление.СостоянияКонтроля.СнятсКонтроляНесколько)
		|			ТОГДА 7
		|		КОГДА КешИнформацииОбОбъектах.СостояниеКонтроля = ЗНАЧЕНИЕ(Перечисление.СостоянияКонтроля.СмешанноНесколько)
		|			ТОГДА ВЫБОР
		|				КОГДА КешИнформацииОбОбъектах.СрокИсполнения = ДАТАВРЕМЯ(1, 1, 1)
		|				ИЛИ КешИнформацииОбОбъектах.СрокИсполнения >= &ТекущаяДата
		|					ТОГДА 9
		|				ИНАЧЕ 10
		|			КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СостояниеКонтроля,
		|	ЗадачиПоИсполнителю.ПринятаКИсполнению КАК ПринятаКИсполнению,
		|	ЛОЖЬ КАК ТолькоЗаголовок,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(ЗадачиПоИсполнителю.БизнесПроцесс) = ТИП(БизнесПроцесс.Исполнение)
		|			ТОГДА ЗадачиПоИсполнителю.БизнесПроцесс
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК ПроцессИсполнение,
		|	ЗадачиПоИсполнителю.РольИсполнителя.Владелец КАК РольИсполнителя,
		|	ЗадачиПоИсполнителю.БизнесПроцесс КАК БизнесПроцесс,
		|	ЗадачиПоИсполнителю.СостояниеБизнесПроцесса КАК Состояние,
		|	ЗадачиПоИсполнителю.СостояниеБизнесПроцесса.Порядок КАК СостояниеИндекс,
		|	ОтметкиВремениСсылочныхОбъектов.Граница КАК Граница,
		|	ЗадачиПоИсполнителю.ПометкаУдаления КАК ПометкаУдаления,
		|	ЗадачиПоИсполнителю.РезультатВыполнения КАК КомментарийИсполнителя,
		|	ФлагиОбъектов.Флаг КАК Флаг,
		|	ЗадачиПоИсполнителю.Важность КАК Важность
		|ПОМЕСТИТЬ ИзмененияЗадач
		|ИЗ
		|	Задача.ЗадачаИсполнителя КАК ЗадачиПоИсполнителю
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФлагиОбъектов КАК ФлагиОбъектов
		|		ПО ЗадачиПоИсполнителю.Ссылка = ФлагиОбъектов.Объект
		|		И (ФлагиОбъектов.Пользователь = &Пользователь)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КешИнформацииОбОбъектах КАК КешИнформацииОбОбъектах
		|		ПО ЗадачиПоИсполнителю.Ссылка = КешИнформацииОбОбъектах.Объект
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтметкиВремениСсылочныхОбъектов КАК ОтметкиВремениСсылочныхОбъектов
		|		ПО ЗадачиПоИсполнителю.Ссылка = ОтметкиВремениСсылочныхОбъектов.Ключ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗадачиДляВыполнения КАК ЗадачиДляВыполнения
		|		ПО (ЗадачиДляВыполнения.Задача = ЗадачиПоИсполнителю.Ссылка)
		|ГДЕ
		|	ЗадачиПоИсполнителю.СостояниеБизнесПроцесса = ЗНАЧЕНИЕ(Перечисление.СостоянияБизнесПроцессов.Активен)
		|	И НЕ ТИПЗНАЧЕНИЯ(ЗадачиПоИсполнителю.БизнесПроцесс) = ТИП(БизнесПроцесс.КомплексныйПроцесс)	
		|	И НЕ ЗадачиПоИсполнителю.ПометкаУдаления
		|	И ЗадачиПоИсполнителю.ИсключенаИзПроцесса = ЛОЖЬ
		|	И ИСТИНА В
		|		(ВЫБРАТЬ ПЕРВЫЕ 1
		|			ИСТИНА
		|		ИЗ
		|			РегистрСведений.ИсполнителиРолейИДелегаты КАК ИсполнителиРолейИДелегаты
		|		ГДЕ
		|			ИсполнителиРолейИДелегаты.РольПользователь = ЗадачиПоИсполнителю.ТекущийИсполнитель
		|			И ИсполнителиРолейИДелегаты.ИсполнительДелегат = &Пользователь
		|			И ИсполнителиРолейИДелегаты.ИмяОбластиДелегирования В ("""", ""ПроцессыИЗадачи""))
		|	И НЕ ЗадачиПоИсполнителю.Выполнена
		|	И (ЗадачиДляВыполнения.СостояниеВыполнения ЕСТЬ NULL
		|	ИЛИ ЗадачиДляВыполнения.СостояниеВыполнения <> ЗНАЧЕНИЕ(Перечисление.СостоянияЗадачДляВыполнения.ГотоваКВыполнению))";
	
КонецФункции

функция ТекстЗапросаПроцессы()
	
	Возврат
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Исполнение.Наименование КАК Наименование,
		|	Исполнение.Автор КАК Автор,
		|	Исполнение.Важность КАК Важность,
		|	Исполнение.ВедущаяЗадача КАК ГлавнаяЗадача,
		|	Исполнение.Дата КАК ДатаСоздания,
		|	Исполнение.ДатаНачала КАК ДатаСтарта,
		|	Исполнение.Состояние КАК Состояние,
		|	Исполнение.СрокИсполненияПроцесса КАК Срок,
		|	КешИнформацииОбОбъектах.ЕстьФайлы КАК ЕстьВложения,
		|	Исполнение.Описание КАК Описание,
		|	ВЫБОР
		|		КОГДА КешИнформацииОбОбъектах.СостояниеКонтроля = ЗНАЧЕНИЕ(Перечисление.СостоянияКонтроля.НаКонтроле)
		|			ТОГДА ВЫБОР
		|				КОГДА КешИнформацииОбОбъектах.СрокИсполнения = ДАТАВРЕМЯ(1, 1, 1)
		|				ИЛИ КешИнформацииОбОбъектах.СрокИсполнения >= &ТекущаяДата
		|					ТОГДА 1
		|				ИНАЧЕ 3
		|			КОНЕЦ
		|		КОГДА КешИнформацииОбОбъектах.СостояниеКонтроля = ЗНАЧЕНИЕ(Перечисление.СостоянияКонтроля.СнятсКонтроля)
		|			ТОГДА 4
		|		КОГДА КешИнформацииОбОбъектах.СостояниеКонтроля = ЗНАЧЕНИЕ(Перечисление.СостоянияКонтроля.НаКонтролеНесколько)
		|			ТОГДА ВЫБОР
		|				КОГДА КешИнформацииОбОбъектах.СрокИсполнения = ДАТАВРЕМЯ(1, 1, 1)
		|				ИЛИ КешИнформацииОбОбъектах.СрокИсполнения >= &ТекущаяДата
		|					ТОГДА 5
		|				КОГДА КешИнформацииОбОбъектах.СрокИсполненияОбщий = ДАТАВРЕМЯ(1, 1, 1)
		|				ИЛИ КешИнформацииОбОбъектах.СрокИсполненияОбщий >= &ТекущаяДата
		|					ТОГДА 8
		|				ИНАЧЕ 6
		|			КОНЕЦ
		|		КОГДА КешИнформацииОбОбъектах.СостояниеКонтроля = ЗНАЧЕНИЕ(Перечисление.СостоянияКонтроля.СнятсКонтроляНесколько)
		|			ТОГДА 7
		|		КОГДА КешИнформацииОбОбъектах.СостояниеКонтроля = ЗНАЧЕНИЕ(Перечисление.СостоянияКонтроля.СмешанноНесколько)
		|			ТОГДА ВЫБОР
		|				КОГДА КешИнформацииОбОбъектах.СрокИсполнения = ДАТАВРЕМЯ(1, 1, 1)
		|				ИЛИ КешИнформацииОбОбъектах.СрокИсполнения >= &ТекущаяДата
		|					ТОГДА 9
		|				ИНАЧЕ 10
		|			КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СостояниеКонтроля,
		|	Исполнение.Ссылка КАК Ссылка,
		|	ОтметкиВремениСсылочныхОбъектов.Граница КАК Граница,
		|	ЛОЖЬ КАК ТолькоЗаголовок,
		|	Исполнение.ПометкаУдаления
		|	ИЛИ Исполнение.Завершен КАК ПометкаУдаления,
		|	Исполнение.ДатаЗавершения КАК ДатаЗавершения,
		|	ФлагиОбъектов.Флаг КАК Флаг,
		|	NULL КАК ИнициаторПроцесса,
		|	NULL КАК ДатаНачала,
		|	NULL КАК СрокИсполнения,
		|	NULL КАК ПринятоеРешение,
		|	NULL КАК ОтправитьНаУточнение
		|ПОМЕСТИТЬ ИзмененияПроцессовИсполнения
		|ИЗ
		|	БизнесПроцесс.Исполнение КАК Исполнение
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтметкиВремениСсылочныхОбъектов КАК ОтметкиВремениСсылочныхОбъектов
		|		ПО Исполнение.Ссылка = ОтметкиВремениСсылочныхОбъектов.Ключ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФлагиОбъектов КАК ФлагиОбъектов
		|		ПО Исполнение.Ссылка = ФлагиОбъектов.Объект
		|		И (ФлагиОбъектов.Пользователь = &Пользователь)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КешИнформацииОбОбъектах КАК КешИнформацииОбОбъектах
		|		ПО Исполнение.Ссылка = КешИнформацииОбОбъектах.Объект
		|ГДЕ
		|	НЕ Исполнение.Завершен
		|	И НЕ Исполнение.ПометкаУдаления
		|	И Исполнение.Автор = &Пользователь
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РешениеВопросовВыполненияЗадач.Наименование КАК Наименование,
		|	РешениеВопросовВыполненияЗадач.Автор КАК Автор,
		|	РешениеВопросовВыполненияЗадач.Важность КАК Важность,
		|	РешениеВопросовВыполненияЗадач.ГлавнаяЗадача КАК ГлавнаяЗадача,
		|	РешениеВопросовВыполненияЗадач.Дата КАК ДатаСоздания,
		|	РешениеВопросовВыполненияЗадач.Состояние КАК Состояние,
		|	КешИнформацииОбОбъектах.ЕстьФайлы КАК ЕстьВложения,
		|	РешениеВопросовВыполненияЗадач.Описание КАК Описание,
		|	РешениеВопросовВыполненияЗадач.Ссылка КАК Ссылка,
		|	ОтметкиВремениСсылочныхОбъектов.Граница КАК Граница,
		|	ЛОЖЬ КАК ТолькоЗаголовок,
		|	РешениеВопросовВыполненияЗадач.ПометкаУдаления КАК ПометкаУдаления,
		|	РешениеВопросовВыполненияЗадач.ДатаЗавершения КАК ДатаЗавершения,
		|	ФлагиОбъектов.Флаг КАК Флаг,
		|	РешениеВопросовВыполненияЗадач.ИнициаторПроцесса КАК ИнициаторПроцесса,
		|	РешениеВопросовВыполненияЗадач.ДатаНачала КАК ДатаНачала,
		|	РешениеВопросовВыполненияЗадач.СрокИсполнения КАК СрокИсполнения,
		|	РешениеВопросовВыполненияЗадач.ПринятоеРешение КАК ПринятоеРешение,
		|	РешениеВопросовВыполненияЗадач.ОтправитьНаУточнение КАК ОтправитьНаУточнение,
		|	ВЫБОР
		|		КОГДА РешениеВопросовВыполненияЗадач.ИнициаторПроцесса ССЫЛКА Справочник.Пользователи
		|			ТОГДА ""User""
		|		ИНАЧЕ ""Role""
		|	КОНЕЦ КАК ТипИнициатораПроцесса
		|ПОМЕСТИТЬ ИзмененияПроцессовВопросы
		|ИЗ
		|	БизнесПроцесс.РешениеВопросовВыполненияЗадач КАК РешениеВопросовВыполненияЗадач
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтметкиВремениСсылочныхОбъектов КАК ОтметкиВремениСсылочныхОбъектов
		|		ПО РешениеВопросовВыполненияЗадач.Ссылка = ОтметкиВремениСсылочныхОбъектов.Ключ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФлагиОбъектов КАК ФлагиОбъектов
		|		ПО РешениеВопросовВыполненияЗадач.Ссылка = ФлагиОбъектов.Объект
		|		И (ФлагиОбъектов.Пользователь = &Пользователь)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КешИнформацииОбОбъектах КАК КешИнформацииОбОбъектах
		|		ПО РешениеВопросовВыполненияЗадач.Ссылка = КешИнформацииОбОбъектах.Объект
		|ГДЕ
		|	НЕ РешениеВопросовВыполненияЗадач.ПометкаУдаления
		|	И РешениеВопросовВыполненияЗадач.Автор = &Пользователь
		|	И НЕ РешениеВопросовВыполненияЗадач.Завершен";
	
КонецФункции

Функция ТекстЗапросаКалендарь()
	
	Возврат
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаписиРабочегоКалендаря.Ссылка КАК Ссылка,
	|	ЗаписиРабочегоКалендаря.Наименование КАК Наименование,
	|	ЗаписиРабочегоКалендаря.Пользователь КАК Пользователь,
	|	ЗаписиРабочегоКалендаря.ВесьДень КАК ВесьДень,
	|	ЗаписиРабочегоКалендаря.ДатаНачала КАК ДатаНачала,
	|	ЗаписиРабочегоКалендаря.ДатаНачалаПовторения КАК ДатаНачалаПовторения,
	|	ЗаписиРабочегоКалендаря.ДатаОкончания КАК ДатаОкончания,
	|	ЗаписиРабочегоКалендаря.ДатаОкончанияПовторения КАК ДатаОкончанияПовторения,
	|	ЗаписиРабочегоКалендаря.ИнтервалПовторения КАК ИнтервалПовторения,
	|	ЗаписиРабочегоКалендаря.КоличествоПовторов КАК КоличествоПовторов,
	|	ЗаписиРабочегоКалендаря.Описание КАК Описание,
	|	ЗаписиРабочегоКалендаря.ПовторениеПоДнямМесяца КАК ПовторениеПоДнямМесяца,
	|	ЗаписиРабочегоКалендаря.ПовторениеПоМесяцам КАК ПовторениеПоМесяцам,
	|	ЗаписиРабочегоКалендаря.ПравилоОкончанияПовторения КАК ПравилоОкончанияПовторения,
	|	ЗаписиРабочегоКалендаря.Связанная КАК Связанная,
	|	ЗаписиРабочегоКалендаря.Предмет КАК Предмет,
	|	ЗаписиРабочегоКалендаря.Состояние КАК Состояние,
	|	ЗаписиРабочегоКалендаря.ТипЗаписиКалендаря КАК ТипЗаписиКалендаря,
	|	ЗаписиРабочегоКалендаря.ЧастотаПовторения КАК ЧастотаПовторения,
	|	ЗаписиРабочегоКалендаря.ИсключенияПовторения.(
	|		ДатаИсключения КАК ДатаИсключения,
	|		ЗаписьИсключения КАК ЗаписьИсключения
	|	) КАК ИсключенияПовторения,
	|	ЗаписиРабочегоКалендаря.ПовторениеПоДням.(
	|		ДеньНедели КАК ДеньНедели,
	|		НомерВхождения КАК НомерВхождения
	|	) КАК ПовторениеПоДням,
	|	ЗаписиРабочегоКалендаря.ПометкаУдаления КАК ПометкаУдаления,
	|	ОтметкиВремениСсылочныхОбъектов.Граница КАК Граница,
	|	ЕСТЬNULL(НастройкиОтображенияЗаписейОтработано.ЗначениеНастройки, ЛОЖЬ) КАК Отработано,
	|	ВЫБОР
	|		КОГДА НастройкиОтображенияЗаписейЦвета.ЗначениеНастройки = ЗНАЧЕНИЕ(Перечисление.ЦветаРабочегоКалендаря.Нет)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ЦветаРабочегоКалендаря.ПустаяСсылка)
	|		ИНАЧЕ ЕСТЬNULL(НастройкиОтображенияЗаписейЦвета.ЗначениеНастройки, ЗНАЧЕНИЕ(Перечисление.ЦветаРабочегоКалендаря.ПустаяСсылка))
	|	КОНЕЦ КАК Цвет
	|ИЗ
	|	Справочник.ЗаписиРабочегоКалендаря КАК ЗаписиРабочегоКалендаря
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтметкиВремениСсылочныхОбъектов КАК ОтметкиВремениСсылочныхОбъектов
	|		ПО ЗаписиРабочегоКалендаря.Ссылка = ОтметкиВремениСсылочныхОбъектов.Ключ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиОтображенияЗаписейРабочегоКалендаря КАК НастройкиОтображенияЗаписейОтработано
	|		ПО (НастройкиОтображенияЗаписейОтработано.Пользователь = &Пользователь)
	|			И ЗаписиРабочегоКалендаря.Ссылка = НастройкиОтображенияЗаписейОтработано.Событие
	|			И (НастройкиОтображенияЗаписейОтработано.Настройка = ЗНАЧЕНИЕ(Перечисление.НастройкиЗаписейРабочегоКалендаря.Отработана))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиОтображенияЗаписейРабочегоКалендаря КАК НастройкиОтображенияЗаписейЦвета
	|		ПО (НастройкиОтображенияЗаписейЦвета.Пользователь = &Пользователь)
	|			И ЗаписиРабочегоКалендаря.Ссылка = НастройкиОтображенияЗаписейЦвета.Событие
	|			И (НастройкиОтображенияЗаписейЦвета.Настройка = ЗНАЧЕНИЕ(Перечисление.НастройкиЗаписейРабочегоКалендаря.Цвет))
	|ГДЕ
	|	НЕ ЗаписиРабочегоКалендаря.ПометкаУдаления
	|	И ЗаписиРабочегоКалендаря.Пользователь = &Пользователь
	|	И (ЗаписиРабочегоКалендаря.ДатаОкончания >= &ДатаНачалаВыгрузки
	|			ИЛИ ЗаписиРабочегоКалендаря.ПравилоОкончанияПовторения = ЗНАЧЕНИЕ(Перечисление.ПравилаОкончанияПовторения.Никогда))";

КонецФункции

Функция ТекстЗапросаКонтроль()
	
	Возврат
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Контроль.Ссылка КАК Ссылка,
		|	Контроль.Автор КАК Автор,
		|	Контроль.Наименование КАК Наименование,
		|	Контроль.ДатаПостановкиНаКонтроль КАК ДатаПостановкиНаКонтроль,
		|	Контроль.ДатаСнятияСКонтроля КАК ДатаСнятияСКонтроля,
		|	Контроль.ИсполнителиСтрокой КАК ИсполнителиСтрокой,
		|	Контроль.Источник КАК Источник,
		|	Контроль.Контролер КАК Контролер,
		|	Контроль.Описание КАК Описание,
		|	Контроль.ПоставленНаКонтроль КАК ПоставленНаКонтроль,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(Контроль.Предмет) = ТИП(Справочник.ВеткиПереписки)
		|			ТОГДА Контроль.Предмет.КорневоеПисьмо
		|		ИНАЧЕ Контроль.Предмет
		|	КОНЕЦ КАК Предмет,
		|	Контроль.СнятСКонтроля КАК СнятСКонтроля,
		|	Контроль.СрокИсполнения КАК СрокИсполнения,
		|	Контроль.Исполнители.(
		|		ДатаИсполнения КАК ДатаИсполнения,
		|		ДатаКонтроля КАК ДатаКонтроля,
		|		Исполнено КАК Исполнено,
		|		Ответственный КАК Ответственный,
		|		Проконтролировано КАК Проконтролировано,
		|		ВЫБОР
		|			КОГДА ТИПЗНАЧЕНИЯ(Контроль.Исполнители.Исполнитель) = ТИП(Справочник.ПолныеРоли)
		|				ТОГДА Контроль.Исполнители.Исполнитель.Владелец
		|			ИНАЧЕ Контроль.Исполнители.Исполнитель
		|		КОНЕЦ КАК Исполнитель,
		|		Источник КАК Источник,
		|		ПРЕДСТАВЛЕНИЕССЫЛКИ(Контроль.Исполнители.Исполнитель) КАК ИсполнительСтрокой,
		|		ВЫБОР
		|			КОГДА Контроль.Исполнители.Исполнитель ССЫЛКА Справочник.Пользователи
		|				ТОГДА ""User""
		|			КОГДА Контроль.Исполнители.Исполнитель ССЫЛКА Справочник.АдресатыПочтовыхСообщений
		|				ТОГДА ""Addressee""
		|			ИНАЧЕ ""Role""
		|		КОНЕЦ КАК ТипИсполнителя,
		|		ВЫБОР
		|			КОГДА ТИПЗНАЧЕНИЯ(Контроль.Предмет) = ТИП(Справочник.ВеткиПереписки)
		|				ТОГДА ВЫБОР
		|						КОГДА ТИПЗНАЧЕНИЯ(Контроль.Исполнители.Источник) = ТИП(Документ.ВходящееПисьмо)
		|							ТОГДА &СостояниеОтветПолучен
		|						КОГДА ТИПЗНАЧЕНИЯ(Контроль.Исполнители.Источник) = ТИП(Документ.ИсходящееПисьмо)
		|							ТОГДА &СостояниеОтветНеПолучен
		|					КОНЕЦ
		|			КОГДА ТИПЗНАЧЕНИЯ(Контроль.Предмет) В (&ТипыБП)
		|					ИЛИ ТИПЗНАЧЕНИЯ(Контроль.Предмет) = ТИП(Задача.ЗадачаИсполнителя)
		|				ТОГДА ВЫБОР
		|						КОГДА Контроль.Исполнители.Исполнено
		|							ТОГДА &СостояниеИсполнено
		|					КОНЕЦ
		|		КОНЕЦ КАК Состояние
		|	) КАК Исполнители,
		|	ОтметкиВремениСсылочныхОбъектов.Граница КАК Граница,
		|	Контроль.ПометкаУдаления КАК ПометкаУдаления
		|ИЗ
		|	Справочник.Контроль КАК Контроль
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтметкиВремениСсылочныхОбъектов КАК ОтметкиВремениСсылочныхОбъектов
		|		ПО Контроль.Ссылка = ОтметкиВремениСсылочныхОбъектов.Ключ
		|ГДЕ
		|	ИСТИНА В
		|			(ВЫБРАТЬ ПЕРВЫЕ 1
		|				ИСТИНА
		|			ИЗ
		|				РегистрСведений.ИсполнителиРолейИДелегаты КАК ИсполнителиРолейИДелегаты
		|			ГДЕ
		|				ИсполнителиРолейИДелегаты.РольПользователь = Контроль.Контролер
		|				И ИсполнителиРолейИДелегаты.ИсполнительДелегат = &Пользователь
		|				И ИсполнителиРолейИДелегаты.ИмяОбластиДелегирования В ("""", ""Контроль""))
		|	И НЕ Контроль.СнятСКонтроля
		|	И НЕ Контроль.ПометкаУдаления";
	
КонецФункции

Функция ТекстЗапросаМоиОтсутствияИВидыОтсутствий()
	
	Возврат
		"ВЫБРАТЬ
		|	Отсутствие.Ссылка КАК Ссылка,
		|	Отсутствие.ПометкаУдаления КАК ПометкаУдаления,
		|	Отсутствие.Номер КАК Номер,
		|	Отсутствие.Дата КАК Дата,
		|	Отсутствие.БудуРазбиратьЗадачи КАК БудуРазбиратьЗадачи,
		|	Отсутствие.ВесьДень КАК ВесьДень,
		|	Отсутствие.ВидОтсутствия КАК ВидОтсутствия,
		|	Отсутствие.ДатаНачала КАК ДатаНачала,
		|	Отсутствие.ДатаОкончания КАК ДатаОкончания,
		|	Отсутствие.Комментарий КАК Комментарий,
		|	Отсутствие.ПериодСтрокой КАК ПериодСтрокой,
		|	Отсутствие.Заместители.(
		|		Заместитель КАК Заместитель,
		|		Комментарий КАК Комментарий
		|	) КАК Заместители,
		|	ОтметкиВремениСсылочныхОбъектов.Граница КАК Граница
		|ИЗ
		|	Документ.Отсутствие КАК Отсутствие
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтметкиВремениСсылочныхОбъектов КАК ОтметкиВремениСсылочныхОбъектов
		|		ПО Отсутствие.Ссылка = ОтметкиВремениСсылочныхОбъектов.Ключ
		|ГДЕ
		|	Отсутствие.Сотрудник = &Пользователь
		|	И НЕ Отсутствие.ПометкаУдаления
		|	И Отсутствие.ДатаОкончания >= &ДатаНачалаВыгрузки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВидыОтсутствий.Ссылка КАК Ссылка,
		|	ВидыОтсутствий.Наименование КАК Наименование,
		|	ВидыОтсутствий.Комментарий КАК Комментарий,
		|	ВидыОтсутствий.ЭтоУдаленнаяРабота КАК ЭтоУдаленнаяРабота
		|ИЗ
		|	Справочник.ВидыОтсутствий КАК ВидыОтсутствий
		|ГДЕ
		|	НЕ ВидыОтсутствий.ПометкаУдаления";
		
КонецФункции

Функция ТекстЗапросаМаксРазмерВложения() Экспорт
	
	Возврат
		"ВЫБРАТЬ
		|	МаксимальныйРазмерФайла.Значение КАК Значение
		|ИЗ
		|	Константа.МаксимальныйРазмерФайла КАК МаксимальныйРазмерФайла";
	
КонецФункции

Функция ТекстЗапросаМоиСамочувствия()
	
	Возврат
		"ВЫБРАТЬ
		|	ГрафикУчетаСамочувствияСотрудников.Ссылка КАК Ссылка,
		|	ГрафикУчетаСамочувствияСотрудников.ВремяЗамера КАК ВремяЗамера,
		|	ГрафикУчетаСамочувствияСотрудников.Недействительная КАК Недействительная,
		|	ГрафикУчетаСамочувствияСотрудников.ПометкаУдаления КАК ПометкаУдаления
		|ИЗ
		|	Справочник.ГрафикУчетаСамочувствияСотрудников КАК ГрафикУчетаСамочувствияСотрудников
		|ГДЕ
		|	НЕ ГрафикУчетаСамочувствияСотрудников.ПометкаУдаления
		|	И НЕ ГрафикУчетаСамочувствияСотрудников.Недействительная
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СамочувствиеСотрудника.Ссылка КАК Ссылка,
		|	СамочувствиеСотрудника.ПометкаУдаления КАК ПометкаУдаления,
		|	СамочувствиеСотрудника.Номер КАК Номер,
		|	СамочувствиеСотрудника.Дата КАК Дата,
		|	ОтметкиВремениСсылочныхОбъектов.Граница КАК Граница,
		|	СамочувствиеСотрудника.Температура КАК Температура,
		|	СамочувствиеСотрудника.Самочувствие КАК Самочувствие,
		|	СамочувствиеСотрудника.ПовышеннаяТемпература КАК ПовышеннаяТемпература,
		|	СамочувствиеСотрудника.ДатаЗамера КАК ДатаЗамера,
		|	СамочувствиеСотрудника.ТочкаЗамера КАК ТочкаЗамера
		|ИЗ
		|	Документ.СамочувствиеСотрудника КАК СамочувствиеСотрудника
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтметкиВремениСсылочныхОбъектов КАК ОтметкиВремениСсылочныхОбъектов
		|		ПО СамочувствиеСотрудника.Ссылка = ОтметкиВремениСсылочныхОбъектов.Ключ
		|ГДЕ
		|	СамочувствиеСотрудника.Сотрудник = &Пользователь
		|	И НЕ СамочувствиеСотрудника.ПометкаУдаления
		|	И СамочувствиеСотрудника.Дата > &ДатаНачалаВыгрузки";
	
Конецфункции

Функция ТекстЗапросаМероприятия()
	
	Возврат
		"ВЫБРАТЬ
		|	СправочникМероприятия.Ссылка КАК Ссылка,
		|	СправочникМероприятия.ДатаНачала КАК ДатаНачала,
		|	СправочникМероприятия.ДатаОкончания КАК ДатаОкончания,
		|	СправочникМероприятия.Наименование КАК Наименование,
		|	СправочникМероприятия.Предмет КАК Предмет,
		|	УчастникиМероприятия.СостояниеПриглашения КАК СостояниеПриглашения,
		|	КешИнформацииОбОбъектах.ЕстьФайлы КАК ЕстьФайлы,
		|	КешИнформацииОбОбъектах.СостояниеКонтроля КАК СостояниеКонтроля,
		|	СправочникМероприятия.Важность КАК Важность,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(СправочникМероприятия.Организатор) КАК ОрганизаторСтрокой,
		|	СправочникМероприятия.ПериодСтрокой КАК ПериодСтрокой,
		|	СправочникМероприятия.Подготовил КАК Подготовил,
		|	ВЫБОР
		|		КОГДА 1 В
		|				(ВЫБРАТЬ
		|					1
		|				ИЗ
		|					РегистрСведений.СостоянияМероприятий КАК СостоянияМероприятий
		|				ГДЕ
		|					СостоянияМероприятий.Мероприятие = СправочникМероприятия.Ссылка
		|					И СостоянияМероприятий.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияМероприятий.МероприятиеПроведено))
		|			ТОГДА 4
		|		КОГДА 1 В
		|				(ВЫБРАТЬ
		|					1
		|				ИЗ
		|					РегистрСведений.СостоянияМероприятий КАК СостоянияМероприятий
		|				ГДЕ
		|					СостоянияМероприятий.Мероприятие = СправочникМероприятия.Ссылка
		|					И СостоянияМероприятий.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияМероприятий.МероприятиеОтменено))
		|			ТОГДА 5
		|		КОГДА СправочникМероприятия.ДатаНачала = ДАТАВРЕМЯ(1, 1, 1)
		|				И 1 В
		|					(ВЫБРАТЬ
		|						1
		|					ИЗ
		|						РегистрСведений.СостоянияМероприятий КАК СостоянияМероприятий
		|					ГДЕ
		|						СостоянияМероприятий.Мероприятие = СправочникМероприятия.Ссылка
		|						И СостоянияМероприятий.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияМероприятий.МероприятиеВСтадииПодготовки))
		|			ТОГДА 1
		|		КОГДА СправочникМероприятия.ДатаОкончания < &ТекущаяДата
		|				И 1 В
		|					(ВЫБРАТЬ
		|						1
		|					ИЗ
		|						РегистрСведений.СостоянияМероприятий КАК СостоянияМероприятий
		|					ГДЕ
		|						СостоянияМероприятий.Мероприятие = СправочникМероприятия.Ссылка
		|						И СостоянияМероприятий.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияМероприятий.МероприятиеВСтадииПодготовки))
		|			ТОГДА 3
		|		КОГДА 1 В
		|				(ВЫБРАТЬ
		|					1
		|				ИЗ
		|					РегистрСведений.СостоянияМероприятий КАК СостоянияМероприятий
		|				ГДЕ
		|					СостоянияМероприятий.Мероприятие = СправочникМероприятия.Ссылка
		|					И СостоянияМероприятий.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияМероприятий.МероприятиеВСтадииПодготовки))
		|			ТОГДА 2
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ИндексКартинкиСостояния,
		|	ОтметкиВремениСсылочныхОбъектов.Граница КАК Граница
		|ИЗ
		|	Справочник.Мероприятия КАК СправочникМероприятия
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчастникиМероприятия КАК УчастникиМероприятия
		|		ПО СправочникМероприятия.Ссылка = УчастникиМероприятия.Мероприятие
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КешИнформацииОбОбъектах КАК КешИнформацииОбОбъектах
		|		ПО СправочникМероприятия.Ссылка = КешИнформацииОбОбъектах.Объект
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтметкиВремениСсылочныхОбъектов КАК ОтметкиВремениСсылочныхОбъектов
		|		ПО СправочникМероприятия.Ссылка = ОтметкиВремениСсылочныхОбъектов.Ключ
		|ГДЕ
		|	УчастникиМероприятия.Исполнитель = &Пользователь
		|	И СправочникМероприятия.ДатаОкончания >= &ДатаНачалаВыгрузки";
	
Конецфункции

Функция ТекстЗапросаУчетВремени() Экспорт
	
	Возврат
		"ВЫБРАТЬ
		|	ВидыРабот.Ссылка КАК Ссылка,
		|	ВидыРабот.ПометкаУдаления КАК ПометкаУдаления,
		|	ВидыРабот.Родитель КАК Родитель,
		|	ВидыРабот.ЭтоГруппа КАК ЭтоГруппа,
		|	ВидыРабот.Код КАК Код,
		|	ВидыРабот.Наименование КАК Наименование,
		|	ВидыРабот.ВидВремени КАК ВидВремени,
		|	ОтметкиВремениСсылочныхОбъектов.Граница КАК Граница
		|ИЗ
		|	Справочник.ВидыРабот КАК ВидыРабот
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтметкиВремениСсылочныхОбъектов КАК ОтметкиВремениСсылочныхОбъектов
		|		ПО ВидыРабот.Ссылка = ОтметкиВремениСсылочныхОбъектов.Ключ
		|ГДЕ
		|	НЕ ВидыРабот.ПометкаУдаления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕжедневныйОтчет.Ссылка КАК Ссылка,
		|	ЕжедневныйОтчет.ДлительностьРабот КАК ДлительностьРабот,
		|	ЕжедневныйОтчет.НачалоДня КАК НачалоДня,
		|	ЕжедневныйОтчет.НекорректнаяДлительность КАК НекорректнаяДлительность,
		|	ЕжедневныйОтчет.ОкончаниеДня КАК ОкончаниеДня,
		|	ЕжедневныйОтчет.Дата КАК Дата,
		|	ЕжедневныйОтчет.Ссылка КАК ID,
		|	ОтметкиВремениСсылочныхОбъектов.Граница КАК Граница,
		|	ЕжедневныйОтчет.СпособУказанияВремени КАК СпособУказанияВремени,
		|	ЕжедневныйОтчет.ДлительностьРабочая КАК ДлительностьРабочая
		|ИЗ
		|	Документ.ЕжедневныйОтчет КАК ЕжедневныйОтчет
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтметкиВремениСсылочныхОбъектов КАК ОтметкиВремениСсылочныхОбъектов
		|		ПО ЕжедневныйОтчет.Ссылка = ОтметкиВремениСсылочныхОбъектов.Ключ
		|ГДЕ
		|	ЕжедневныйОтчет.Пользователь = &Пользователь
		|	И ЕжедневныйОтчет.Дата >= &ДатаНачалаВыгрузки
		|	И НЕ ЕжедневныйОтчет.ПометкаУдаления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕженедельныйОтчет.Ссылка КАК Ссылка,
		|	ЕженедельныйОтчет.ДлительностьРабот КАК ДлительностьРабот,
		|	ЕженедельныйОтчет.ДлительностьРабочая КАК ДлительностьРабочая,
		|	ЕженедельныйОтчет.ДниНедели.(
		|		ДеньНедели КАК ДеньНедели,
		|		Дата КАК Дата,
		|		ВидДня КАК ВидДня,
		|		НачалоДня КАК НачалоДня,
		|		ОкончаниеДня КАК ОкончаниеДня,
		|		ЕжедневныйОтчет КАК ЕжедневныйОтчет,
		|		ДлительностьРабот КАК ДлительностьРабот,
		|		ДлительностьРабочая КАК ДлительностьРабочая) КАК ДниНедели,
		|	ЕженедельныйОтчет.Достижения КАК Достижения,
		|	ЕженедельныйОтчет.Планы КАК Планы,
		|	ЕженедельныйОтчет.Неудачи КАК Неудачи,
		|	ЕженедельныйОтчет.Дата КАК Дата,
		|	ЕженедельныйОтчет.Ссылка КАК ID,
		|	ОтметкиВремениСсылочныхОбъектов.Граница КАК Граница
		|ИЗ
		|	Документ.ЕженедельныйОтчет КАК ЕженедельныйОтчет
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтметкиВремениСсылочныхОбъектов КАК ОтметкиВремениСсылочныхОбъектов
		|		ПО ЕженедельныйОтчет.Ссылка = ОтметкиВремениСсылочныхОбъектов.Ключ
		|ГДЕ
		|	ЕженедельныйОтчет.Пользователь = &Пользователь
		|	И ЕженедельныйОтчет.Дата >= &ДатаНачалаВыгрузки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ФактическиеТрудозатраты.Начало КАК Начало,
		|	ФактическиеТрудозатраты.Окончание КАК Окончание,
		|	ФактическиеТрудозатраты.ДатаДобавления КАК ДатаДобавления,
		|	ФактическиеТрудозатраты.НомерДобавления КАК НомерДобавления,
		|	ФактическиеТрудозатраты.ВидРабот КАК ВидРабот,
		|	ФактическиеТрудозатраты.ЕжедневныйОтчет КАК ОтчетИсточник,
		|	ФактическиеТрудозатраты.ОписаниеРаботы КАК ОписаниеРаботы,
		|	ФактическиеТрудозатраты.Длительность КАК Длительность,
		|	ФактическиеТрудозатраты.Проект КАК Проект,
		|	МП_ИзмененныеТрудозатраты.Граница КАК Граница,
		|	МП_ИзмененныеТрудозатраты.Удалена КАК Удалена
		|ИЗ
		|	РегистрСведений.ФактическиеТрудозатраты КАК ФактическиеТрудозатраты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МП_ИзмененныеТрудозатраты КАК МП_ИзмененныеТрудозатраты
		|		ПО ФактическиеТрудозатраты.Пользователь = МП_ИзмененныеТрудозатраты.Пользователь
		|		И (КОНЕЦПЕРИОДА(ФактическиеТрудозатраты.ДатаДобавления, ДЕНЬ) = МП_ИзмененныеТрудозатраты.День)
		|		И ФактическиеТрудозатраты.НомерДобавления = МП_ИзмененныеТрудозатраты.НомерДобавления
		|ГДЕ
		|	ФактическиеТрудозатраты.Пользователь = &Пользователь
		|	И ФактическиеТрудозатраты.ДатаДобавления >= НАЧАЛОПЕРИОДА(&ДатаНачалаВыгрузки, ДЕНЬ)
		|	И НЕ ФактическиеТрудозатраты.Удалена
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МП_СинхронизацияПроектов.Проект КАК Ссылка,
		|	ЕСТЬNULL(МП_СинхронизацияПроектов.Удален, Проекты.ПометкаУдаления) КАК ПометкаУдаления,
		|	Проекты.Наименование КАК Наименование,
		|	Проекты.Код КАК Код,
		|	Проекты.Состояние КАК Состояние,
		|	Проекты.Руководитель КАК Руководитель
		|ИЗ
		|	РегистрСведений.МП_СинхронизацияПроектов КАК МП_СинхронизацияПроектов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Проекты КАК Проекты
		|		ПО МП_СинхронизацияПроектов.Проект = Проекты.Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ФактическиеТрудозатраты.Проект,
		|	NULL,
		|	Проекты.Наименование,
		|	Проекты.Код,
		|	Проекты.Состояние,
		|	Проекты.Руководитель
		|ИЗ
		|	РегистрСведений.ФактическиеТрудозатраты КАК ФактическиеТрудозатраты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Проекты КАК Проекты
		|		ПО ФактическиеТрудозатраты.Проект = Проекты.Ссылка
		|ГДЕ
		|	ФактическиеТрудозатраты.Пользователь = &Пользователь
		|	И ФактическиеТрудозатраты.ДатаДобавления >= НАЧАЛОПЕРИОДА(&ДатаНачалаВыгрузки, ДЕНЬ)
		|	И НЕ ФактическиеТрудозатраты.Удалена";
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
Функция ИнициализироватьЗапрос(ПараметрыСинхронизации, ДатаНачалаВыгрузки) 
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("МобильноеПриложение", ПараметрыСинхронизации.МобильноеПриложение);
	
	Запрос.УстановитьПараметр("Пользователь", ПараметрыСинхронизации.ТекущийПользователь);
	
	Запрос.УстановитьПараметр("ДатаНачалаВыгрузки", ДатаНачалаВыгрузки);
	
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДата());
	
	Возврат Запрос;
	
КонецФункции
#КонецОбласти


