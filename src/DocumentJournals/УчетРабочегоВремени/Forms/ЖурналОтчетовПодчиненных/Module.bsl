#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДатаОтбора = ТекущаяДатаСеанса();
	Если Не ЗначениеЗаполнено(Подразделение) Тогда
		Подразделение = РаботаСПользователями.ПолучитьПодразделение(
			Пользователи.ТекущийПользователь());
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(СписокОтчетов,
			"Подразделение", Подразделение);
		ОбновитьОтчетыСервер();
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьЕжедневныеОтчеты") Тогда
		Элементы.ОтчетыВсегоЗаДень.Видимость = Ложь;
		Элементы.ОтчетыРабочееЗаДень.Видимость = Ложь;
		Элементы.ОтчетыПроцентФондаЗаДень.Видимость = Ложь;
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьЕженедельныеОтчеты") Тогда
		Элементы.ОтчетыВсегоЗаНеделю.Видимость = Ложь;
		Элементы.ОтчетыРабочееЗаНеделю.Видимость = Ложь;
		Элементы.ОтчетыПроцентФондаЗаНеделю.Видимость = Ложь;
	КонецЕсли;
	
	Если Не Пользователи.ЭтоПолноправныйПользователь() Тогда
		Пользователь = Пользователи.ТекущийПользователь();
		МассивПодразделений = 
			ЖурналыДокументов.УчетРабочегоВремени.МассивПодразделенийДляПросмотраОтчетовОТрудозатратах(
			Пользователь);
		ДоступныеПодразделения.ЗагрузитьЗначения(МассивПодразделений);
	КонецЕсли;

	Если ДоступныеПодразделения.Количество() = 1 Тогда
		Подразделение = ДоступныеПодразделения[0].Значение;
		Элементы.Подразделение.Видимость = Ложь;
	КонецЕсли;

	Если ДоступныеПодразделения.Количество() Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СписокОтчетов,
			"Подразделение", ДоступныеПодразделения);
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(СписокОтчетов, "Пользователь",
		Пользователи.ТекущийПользователь());
		
	ЭтоМобильныйКлиент = ПараметрыСеанса.ЭтоМобильныйКлиент;
	МК_НастроитьЭлементыФормы();
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)

	Если Не ЗначениеЗаполнено(Настройки["Подразделение"]) Тогда
		Подразделение = РаботаСПользователями.ПолучитьПодразделение(
			Пользователи.ТекущийПользователь());
	КонецЕсли;
	
	Если ДоступныеПодразделения.Количество() И ЗначениеЗаполнено(Подразделение)
		И ДоступныеПодразделения.НайтиПоЗначению(Подразделение) = Неопределено Тогда
		Подразделение = Неопределено;
	КонецЕсли;
	
	Если ДоступныеПодразделения.Количество() = 1 Тогда
		Подразделение = ДоступныеПодразделения[0].Значение;
		Элементы.Подразделение.Видимость = Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Подразделение) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(СписокОтчетов,
			"Подразделение",
			Подразделение);
		ОбновитьОтчетыСервер();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ИзмененЕженедельныйОтчет"
		Или ИмяСобытия = "ИзмененЕжедневныйОтчет" Тогда
		ОбновитьОтчеты();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеПриИзменении(Элемент)
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(СписокОтчетов,
		"Подразделение",
		Подразделение);
	ОбновитьОтчеты();
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ДоступныеПодразделения.Количество() 
		И ДоступныеПодразделения.НайтиПоЗначению(ВыбранноеЗначение) = Неопределено Тогда
		СтандартнаяОбработка = Ложь;
		СообщениеОбОшибке = СтрШаблон(НСтр("ru = 'У вас нет прав на просмотр отчетов по подразделению %1.'"),
			ВыбранноеЗначение);
		ОбщегоНазначенияКлиент.СообщитьПользователю(СообщениеОбОшибке, , "Подразделение");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ДоступныеПодразделения.Количество() Тогда
		СтандартнаяОбработка = Ложь;
		ПараметрыФормы = Новый Структура("ДоступныеПодразделения", ДоступныеПодразделения);
		ОткрытьФорму("Справочник.СтруктураПредприятия.ФормаВыбора", ПараметрыФормы, Элемент);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаОтбораПриИзменении(Элемент)
	ОбновитьОтчеты();
КонецПроцедуры

&НаКлиенте
Процедура ОтчетыПриАктивизацииСтроки(Элемент)
	Если Элементы.Отчеты.ТекущиеДанные = Неопределено Тогда
		ТекущийПользователь = Неопределено;
	Иначе
		ТекущийПользователь = Элементы.Отчеты.ТекущиеДанные.Пользователь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОтчетыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "ОтчетыВсегоЗаДень"
		Или Поле.Имя = "ОтчетыРабочееЗаДень"
		Или Поле.Имя = "ОтчетыПроцентФондаЗаДень" Тогда
		СтандартнаяОбработка = Ложь;
		Отчет = Элементы.Отчеты.ТекущиеДанные.ЕжедневныйОтчет;
		Если ЗначениеЗаполнено(Отчет) Тогда
			ПоказатьЗначение(, Отчет);
		Иначе
			ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", Новый Структура);
			ПараметрыФормы.ЗначенияЗаполнения.Вставить("Пользователь", 
				Элементы.Отчеты.ТекущиеДанные.Пользователь);
			ПараметрыФормы.ЗначенияЗаполнения.Вставить("Дата", 
				ДатаОтбора);
			ОткрытьФорму("Документ.ЕжедневныйОтчет.Форма.ФормаДокумента",
				ПараметрыФормы,
				Элемент);
		КонецЕсли;
	КонецЕсли;
		
	Если Поле.Имя = "ОтчетыВсегоЗаНеделю"
		Или Поле.Имя = "ОтчетыРабочееЗаНеделю"
		Или Поле.Имя = "ОтчетыПроцентФондаЗаНеделю" Тогда
		СтандартнаяОбработка = Ложь;
		Отчет = Элементы.Отчеты.ТекущиеДанные.ЕженедельныйОтчет;
		Если ЗначениеЗаполнено(Отчет) Тогда
			ПоказатьЗначение(, Отчет);
		Иначе
			ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", Новый Структура);
			ПараметрыФормы.ЗначенияЗаполнения.Вставить("Пользователь", 
				Элементы.Отчеты.ТекущиеДанные.Пользователь);
			ПараметрыФормы.ЗначенияЗаполнения.Вставить("Дата", 
				ДатаОтбора);
			ОткрытьФорму("Документ.ЕженедельныйОтчет.Форма.ФормаДокумента",
				ПараметрыФормы,
				Элемент);
		КонецЕсли;
	КонецЕсли;
	
	Если Поле.Имя = "Отсутствие" Тогда
		СтандартнаяОбработка = Ложь;
		Отсутствие = Элементы.Отчеты.ТекущиеДанные.Отсутствие;
		Если ЗначениеЗаполнено(Отсутствие) Тогда
			ПоказатьЗначение(, Отсутствие);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетыОбработкаЗапросаОбновления()
	
	ОбновитьОтчетыСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ДатаОтбораПриАктивизацииДаты(Элемент)
	
	Элементы.ДатаОтбора.Видимость = Ложь;
	Элементы.ДатаОтбораПолеВвода.Видимость = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ДатаОтбораПолеВводаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Элементы.ДатаОтбора.Видимость = Истина;
	Элементы.ДатаОтбораПолеВвода.Видимость = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокОтчетовПриАктивизацииСтроки(Элемент)
	Если Элементы.СписокОтчетов.ТекущиеДанные = Неопределено Тогда
		ТекущийПользователь = Неопределено;
	Иначе
		ТекущийПользователь = Элементы.СписокОтчетов.ТекущиеДанные.Пользователь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ГруппаСтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	Если ТекущаяСтраница = Элементы.СтраницаПоДатам Тогда
		Если Элементы.Отчеты.ТекущиеДанные = Неопределено Тогда
			ТекущийПользователь = Неопределено;
		Иначе
			ТекущийПользователь = Элементы.Отчеты.ТекущиеДанные.Пользователь;
		КонецЕсли;
	КонецЕсли;
	Если ТекущаяСтраница = Элементы.СтраницаСписком Тогда
		Если Элементы.СписокОтчетов.ТекущиеДанные = Неопределено Тогда
			ТекущийПользователь = Неопределено;
		Иначе
			ТекущийПользователь = Элементы.СписокОтчетов.ТекущиеДанные.Пользователь;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииПараметровЭкрана()
	
	ПриИзмененииПараметровЭкранаНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьОтчеты()
	ОбновитьОтчетыСервер();
КонецПроцедуры

&НаСервере
Процедура ОбновитьОтчетыСервер()
	
	ТаблицаОтчетов.Очистить();
	Если Не ЗначениеЗаполнено(Подразделение) Тогда
		Возврат;
	КонецЕсли;
	
	ПользователиПодразделения = РаботаСПользователями.ПолучитьПользователейПодразделения(Подразделение);
	ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияЗначенияИзМассива(ПользователиПодразделения,
		Пользователи.ТекущийПользователь());
	
	ПолучатьОтсутствия = ПолучитьФункциональнуюОпцию("ИспользоватьОтсутствия");
	Если ПолучатьОтсутствия Тогда
		НастройкиОтсутствий = ОтсутствияКлиентСервер.НастройкиПроверкиОтсутствий();
		НастройкиОтсутствий.УчитыватьФлагБудуРазбиратьЗадачи = Ложь;
		ТаблицаОтсутствий = Отсутствия.ПолучитьТаблицуОтсутствий(НачалоДня(ДатаОтбора),
			КонецДня(ДатаОтбора),
			ПользователиПодразделения,
			НастройкиОтсутствий);
	КонецЕсли;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Пользователи.Ссылка КАК Пользователь,
		|	ЕжедневныйОтчет.Ссылка КАК ЕжедневныйОтчет,
		|	ЕжедневныйОтчет.ДлительностьРабот КАК ЕжедневныйОтчетДлительностьРабот,
		|	ЕжедневныйОтчет.ДлительностьРабочая КАК ЕжедневныйОтчетДлительностьРабочая,
		|	ЕженедельныйОтчет.Ссылка КАК ЕженедельныйОтчет,
		|	ЕженедельныйОтчет.ДлительностьРабот КАК ЕженедельныйОтчетДлительностьРабот,
		|	ЕженедельныйОтчет.ДлительностьРабочая КАК ЕженедельныйОтчетДлительностьРабочая
		|ИЗ
		|	Справочник.Пользователи КАК Пользователи
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЕжедневныйОтчет КАК ЕжедневныйОтчет
		|		ПО (ЕжедневныйОтчет.Пользователь = Пользователи.Ссылка)
		|			И (НЕ ЕжедневныйОтчет.ПометкаУдаления)
		|			И (ЕжедневныйОтчет.Дата МЕЖДУ &НачалоДня И &КонецДня)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЕженедельныйОтчет КАК ЕженедельныйОтчет
		|		ПО (ЕженедельныйОтчет.Пользователь = Пользователи.Ссылка)
		|			И (НЕ ЕженедельныйОтчет.ПометкаУдаления)
		|			И (ЕженедельныйОтчет.Дата МЕЖДУ &НачалоНедели И &КонецНедели)
		|ГДЕ
		|	Пользователи.Ссылка В (&ПользователиПодразделения)
		|УПОРЯДОЧИТЬ ПО
		|	Пользователь");
	Запрос.УстановитьПараметр("ПользователиПодразделения", ПользователиПодразделения);
	Запрос.УстановитьПараметр("НачалоДня", НачалоДня(ДатаОтбора));
	Запрос.УстановитьПараметр("КонецДня", КонецДня(ДатаОтбора));
	
	Если ДеньНедели(ДатаОтбора) > 5 Тогда 
		НачалоНедели = УчетВремениКлиентСервер.КонецВоскресенья(ДатаОтбора) + 1 - 3600 * 24 * 2;
		КонецНедели = УчетВремениКлиентСервер.КонецВоскресенья(
			УчетВремениКлиентСервер.КонецВоскресенья(ДатаОтбора) + 1) - 3600 * 24 * 2;
	Иначе
		НачалоНедели = УчетВремениКлиентСервер.НачалоПонедельника(ДатаОтбора) - 3600 * 24 * 2;
		КонецНедели = УчетВремениКлиентСервер.КонецВоскресенья(ДатаОтбора) - 3600 * 24 * 2;
	КонецЕсли;
	Запрос.УстановитьПараметр("НачалоНедели", НачалоНедели);
	Запрос.УстановитьПараметр("КонецНедели", КонецНедели);
	
	ПользователиМассивом = Новый Массив;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ПользователиМассивом.Добавить(Выборка.Пользователь);
	КонецЦикла;
	
	// Соберем длительность за день.
	Фонд = УчетВремени.ФондРабочегоВремени(ПользователиМассивом, НачалоНедели, КонецНедели);
	Фонд.Колонки.Длительность.Имя = "ДлительностьЗаНеделю";
	Фонд.Колонки.Добавить("ДлительностьЗаДень", Новый ОписаниеТипов("Число"));
	Для Каждого СтрокаФонда Из Фонд Цикл
		Если СтрокаФонда.ДатаОкончания < НачалоДня(ДатаОтбора)
			Или СтрокаФонда.ДатаНачала > КонецДня(ДатаОтбора) Тогда
			Продолжить;
		КонецЕсли;
		ДатаНачала = Макс(СтрокаФонда.ДатаНачала, НачалоДня(ДатаОтбора));
		ДатаОкончания = Мин(СтрокаФонда.ДатаОкончания, КонецДня(ДатаОтбора));
		СтрокаФонда.ДлительностьЗаДень = Макс(0, ДатаОкончания - ДатаНачала);
	КонецЦикла;
	Фонд.Свернуть("Пользователь", "ДлительностьЗаНеделю, ДлительностьЗаДень");
	
	ИтогВсегоЗаНеделюСек = 0;
	ИтогРабочееЗаНеделюСек = 0;
	ИтогФондЗаНеделюСек = 0;
	ИтогВсегоЗаДеньСек = 0;
	ИтогРабочееЗаДеньСек = 0;
	ИтогФондЗаДеньСек = 0;
	
	Выборка.Сбросить();
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Строка = ТаблицаОтчетов.Добавить();
		ЗаполнитьЗначенияСвойств(Строка, Выборка);
		
		// Ежедневный отчет.
		Если ЗначениеЗаполнено(Выборка.ЕжедневныйОтчет) Тогда
			
			Строка.ВсегоЗаДень = Выборка.ЕжедневныйОтчетДлительностьРабот;
			ВсегоЗаДеньСек = УчетВремениКлиентСервер.ЧислоИзСтроки(Выборка.ЕжедневныйОтчетДлительностьРабот);
			ИтогВсегоЗаДеньСек = ИтогВсегоЗаДеньСек + ВсегоЗаДеньСек;
			
			Строка.РабочееЗаДень = Выборка.ЕжедневныйОтчетДлительностьРабочая;
			РабочееЗаДеньСек = УчетВремениКлиентСервер.ЧислоИзСтроки(Выборка.ЕжедневныйОтчетДлительностьРабочая);
			ИтогРабочееЗаДеньСек = ИтогРабочееЗаДеньСек + РабочееЗаДеньСек;
			
			СтрокаФонда = Фонд.Найти(Выборка.Пользователь);
			ФондЗаДеньСек = ?(СтрокаФонда = Неопределено, 0, СтрокаФонда.ДлительностьЗаДень);
			ПроцентФондаЗаДень = ?(ФондЗаДеньСек = 0, 0, РабочееЗаДеньСек / ФондЗаДеньСек * 100);
			Строка.ПроцентФондаЗаДень = Формат(ПроцентФондаЗаДень, "ЧДЦ=0; ЧН=0; ЧГ=0") + "%";
			ИтогФондЗаДеньСек = ИтогФондЗаДеньСек + ФондЗаДеньСек;
			
		Иначе
			Строка.ВсегоЗаДень = НСтр("ru = 'нет'");
			Строка.РабочееЗаДень = НСтр("ru = 'нет'");
			Строка.ПроцентФондаЗаДень = НСтр("ru = 'нет'");
		КонецЕсли;
		
		// Отчет за неделю.
		Если ЗначениеЗаполнено(Выборка.ЕженедельныйОтчет) Тогда
			
			Строка.ВсегоЗаНеделю = Выборка.ЕженедельныйОтчетДлительностьРабот;
			ВсегоЗаНеделюСек = УчетВремениКлиентСервер.ЧислоИзСтроки(Выборка.ЕженедельныйОтчетДлительностьРабот);
			ИтогВсегоЗаНеделюСек = ИтогВсегоЗаНеделюСек + ВсегоЗаНеделюСек;
			
			Строка.РабочееЗаНеделю = Выборка.ЕженедельныйОтчетДлительностьРабочая;
			РабочееЗаНеделюСек = УчетВремениКлиентСервер.ЧислоИзСтроки(Выборка.ЕженедельныйОтчетДлительностьРабочая);
			ИтогРабочееЗаНеделюСек = ИтогРабочееЗаНеделюСек + РабочееЗаНеделюСек;
			
			СтрокаФонда = Фонд.Найти(Выборка.Пользователь);
			ФондЗаНеделюСек = ?(СтрокаФонда = Неопределено, 0, СтрокаФонда.ДлительностьЗаНеделю);
			ПроцентФондаЗаНеделю = ?(ФондЗаНеделюСек = 0, 0, РабочееЗаНеделюСек / ФондЗаНеделюСек * 100);
			Строка.ПроцентФондаЗаНеделю = Формат(ПроцентФондаЗаНеделю, "ЧДЦ=0; ЧН=0; ЧГ=0") + "%";
			ИтогФондЗаНеделюСек = ИтогФондЗаНеделюСек + ФондЗаНеделюСек;
			
		Иначе
			Строка.ВсегоЗаНеделю = НСтр("ru = 'нет'");
			Строка.РабочееЗаНеделю = НСтр("ru = 'нет'");
			Строка.ПроцентФондаЗаНеделю = НСтр("ru = 'нет'");
		КонецЕсли;
		
		// Отсутствие.
		Если ПолучатьОтсутствия Тогда
			СтрокаОтсутствия = ТаблицаОтсутствий.Найти(Выборка.Пользователь, "Сотрудник");
			Если СтрокаОтсутствия <> Неопределено Тогда
				Строка.Отсутствие = СтрокаОтсутствия.Ссылка;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	ИтогВсегоЗаНеделю = УчетВремениКлиентСервер.ЧислоВСтроку(ИтогВсегоЗаНеделюСек, "нет");
	ИтогРабочееЗаНеделю = УчетВремениКлиентСервер.ЧислоВСтроку(ИтогРабочееЗаНеделюСек, "нет");
	ИтогПроцентФондаЗаНеделю = ?(ИтогФондЗаНеделюСек = 0, НСтр("ru = 'нет'"),
		Формат(100 * ИтогРабочееЗаНеделюСек / ИтогФондЗаНеделюСек, "ЧДЦ=0; ЧН=нет; ЧГ=0") + "%");
		
	ИтогВсегоЗаДень = УчетВремениКлиентСервер.ЧислоВСтроку(ИтогВсегоЗаДеньСек, "нет");
	ИтогРабочееЗаДень = УчетВремениКлиентСервер.ЧислоВСтроку(ИтогРабочееЗаДеньСек, "нет");
	ИтогПроцентФондаЗаДень = ?(ИтогФондЗаДеньСек = 0, НСтр("ru = 'нет'"),
		Формат(100 * ИтогРабочееЗаДеньСек / ИтогФондЗаДеньСек, "ЧДЦ=0; ЧН=нет; ЧГ=0") + "%");
	
КонецПроцедуры

&НаСервере
Процедура МК_НастроитьЭлементыФормы()
	
	Если Не ЭтоМобильныйКлиент Тогда
		Возврат;
	КонецЕсли;
	
	СворачиваниеЭлементовПоВажности = СворачиваниеЭлементовФормыПоВажности.НеИспользовать;
	ВертикальныйИнтервал = ИнтервалМеждуЭлементамиФормы.Половинный;
	
	Элементы.ТаблицаОбщаяКомандаОбсуждения.Видимость = Ложь;
	Элементы.СписокОбщаяКомандаОбсуждения.Видимость = Ложь;
	Элементы.ТаблицаОбщаяКомандаСоздатьПисьмоНаОсновании.Видимость = Ложь;
	Элементы.СписокОбщаяКомандаСоздатьПисьмоНаОсновании.Видимость = Ложь;
	
	Элементы.ГруппаКалендарьОтчеты.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	// ДатаОтбора.
	Элементы.ДатаОтбораПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	Элементы.ДатаОтбораПолеВвода.Заголовок = НСтр("ru = 'Сведения на дату'");
	Элементы.ДатаОтбораПолеВвода.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
	Элементы.ДатаОтбораПолеВвода.Видимость = Истина;
	
	Элементы.ДатаОтбора.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	Элементы.ДатаОтбора.Видимость = Ложь;
	Элементы.ДатаОтбора.ОтображатьТекущуюДату = Ложь;
	
	// Список отчетов.
	Элементы.СписокОтчетов.Шапка = Ложь;
	Элементы.СписокОтчетовТип.Видимость = Ложь;
	Элементы.СписокОтчетовСправа.Видимость = Ложь;
	
	Элементы.СписокОтчетовПользователь.ЦветТекста = ЦветаСтиля.МК_ЦветАвтораИсполнителя;
	Элементы.СписокОтчетовПользователь.Шрифт = ШрифтыСтиля.МК_ШрифтАвтораИсполнителя;
	Элементы.СписокОтчетовДата.Формат = НСтр("ru='ДФ=''dd MMMM yyyy'''");
	
	Элементы.МК_СписокОтчетовГруппаКолонок.Видимость = Истина;
	
	Элементы.Переместить(Элементы.СписокОтчетовДата, Элементы.ГруппаДатаСотрудник);
	Элементы.Переместить(Элементы.СписокОтчетовПользователь, Элементы.ГруппаДатаСотрудник);
	Элементы.Переместить(Элементы.СписокОтчетовДлительностьРабот, Элементы.ГруппаДлительность);
	Элементы.Переместить(Элементы.СписокОтчетовДлительностьРабочая, Элементы.ГруппаДлительность);
	
	Элементы.СписокОтчетовПользователь.Высота = 2;
	
	// Контроль по датам.
	Элементы.Отчеты.Шапка = Ложь;
	Элементы.Отчеты.Подвал = Ложь;
	Элементы.ОтчетыПроцентФондаЗаНеделю.Видимость = Ложь;
	Элементы.ОтчетыДень.Видимость = Ложь;
	Элементы.Отсутствие.Видимость = Ложь;
	
	Элементы.ОтчетыНеделя.Группировка = ГруппировкаКолонок.Вертикальная;
	Элементы.Пользователь.Высота = 2;
	Элементы.Пользователь.РастягиватьПоГоризонтали = Истина;
	Элементы.ОтчетыНеделя.ФиксацияВТаблице = ФиксацияВТаблице.Право;
	Элементы.ОтчетыНеделя.Ширина = 8;
	
	ПриИзмененииПараметровЭкранаНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииПараметровЭкранаНаСервере()
	
	ЭтоМобильныйСБольшимЭкраном = МобильныйКлиентВызовСервера.ЭтоМобильныйСБольшимЭкраном();
	ЭтоМобильныйВПортретномРежиме = МобильныйКлиентВызовСервера.ЭтоМобильныйВПортретномРежиме();
	
	Если ЭтоМобильныйСБольшимЭкраном И Не ЭтоМобильныйВПортретномРежиме Тогда
		МК_НастроитьФормуВАльбомномРежиме();
	Иначе
		МК_НастроитьФормуВПортретномРежиме();
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Процедура МК_НастроитьФормуВПортретномРежиме()
	
	Элементы.ГруппаКалендарьОтчеты.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	Элементы.ДатаОтбора.Видимость = Ложь;
	Элементы.ДатаОтбораПолеВвода.Видимость = Истина;

КонецПроцедуры

&НаСервере
Процедура МК_НастроитьФормуВАльбомномРежиме()
	
	Элементы.ГруппаКалендарьОтчеты.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	Элементы.ДатаОтбора.Видимость = Истина;
	Элементы.ДатаОтбораПолеВвода.Видимость = Ложь;

КонецПроцедуры

#КонецОбласти
