
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Список.Параметры.УстановитьЗначениеПараметра("Пользователь", Пользователи.ТекущийПользователь());
	Список.Параметры.УстановитьЗначениеПараметра("ИспользоватьЕжедневныеОтчеты", 
		ПолучитьФункциональнуюОпцию("ИспользоватьЕжедневныеОтчеты"));
	Список.Параметры.УстановитьЗначениеПараметра("ИспользоватьЕженедельныеОтчеты", 
		ПолучитьФункциональнуюОпцию("ИспользоватьЕженедельныеОтчеты"));
		
	ЭтоМобильныйКлиент = ПараметрыСеанса.ЭтоМобильныйКлиент;
	МК_НастроитьЭлементыФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	ПерсональныеНастройки = ФайловыеФункцииКлиентПовтИсп.ПолучитьПерсональныеНастройкиРаботыСФайлами();
	Если ИмяСобытия = "ДобавленаСтрокаЕжедневногоОтчета" Тогда 
		Если ЗначениеЗаполнено(Параметр) И ТипЗнч(Параметр) = Тип("ДокументСсылка.ЕжедневныйОтчет")
			И ОбщегоНазначенияДокументооборотВызовСервера.ЗначениеРеквизитаОбъекта(Параметр, "Пользователь") = ПерсональныеНастройки.ТекущийПользователь Тогда 
			Элементы.Список.Обновить();
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СоздатьЕжедневныйОтчетВыполнить()
	
	ОткрытьФорму("Документ.ЕжедневныйОтчет.ФормаОбъекта",, Элементы.Список);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьОтчетЗаНеделюВыполнить()

	ОткрытьФорму("Документ.ЕженедельныйОтчет.ФормаОбъекта",, Элементы.Список);

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОтчетЗаСегодня(Команда)
	
	ПараметрыФормы = Новый Структура;
	
	ОтчетЗаСегодня = ПолучитьЕжедневныеОтчетыТекущейДаты();
	Если ЗначениеЗаполнено(ОтчетЗаСегодня) Тогда 
		ПараметрыФормы.Вставить("Ключ", ОтчетЗаСегодня);
	КонецЕсли;
	
	ОткрытьФорму("Документ.ЕжедневныйОтчет.ФормаОбъекта", ПараметрыФормы, Элементы.Список);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьЕжедневныеОтчетыТекущейДаты()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЕжедневныйОтчет.Ссылка
	|ИЗ
	|	Документ.ЕжедневныйОтчет КАК ЕжедневныйОтчет
	|ГДЕ
	|	ЕжедневныйОтчет.Пользователь = &Пользователь
	|	И НАЧАЛОПЕРИОДА(ЕжедневныйОтчет.Дата, ДЕНЬ) = НАЧАЛОПЕРИОДА(&Дата, ДЕНЬ)
	|	И НЕ ЕжедневныйОтчет.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("Дата", ТекущаяДата());
	Запрос.УстановитьПараметр("Пользователь", ПользователиКлиентСервер.ТекущийПользователь());
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

&НаКлиенте
Процедура СписокОбработкаЗапросаОбновления(Элемент)
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СлужебныеПроцедурыИФункции_МобильныйКлиент

&НаСервере
Процедура МК_НастроитьЭлементыФормы()
	
	Если Не ЭтоМобильныйКлиент Тогда
		Возврат;
	КонецЕсли;
	
	СворачиваниеЭлементовПоВажности = СворачиваниеЭлементовФормыПоВажности.НеИспользовать;
	ВертикальныйИнтервал = ИнтервалМеждуЭлементамиФормы.Половинный;
	
	Элементы.СписокКонтекстноеМенюСоздатьЕжедневныйОтчет.Видимость = Ложь;
	Элементы.СписокКонтекстноеМенюСоздатьОтчетЗаНеделю.Видимость = Ложь;
	Элементы.СписокКонтекстноеМенюОткрытьОтчетЗаСегодня.Видимость = Ложь;
	Элементы.ДлительностьРабот.Видимость = Ложь;
	Элементы.ДлительностьРабочая.Видимость = Ложь;
	
	Элементы.Переместить(Элементы.СоздатьЕжедневныйОтчет, Элементы.КоманднаяПанельСоздатьПодменю);
	Элементы.Переместить(Элементы.СоздатьОтчетЗаНеделю, Элементы.КоманднаяПанельСоздатьПодменю);
	Элементы.КоманднаяПанельСоздатьПодменю.Видимость = Истина;
	Элементы.КоманднаяПанельСоздать.Видимость = Ложь;
	
	Элементы.Список.Шапка = Ложь;
	Элементы.МК_ГруппаКолонок.Видимость = Истина;
	Элементы.Переместить(Элементы.Тип, Элементы.МК_ГруппаКолонок);
	Элементы.Переместить(Элементы.Дата, Элементы.ГруппаДатаДеньНедели);
	Элементы.Переместить(Элементы.ДеньНедели, Элементы.ГруппаДатаДеньНедели);
	
	Элементы.Дата.Формат = НСтр("ru='ДФ=''dd MMMM yyyy'''");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
