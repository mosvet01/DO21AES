#Область ПрограммныйИнтерфейс
	
// Возвращает список с подразделениями, в которых переданный пользователь является руководителем
// с учетом делегирования прав по ежедневным отчетам или всех прав
// Параметры:
//  Пользователь - СправочникСсылка.Пользователи
// 
// Возвращаемое значение:
//  Массив - Массив подразделений, где пользователь руководит непосредственно
//	или через делегирование. Отсортирован по представлениям подразделений.
//
Функция МассивПодразделенийДляПросмотраОтчетовОТрудозатратах(Пользователь) Экспорт

	ЗапросПользователей = Новый Запрос;
	ЗапросПользователей.Текст = 
		"ВЫБРАТЬ
		|	ИсполнителиРолейИДелегаты.РольПользователь КАК РольПользователь
		|ИЗ
		|	РегистрСведений.ИсполнителиРолейИДелегаты КАК ИсполнителиРолейИДелегаты
		|ГДЕ
		|	ИсполнителиРолейИДелегаты.ИсполнительДелегат = &ИсполнительДелегат
		|	И ИсполнителиРолейИДелегаты.НастройкаДелегирования <> ЗНАЧЕНИЕ(Справочник.ДелегированиеПрав.ПустаяСсылка)
		|	И (ИсполнителиРолейИДелегаты.ИмяОбластиДелегирования = ""ЕжедневныеОтчеты""
		|			ИЛИ ИсполнителиРолейИДелегаты.ИмяОбластиДелегирования = """")";
	
	ЗапросПользователей.УстановитьПараметр("ИсполнительДелегат", Пользователь);
	МассивПользователей = ЗапросПользователей.Выполнить().Выгрузить().ВыгрузитьКолонку("РольПользователь");
	МассивПользователей.Добавить(Пользователь);
	
	Подразделения = Справочники.СтруктураПредприятия.ПодчиненныеПодразделения(МассивПользователей);
	
	СписокПодразделений = Новый СписокЗначений;
	СписокПодразделений.ЗагрузитьЗначения(Подразделения);
	СписокПодразделений.СортироватьПоЗначению();
	
	Возврат СписокПодразделений.ВыгрузитьЗначения();
	
КонецФункции

#КонецОбласти